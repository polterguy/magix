

inspect=>@"initializes the common settings for a 
new wizard application"

_data
set=>[_data].Value
  value=>[$][file].Value
replace=>[_data].Value
  what=>system42\admin\app-wizard\wizards\
replace=>[_data].Value
  what=>.hl

set=>[magix.data.load][prototype][type].Value
  value=>@"magix.{0}.template"
    v0=>[_data].Value

magix.data.load
  prototype
    type

for-each=>[magix.data.load][objects]
  _data
  set=>[@][_data][0].Name
    value=>[.].Name
  set=>[@][_data][0].Value
    value=>[.].Name
  replace=>[@][_data][0].Value
    what=>magix.crud-app.template.
  replace=>[@][_data][0].Name
    what=>magix.crud-app.template.
  add=>[/][magix.forms.create-web-part][controls][panel][controls][select][items]
    value=>[@][_data][0]

set=>[magix.forms.create-web-part][controls][panel][controls][button:1][onclick][magix.execute.execute-file].Value
  value=>[$][file].Value
set=>[magix.forms.create-web-part][controls][panel][controls][select][onselectedindexchanged][magix.execute.execute-file].Value
  value=>[$][file].Value

set=>[magix.forms.create-web-part][controls][panel][controls][button:1][onclick][set][value][v0].Value
  value=>[$][file].Value
replace=>[magix.forms.create-web-part][controls][panel][controls][button:1][onclick][set][value][v0].Value
  what=>system42\admin\app-wizard\wizards\
replace=>[magix.forms.create-web-part][controls][panel][controls][button:1][onclick][set][value][v0].Value
  what=>.hl

magix.forms.create-web-part
  container=>content2
  form-id=>init-app
  css=>span-14 top-3 last
  controls
    panel=>wrp
      default=>next
      controls
        label
          text=>give your new application template a name, or edit existing template
          tag=>label
          css=>span-14 last bottom-1
        select=>editExisting
          css=>span-5
          items
            _default=>choose existing app ...
          onselectedindexchanged
            magix.forms.get-value
              form-id=>init-app
              id=>editExisting
            set=>[magix.execute.execute-file][name].Value
              value=>[magix.forms.get-value][value].Value
            magix.execute.execute-file
        text-box=>name
          place-holder=>name ...
          css=>span-6
        button=>wizard
          text=>&lt;&lt; back
          css=>span-3 left-5 top-1 clear
          onclick
            execute-file=>system42/admin/app-wizard/app-wizard.hl
        button=>next
          text=>next &gt;&gt;
          css=>span-3 last top-1
          onclick
            magix.forms.get-value
              form-id=>init-app
              id=>name
            if=>equals
              lhs=>[magix.forms.get-value][value].Value
              rhs=>
              code
                magix.viewport.show-message
                  message=>you must give your application a unique name
                magix.forms.set-focus
                  form-id=>init-app
                  id=>name
                stop
            set=>[magix.data.load][id].Value
              value=>magix.{0}.template.{1}
                v0
                v1=>[magix.forms.get-value][value].Value
            magix.data.load
            if=>exist
              lhs=>[magix.data.load][objects]
              code
                magix.viewport.show-message
                  message=>application with that name already exists
                stop
            set=>[magix.execute.execute-file][name].Value
              value=>[magix.forms.get-value][value].Value
            magix.execute.execute-file

magix.forms.set-focus
  form-id=>init-app
  id=>name

