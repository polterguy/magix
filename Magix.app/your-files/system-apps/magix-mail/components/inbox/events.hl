

inspect=>@"active events for show-emails.hl file

contains active events for the inbox list of emails web part"


// toggles email as visible or invisible according to whether or not it's already being viewed
magix.emails.toggle-email
  using=>magix.forms
    get-value
      form-id=>inbox-list
      id=>viewing-{0}
        =>[$][id].value
  if=>equals
    lhs=>[using][get-value][value].value
    rhs=>true
    code
      // already viewing, "unviewing" email
      using=>magix.forms
        set-value
          form-id=>inbox-list
          id=>viewing-{0}
            =>[$][id].value
          value=>false
      set=>[@][magix.viewport.clear-controls][container].value
        value=>dynamic-{0}
          =>[$][id].value
      magix.viewport.clear-controls
        reset-class
          new-class=>emails-sleeping-dynamic
  else
    // making email show
    using=>magix.forms
      set-value
        form-id=>inbox-list
        id=>viewing-{0}
          =>[$][id].value
        value=>true
      set-style
        form-id=>inbox-list
        id=>subject-{0}
          =>[$][id].value
        key=>font-weight
        value=>normal
    set=>[@][magix.emails.load-email][container].value
      value=>dynamic-{0}
        =>[$][id].value
    set=>[@][magix.emails.load-email][email-id].value
      value=>[$][email-id].value
    magix.emails.load-email


// loads email for viewing
magix.emails.load-email
  iterate=>[magix.execute]
    if=>equals
      lhs=>[.].value
      rhs=>{container}
      code
        set=>[.].value
          value=>[$][container].value
    else-if=>equals
      lhs=>[.].value
      rhs=>{reply-container}
      code
        set=>[.].value
          value=>reply-{0}
            id=>[$][container].value
  add=>[magix.execute]
    value=>[$]
  magix.execute
    using=>magix.data
      load
        id=>[$][email-id].value

    // making sure email is marked as read
    add=>[using:1][save][value]
      values=>[using:0][load][value]
    set=>[using:1][save][value][read].value
      value=>true
    using=>magix.data
      save
        id=>[$][email-id].value

    // extracting body, prioritizing html view
    _body
    if=>exist
      lhs=>[using][load][value][body][html]
      code
        set=>[_body].value
          value=>[using][load][value][body][html].value
    else
      set=>[_body].value
        value=>[using][load][value][body][plain].value

      // email is not html, making sure all carriage returns are replaced with <br/> tags
      replace=>[_body].value
        what=>\n
        with=><br/>
      replace=>[_body].value
        what=>\r

    set=>[_controls][**label=>body][value].value
      value=>[_body].value

    set=>[_controls][**label=>email-header][value].value
      value=>[using][load][value][subject].value
    if=>more-than
      lhs=>[using][load][value][attachments].count
      rhs=>0
      code
        for-each=>[using][load][value][attachments]
          _lb
            link-button
              class=>span-7 last
              onclick
                set=>[magix.file.download][file].value
                  value=>[$][info].value
                set=>[magix.file.download][as].value
                  value=>[$][value].value
                magix.file.download
          set=>[@][_lb][link-button][value].value
            value=>[.].value
          set=>[@][_lb][link-button][info].value
            value=>[.][local-file-name].value
          add=>[/][_controls][panel=>attachments][controls]
            values=>[@][_lb]
    else
      set=>[_controls][panel=>attachments]
    set=>[_controls][**button=>reply][info].value
      value=>[$][email-id].value
    set=>[_controls][**button=>delete][info].value
      value=>[$][email-id].value
    magix.viewstate.get
      id=>magix.emails.current-view
    if=>equals
      lhs=>[@][magix.viewstate.get][value][mailbox].value
      rhs=>trash
      code
        set=>[**button=>reply]
        set=>[**panel=>btn-group][class].value
          value=>span-2 last right clear
    else-if=>equals
      lhs=>[@][magix.viewstate.get][value][mailbox].value
      rhs=>sent
      code
        set=>[**button=>reply]
        set=>[**button=>delete]
    _controls
      panel=>email-content
        class=>span-17 last email-content-body
        controls
          label=>email-header
            class=>span-17 last bottom-1
            tag=>h3
          label=>body
            class=>clear last email-content-body-body
            tag=>div
      panel=>attachments
        class=>span-7 clear email-attachments
        controls
          label
            class=>span-7 last
            tag=>label
            value=>attachments
      panel=>btn-group
        class=>btn-group span-4 last right clear
        controls
          button=>reply
            class=>span-2
            value=>reply
            onclick
              magix.email.current-reply-container
              if=>exist
                lhs=>[magix.email.current-reply-container][container]
                code
                  using=>magix.browser
                    scroll
                      id=>[magix.email.current-reply-container][container].value
                  magix.viewport.show-message
                    message=>you must first finish your existing reply
                    color=>#ffaaaa
                  stop
              set=>[execute-script][params][email-id].value
                value=>[$][info].value
              set=>[execute-script][params][reply-container].value
                value=>reply-{0}
                  =>[$][container].value
              execute-script
                file=>your-files/system-apps/magix-mail/components/reply/reply-web-part.hl
          button=>delete
            class=>span-2 last
            value=>delete
            onfirstload
              magix.viewstate.get
                id=>magix.emails.current-view
              if=>equals
                lhs=>[magix.viewstate.get][value][mailbox].value
                rhs=>trash
                code
                  magix.forms.set-value
                    form-id=>inbox
                    id=>delete
                    value=>un-delete
            onclick
              magix.viewstate.get
                id=>magix.emails.current-view
              if=>equals
                lhs=>[magix.viewstate.get][value][mailbox].value
                rhs=>trash
                code

                  // we're in trashcan, undeleting email
                  using=>magix.data
                    load
                      id=>[$][info].value
                  set=>[@][using][load][value][folder].value
                    value=>inbox
                  using=>magix.data
                    save
                      id=>[$][info].value
                      value=>[@][..][using][load][value]
                  magix.viewport.show-message
                    message=>message was moved to inbox
                    time=>1000
                  execute-script
                    file=>your-files/system-apps/magix-mail/components/inbox/show-emails.hl
                    params
                      frozen=>true
              else

                // we're in inbox, deleting message
                using=>magix.data
                  load
                    id=>[$][info].value
                set=>[@][using][load][value][folder].value
                  value=>trash
                using=>magix.data
                  save
                    id=>[$][info].value
                    value=>[@][..][using][load][value]
                magix.viewport.show-message
                  message=>message was moved to trash
                  time=>1000
                execute-script
                  file=>your-files/system-apps/magix-mail/components/inbox/show-emails.hl
                  params
                    frozen=>true
      dynamic=>{reply-container}
    using=>magix.forms
      create-web-part
        container=>{container}
        class=>span-18 last email-item-body bottom-1 top-1
        controls=>[_controls]
    magix.email.update-unread-email-count

