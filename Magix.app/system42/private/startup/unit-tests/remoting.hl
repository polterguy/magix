

inspect=>@"creates unit tests for the remoting features of magix

creates unit tests to test the [magix.execute.remote], [magix.execute.open], 
[magix.execute.close] and to some extent the [magix.execute.event] keywords"


event
  name=>magix.test.remote-open
  persist=>false
  inspect=>@"verifies [open], [close] and [remote] works

creates an event, opens it up for external sources to invoke, 
for then to verify the active event could successfully be invoked 
over http"
  code
    event
      name=>magix.test.temp-test-event
      persist=>false
      code
        set=>[$][output].value
          value=>{0} {1}
            =>[$][input].value
            =>world
    open
      name=>magix.test.temp-test-event
    try
      code
        magix.configuration.get-base-directory
        set=>[@][remote][url].value
          value=>[@][magix.configuration.get-base-directory][value].value
        remote
          name=>magix.test.temp-test-event
          params
            input=>hello
      catch
    close
      name=>magix.test.temp-test-event
    event
      name=>magix.test.temp-test-event
    if=>not-equals
      lhs=>[try][code][remote][params][output].value
      rhs=>hello world
      code
        throw=>[open] and/or [remote] misbehaved
    list-events
      open=>true
    if=>exist
      lhs=>[list-events][events][magix.test.temp-test-event]
      code
        throw=>[close] misbehaved


event
  name=>magix.test.remote-throw-inspect
  persist=>false
  inspect=>@"verifies inspect fails on remote events

creates an open active event, then tries to inspect it 
remotely over http, for then to verify the invocation 
throws an exception"
  code
    _success=>false
    event
      name=>magix.test.temp-test-event
      persist=>false
      code
    open
      name=>magix.test.temp-test-event
    try
      code
        magix.configuration.get-base-directory
        set=>[@][remote][url].value
          value=>[@][magix.configuration.get-base-directory][value].value
        remote
          name=>magix.test.temp-test-event
          params
            inspect
      catch
        set=>[_success].value
          value=>true
    close
      name=>magix.test.temp-test-event
    event
      name=>magix.test.temp-test-event
    if=>not-equals
      lhs=>[_success].value
      rhs=>true
      code
        throw=>[remote] misbehaved with inspect

