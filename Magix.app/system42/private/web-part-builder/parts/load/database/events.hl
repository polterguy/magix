


inspect=>@"creates the events for loading a web part from the database

contains the events for helping user load a web part from the database"


// tries to load from database
magix.ide.load_onclick
  magix.forms.get-value
    form-id=>load-content
    id=>database-id
  if=>equals
    lhs=>[magix.forms.get-value][value].value
    rhs=>_default
    code
      magix.viewport.show-message
        message=>you must choose a form from your database
        color=>#ffaaaa
      magix.forms.effect
        type=>focus-and-select
        form-id=>load-content
        id=>database-id
      stop
  set=>[magix.data.load][id].value
    value=>[magix.forms.get-value][value].value
  magix.data.load
  if=>not-exist
    lhs=>[magix.data.load][value]
    or=>not-equals
      lhs=>[magix.data.load][value][type].value
      rhs=>magix.forms.web-part
    code
      magix.viewport.show-message
        message=>that object either doesn't exist, or is not a web part
        color=>#ffbbbb
  else
    magix.ide.load-from-database
  set=>[magix.viewstate.set][value].value
    value=>[magix.data.load][id].value
  magix.viewstate.set
    id=>magix.ide.last-database-id

// actually loads the form
magix.ide.load-from-database
  magix.forms.get-value
    form-id=>load-content
    id=>database-id
  using=>magix.data
    load
      id=>[magix.forms.get-value][value].value
  add=>[magix.ide.set-form][value]
    value=>[using][load][value][surface]
    children-only=>true
  magix.ide.set-form
    clear-undo=>true
  magix.viewport.clear-controls
    container=>editor-persist-wrapper
    reset-class=>true

// sets the last used db id on the text-box asking user for id to save web part as
magix.ide.database-id_onfirstload
  magix.data.load
    only-id=>true
    prototype
      type=>magix.forms.web-part
  _items
    _default=>choose database id ...
  for-each=>[magix.data.load][objects]
    add=>[/][_items]
      value=>[.][id].value
        value=>[.][id].value
  add=>[magix.forms.set-values][values]
    value=>[_items]
    children-only=>true
  magix.forms.set-values
    form-id=>load-content
    id=>database-id
  magix.viewstate.get
    id=>magix.ide.last-database-id
  if=>exist
    lhs=>[magix.viewstate.get][value]
    code
      using=>magix.forms
        set-value
          form-id=>load-content
          id=>database-id
          value=>[magix.viewstate.get][value].value
  magix.forms.effect
    type=>timeout
    time=>25
    chained
      e0
        type=>focus-and-select
        form-id=>load-content
        id=>database-id

