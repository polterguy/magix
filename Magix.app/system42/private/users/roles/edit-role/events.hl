

inspect=>@"contains events for editing one role

contains events for editing one role"


// adds a child role according to the position of the editing role, and the text value of the text-box
// expects [parent]
magix.roles.add-role
  magix.forms.get-value
    form-id=>edit-role
    id=>txt-role-name
  _role
  magix.data.load
    prototype
      type=>magix.core.role

  // checking to see if role exists from before
  _found
  for-each=>[magix.data.load][objects]
    iterate=>[.]
      if=>equals
        lhs=>[/][magix.forms.get-value][value].value
        rhs=>[.].value
        and=>equals
          lhs=>[.].name
          rhs=>name
        code
          set=>[/][**_found].value
            value=>true

  // role already exists with the same name if this evaluates to true
  if=>equals
    lhs=>[_found].value
    rhs=>true
    code
      magix.viewport.show-message
        message=>role exists from before
        color=>red
      magix.forms.effect
        type=>focus-and-select
        form-id=>edit-role
        id=>txt-role-name
      stop

  // finding the correct parent role to update
  for-each=>[magix.data.load][objects]
    _role-id
    _found
    set=>[@][_role-id].value
      value=>[.][id].value
    iterate=>[.][value]
      if=>equals
        lhs=>[.].name
        rhs=>name
        and=>equals
          lhs=>[.].value
          rhs=>[$][parent].value
        code
          // this is the right role
          set=>[@][..][..][..][_found].value
            value=>true

          // adding child role
          _child
            child
              name
          set=>[@][_child][child][name].value
            value=>[/][magix.forms.get-value][value].value
          add=>[.][..]
            value=>[@][_child]
            children-only=>true
          stop
    if=>equals
      lhs=>[@][_found].value
      rhs=>true
      code
        add=>[/][_role]
          value=>[.]
          children-only=>true
        stop
  add=>[magix.data.save]
    value=>[_role]
    children-only=>true
  magix.data.save
  magix.roles.show-roles-tree
  set=>[magix.log.append-username][body].value
    value=>role '{0}' was created
      =>[magix.forms.get-value][value].value
  magix.log.append-username
    header=>role created

// deletes role, expects [role]
magix.roles.delete-role
  _role
  magix.data.load
    prototype
      type=>magix.core.role
  for-each=>[magix.data.load][objects]
    _role-id
    _found
    set=>[@][_role-id].value
      value=>[.][id].value
    iterate=>[.][value]
      if=>equals
        lhs=>[.].name
        rhs=>name
        and=>equals
          lhs=>[.].value
          rhs=>[$][role].value
        code
          // this is the right role
          set=>[@][..][..][..][_found].value
            value=>true

          // clearing role
          set=>[.][..]
          stop
    if=>equals
      lhs=>[@][_found].value
      rhs=>true
      code
        add=>[/][_role]
          value=>[.]
          children-only=>true
        stop
  add=>[magix.data.save]
    value=>[_role]
    children-only=>true
  magix.data.save
  magix.roles.clean-up-users
  set=>[magix.log.append-username][body].value
    value=>role '{0}' was deleted
      =>[$][role].value
  magix.log.append-username
    header=>role deleted

// cleaning up all roles from user objects that doesn't exist in role hierarchy
magix.roles.clean-up-users
  _roles
  magix.data.load
    prototype
      type=>magix.core.role
  iterate=>[magix.data.load][objects]
    if=>equals
      lhs=>[.].name
      rhs=>name
      code
        add=>[/][_roles]
          value=>[.].value
  magix.data.load
    prototype
      type=>magix.core.user
  for-each=>[magix.data.load:1][objects]
    _current-user
    _has-changes
    set=>[@][_has-changes].value
    set=>[@][_current-user][0]
      value=>[.]
    for-each=>[.][value][roles]
      if=>not-exist
        lhs=>[/][_roles][[.].name]
        code
          // role that user belongs to was deleted
          set=>[@][..][..][..][_current-user][object][value][roles][[.].name]
          set=>[@][..][..][..][_has-changes].value
            value=>true
    if=>equals
      lhs=>[@][_has-changes].value
      rhs=>true
      code
        set=>[@][magix.data.save][value]
          value=>[@][..][..][_current-user][object][value]
        set=>[@][magix.data.save][id]
          value=>[@][..][..][_current-user][object][id]
        magix.data.save

