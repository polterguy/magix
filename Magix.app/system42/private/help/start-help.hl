

inspect=>@"starts help system

starts help system, by loading index.mml into 'help' container viewport.  if 
user somehow has previously opened another help page, then this help page will 
be opened instead"


// cleans up from previous calls
magix.session.set
  id=>magix.help.cached-help-files

// includes necessary javascript and css files for our autocompleter
magix.viewport.include-client-file
  type=>javascript
  file=>media/bootstrap/js/jQuery.js
magix.viewport.include-client-file
  type=>javascript
  file=>media/bootstrap/js/bootstrap.min.js
magix.viewport.include-client-file
  type=>css
  file=>media/back-end/typeahead.css
magix.viewport.include-client-file
  type=>css
  file=>media/back-end/help.css

// opens up the help file the user last had open, or the index file if no help file is in the list of items
event
  name=>magix.help.open-last-help-file
  inspect=>@"opens up last viewed help file

opens up the last viewed help file, or the index help file if no file has 
been viewed in the current session"
  code

    // checking to see if we've got some help files in our list of items
    magix.session.get
      id=>magix.help.stack-of-help
    if=>exist
      lhs=>[magix.session.get][value]
      code

        // we have a stack of open items, figuring out which file is at the end of our stack of items
        // and opening that page, instead of the default index page
        using=>magix.math
          subtract
            =>[magix.session.get][value].count
            =>1
        set=>[magix.help.open-file][file].value
          value=>[magix.session.get][value][[@][using][subtract].value].value
        set=>[magix.help.open-file][freeze-stack].value
          value=>true
    magix.help.open-file
      file=>system42/private/help/index.mml

// opens the given [file] help file, and adds it to the stack of items, such that back and forward works
event
  name=>magix.help.open-file
  persist=>false
  code
    using=>magix.forms
      create-mml-web-part
        form-id=>help-content
        container=>help-content
        mml-file=>[$][file].value
    if=>not-equals
      lhs=>[$][freeze-stack].value
      rhs=>true
      code
        magix.session.get
          id=>magix.help.stack-of-help
        if=>exist
          lhs=>[@][magix.session.get][value]
          code
            set=>[@][..][..][magix.session.set][value]
              value=>[@][..][..][magix.session.get][value]
        add=>[@][magix.session.set][value]
          value=>file
            value=>[$][file].value
        magix.session.set
          id=>magix.help.stack-of-help
    magix.session.get
      id=>magix.help.stack-of-help

// sets the next help page for the system, which will trigger if user clicks 'next page'
// [next] contains file to put into que
event
  name=>magix.help.set-next
  persist=>false
  code
    set=>[magix.viewstate.set][value].value
      value=>[$][next].value
    magix.viewstate.set
      id=>magix.help.next-page

// moves helpsystem backwards, if possible
event
  name=>magix.help.move-previous
  persist=>false
  code
    magix.session.get
      id=>magix.help.stack-of-help
    if=>more-than
      lhs=>[magix.session.get][value].count
      rhs=>1
      code
        using=>magix.math
          subtract
            =>[magix.session.get][value].count
            =>1
          subtract
            =>[magix.session.get][value].count
            =>2

        // deleting last file, which is the currently viewed page
        set=>[magix.session.get][value][[@][using][subtract:0].value]

        // opening up the second last page in stack, which is the previously seen page
        set=>[@][magix.help.open-file][file].value
          value=>[magix.session.get][value][[@][using][subtract:1].value].value
        set=>[@][magix.session.set][value]
          value=>[magix.session.get][value]
        magix.session.set
          id=>magix.help.stack-of-help
        magix.help.open-file
          freeze-stack=>true
    else
      magix.viewport.show-message
        message=>no more previous help files

// moves helpsystem forward, if possible
event
  name=>magix.help.move-next
  persist=>false
  code
    magix.viewstate.get
      id=>magix.help.next-page
    set=>[magix.help.open-file][file].value
      value=>[magix.viewstate.get][value].value

// search for the file in [$][header], and loads it if found
event
  name=>magix.help.search-for-file
  persist=>false
  code
    magix.session.get
      id=>magix.help.cached-help-files
    if=>exist
      lhs=>[magix.session.get][value][[$][header].value]
      code
        set=>[@][magix.help.open-file][file].value
          value=>[magix.session.get][value][[$][header].value].value
        magix.help.open-file
    else
      magix.viewport.show-message
        message=>file not found

// expects [file] and [header], links the header towards the file in a session object, for making it 
// possible to access a file from its header
event
  name=>magix.help.link-header-to-file
  persist=>false
  code
    magix.session.get
      id=>magix.help.cached-help-files
    if=>exist
      lhs=>[magix.session.get][value]
      code
        add=>[magix.session.set][value]
          value=>[magix.session.get][value]
          children-only=>true
    add=>[magix.session.set][value]
      value=>[$][header].value
        value=>[$][file].value
    magix.session.set
      id=>magix.help.cached-help-files
    using=>magix.session
      get
        id=>magix.help.cached-help-files


// event that iterates one directory for all *.mml files, and recursively traverse all child directories
// creates the json necessary to databind the autocompleter
event
  name=>magix.help.iterate-one-directory
  persist=>false
  code

    // default directory is main help directory if no directory is given
    _directory=>system42/private/help
    _return=>
    if=>exist
      lhs=>[$][directory]
      code
        set=>[_directory].value
          value=>[$][directory].value

    // listing all *.mml files, and all child directories
    using=>magix.file
      list-files
        directory=>[_directory].value
        filter=>*.mml
      list-directories
        directory=>[_directory].value

    // iterating through files in directory
    for-each=>[using][list-files][files]
      using=>magix.file
        load
          file=>[.].name

      // lookinf for an h2 header to use as the friendly text of our autocompleter
      split=>[@][using][load][value].value
        what=><h2>
        trim=>true
      if=>exist
        lhs=>[@][split][result]
        code
          split=>[@][..][..][split][result][0].value
            what=></h2>
            trim=>true
          // split-2 now contains file header
          if=>not-equals
            lhs=>[/][_return].value
            rhs=>
            code
              set=>[/][_return].value
                value=>{0},
                  =>[/][_return].value
          set=>[/][_return].value
            value=>@"{0}""{1}"""
              =>[/][_return].value
              =>[@][split][result][0].value

          // creating an association between the file's header and the file itself, such that we can later 
          // load a specific file from a given header
          set=>[@][magix.help.link-header-to-file][header].value
            value=>[@][split][result][0].value
          set=>[@][magix.help.link-header-to-file][file].value
            value=>[.].name
          magix.help.link-header-to-file

    // iterating through directories in directory and recursively calling self
    for-each=>[using][list-directories][directories]
      set=>[@][magix.help.iterate-one-directory][directory].value
        value=>[.].name
      magix.help.iterate-one-directory

      // adding the entire result from the child directory to the output
      set=>[/][_return].value
        value=>@"{0},{1}"
          =>[/][_return].value
          =>[@][magix.help.iterate-one-directory][files].value

    // returning json back to caller
    set=>[$][files].value
      value=>[_return].value

// setting the autocompleter's datasource
set=>[**@data-source=>{data-source}].value
  value=>\[{0}]
    =>[:magix.help.iterate-one-directory][files].value

// creates help web part
magix.forms.create-web-part
  form-id=>help-wrapper
  container=>help
  class=>span-22 last clear
  controls

    // navigation parts
    panel=>help-navigation
      class=>span-22 last bottom-1 top-2 btn-group
      default=>search-button
      controls
        button=>help-back
          class=>large span-2
          value=>back
          onclick
            magix.help.move-previous
            magix.browser.scroll
              id=>help
        button=>help-index
          class=>large span-2
          value=>index
          onclick
            magix.help.open-file
              file=>system42/private/help/index.mml
            magix.browser.scroll
              id=>help
        button=>close-help
          value=>close
          class=>large span-2
          onclick
            magix.viewport.clear-controls
              container=>help
        text-box=>search-help
          placeholder=>search ...
          class=>span-11 large
          autocomplete=>false
          @data-provide=>typeahead
          @data-items=>10
          @data-source=>{data-source}
          onfocus
            magix.forms.effect
              type=>focus-and-select
              form-id=>help-wrapper
              id=>search-help
        button=>search-button
          class=>span-2 large
          value=>search
          onclick
            magix.forms.get-value
              form-id=>help-wrapper
              id=>search-help
            set=>[magix.help.search-for-file][header].value
              value=>[magix.forms.get-value][value].value
            magix.help.search-for-file
            magix.browser.scroll
              id=>help
            magix.forms.set-focus
              id=>next-help-page
        button=>next-help-page
          value=>next page
          class=>large span-3 last
          onclick
            magix.help.move-next
            magix.browser.scroll
              id=>help

    // contains actual help content
    dynamic=>help-content
      class=>span-22 last
      onfirstload
        magix.help.open-last-help-file

    // additional navigation at the bottom, to make it easier to navigate
    panel=>bottom-help-navigation
      class=>span-6 right last btn-group top-3
      controls
        button=>help-back-bottom
          value=>back
          class=>large span-3
          onclick
            magix.help.move-previous
            magix.browser.scroll
              id=>help
        button=>next-help-page-bottom
          value=>next page
          class=>large span-3 last
          onclick
            magix.help.move-next
            magix.browser.scroll
              id=>help

