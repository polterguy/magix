

inspect=>@"tests for [try], [catch] and [throw]

creates unit tests to test verify that the exception logic and 
keywords works the way they should"


event
  name=>magix.test.exception
  persist=>false
  inspect=>@"verifies [try], [catch] and [throw] works

tries to throw an exception that is being catched to 
make sure the exception keywords functions the way they 
should work"
  code
    _thrown
    try
      code
        throw=>exception thrown
      catch
        if=>equals
          lhs=>[@][exception].value
          rhs=>exception thrown
          code
            set=>[_thrown].value
              value=>yes
    if=>not-equals
      lhs=>[_thrown].value
      rhs=>yes
      code
        throw=>[catch]/[try] misbehaved


event
  name=>magix.test.exception-finally
  persist=>false
  inspect=>@"verifies [finally] works

tries to throw an exception to verify that the 
[finally] keyword works the way it should"
  code
    _finally
    try
      code
        throw=>exception thrown
      catch
      finally
        set=>[_finally].value
          value=>yes
    if=>not-equals
      lhs=>[_finally].value
      rhs=>yes
      code
        throw=>[finally] misbehaved


event
  name=>magix.test.exception-finally-no-catch
  persist=>false
  inspect=>@"verifies [finally] works without a catch

tries to throw an exception to verify that the 
[finally] keyword works the way it should, 
also without a [catch] block"
  code
    _finally
    try
      code
        try
          code
            throw=>exception thrown
          finally
            set=>[_finally].value
              value=>yes
      catch
    if=>not-equals
      lhs=>[_finally].value
      rhs=>yes
      code
        throw=>[finally] misbehaved


event
  name=>magix.test.exception-finally-no-throw
  persist=>false
  inspect=>@"verifies [finally] works without a catch

verifies that the [finally] keyword is executed, also 
when no exception is thrown"
  code
    _finally
    try
      code
      finally
        set=>[_finally].value
          value=>yes
    if=>not-equals
      lhs=>[_finally].value
      rhs=>yes
      code
        throw=>[finally] misbehaved


event
  name=>magix.test.exception-types
  persist=>false
  inspect=>@"verifies [catch] works with types

verifies that the [catch] keyword is executed, but only 
when the [type] matches the exception"
  code
    _error
    try
      code
        try
          code
            throw=>my exception
              type=>my-type
          catch=>not-to-be-catched
            set=>[_error].value
              value=>true
      catch=>my-type
    if=>equals
      lhs=>[_error].value
      rhs=>true
      code
        throw=>[catch] misbehaved


event
  name=>magix.test.exception-multiple-catch
  persist=>false
  inspect=>@"verifies [catch] works with types

verifies that the [catch] keyword is executed, but only 
when the [type] matches the exception"
  code
    _success
    try
      code
        throw=>my exception
          type=>correct-type
      catch=>wrong-type
        set=>[_success].value
          value=>false
      catch=>correct-type
        if=>not-exist
          lhs=>[_success].value
          code
            set=>[_success].value
              value=>true
      catch=>wrong-type-2
        set=>[_success].value
          value=>false
    if=>not-equals
      lhs=>[_success].value
      rhs=>true
      code
        throw=>[catch] misbehaved


event
  name=>magix.test.exception-system-types
  persist=>false
  inspect=>@"verifies [catch] works with types

verifies that the [catch] keyword is executed, but only 
when the [type] matches the system type of the exception 
thrown"
  code
    _error
    try
      code
        try
          code
            magix.file.load
              file=>this-file-should-definitely-not-exist.hl
          catch=>not-to-be-catched
            set=>[_error].value
              value=>true
      catch=>System.IO.FileNotFoundException
    if=>equals
      lhs=>[_error].value
      rhs=>true
      code
        throw=>[catch] misbehaved


event
  name=>magix.test.exception-rethrow
  persist=>false
  inspect=>@"verifies [throw] works when re-thrown

verifies that a [catch] can successfully rethrow 
an exception by using [throw] without any message 
or type"
  code
    _success=>false
    try
      code
        try
          code
            magix.file.load
              file=>this-file-should-definitely-not-exist.hl
          catch
            throw
      catch=>System.IO.FileNotFoundException
        set=>[_success].value
          value=>true
    if=>not-equals
      lhs=>[_success].value
      rhs=>true
      code
        throw=>[throw] misbehaved


event
  name=>magix.test.exception-no-catch-finally
  persist=>false
  inspect=>@"verifies [try] does not work without [catch] or [finally]

verifies that [try] does not allow execution, unless you have either a 
[finally] or a [catch] block within your [try]"
  code
    _expected-exception
    try
      code

