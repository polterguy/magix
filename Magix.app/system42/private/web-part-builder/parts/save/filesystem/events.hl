

inspect=>@"creates the events for saving form into filesystem

contains the events for helping user saving his form into the filesystem"


// tries to save to filesystem, asking user to confirm any overwritings
magix.ide.save_onclick
  magix.forms.get-value
    form-id=>save-content
    id=>filename
  if=>equals
    lhs=>[magix.forms.get-value][value].value
    rhs=>
    code
      magix.viewport.show-message
        message=>you must supply a filename to save your form into the filesystem
      stop
  set=>[magix.file.file-exist][file].value
    value=>[magix.forms.get-value][value].value
  magix.file.file-exist
  if=>equals
    lhs=>[magix.file.file-exist][value].value
    rhs=>true
    code
      magix.viewport.confirm
        message=>that file already exist, do you wish to overwrite it?
        code
          magix.ide.save-to-filesystem
  else
    magix.ide.save-to-filesystem

// actually saves form to filesystem
magix.ide.save-to-filesystem
  magix.forms.get-value
    form-id=>save-content
    id=>filename
  set=>[magix.file.save][file].value
    value=>[magix.forms.get-value][value].value
  magix.ide.get-form
  set=>[node-2-code][node].value
    value=>[magix.ide.get-form]
  node-2-code
    remove-root=>true
  set=>[magix.file.save][value].value
    value=>[node-2-code][code].value
  magix.file.save
  magix.viewport.clear-controls
    container=>editor-persist-wrapper
    reset-class=>true
  set=>[magix.data.save-username][value][filename].value
    value=>[magix.forms.get-value][value].value
  magix.data.save-username
    id=>magix.ide.last-used-filename
  using=>magix.viewport
    show-message
      message=>form was successfully save to filesystem as '{0}'
        =>[magix.forms.get-value][value].value
  set=>[magix.log.append-username][body].value
    value=>web part was saved to filesystem with filename of '{0}'
      =>[magix.forms.get-value][value].value
  magix.log.append-username
    header=>web part was saved

// sets the last used db id on the text-box asking user for id to save web part as
magix.ide.filename_onfirstload
  magix.data.load-username
    id=>magix.ide.last-used-filename
  if=>exist
    lhs=>[magix.data.load-username][value][filename]
    code
      set=>[@][magix.forms.set-value][value].value
        value=>[magix.data.load-username][value][filename].value
      magix.forms.set-value
        form-id=>save-content
        id=>filename
  magix.forms.effect
    type=>timeout
    time=>25
    chained
      e0
        type=>focus-and-select
        form-id=>save-content
        id=>filename

// active event for when user clicks the browse button
magix.ide.browse_onclick
  execute-script
    file=>system42/private/file-manager/file-manager.hl
    params
      filter=>*.hl
      container=>helper-for-persist-wrapper
      class=>span-15 last boxed air-padding bottom-1
      editor-container=>editor-for-persist-wrapper
      onselect
        set=>[magix.forms.set-value][value].value
          value=>[$][file].value
        magix.forms.set-value
          form-id=>save-content
          id=>filename
        magix.viewport.clear-controls
          container=>helper-for-persist-wrapper
          reset-class=>true
        magix.forms.effect
          form-id=>save-content
          id=>filename
          type=>focus-and-select
      oncancel
        magix.viewport.clear-controls
          container=>helper-for-persist-wrapper
          reset-class=>true
        magix.viewport.clear-controls
          container=>editor-persist-wrapper
          reset-class=>true

