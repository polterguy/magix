

inspect=>@"creates the username magix.data helper events

creates [magix.data.save-username], [magix.data.load-username] 
and [magix.data.remove-username]

these active events are useful for storing user specific data 
in the database, since they append the currently logged in user 
to the [id] of their magix.data counterparts, and hence creates 
unique ids, according to the username of the currently logged 
in user, and hence serves as a user specific data-storage of 
persistent objects

active event will throw an exception if no user is currently 
logged in"


event
  name=>magix.data.save-username
  persist=>false
  inspect=>@"save a node with username id

saves a node to the database, appending the currently logged in 
user's username to the id given.  this is useful for saving data 
on a per user basis"
  code
    magix.web.get-session
      id=>magix.core.user
    if=>not-exist
      lhs=>[magix.web.get-session][value]
      code
        throw=>no user is currently logged in
    set=>[magix.data.save][id].Value
      value=>{0}-{1}
        =>[$][id].Value
        =>[magix.web.get-session][value][username].Value
    add=>[magix.data.save]
      value=>[$][value]
    magix.data.save


event
  name=>magix.data.load-username
  persist=>false
  inspect=>@"loads a node with username id

loads a node from the database, appending the currently logged in 
user's username to the id given.  this is useful for storing and 
retrieving data on a per user basis"
  code
    magix.web.get-session
      id=>magix.core.user
    if=>not-exist
      lhs=>[magix.web.get-session][value]
      code
        throw=>no user is currently logged in
    set=>[magix.data.load][id].Value
      value=>{0}-{1}
        =>[$][id].Value
        =>[magix.web.get-session][value][username].Value
    magix.data.load
    if=>exist
      lhs=>[magix.data.load][value]
      code
        add=>[$]
          value=>[magix.data.load][value]


event
  name=>magix.data.remove-username
  persist=>false
  inspect=>@"removes a node with username id

removes a node in the database, appending the currently logged in 
user's username to the id given.  this is useful for manipulating 
data on a per user basis"
  code
    magix.web.get-session
      id=>magix.core.user
    if=>not-exist
      lhs=>[magix.web.get-session][value]
      code
        throw=>no user is currently logged in
    set=>[magix.data.remove][id].Value
      value=>{0}-{1}
        =>[$][id].Value
        =>[magix.web.get-session][value][username].Value
    magix.data.remove

