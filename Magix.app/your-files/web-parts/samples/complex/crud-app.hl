panel=>paging-wrapper
  class=>span-2 left-10 bottom-1
  onfirstload
    magix.data.count
      prototype
        type=>magix.samples.todo
    if=>less-than
      lhs=>[magix.data.count][count].value
      rhs=>10
      code
        magix.forms.set-visible
          id=>paging-wrapper
          value=>false
  controls
    link-button=>previous
      class=>span-1
      value=>&lt;&lt;
      onclick
        magix.viewstate.get
          id=>magix.crud-app.paging-start
        if=>exist
          lhs=>[magix.viewstate.get][value]
          code
            set=>[magix.todo-grid.re-render][start].value
              value=>[magix.viewstate.get][value].value
        if=>not-equals
          lhs=>[magix.todo-grid.re-render][start].value
          rhs=>0
          code
            using=>magix.math
              subtract=>[magix.todo-grid.re-render][start]
                =>[magix.todo-grid.re-render][start].value
                =>10
              add=>[magix.todo-grid.re-render][end]
                =>[magix.todo-grid.re-render][start].value
                =>10
        else
          magix.viewport.show-message
            message=>you're already at the beginning
            color=>#ffaaaa
          stop
        set=>[magix.viewstate.set][value].value
          value=>[magix.todo-grid.re-render][start].value
        magix.viewstate.set
          id=>magix.crud-app.paging-start
        magix.todo-grid.re-render
          start=>0
          end=>10
    link-button=>next
      class=>span-1 last
      value=>&gt;&gt;
      onclick
        magix.viewstate.get
          id=>magix.crud-app.paging-start
        if=>not-exist
          lhs=>[magix.viewstate.get][value]
          code
            set=>[magix.viewstate.get][value].value
              value=>10
        else
          using=>magix.math
            add=>[magix.viewstate.get][value]
              =>[magix.viewstate.get][value].value
              =>10
        magix.data.count
          prototype
            type=>magix.samples.todo
        if=>less-than
          lhs=>[magix.viewstate.get][value].value
          rhs=>[magix.data.count][count].value
          code
            set=>[magix.todo-grid.re-render][start].value
              value=>[magix.viewstate.get][value].value
            using=>magix.math
              add=>[magix.todo-grid.re-render][end]
                =>[magix.todo-grid.re-render][start].value
                =>10
        else
          magix.viewport.show-message
            message=>you're already at the end
            color=>#ffaaaa
          stop
        set=>[magix.viewstate.set][value].value
          value=>[magix.todo-grid.re-render][start].value
        magix.viewstate.set
          id=>magix.crud-app.paging-start
        magix.todo-grid.re-render
          start=>0
          end=>10
dynamic=>dynamic-todo-items
  class=>span-20 last boxed-light-green air-padding bottom-1
  onfirstload
    magix.viewstate.set
      id=>magix.crud-app.paging-start
    magix.todo-grid.re-render
  events
    magix.todo-grid.re-render
      magix.forms.get-children-values
        id=>panel-edit-item
      for-each=>[magix.forms.get-children-values][values]
        _label
          label
            class=>span-10
            tag=>label
        set=>[@][_label][label][value].value
          value=>[.].name
        add=>[/][magix.forms.create-web-part][controls]
          value=>[@][_label][label]
      set=>[magix.forms.create-web-part][controls][>last][class].value
        value=>span-10 last
      if=>exist
        lhs=>[$][start].value
        code
          set=>[magix.data.load][start].value
            value=>[$][start].value
          set=>[magix.data.load][end].value
            value=>[$][end].value
      magix.data.load
        start=>0
        end=>10
        prototype
          type=>magix.samples.todo
      for-each=>[magix.data.load][objects]
        _panel
          panel
            class=>span-20 last line-item
        _item
        add=>[@][_item]
          value=>[.]
          children-only=>true
        for-each=>[/][magix.forms.get-children-values][values]
          _label
            label
              class=>span-10
          if=>not-exist
            lhs=>[@][..][_item][value][[.].name].value
            code
              set=>[@][..][..][_label][label][value].value
                value=>&nbsp;
          else
            set=>[@][..][_label][label][value].value
              value=>[@][..][..][_item][value][[.].name].value
          add=>[@][..][_panel][panel][controls]
            value=>[@][_label][label]
        set=>[@][_panel][panel][controls][>last][class].value
          value=>span-10 last no-code
        set=>[@][_panel][panel][controls][>last][tag].value
          value=>pre
        add=>[/][magix.forms.create-web-part][controls]
          value=>[@][_panel][panel]
      magix.forms.create-web-part
        form-id=>dynamic-todo-items
        container=>dynamic-todo-items
panel=>panel-edit-item
  class=>span-10 left-5 air-padding last boxed-light-green
  controls
    label=>label-0
      value=>header
      class=>span-10 last
      tag=>label
    text-box=>header
      class=>span-10 last
      placeholder=>header ...
    label=>label-1
      value=>body
      class=>span-10 last top-1
      tag=>label
    text-area=>body
      class=>span-10 last
      rows=>7
      placeholder=>body ...
    button=>save
      value=>save
      class=>span-2 left-8 last top-1
      onclick
        magix.forms.get-children-values
          id=>panel-edit-item
        add=>[magix.data.save][value]
          value=>[magix.forms.get-children-values][values]
          children-only=>true
        magix.data.save
          value
            type=>magix.samples.todo
        magix.forms.clear-children-values
          id=>panel-edit-item
        magix.viewport.show-message
          message=>item was saved
        magix.viewstate.get
          id=>magix.crud-app.paging-start
        if=>exist
          lhs=>[magix.viewstate.get][value]
          code
            set=>[magix.todo-grid.re-render][start].value
              value=>[magix.viewstate.get][value].value
            using=>magix.math
              add=>[magix.todo-grid.re-render][end]
                =>[magix.todo-grid.re-render][start].value
                =>10
        magix.todo-grid.re-render
          start=>0
          end=>10
        magix.data.count
          prototype
            type=>magix.samples.todo
        if=>equals
          lhs=>[magix.data.count][count].value
          rhs=>11
          code
            magix.forms.set-visible
              id=>paging-wrapper
              value=>true
