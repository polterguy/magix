Method:magix.execute

inspect=>@"Will show a screen of all the 
execute keywords in the system, and allow
the user to inspect them by clicking
them."

magix.forms.create-web-page
  container=>header
  html=>@"<h1>unit tests</h1>"
  form-id=>header
magix.admin.get-active-events
  begins-with=>magix.test.
set=>[magix.admin.get-active-events].Name
  value=>Data
for-each=>[Data][ActiveEvents]
  Tpl
    LinkButton
      CssClass=>span4 btn btn-small btn-left
      Text
      OnClick
        void.foo
          inspect
        set=>[2][value].Value
          value=>[0][inspect].Value
        magix.forms.set-value
          form-id=>form1
          id=>desc
          value
        set=>[4][value].Value
          value=>[0].Name
        magix.forms.set-value
          form-id=>form1
          id=>header
          value
        set=>[0][inspect]
        set=>[magix.code.node-2-code][JSON].Value
          value=>[0]
        magix.code.node-2-code
          JSON
        set=>[9][value].Value
          value=>[magix.code.node-2-code][code].Value
        magix.forms.set-value
          form-id=>form1
          id=>code
          value
        set=>[0][inspect].Name
          value=>inspect
  set=>[.ip][..][Tpl][LinkButton][OnClick][0].Name
    value=>[.].Value
  set=>[.ip][..][Tpl][LinkButton][Text].Value
    value=>[.].Value
  add=>[/][cf][controls][Panel][controls]
    value=>[.ip][..][Tpl][LinkButton]
set=>[cf][controls][Panel2].Name
  value=>Panel
set=>[cf].Name
  value=>magix.forms.create-form
cf
  form-id=>form1
  container=>content
  controls
    Panel2
      CssClass=>span5
      controls
        Label=>header
          Text=>name of unit test
          CssClass=>span4
          Tag=>strong
        Label=>desc
          CssClass=>span4
          Text=>@"description of your unit test.&nbsp;&nbsp;
click any of the unit tests to the right to inspect them"
        Label=>code
          CssClass=>span4
          Text=>code ...
          Tag=>pre
        Panel=>wrp
          CssClass=>input-append span3 offset2
          controls
            LinkButton=>run
              Text=>inspect
              CssClass=>btn btn-primary
              OnClick
                magix.forms.get-value
                  form-id=>form1
                  id=>code
                set=>[.ip][..][magix.admin.load-executor-code][code].Value
                  value=>[.ip][..][magix.forms.get-value][value].Value
                magix.admin.load-executor-code
                  code
            LinkButton=>run2
              Text=>run
              CssClass=>btn btn-primary
              OnClick
                magix.forms.get-value
                  form-id=>form1
                  id=>header
                set=>[.ip][..][evtName].Name
                  value=>[.ip][..][magix.forms.get-value][value].Value
                evtName
                magix.viewport.show-message
                  message=>success
                  color=>LightGreen
                  header=>finished
                  time=>500
            LinkButton=>run3
              Text=>run all
              CssClass=>btn btn-primary
              OnClick
                magix.tests.run
    Panel=>wrp
      CssClass=>span3
      controls
