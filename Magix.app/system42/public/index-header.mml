
<div class="span-22 last relative">
{{
system42.travel.change-login-box
  magix.session.get
    id=>magix.core.user
  if=>not-exist
    lhs=>[magix.session.get][value]
    code
      magix.forms.set-visible
        form-id=>header
        id=>loginWrp
        value=>true
      magix.forms.set-visible
        form-id=>header
        id=>logoutWrp
        value=>false
  else
    magix.forms.set-visible
      form-id=>header
      id=>loginWrp
      value=>false
    magix.forms.set-visible
      form-id=>header
      id=>logoutWrp
      value=>true

system42.travel.try-login
  set=>[magix.data.load][prototype][username].value
    value=>[$][username].value
  set=>[magix.data.load][prototype][pwd].value
    value=>[$][pwd].value
  magix.data.load
    prototype
      type=>magix.core.user
  if=>more-than
    lhs=>[magix.data.load][objects].count
    rhs=>0
    code
      add=>[@][magix.session.set][value]
        value=>[magix.data.load][objects][0][value]
        children-only=>true
      magix.session.set
        id=>magix.core.user
      if=>exist
        lhs=>[magix.data.load][objects][0][value][roles][admin]
        code
          magix.browser.redirect=>~?dashboard=
      else
        magix.viewport.execute-javascript
          script=>@"location.reload();"
  else
    magix.forms.set-value
      form-id=>header
      id=>error
      value=>(*) error
    magix.forms.set-focus
      form-id=>header
      id=>username
    magix.forms.select-all
      form-id=>header
      id=>username

lambda=>header
  oncreatecontrols
    _data
      label=>headerCtrl
        class=>span-14 top-2 bottom-1
        value=>magix illuminate - phosphorus
        tag=>h1
    magix.configuration.get-setting
      name=>magix.core.portal-name
    set=>[_data][label][value].value
      value=>[magix.configuration.get-setting][value].value
    add=>[$]
      value=>[_data][label]

panel=>loginWrp
  onfirstload
    system42.travel.change-login-box
  class=>span-8 top-1 last
  controls
    panel=>loginPnl
      class=>span-4 left-4 last
      default=>loginBtn
      controls
        label=>descr
          tag=>label
          value=>please login
        text-box=>username
          placeholder=>username ...
          class=>span-4
        text-box=>pwd
          placeholder=>password ...
          type=>password
          class=>span-4
        label=>error
          class=>error span-2
        button=>loginBtn
          value=>login
          class=>right span-2 last
          onclick
            magix.forms.get-value
              form-id=>header
              id=>username
            magix.forms.get-value
              form-id=>header
              id=>pwd
            set=>[system42.travel.try-login][username].value
              value=>[magix.forms.get-value][value].value
            set=>[system42.travel.try-login][pwd].value
              value=>[magix.forms.get-value:1][value].value
            system42.travel.try-login

panel=>logoutWrp
  class=>absolute top right span-4 top-1 z-1 last
  controls
    button=>logOut
      class=>span-4 large
      value=>logout
      onclick
        magix.session.set
          id=>magix.core.user
        magix.browser.redirect=>~
    lambda=>createLoadDashboard
      oncreatecontrols
        magix.session.get
          id=>magix.core.user
        if=>exist
          lhs=>[magix.session.get][value][roles][admin]
          code
            _lnk=>load_dashboard
              class=>span-4 text-right
            magix.web.get-parameter
              name=>dashboard
            if=>exist
              lhs=>[@][magix.web.get-parameter][value]
              code
                set=>[if][code][_lnk][href].value
                  value=>~
                set=>[if][code][_lnk][value].value
                  value=>website
            else
              set=>[if][code][_lnk][href].value
                value=>~?dashboard=
              set=>[if][code][_lnk][value].value
                value=>dashboard
            add=>[$]
              value=>[@][_lnk]
            set=>[$][_lnk].name
              value=>hyperlink
}}
</div>

