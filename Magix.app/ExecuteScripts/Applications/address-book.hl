
event:magix.execute

inspect=>@"A port of the famous Qt Address Book 
example. Demonstrates how to dynamically create a event,
raise events and events, set values of nodes and create
nodes, if branching, creating forms, partially dynamically,
partially by iterating and modifying the original form
node hierarchy, for-each, basic web controls, etc.
Also clearly demonstrates self-modifying code and
pretty advanced expressions"

event=>magix.demo.load-address
  code
    magix.data.load=>adr
    for-each=>[/][magix.data.load][object]
      set=>[/][2][2][5][controls][[/][2][2][5][controls].Count].Value
        value=>
      set=>[/][2][2][5][controls][item][CssClass].Value
        value=>span2
      set=>[/][2][2][5][controls][item][Text].Value
        value=>[.][first].Value
      if=>[/][2][2][5][controls][item][Text].Value==@""
        set=>[/][2][2][5][controls][item][Text].Value
          value=>&nbsp;
      set=>[/][2][2][5][controls][item].Name
        value=>Label
      set=>[/][2][2][5][controls][[/][2][2][5][controls].Count].Value
        value=>
      set=>[/][2][2][5][controls][item][CssClass].Value
        value=>span2
      set=>[/][2][2][5][controls][item][Text].Value
        value=>[.][last].Value
      if=>[/][2][2][5][controls][item][Text].Value==@""
        set=>[/][2][2][5][controls][item][Text].Value
          value=>&nbsp;
      set=>[/][2][2][5][controls][item].Name
        value=>Label
      set=>[/][2][2][5][controls][[/][2][2][5][controls].Count].Value
        value=>
      set=>[/][2][2][5][controls][item][CssClass].Value
        value=>span2
      set=>[/][2][2][5][controls][item][Text].Value
        value=>[.][phone].Value
      if=>[/][2][2][5][controls][item][Text].Value==@""
        set=>[/][2][2][5][controls][item][Text].Value
          value=>&nbsp;
      set=>[/][2][2][5][controls][item].Name
        value=>Label
      set=>[/][2][2][5][controls][[/][2][2][5][controls].Count].Value
        value=>
      set=>[/][2][2][5][controls][item][CssClass].Value
        value=>span2
      set=>[/][2][2][5][controls][item][Text].Value
        value=>[.][email].Value
      if=>[/][2][2][5][controls][item][Text].Value==@""
        set=>[/][2][2][5][controls][item][Text].Value
          value=>&nbsp;
      set=>[/][2][2][5][controls][item].Name
        value=>Label
    magix.forms.create-form
      container=>content
      form-id=>addressBook
      controls
        Panel
          CssClass=>span9
          controls
            Image
              ImageUrl=>media/images/magix-logo.png
              AlternateText=>Magix Illuminate Logo
              CssClass=>span2
            Label
              CssClass=>span6
              Text=>@"Welcome to the Magix Illuminate version of Address Book.
Address Book is one of the main examples from Qt. The application
basically shows how you can store, update and retreieve objects,
some basic forms manipulation, dynamic creation of nodes according
to contents in stored objects, and some other things."
        Label
          CssClass=>span2
          Text=>First Name
          Tag=>strong
        Label
          CssClass=>span2
          Text=>Last name
          Tag=>strong
        Label
          CssClass=>span2
          Text=>Phone no
          Tag=>strong
        Label
          CssClass=>span2
          Text=>Email
          Tag=>strong
        Panel=>rep
          CssClass=>span9
        Panel=>new
          CssClass=>span9
          controls
            Label
              Text=>First Name
              CssClass=>span2
            Label
              Text=>Last Name
              CssClass=>span2
            Label
              Text=>Phone
              CssClass=>span2
            Label
              Text=>Email
              CssClass=>span2
            TextBox=>first
              PlaceHolder=>First Name ...
              CssClass=>span2
            TextBox=>last
              PlaceHolder=>Last ...
              CssClass=>span2
            TextBox=>phone
              PlaceHolder=>Phone ...
              CssClass=>span2
            TextBox=>email
              PlaceHolder=>Email ...
              CssClass=>span2
            Button=>save
              CssClass=>span2
              Text=>Save
              OnClick
                magix.data.load=>adr
                magix.forms.get-value
                  id=>first
                  form-id=>addressBook
                set=>[.ip][..][0][object][[.ip][..][0][object].Count][first].Value
                  value=>[.ip][..][1][value].Value
                magix.forms.get-value
                  id=>last
                  form-id=>addressBook
                set=>[.ip][..][0][object][item][last].Value
                  value=>[.ip][..][3][value].Value
                magix.forms.get-value
                  id=>phone
                  form-id=>addressBook
                set=>[.ip][..][0][object][item][phone].Value
                  value=>[.ip][..][5][value].Value
                magix.forms.get-value
                  id=>email
                  form-id=>addressBook
                set=>[.ip][..][0][object][item][email].Value
                  value=>[.ip][..][7][value].Value
                set=>[.ip][..][0][object][item].Name
                  value=>Item
                set=>[.ip][..][11][object]
                  value=>[.ip][..][0][object]
                magix.data.save=>adr
                  object
                raise=>magix.demo.load-address
raise=>magix.demo.load-address
