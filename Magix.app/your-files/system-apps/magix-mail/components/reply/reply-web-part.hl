

inspect=>@"web part for replying to an email

creates the web part necessary to reply to an email"


using=>magix.data
  load
    id=>[$][email-id].value

index-of=>[using][load][value][subject].value
  what=>re:
  case=>false
if=>not-exist
  lhs=>[index-of][result]
  code
    set=>[using][load][value][subject].value
      value=>Re: {0}
        =>[using][load][value][subject].value

if=>not-exist
  lhs=>[using][load][value][body][plain]
  rhs=>true
  code
    using=>magix.web
      strip-html
        value=>[using][load][value][body][html].value
    set=>[using][load][value][body].value
      value=>[@][using][strip-html][result].value
else
  set=>[using][load][value][body].value
    value=>[using][load][value][body][plain].value

replace=>[using][load][value][body].value
  what=>\n
  with=>\n>
set=>[using][load][value][body].value
  value=>>{0}
    =>[using][load][value][body].value

add=>[@][execute-script][params][email]
  values=>[using][load][value]
add=>[@][execute-script][params]
  values=>[$]
set=>[@][execute-script][params][script-file]
set=>[@][execute-script][params][script-directory]
set=>[@][execute-script][params][email-id].value
  value=>[$][email-id].value
execute-script
  file=>your-files/system-apps/magix-mail/components/reply/reply-no-html-web-part.hl




