

inspect=>@"file run upon every initial page load request, 
such as refreshing, or first load from user/machine, etc.&nbsp;&nbsp;
will create the common header, with the login box and logout logic.&nbsp;&nbsp;
then depending upon if it is a user logged in, belinging to 
an admin role, the desktop will be loaded, or the if the user is 
not an admin, then the run-page.hl script will be executed"

magix.web.get-parameter
  name=>dashboard

// checking to see if we're in dashboard
if=>exist
  lhs=>[magix.web.get-parameter][value]
  code

    // create default settings, if they haven't been created already, per user
    magix.admin.create-default-settings-per-user

    // stuffing in our default back-end css file, or back-end 'skin'
    magix.data.load
      id=>magix.admin.settings.default-back-end-css-file
    set=>[@][magix.viewport.include-client-file][file].Value
      value=>[@][magix.data.load][value][file].Value
    magix.viewport.include-client-file
      type=>css

    // loading our login/logout box
    magix.forms.create-mml-web-part
      mml-file=>system42/public/index-header.mml
      container=>header
      form-id=>header
      class=>span-22 last

    // checking to see if user is logged in, and if he is 'admin'
    magix.session.get
      id=>magix.core.user
    if=>exist
      lhs=>[@][magix.session.get][value][roles][admin]
      code

        // user is admin, loading dashboard
        magix.forms.create-mml-web-part
          mml-file=>system42/private/dashboard/menu.mml
          container=>menu
          form-id=>admin-menu
          class=>span-5
        execute-script
          file=>system42/private/dashboard/desktop.hl

        // checking to see if we should show help by default
        magix.data.load-username
          id=>magix.admin.setting.show-help
        if=>equals
          lhs=>[@][magix.data.load-username][value][value].Value
          rhs=>true
          code
            execute-script
              file=>system42/private/help/start-help.hl
    else
      magix.forms.create-mml-web-part
        container=>content1
        form-id=>description
        class=>span-22 last
        mml=>@"
<p>sorry, you do not have sufficient access rights here.&nbsp;&nbsp;please login, or go back to where you came from</p>"
else

  // not in dashboard, executing "load-index-page" active event, which is the public tie-down active event
  // for the default front-end logic
  _event=>magix.tiedown.load-index-page
  magix.data.load
    id=>magix.tiedown.default-index-page-event
  if=>exist
    lhs=>[@][magix.data.load][value]
    code
      set=>[@][..][..][_event].Value
        value=>[@][..][..][magix.data.load][value][event].Value
  set=>[@][_raise].Name
    value=>[@][_event].Value
  _raise

