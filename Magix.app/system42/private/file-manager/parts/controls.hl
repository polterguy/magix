

inspect=>@"contains the controls for the file manager

contains all controls for the file-manager.hl web part"


dynamic=>file-wrp
  class=>span-20 last file-manager
  onfirstload
    magix.file-manager.show-files
    magix.file-manager.refresh-toolbar
text-box=>selected-object
  class=>span-6 top-1
  placeholder=>selected object ...
  onenterpressed
    magix.viewport.show-message
      message=>choose one of the buttons on the toolbar to initiate an action
      color=>#ffaaaa
      time=>3000
panel=>toolbar-wrapper
  class=>btn-group span-20 right last top-1 clear
  controls
    button=>back
      class=>span-2
      disabled=>true
      value=>back
      onclick
        magix.file-manager.go-back
    button=>open
      class=>span-2
      disabled=>true
      value=>open
      onclick
        magix.file-manager.open-selected-directory
    button=>rename
      class=>span-2
      disabled=>true
      value=>rename
      onclick
        magix.file-manager.rename-object
    button=>create
      class=>span-2
      disabled=>false
      title=>creates a new directory
      value=>create
      onclick
        magix.file-manager.create-directory
    button=>delete
      class=>span-2
      disabled=>true
      value=>delete
      onclick
        magix.file-manager.delete-object
    button=>copy
      class=>span-2
      disabled=>true
      value=>copy
      onclick
        magix.file-manager.copy-object
    button=>cut
      class=>span-2
      disabled=>true
      value=>cut
      onclick
        magix.file-manager.cut-object
    button=>paste
      class=>span-2 last
      disabled=>true
      value=>paste
      onclick
        magix.file-manager.paste-object
    button=>download
      class=>span-2
      disabled=>true
      value=>download
      onclick
        magix.viewstate.get
          id=>magix.file-manager.selected-object
        set=>[magix.file.download][file].value
          value=>[magix.viewstate.get][value][object].value
        magix.file.download
    button=>select
      class=>span-2
      disabled=>true
      value=>select
      onclick
        magix.viewstate.get
          id=>magix.file-manager.onselect
        magix.viewstate.get
          id=>magix.file-manager.selected-object
        if=>equals
          lhs=>[magix.viewstate.get:1][value][type].value
          rhs=>file
          code
            set=>[lambda][file].value
              value=>[magix.viewstate.get:1][value][object].value
        else
          magix.viewstate.get
            id=>magix.file-manager.select-non-existent
          magix.viewstate.get
            id=>magix.file-manager.current-directory
          if=>equals
            lhs=>[@][magix.viewstate.get][value].value
            rhs=>true
            code
              magix.forms.get-value
                form-id=>file-manager
                id=>selected-object
              if=>equals
                lhs=>[@][magix.forms.get-value][value].value
                rhs=>
                code
                  magix.viewport.show-message
                    message=>you need to supply a file name into the text-box
                    color=>#ffaaaa
                    time=>5000
                  stop
              else
                if=>equals
                  lhs=>[@][..][..][..][magix.viewstate.get:1][value].value
                  rhs=>
                  and=>not-equals
                    lhs=>[@][..][magix.forms.get-value][value].value
                    rhs=>[magix.viewstate.get:1][value][object].value
                  or=>not-exist
                    lhs=>[@][..][..][..][magix.viewstate.get:1][value]
                    and=>not-equals
                      lhs=>[@][..][magix.forms.get-value][value].value
                      rhs=>[magix.viewstate.get:1][value][object].value
                  code
                    set=>[lambda][file].value
                      value=>[@][..][..][..][magix.forms.get-value][value].value
                else-if=>not-equals
                  lhs=>{0}/{1}
                    =>[@][..][..][..][magix.viewstate.get:1][value].value
                    =>[@][..][magix.forms.get-value][value].value
                  rhs=>[magix.viewstate.get:1][value][object].value
                  and=>not-equals
                    lhs=>[@][..][magix.forms.get-value][value].value
                    rhs=>[magix.viewstate.get:1][value][object].value
                  code
                    set=>[lambda][file].value
                      value=>{0}/{1}
                        =>[@][..][..][..][..][..][magix.viewstate.get:1][value].value
                        =>[@][..][..][..][magix.forms.get-value][value].value
                else
                  magix.viewport.show-message
                    message=>you need to supply a file name into the text-box
                    color=>#ffaaaa
                    time=>5000
                  stop
          else
            magix.viewport.show-message
              message=>you need to select an existing file
              color=>#ffaaaa
              time=>5000
            stop
        add=>[_exe]
          values=>[magix.viewstate.get][value]
        lambda=>[_exe]
        _exe
    uploader=>file-manager-uploader
      class=>mux-file-uploader
      directory=>
      onuploaded
        magix.file-manager.show-files
    button=>cancel-button
      class=>span-2
      value=>cancel
      onclick
        magix.viewstate.get
          id=>magix.file-manager.oncancel
        add=>[execute]
          values=>[magix.viewstate.get][value]
        execute

