

inspect=>@"contains add controls active events for form builder

used to create the form builder.  contains the active events necessary 
to create the add controls parts of the form builder"

// creates the [magix.ide.add-control] code for adding controls to design surface
magix.ide.add-control-to-surface
  magix.forms.get-value
    form-id=>add-control
    id=>select-add-where
  set=>[$][magix.ide.add-control][where][position].value
    value=>[magix.forms.get-value][value].value
  set=>[$][magix.ide.add-control][control][0].name
    value=>[$][value].value
  set=>[$][magix.ide.add-control][auto-select].value
    value=>true

  // decorating returned control with default properties
  with=>[$][magix.ide.add-control][control][0]
    switch=>[$][0].value
      case=>button
        set=>[value].value
          value=>your button
        set=>[class].value
          value=>span-3
      case=>hyperlink
        set=>[value].value
          value=>your link
        set=>[class].value
          value=>span-4
        set=>[href].value
          value=>http://magixilluminate.com
      case=>img
        set=>[alt].value
          value=>magix illuminate logo
        set=>[src].value
          value=>media/images/icons/start-button.png
        set=>[class].value
          value=>span-4 last
      case=>label
        set=>[value].value
          value=>your label
        set=>[class].value
          value=>span-3
      case=>link-button
        set=>[value].value
          value=>your button
        set=>[class].value
          value=>span-3
      case=>literal
        set=>[text].value
          value=>your html
      case=>select
        set=>[class].value
          value=>span-4
      case=>text-area
        set=>[class].value
          value=>span-4
      case=>text-box
        set=>[class].value
          value=>span-4
      case=>panel
        set=>[class].value
          value=>span-8 min-height-5 boxed-light-green
      case=>dynamic
        set=>[class].value
          value=>span-8 min-height-5 boxed-light-green
      case=>check-box
        set=>[class].value
          value=>span-1
      case=>radio
        set=>[class].value
          value=>span-1
      case=>wysiwyg
        set=>[class].value
          value=>span-14

  // returning add control code
  set=>[$][0]

// raises the active event that changes the visibility of the "clear all" buttons according to
// if the form has any controls at all
magix.ide.surface-changed
  magix.ide.set-clear-all-control-enabled

// changes the enabled property of the "clear all" button according to if the form has controls or not
magix.ide.set-clear-all-control-enabled
  magix.ide.get-form
  if=>equals
    lhs=>[magix.ide.get-form].count
    rhs=>0
    code
      magix.forms.set-style
        form-id=>add-control
        id=>panel-position
        key=>display
        value=>none
  else
    magix.forms.set-style
      form-id=>add-control
      id=>panel-position
      key=>display
      value=>block

