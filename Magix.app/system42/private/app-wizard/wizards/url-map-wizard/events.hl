

inspect=>@"contains the controls for the url map wizard

contains the controls for the web part that allows the user to create an 
active event that maps http get parameters to active events"


magix.url-map.add-value_onclick
  magix.forms.get-value
    form-id=>wizard
    id=>active-event
  magix.forms.get-value
    form-id=>wizard
    id=>parameter-value
  if=>equals
    lhs=>[magix.forms.get-value:0][value].value
    rhs=>
    or=>equals
      lhs=>[magix.forms.get-value:1][value].value
      rhs=>
    code
      magix.viewport.show-message
        message=>you must declare both an active event and a value for your http get parameter
        color=>#ffaaaa
        time=>5000
      magix.forms.effect
        form-id=>wizard
        id=>active-event
        type=>focus-and-select
      stop
  magix.viewstate.get
    id=>magix.url-map.items
  if=>exist
    lhs=>[magix.viewstate.get][value]
    code
      add=>[magix.viewstate.set]
        value=>[magix.viewstate.get][value]
  add=>[magix.viewstate.set][value]
    value=>[magix.forms.get-value:1][value].value
      value=>[magix.forms.get-value:0][value].value
  magix.viewstate.set
    id=>magix.url-map.items
  magix.url-map.databind-items
  magix.forms.set-value
    form-id=>wizard
    id=>active-event
  magix.forms.set-value
    form-id=>wizard
    id=>parameter-value
  magix.forms.effect
    type=>focus-and-select
    form-id=>wizard
    id=>active-event

magix.url-map.create_onclick
  magix.forms.get-value
    form-id=>wizard
    id=>parameter-name
  magix.forms.get-value
    form-id=>wizard
    id=>active-event-name
  magix.viewstate.get
    id=>magix.url-map.items
  if=>equals
    lhs=>[magix.forms.get-value:0][value].value
    rhs=>
    or=>equals
      lhs=>[magix.forms.get-value:1][value].value
      rhs=>
    or=>not-exist
      lhs=>[magix.viewstate.get][value]
    code
      magix.viewport.show-message
        message=>you must give me an active event name, a parameter name, and a list of values to active events
        color=>#ffaaaa
        time=>7000
      stop
  _event
    event
      name=>{active-event-name}
      inspect=>@"url map wizard active event

will map an http get parameter to an active event according to a list of 
values to active events.  the name of the http get parameter that is used 
here is '{0}', and its different mapping values can be found in the [_items] 
collection of the code block of the active event"
        =>{parameter}
      code
        _items
        magix.web.get-parameter
          name=>{parameter}
        if=>exist
          lhs=>[magix.web.get-parameter][value]
          and=>exist
            lhs=>[_items][[magix.web.get-parameter][value].value]
          code
            set=>[@][_invoke].name
              value=>[_items][[magix.web.get-parameter][value].value].value
            _invoke
  add=>[_event][**_items]
    value=>[magix.viewstate.get][value]
    children-only=>true
  iterate=>[_event]
    if=>equals
      lhs=>[.].value
      rhs=>{active-event-name}
      code
        set=>[.].value
          value=>[/][magix.forms.get-value:1][value].value
    else-if=>equals
      lhs=>[.].value
      rhs=>{parameter}
      code
        set=>[.].value
          value=>[/][magix.forms.get-value:0][value].value
  lambda=>[_event]
  magix.viewport.show-message
    message=>your active event was created
  using=>magix.forms
    set-info
      form-id=>wizard
      id=>edit-event
      value=>[magix.forms.get-value:1][value].value
    set-visible
      form-id=>wizard
      id=>edit-event
      value=>true

magix.url-map.databind-items
  magix.viewstate.get
    id=>magix.url-map.items
  if=>equals
    lhs=>[magix.viewstate.get][value].count
    rhs=>0
    or=>not-exist
      lhs=>[magix.viewstate.get][value]
    code
      magix.viewport.clear-controls
        container=>url-parameters
        reset-class=>true
      stop
  _items
    panel
      class=>span-15 last
  for-each=>[magix.viewstate.get][value]
    _item
      label
        class=>span-3
      label
        class=>span-10
      link-button
        class=>span-2 last text-right
        value=>delete
        onclick
          magix.viewstate.get
            id=>magix.url-map.items
          set=>[magix.viewstate.get][value][[$][info].value]
          add=>[magix.viewstate.set][value]
            value=>[magix.viewstate.get][value]
            children-only=>true
          magix.viewstate.set
            id=>magix.url-map.items
          magix.url-map.databind-items
    set=>[@][_item][label:0][value].value
      value=>[.].name
    set=>[@][_item][label:1][value].value
      value=>[.].value
    set=>[@][_item][link-button][info].value
      value=>[.].name
    add=>[/][_items][panel][controls]
      value=>[@][_item]
      children-only=>true
  using=>magix.forms
    create-web-part
      form-id=>url-items
      class=>span-15 last bottom-1
      container=>url-parameters
      controls=>[_items]

