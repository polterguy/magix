

inspect=>@"creates the compose email web part

creates the compose email web part in content3"


// cleaning out recipients
magix.viewstate.set
  id=>magix.email.recipients

// including css files
magix.viewport.include-client-file
  type=>css
  file=>media/back-end/wysihtml.css
magix.viewport.include-client-file
  type=>css
  file=>media/back-end/file-uploader.css

// changing class of inbox web part such that it won't go left
magix.forms.set-class
  id=>content4
  value=>span-18 last boxed air-padding bottom-1 left-4

magix.forms.create-web-part
  container=>content3
  class=>span-18 last boxed air-padding bottom-1
  form-id=>composer
  events-file=>your-files/system-apps/magix-mail/components/composer/events.hl
  controls
    label
      class=>span-16 bottom-1
      value=>compose email
      tag=>h3
    button=>add-recipient
      class=>span-2 last bottom-1
      value=>+
      title=>add recipient
      onclick
        magix.email.add-recipient
    dynamic=>dynamic-add-recipient
    dynamic=>dynamic-recipients
      onfirstload
        magix.email.create-recipient-list
    text-box=>compose-subject
      class=>span-18 last bottom-1
      placeholder=>subject ...
      onfirstload
        magix.forms.effect
          form-id=>composer
          id=>compose-subject
          type=>focus-and-select
    text-area=>compose-body
      class=>span-18 last bottom-1
      rows=>15
      placeholder=>body ...
    dynamic=>compose-attachments
    panel
      class=>btn-group span-4 last right
      controls
        button=>send
          class=>span-2
          value=>send
          onclick
            magix.viewstate.get=>recipients
              id=>magix.email.recipients
            if=>not-exist
              lhs=>[magix.viewstate.get=>recipients][value][to]
              or=>equals
                lhs=>[magix.viewstate.get=>recipients][value][to].count
                rhs=>0
              code
                magix.viewport.show-message
                  message=>your email must have at least one to recipient.&nbsp;&nbsp;click the plus button to add recipients to your email
                  color=>#ffaaaa
                stop
            magix.viewstate.get=>attachments
              id=>magix.email.attachments
            using=>magix.forms
              get-value=>subject
                form-id=>composer
                id=>compose-subject
              get-value=>body
                form-id=>composer
                id=>compose-body
            magix.data.load-username=>smtp-settings
              id=>magix.smtp.settings
            set=>[magix.smtp.send-message][from].value
              value=>[magix.data.load-username=>smtp-settings][value][email].value
            set=>[magix.smtp.send-message][from][display-name].value
              value=>[magix.data.load-username=>smtp-settings][value][display-name].value
            set=>[magix.data.load-username=>smtp-settings][value][email]
            add=>[magix.smtp.send-message]
              values=>[magix.data.load-username=>smtp-settings][value]

            // to recipients
            for-each=>[magix.viewstate.get=>recipients][value][to]
              add=>[/][magix.smtp.send-message][to]
                value=>
                  value=>[.].value
              if=>exist
                lhs=>[.][display-name].value
                code
                  add=>[/][magix.smtp.send-message][to][>last]
                    value=>display-name
                      value=>[.][display-name].value

            // cc recipients
            for-each=>[magix.viewstate.get=>recipients][value][cc]
              add=>[/][magix.smtp.send-message][cc]
                value=>
                  value=>[.].value
              if=>exist
                lhs=>[.][display-name].value
                code
                  add=>[/][magix.smtp.send-message][cc][>last]
                    value=>display-name
                      value=>[.][display-name].value

            // bcc recipients
            for-each=>[magix.viewstate.get=>recipients][value][bcc]
              add=>[/][magix.smtp.send-message][bcc]
                value=>
                  value=>[.].value
              if=>exist
                lhs=>[.][display-name].value
                code
                  add=>[/][magix.smtp.send-message][bcc][>last]
                    value=>display-name
                      value=>[.][display-name].value

            // subject and body
            set=>[magix.smtp.send-message][subject].value
              value=>[**get-value=>subject][value].value
            set=>[magix.smtp.send-message][body][plain].value
              value=>[**get-value=>body][value].value

            // attachments
            for-each=>[magix.viewstate.get=>attachments][value]
              add=>[/][magix.smtp.send-message][attachments]
                value=>
                  value=>[.].value

            // retrieving username
            magix.session.get
              id=>magix.core.user

            // saving sent email
            set=>[magix.data.save][value][username].value
              value=>[magix.session.get][value][username].value
            set=>[magix.data.save][value][from]
              value=>[magix.smtp.send-message][from]
            set=>[magix.data.save][value][date].value
              value=>[:magix.date.now][value].value
            add=>[magix.data.save][value][to]
              values=>[magix.smtp.send-message][to]
            if=>exist
              lhs=>[magix.smtp.send-message][cc]
              code
                add=>[magix.data.save][value][cc]
                  values=>[magix.smtp.send-message][cc]
            if=>exist
              lhs=>[magix.smtp.send-message][bcc]
              code
                add=>[magix.data.save][value][bcc]
                  values=>[magix.smtp.send-message][bcc]
            set=>[magix.data.save][value][subject].value
              value=>[**get-value=>subject][value].value
            set=>[magix.data.save][value][body][plain].value
              value=>[**get-value=>body][value].value
            magix.data.save
              value
                type=>magix.email.message
                read=>true
                folder=>sent

            // sending email
            magix.smtp.send-message

            // showing user feedback, and clearing viewport container
            magix.viewport.show-message
              message=>email successfully sent
              color=>#aaffaa
            magix.viewport.clear-controls
              container=>content3
              reset-class=>true

            // resettings class back on inbox
            magix.forms.set-class
              id=>content4
              value=>span-18 last boxed air-padding bottom-1

        button=>cancel
          class=>span-2 last
          value=>cancel
          onclick
            // changing class of inbox web part such that it won't go left
            magix.forms.set-class
              id=>content4
              value=>span-18 last boxed air-padding bottom-1
            magix.viewport.clear-controls
              container=>content3
              reset-class=>true
    lambda=>create-file-uploader
      oncreatecontrols
        _lb
          uploader=>attachments-uploader
            class=>mux-file-uploader
            onuploaded
              magix.viewstate.get
                id=>magix.email.attachments
              split=>[$][filename].value
                what=>/
              if=>exist
                lhs=>[magix.viewstate.get][value]
                code
                  if=>exist
                    lhs=>[magix.viewstate.get][value][[split][result][>last].value]
                    code
                      magix.viewport.show-message
                        message=>file already attached to email
                        color=>#ffaaaa
                      stop
                  add=>[magix.viewstate.set][value]
                    values=>[magix.viewstate.get][value]
              add=>[magix.viewstate.set][value]
                value=>[split][result][>last].value
                  value=>[$][filename].value
              magix.viewstate.set
                id=>magix.email.attachments
              magix.email.create-attachment-list
              magix.forms.effect
                form-id=>composer
                id=>compose-subject
                type=>focus-and-select

        // getting atttachment directory
        magix.data.load
          id=>magix.email.attachments-directory
        magix.session.get
          id=>magix.core.user
        set=>[_attachment-directory].value
          value=>{0}/{1}
            =>[magix.data.load][value][directory].value
            =>[magix.session.get][value][username].value
        _attachment-directory

        // making sure attachment directory exist
        using=>magix.file
          directory-exist
            directory=>[_attachment-directory].value
        if=>not-equals
          lhs=>[using][directory-exist][value].value
          rhs=>true
          code
            using=>magix.file
              create-directory
                directory=>[_attachment-directory].value

        // setting uploader's directory to user's attachment directory
        set=>[_lb][uploader][directory].value
          value=>[_attachment-directory].value
        add=>[$]
          values=>[_lb]

