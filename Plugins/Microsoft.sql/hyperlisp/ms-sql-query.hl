

inspect=>@"opens up the microsoft sql 
server query tool"


magix.viewport.clear-controls
  container=>content2
magix.forms.create-web-part
  container=>content1
  class=>span-17 last
  form-id=>sql

  events

    microsoft.sql.populate-selector
      magix.data.load
        prototype
          type=>microsoft.sql.connection
      for-each=>[magix.data.load][objects]
        _con
        set=>[@][0].Name
          value=>[.].Name
        set=>[@][0].Value
          value=>[.][connection].Value
        add=>[/][magix.forms.set-values][values]
          value=>[@][0]
      magix.forms.set-values
        form-id=>sql
        id=>ddlConnection
        values
          _default=>choose connection ...

    microsoft.sql.test-connection
      set=>[try][code][microsoft.sql.select][connection].Value
        value=>[$][connection].Value
      try
        code
          microsoft.sql.select
            sql=>@"use master"
          set=>[$][success].Value
            value=>true
        catch
          set=>[@][magix.viewport.show-message][message].Value
            value=>[@][exception].Value
          set=>[$][success].Value
            value=>false
          magix.viewport.show-message
            color=>#ffaaaa
            time=>5000

    microsoft.sql.open-selected-connection
      magix.forms.get-value
        form-id=>sql
        id=>ddlConnection
      set=>[magix.data.load][id].Value
        value=>[magix.forms.get-value][value].Value
      magix.data.load
      set=>[microsoft.sql.test-connection][connection].Value
        value=>[magix.data.load][objects][0][connection].Value
      magix.viewport.clear-controls
        container=>table-list
      magix.viewport.clear-controls
        container=>column-list
      magix.viewport.clear-controls
        container=>select-query
      magix.viewport.clear-controls
        container=>content2
      microsoft.sql.test-connection
      if=>equals
        lhs=>[microsoft.sql.test-connection][success].Value
        rhs=>false
        code
          magix.viewport.show-message
            message=>that is not a valid connection
            color=>#ffaaaa
            time=>5000
          stop
      set=>[magix.forms.set-value][value].Value
        value=>[magix.data.load][objects][0][connection].Value
      magix.forms.set-value
        form-id=>sql
        id=>txtNewConnection
      set=>[microsoft.sql.select][connection].Value
        value=>[magix.data.load][objects][0][connection].Value
      microsoft.sql.select
        sql=>select distinct * from information_schema.tables where table_type='base table'
      for-each=>[microsoft.sql.select][result]
        _lb
          onclick
            magix.viewport.clear-controls
              container=>content2
            magix.forms.get-value
              form-id=>sql
              id=>txtNewConnection
            set=>[microsoft.sql.select][connection].Value
              value=>[magix.forms.get-value][value].Value
            set=>[microsoft.sql.select][sql].Value
              value=>@"select * from information_schema.columns where table_name = N'{0}'"
                v0=>[$][info].Value
            microsoft.sql.select
            
            set=>[microsoft.sql.select:1][connection].Value
              value=>[magix.forms.get-value][value].Value
            set=>[microsoft.sql.select:1][sql].Value
              value=>@"select * from 
    information_schema.table_constraints tab, 
    information_schema.constraint_column_usage col 
where 
    col.constraint_name = tab.constraint_name
    and col.table_name = tab.table_name
    and constraint_type = 'primary key'
    and col.table_name = N'{0}'"
                v0=>[$][info].Value
            microsoft.sql.select
            _pks
            for-each=>[microsoft.sql.select][result]
              _lb
              set=>[@][_lb][value].Value
                value=>[.][COLUMN_NAME].Value
              set=>[@][_lb][class].Value
                value=>span-5 clear table-column
              _pk
              set=>[@][_pk].Value
                value=>[.][COLUMN_NAME].Value
              for-each=>[@][..][microsoft.sql.select:1][result]
                if=>equals
                  lhs=>[.][COLUMN_NAME].Value
                  rhs=>[@][..][_pk].Value
                  code
                    set=>[@][..][..][..][_pk].Value
                      value=>true
                    add=>[@][..][..][..][..][_pks]
                      value=>[.][COLUMN_NAME].Value
                        value
                    stop
              if=>equals
                lhs=>[@][_pk].Value
                rhs=>true
                code
                  set=>[@][..][..][_lb][class].Value
                    value=>{0} primary-key-column
                      v0=>[@][..][..][_lb][class].Value
                  set=>[@][..][..][_lb][tip].Value
                    value=>primary key
              else
                set=>[@][..][_lb][tip]
              add=>[/][magix.forms.create-web-part:0][controls]
                value=>[@][_lb]
              set=>[/][magix.forms.create-web-part:0][controls][_lb].Name
                value=>label

              set=>[@][_lb][value].Value
                value=>[.][DATA_TYPE].Value
              set=>[@][_lb][class].Value
                value=>span-2
              add=>[/][magix.forms.create-web-part:0][controls]
                value=>[@][_lb]
              set=>[/][magix.forms.create-web-part:0][controls][_lb].Name
                value=>label

              set=>[@][_lb][value].Value
                value=>@"&nbsp;{0}"
                  v0=>[.][CHARACTER_MAXIMUM_LENGTH].Value
              set=>[@][_lb][class].Value
                value=>span-3
              add=>[/][magix.forms.create-web-part:0][controls]
                value=>[@][_lb]
              set=>[/][magix.forms.create-web-part:0][controls][_lb].Name
                value=>label
              if=>equals
                lhs=>[.][IS_NULLABLE].Value
                rhs=>NO
                code
                  set=>[@][..][..][_lb][value].Value
                    value=>not nullable
                  set=>[@][..][..][_lb][class].Value
                    value=>span-2
                  add=>[/][magix.forms.create-web-part:0][controls]
                    value=>[@][..][..][_lb]
                  set=>[/][magix.forms.create-web-part:0][controls][_lb].Name
                    value=>label
              else
                set=>[@][..][_lb][value].Value
                  value=>nullable
                set=>[@][..][_lb][class].Value
                  value=>span-2
                add=>[/][magix.forms.create-web-part:0][controls]
                  value=>[@][..][_lb]
                set=>[/][magix.forms.create-web-part:0][controls][_lb].Name
                  value=>label
            add=>[@][magix.viewport.set-viewstate=>microsoft.sql.current-primary-keys][value]
              value=>[@][_pks]
              children-only=>true
            magix.viewport.set-viewstate=>microsoft.sql.current-primary-keys
            set=>[magix.forms.create-web-part:0][controls][label:1][value].Value
              value=>[$][info].Value
            set=>[@][magix.viewport.set-viewstate=>microsoft.sql.current-table][value].Value
              value=>[$][info].Value
            magix.viewport.set-viewstate=>microsoft.sql.current-table
            magix.forms.create-web-part
              container=>column-list
              forms-id=>column-list
              class=>span-17 last
              controls
                label
                  tag=>hr
                label
                  tag=>h3
            set=>[@][**text-area=>sqlQuery][value].Value
              value=>@"select * from [{0}]"
                v0=>[$][info].Value
            magix.forms.create-web-part
              container=>select-query
              form-id=>select-query
              class=>span-17 last
              controls
                text-area=>sqlQuery
                  class=>span-17 top-1 last
                  rows=>10
                  place-holder=>type your sql query here ...
                button=>btnRunQuery
                  class=>span-3 btn-large right last
                  value=>run
                  onclick
                    microsoft.sql.run-query
            magix.forms.effect
              type=>fade-in
              form-id=>select-query
              id=>select-query
              time=>500
              joined
                e0
                  type=>roll-down
        set=>[@][_lb][value].Value
          value=>[.][TABLE_NAME].Value
        set=>[@][_lb][info].Value
          value=>[.][TABLE_NAME].Value
        set=>[@][_lb][class].Value
          value=>span-5
        add=>[/][magix.forms.create-web-part][controls]
          value=>[@][_lb]
        set=>[/][magix.forms.create-web-part][controls][_lb].Name
          value=>link-button

      magix.forms.create-web-part
        class=>span-17 last
        container=>table-list
        form-id=>table-list
        controls
          label=>tblHeader
            tag=>h3
            class=>top-1 left-6
            value=>list of tables
      magix.forms.effect
        type=>fade-in
        form-id=>table-list
        id=>table-list
        time=>500
        joined
          e0
            type=>roll-down

    microsoft.sql.close-selected-connection
      magix.forms.set-value
        form-id=>sql
        id=>txtNewConnection
        value=>
      magix.viewport.clear-controls
        container=>table-list
      magix.viewport.clear-controls
        container=>column-list
      magix.viewport.clear-controls
        container=>select-query
      magix.viewport.clear-controls
        container=>content2

    microsoft.sql.run-query
      magix.forms.get-value
        form-id=>select-query
        id=>sqlQuery
      magix.forms.get-value
        form-id=>sql
        id=>txtNewConnection
      index-of=>[magix.forms.get-value][value].Value
        what=>select
      if=>less-than
        lhs=>[index-of][result][0].Value
        rhs=>10
        and=>exist
          lhs=>[index-of][result]
        code
          set=>[@][try][code][microsoft.sql.select][sql].Value
            value=>[magix.forms.get-value:0][value].Value
          set=>[@][try][code][microsoft.sql.select][connection].Value
            value=>[magix.forms.get-value:1][value].Value
          try
            code
              if=>exist
                lhs=>[$][start]
                code
                  set=>[@][..][..][microsoft.sql.select][start].Value
                    value=>[$][start].Value
                  set=>[@][..][..][microsoft.sql.select][end].Value
                    value=>[$][end].Value
                  set=>[@][..][..][magix.viewport.set-viewstate][value][start].Value
                    value=>[$][start].Value
                  set=>[@][..][..][magix.viewport.set-viewstate][value][end].Value
                    value=>[$][end].Value
              microsoft.sql.select
                start=>0
                end=>10
              if=>more-than
                lhs=>[@][microsoft.sql.select][end].Value
                rhs=>[@][microsoft.sql.select][record-count].Value
                code
                  set=>[@][..][..][set][value][v1].Value
                    value=>[@][..][..][microsoft.sql.select][record-count].Value
              set=>[/][**panel=>tblPaging][controls][label=>lblCount][value].Value
                value=>@"{0}/{1} of {2}"
                  v0=>[@][microsoft.sql.select][start].Value
                  v1=>[@][microsoft.sql.select][end].Value
                  v2=>[@][microsoft.sql.select][record-count].Value
              set=>[@][magix.viewport.set-viewstate][value][count].Value
                value=>[@][microsoft.sql.select][record-count].Value
              magix.viewport.set-viewstate=>microsoft.sql.select-sub-section
                value
                  start=>0
                  end=>10
            catch
              set=>[@][magix.viewport.show-message][message].Value
                value=>[@][exception].Value
              magix.viewport.show-message
                time=>10000
                color=>#ffaaaa
              stop
          for-each=>[@][try][code][microsoft.sql.select][result][0]
            _th
              tag=>th
            set=>[@][_th][value].Value
              value=>[.].Name
            add=>[@][..][**panel=>tbl][controls][panel][controls]
              value=>[@][_th]
            set=>[@][..][**panel=>tbl][controls][panel][controls][_th].Name
              value=>label
          for-each=>[@][try][code][microsoft.sql.select][result]
            set=>[@][_pnl][controls]
            set=>[@][_pnl][onclick][_columns]
            add=>[@][_pnl][onclick][_columns]
              value=>[.]
              children-only=>true
            _pnl
              tag=>tr
              style=>cursor:pointer;
              title=>click to create update sql statement
              onclick
                _sql=>@"update [___table] set"
                magix.viewport.get-viewstate=>microsoft.sql.current-table
                magix.viewport.get-viewstate=>microsoft.sql.current-primary-keys
                replace=>[_sql].Value
                  what=>\[___table]
                  with=>[magix.viewport.get-viewstate=>microsoft.sql.current-table][value].Value
                _first=>true
                for-each=>[_columns]
                  if=>not-exist
                    lhs=>[/][magix.viewport.get-viewstate=>microsoft.sql.current-primary-keys][value][[.].Name]
                    code
                      if=>equals
                        lhs=>[/][_first].Value
                        rhs=>true
                        code
                          set=>[/][_first].Value
                            value=>false
                      else
                        set=>[/][_sql].Value
                          value=>@"{0},"
                            =>[/][_sql].Value
                      replace=>[.].Value
                        what=>'
                        with=>''
                      set=>[/][_sql].Value
                        value=>@"{0}
  {1}='{2}'"
                          =>[/][_sql].Value
                          =>[.].Name
                          =>[.].Value
                set=>[/][_sql].Value
                  value=>@"{0}
where"
                    =>[/][_sql].Value
                set=>[_first].Value
                  value=>true
                for-each=>[_columns]
                  if=>exist
                    lhs=>[/][magix.viewport.get-viewstate=>microsoft.sql.current-primary-keys][value][[.].Name]
                    code
                      if=>exist
                        lhs=>[.].Value
                        code
                          if=>equals
                            lhs=>[/][_first].Value
                            rhs=>true
                            code
                              set=>[/][_first].Value
                                value=>false
                          else
                            set=>[/][_sql].Value
                              value=>{0} and
                                =>[/][_sql].Value
                          set=>[/][_sql].Value
                            value=>@"{0}
  {1}='{2}'"
                              =>[/][_sql].Value
                              =>[.].Name
                              =>[.].Value
                set=>[magix.forms.set-value][value].Value
                  value=>[_sql].Value
                magix.forms.set-value
                  form-id=>select-query
                  id=>sqlQuery
                magix.viewport.scroll=>sqlQuery
            for-each=>[.]
              _lbl
                tag=>td
              set=>[@][_lbl][value].Value
                value=>[.].Value
              add=>[@][..][_pnl][controls]
                value=>[@][_lbl]
              set=>[@][..][_pnl][controls][_lbl].Name
                value=>label
            add=>[@][..][magix.forms.create-web-part][controls][panel=>tbl][controls]
              value=>[@][_pnl]
            set=>[@][..][magix.forms.create-web-part][controls][panel=>tbl][controls][_pnl].Name
              value=>panel

          magix.forms.create-web-part
            container=>content2
            form-id=>sql-result
            class=>span-22 last top-1
            controls
              panel=>tblPaging
                class=>span-6 left-18 last bottom-1
                controls
                  link-button=>previous
                    class=>span-1
                    value=>&lt;&lt;
                    onclick
                      magix.viewport.get-viewstate=>microsoft.sql.select-sub-section
                      if=>equals
                        lhs=>[magix.viewport.get-viewstate][value][start].Value
                        rhs=>0
                        code
                          magix.viewport.show-message
                            message=>already at beginning
                          stop
                      using=>magix.math
                        subtract
                          =>[/][magix.viewport.get-viewstate][value][start].Value
                          =>10
                        subtract
                          =>[/][magix.viewport.get-viewstate][value][end].Value
                          =>10
                      set=>[microsoft.sql.run-query][start].Value
                        value=>[using][subtract:0].Value
                      set=>[microsoft.sql.run-query][end].Value
                        value=>[using][subtract:1].Value
                      microsoft.sql.run-query
                  label=>lblCount
                    class=>span-4 text-center
                    value=>count
                  link-button=>next
                    class=>span-1 last
                    value=>&gt;&gt;
                    onclick
                      magix.viewport.get-viewstate=>microsoft.sql.select-sub-section
                      if=>more-than-equals
                        lhs=>[magix.viewport.get-viewstate][value][end].Value
                        rhs=>[magix.viewport.get-viewstate][value][count].Value
                        code
                          magix.viewport.show-message
                            message=>already at end
                          stop
                      using=>magix.math
                        add
                          =>[/][magix.viewport.get-viewstate][value][start].Value
                          =>10
                        add
                          =>[/][magix.viewport.get-viewstate][value][end].Value
                          =>10
                      set=>[microsoft.sql.run-query][start].Value
                        value=>[using][add:0].Value
                      set=>[microsoft.sql.run-query][end].Value
                        value=>[using][add:1].Value
                      microsoft.sql.run-query
              panel=>tbl
                tag=>table
                class=>sql-result-table
                controls
                  panel
                    tag=>tr
      else
        set=>[@][try][code][microsoft.sql.execute][sql].Value
          value=>[/][magix.forms.get-value:0][value].Value
        set=>[@][try][code][microsoft.sql.execute][connection].Value
          value=>[/][magix.forms.get-value:1][value].Value
        try
          code
            microsoft.sql.execute
            set=>[@][magix.viewport.show-message][message].Value
              value=>@"{0} records affected"
                v0=>[@][microsoft.sql.execute][result].Value
            magix.viewport.show-message
              time=>5000
              color=>#aaffaa
          catch
            set=>[magix.viewport.show-message][message].Value
              value=>[@][exception].Value
            magix.viewporet.show-message
              time=>10000
              color=>#ffaaaa

  controls
    label=>lblHeader
      tag=>h2
      value=>ms sql query tool
    panel=>connectionWrp
      class=>span-17 last
      default=>btnNew
      controls
        label=>conLbl
          tag=>label
          class=>span-2 right-text
          value=>connection
        select=>ddlConnection
          class=>span-4
          onfirstload
            microsoft.sql.populate-selector
          onselectedindexchanged
            if=>not-equals
              lhs=>[$][value].Value
              rhs=>_default
              code
                magix.forms.set-enabled
                  form-id=>sql
                  id=>delete
                  value=>true
                microsoft.sql.open-selected-connection
            else
              magix.forms.set-enabled
                form-id=>sql
                id=>delete
                value=>false
              microsoft.sql.close-selected-connection
        button=>delete
          class=>span-2
          value=>delete
          disabled=>true
          onclick
            magix.forms.get-value
              form-id=>sql
              id=>ddlConnection
            set=>[magix.data.remove][id].Value
              value=>[magix.forms.get-value][value].Value
            magix.data.remove
            microsoft.sql.populate-selector
            magix.forms.set-enabled
              form-id=>sql
              id=>delete
              value=>false
            microsoft.sql.close-selected-connection
        label=>lblNew
          tag=>label
          class=>span-3 right-text
          value=>or create new
        text-box=>txtNewConnection
          place-holder=>connection string ...
          class=>span-4
        button=>btnNew
          value=>create
          class=>span-2 last
          onclick
            magix.forms.get-value
              form-id=>sql
              id=>txtNewConnection
            set=>[microsoft.sql.test-connection][connection].Value
              value=>[magix.forms.get-value][value].Value
            microsoft.sql.test-connection
            if=>equals
              lhs=>[microsoft.sql.test-connection][success].Value
              rhs=>false
              code
                magix.viewport.show-message
                  message=>that is not a good connection
                stop
            set=>[magix.data.save][value][connection].Value
              value=>[magix.forms.get-value][value].Value
            magix.data.save
              value
                type=>microsoft.sql.connection
            microsoft.sql.populate-selector
            set=>[magix.forms.set-value][value].Value
              value=>[magix.data.save][id].Value
            magix.forms.set-value
              form-id=>sql
              id=>ddlConnection
            magix.forms.set-enabled
              form-id=>sql
              id=>delete
              value=>true
            microsoft.sql.open-selected-connection
    dynamic=>table-list
    dynamic=>column-list
    dynamic=>select-query
