

inspect=>@"creates unit tests for testing if 
web parts behaves correctly"


event=>magix.test.create-mml-web-part

  inspect=>@"verifies that creating a 
mml web part behaves the way it should"

  code
    magix.forms.create-mml-web-part
      container=>content6
      form-id=>web-part-test
      mml=>@"<p>example of html</p>{{
text-box=>txt1
  text=>howdy world
}}"

    magix.forms.get-value
      form-id=>web-part-test
      id=>txt1
    magix.viewport.clear-controls
      container=>content6
    if=>equals
      lhs=>[magix.forms.get-value][value].Value
      rhs=>howdy world
      code
        _success
    else
      throw=>didn't get expected value from text-box in magix.test.create-mml-web-part


event=>magix.test.create-mml-web-part-no-mml-throws

  inspect=>@"verifies that creating a 
mml web part without [mml] throws an exception"

  code
    _success
    try
      code
        magix.forms.create-mml-web-part
          container=>content6
          form-id=>web-part-test
      catch
        set=>[_success].Value
          value=>true
    if=>not-exist
      lhs=>[_success].Value
      code
        throw=>create-mml-web-part didn't throw an exception when no [mml] was given


event=>magix.test.create-mml-web-part-no-container-throws

  inspect=>@"verifies that creating a 
mml web part without [container] throws an exception"

  code
    _success
    try
      code
        magix.forms.create-mml-web-part
          mml=>@"mumbo jumbo"
          form-id=>web-part-test
      catch
        set=>[_success].Value
          value=>true
    if=>not-exist
      lhs=>[_success].Value
      code
        throw=>create-mml-web-part didn't throw an exception when no [container] was given


event=>magix.test.create-mml-web-part-no-form-id-does-not-throws

  inspect=>@"verifies that creating a 
mml web part without [form-id] does not throw exception"

  code
    magix.forms.create-mml-web-part
      mml=>@"mumbo jumbo"
      container=>content6
    magix.viewport.clear-controls
      container=>content6


event=>magix.test.create-mml-web-part-with-lambda

  inspect=>@"verifies that creating a 
mml web part with a lambda behaves correctly"

  code
    magix.forms.create-mml-web-part
      container=>content6
      form-id=>test-form
      mml=>@"<p>some html</p>{{
lambda=>lmb
  oncreatecontrols
    _dt
      text-box=>txt1
        text=>something
    add=>[$]
      value=>[_dt][0]
}}"
    magix.forms.get-value
      form-id=>test-form
      id=>txt1
    magix.viewport.clear-controls
      container=>content6
    if=>equals
      lhs=>[magix.forms.get-value][value].Value
      rhs=>something
      code
        _success
    else
      throw=>didn't get expected value from text-box in magix.test.create-mml-web-part-with-lambda


event=>magix.test.set-text-box-value

  inspect=>@"verifies that setting a text-box 
value behaves correctly"

  code
    magix.forms.create-mml-web-part
      container=>content6
      form-id=>test-form
      mml=>@"{{
text-box=>txt1
  text=>howdy
}}"
    magix.forms.set-value
      form-id=>test-form
      id=>txt1
      value=>howdy1
    magix.forms.get-value
      form-id=>test-form
      id=>txt1
    magix.viewport.clear-controls
      container=>content6
    if=>equals
      lhs=>[magix.forms.get-value][value].Value
      rhs=>howdy1
      code
        _success
    else
      throw=>didn't get expected value from text-box


event=>magix.test.set-text-box-value-wrong-form-id

  inspect=>@"verifies that setting a text-box 
value behaves correctly"

  code
    magix.forms.create-mml-web-part
      container=>content6
      form-id=>test-form
      mml=>@"{{
text-box=>txt1
  text=>howdy
}}"
    magix.forms.set-value
      form-id=>test-form-error
      id=>txt1
      value=>howdy1
    magix.forms.get-value
      form-id=>test-form
      id=>txt1
    magix.viewport.clear-controls
      container=>content6
    if=>equals
      lhs=>[magix.forms.get-value][value].Value
      rhs=>howdy
      code
        _success
    else
      throw=>didn't get expected value from text-box


event=>magix.test.set-text-box-value-no-form-id

  inspect=>@"verifies that setting a text-box 
value behaves correctly"

  code
    magix.forms.create-mml-web-part
      container=>content6
      form-id=>test-form
      mml=>@"{{
text-box=>txt1
  text=>howdy
}}"
    magix.forms.set-value
      id=>txt1
      value=>howdy1
    magix.forms.get-value
      form-id=>test-form
      id=>txt1
    magix.viewport.clear-controls
      container=>content6
    if=>equals
      lhs=>[magix.forms.get-value][value].Value
      rhs=>howdy1
      code
        _success
    else
      throw=>didn't get expected value from text-box


event=>magix.test.set-css-class

  inspect=>@"verifies that setting a text-box 
css class behaves correctly"

  code
    magix.forms.create-mml-web-part
      container=>content6
      form-id=>test-form
      mml=>@"{{
text-box=>txt1
}}"
    magix.forms.set-class
      id=>txt1
      value=>some-css-class
    magix.forms.get-class
      id=>txt1
    magix.viewport.clear-controls
      container=>content6
    if=>equals
      lhs=>[magix.forms.get-class][value].Value
      rhs=>some-css-class
      code
        _success
    else
      throw=>didn't get expected css from text-box


event=>magix.test.test-onfirstload

  inspect=>@"verifies that onfirstload 
behaves correctly"

  code
    magix.forms.create-mml-web-part
      container=>content6
      form-id=>test-form
      mml=>@"{{
text-box=>txt1
  onfirstload
    magix.forms.set-value
      id=>txt1
      value=>success
}}"
    magix.forms.get-value
      id=>txt1
    magix.viewport.clear-controls
      container=>content6
    if=>equals
      lhs=>[magix.forms.get-value][value].Value
      rhs=>success
      code
        _success
    else
      throw=>didn't get expected value from text-box


event=>magix.test.set-visibility

  inspect=>@"verifies that setting a text-box 
visibility behaves correctly"

  code
    magix.forms.create-mml-web-part
      container=>content6
      form-id=>test-form
      mml=>@"{{
text-box=>txt1
}}"
    magix.forms.set-visible
      id=>txt1
      value=>false
    magix.forms.get-visible
      id=>txt1
    magix.viewport.clear-controls
      container=>content6
    if=>equals
      lhs=>[magix.forms.get-visible][value].Value
      rhs=>false
      code
        _success
    else
      throw=>didn't get expected visibility from text-box


event=>magix.test.set-enabled

  inspect=>@"verifies that setting a text-box 
enabled property behaves correctly"

  code
    magix.forms.create-mml-web-part
      container=>content6
      form-id=>test-form
      mml=>@"{{
text-box=>txt1
}}"
    magix.forms.set-enabled
      id=>txt1
      value=>false
    magix.forms.get-enabled
      id=>txt1
    magix.viewport.clear-controls
      container=>content6
    if=>equals
      lhs=>[magix.forms.get-enabled][value].Value
      rhs=>false
      code
        _success
    else
      throw=>didn't get expected enabled value from text-box


event=>magix.test.text-value-into-event-handlers

  inspect=>@"verifies that value of control is
passed into event handlers"

  code
    magix.forms.create-mml-web-part
      container=>content6
      form-id=>test-form
      mml=>@"{{
text-box=>txt1
  text=>howdy
  onfirstload
    if=>equals
      lhs=>[$][value].Value
      rhs=>howdy
      code
        _success
    else
      throw=>value not passed into event handlers
}}"
    magix.viewport.clear-controls
      container=>content6


event=>magix.test.text-info-into-event-handlers

  inspect=>@"verifies that info of control is
passed into event handlers"

  code
    magix.forms.create-mml-web-part
      container=>content6
      form-id=>test-form
      mml=>@"{{
text-box=>txt1
  info=>howdy
  onfirstload
    if=>equals
      lhs=>[$][info].Value
      rhs=>howdy
      code
        _success
    else
      throw=>value not passed into event handlers
}}"
    magix.viewport.clear-controls
      container=>content6

