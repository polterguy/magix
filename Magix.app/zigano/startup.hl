

event=>zigano.travel.create-roles-form
  code
    magix.data.load
      prototype
        type=>zigano.travel.role

    _dt
      panel
        css=>span-19 hover-shadow relative
        controls
          label
          link-button
            css=>span-1 hover-shadow last right z-1 absolute light-red centered
            text=>x
            onclick
              magix.data.remove
                prototype
                  type=>zigano.travel.role
                  name=>
              set=>[magix.data.load][prototype][roles][[magix.data.remove][prototype][name].Value].Name
                value=>[magix.data.remove][prototype][name].Value
              magix.data.load
                prototype
                  type=>zigano.travel.user
                  roles
              for-each=>[magix.data.load][objects]
                set=>[.][roles][[/][magix.data.remove][prototype][name].Value]
                set=>[.ip][..][magix.data.save][object]
                  value=>[.]
                set=>[.ip][..][magix.data.save][1].Name
                  value=>object
                set=>[.ip][..][magix.data.save][object].Value
                set=>[.ip][..][magix.data.save][id].Value
                  value=>[.].Name
                magix.data.save
                  id
              set=>[magix.log.append][body][role].Value
                value=>[magix.data.remove][prototype][name].Value
              magix.log.append
                header=>role deleted
                body=>role {0} was deleted
              zigano.travel.create-roles-form

    for-each=>[magix.data.load][objects]
      set=>[/][_dt][panel][controls][link-button][onclick][magix.data.remove][prototype][name].Value
        value=>[.][name].Value
      set=>[/][_dt][panel][controls][label][text].Value
        value=>[.][name].Value
      add=>[/][magix.forms.create-form][controls][panel:1][controls]
        value=>[/][_dt][0]

    magix.forms.create-form
      form-id=>roles-grid
      container=>content2
      css=>span-19 last
      controls
        panel=>roleGrdHeader
          controls
            label=>headName
              css=>span-18
              text=>name
              tag=>label
              tip=>name of role
            label=>headDelete
              css=>span-1 last
              text=>del
              tag=>label
              tip=>delete role
        panel=>roleGrdWrp
          css=>span-19 light-green
          controls
        panel=>newRoleWrp
          css=>span-19 top-1
          default=>addBtn
          controls
            text-box=>nName
              css=>span-5 right-14 last
              place-holder=>name of new role ...
            button=>addBtn
              css=>btn-large span-3 clear
              text=>add
              onclick
                magix.forms.get-value
                  form-id=>roles-grid
                  id=>nName
                if=>![magix.forms.get-value][value].Value
                  magix.viewport.show-message
                    message=>you must give your role a name
                else
                  set=>[@][magix.data.load][prototype][name].Value
                    value=>[magix.forms.get-value][value].Value
                  magix.data.load
                    prototype
                      type=>zigano.travel.role
                  if=>[@][magix.data.load][objects].Count>0
                    magix.viewport.show-message
                      message=>role exists from before
                  else
                    set=>[@][magix.data.save][object][name].Value
                      value=>[magix.forms.get-value][value].Value
                    magix.data.save
                      object
                        type=>zigano.travel.role
                    set=>[@][magix.log.append][body][role].Value
                      value=>[magix.forms.get-value][value].Value
                    magix.log.append
                      header=>role created
                      body=>role {0} was created
                    zigano.travel.create-roles-form


event=>zigano.travel.edit-user
  code
    set=>[magix.admin.run-file][username].Value
      value=>[$][username].Value
    magix.admin.run-file
      file=>zigano/admin/edit-single-user.hl

event=>zigano.travel.create-users-form
  code
    if=>[$][start]
      set=>[magix.data.load][start].Value
        value=>[$][start].Value
      set=>[magix.data.load][end].Value
        value=>[$][end].Value
    magix.data.load
      start=(int)>0
      end=(int)>10
      prototype
        type=>zigano.travel.user
    magix.data.count
      prototype
        type=>zigano.travel.user
    if=>[magix.data.load][start].Value==0
      set=>[magix.forms.create-form][controls][panel:2][controls][link-button:0][enabled].Value
        value=>false
    else
      set=>[magix.forms.create-form][controls][panel:2][controls][link-button:0][onclick][zigano.travel.create-users-form][start].Value
        value=>[magix.data.load][start].Value-10
      set=>[magix.forms.create-form][controls][panel:2][controls][link-button:0][onclick][zigano.travel.create-users-form][end].Value
        value=>[magix.data.load][end].Value-10
    if=>[magix.data.load][end].Value>=[magix.data.count][count].Value
      set=>[magix.forms.create-form][controls][panel:2][controls][link-button:1][enabled].Value
        value=>false
    else
      set=>[magix.forms.create-form][controls][panel:2][controls][link-button:1][onclick][zigano.travel.create-users-form][start].Value
        value=>[magix.data.load][start].Value+10
      set=>[magix.forms.create-form][controls][panel:2][controls][link-button:1][onclick][zigano.travel.create-users-form][end].Value
        value=>[magix.data.load][end].Value+10
    if=>[magix.data.load][end].Value>[magix.data.count][count].Value
      set=>[magix.data.load][end].Value
        value=>[magix.data.count][count].Value
    set=>[magix.forms.create-form][controls][panel:2][controls][label][text].Value
      value=>@"{0} - {1} of {2}"
        pr0=>[magix.data.load][start].Value+1
        pr1=>[magix.data.load][end].Value
        pr2=>[magix.data.count][count].Value

    _dt
      panel
        css=>span-19 hover-shadow relative
        controls
          label
            css=>span-5
          label
            css=>span-5
          label
            css=>span-5
          link-button
            css=>span-3
            text=>edit
            onclick
              zigano.travel.edit-user
          link-button
            css=>span-1 hover-shadow last right z-1 absolute light-red centered
            text=>x
            onclick
              magix.data.remove
                prototype
                  type=>zigano.travel.user
                  username=>
              magix.log.append
                header=>user deleted
                body=>user {0} deleted
              zigano.travel.create-users-form
              magix.viewport.clear-controls
                container=>content3

    for-each=>[magix.data.load][objects]
      set=>[/][_dt][panel][controls][label:0][text].Value
        value=>[.][name].Value
      set=>[/][_dt][panel][controls][label:1][text].Value
        value=>[.][username].Value
      set=>[/][_dt][panel][controls][label:2][text].Value
        value=>[.][pwd].Value
      set=>[/][_dt][panel][controls][link-button:0][onclick][zigano.travel.edit-user][username].Value
        value=>[.][username].Value
      set=>[/][_dt][panel][controls][link-button:1][onclick][magix.data.remove][prototype][username].Value
        value=>[.][username].Value
      set=>[/][_dt][panel][controls][link-button:1][onclick][magix.log.append][body][username].Value
        value=>[.][username].Value
      add=>[/][magix.forms.create-form][controls][panel:1][controls]
        value=>[/][_dt][0]

    magix.forms.create-form
      form-id=>users-grid
      container=>content2
      css=>span-19 last
      controls
        panel=>userGrdHeader
          controls
            label=>headName
              css=>span-5
              text=>name
              tag=>label
              tip=>name of user
            label=>headUsername
              css=>span-5
              text=>username
              tag=>label
              tip=>username of user
            label=>headPwd
              css=>span-5
              text=>password
              tag=>label
              tip=>password of user
            label=>headEdit
              css=>span-3
              text=>edit
              tag=>label
              tip=>edit user
            label=>headDelete
              css=>span-1 last
              text=>del
              tag=>label
              tip=>delete user
        panel=>userGrdWrp
          css=>span-19 light-green
          controls
        panel=>pagerWrp
          css=>span-19
          controls
            link-button=>previous
              text=>&lt;&lt;
              css=>span-1 left-7
            label=>count
              css=>span-2
              text=>54
            link-button=>next
              text=>&gt;&gt;
              css=>span-1
        panel=>newUserWrp
          css=>span-19 top-1
          default=>addBtn
          controls
            text-box=>nName
              css=>span-5
              place-holder=>name of new user ...
            text-box=>nUsername
              css=>span-5
              place-holder=>username ...
            text-box=>nPwd
              css=>span-5 right-4 last
              place-holder=>password ...
            button=>addBtn
              css=>btn-large span-3 clear
              text=>add
              onclick
                magix.forms.get-value
                  form-id=>users-grid
                  id=>nName
                if=>![magix.forms.get-value][value].Value
                  magix.viewport.show-message
                    message=>you must give new user a name
                else
                  set=>[@][else][else][else][magix.data.save][object][name].Value
                    value=>[magix.forms.get-value][value].Value
                  magix.forms.get-value
                    form-id=>users-grid
                    id=>nUsername
                  if=>![@][magix.forms.get-value][value].Value
                    magix.viewport.show-message
                      message=>you must give new user a unique username
                  else
                    set=>[@][else][else][magix.data.save][object][username].Value
                      value=>[@][..][magix.forms.get-value][value].Value
                    set=>[@][else][else][zigano.travel.edit-user][username].Value
                      value=>[@][..][magix.forms.get-value][value].Value
                    magix.forms.get-value
                      form-id=>users-grid
                      id=>nPwd
                    if=>![@][magix.forms.get-value][value].Value
                      magix.viewport.show-message
                        message=>you must give new user a password
                    else
                      set=>[@][else][magix.data.save][object][pwd].Value
                        value=>[@][..][magix.forms.get-value][value].Value
                      set=>[@][magix.data.load][prototype][username].Value
                        value=>[@][..][..][magix.forms.get-value][value].Value
                      magix.data.load
                        prototype
                          type=>zigano.travel.user
                      if=>[@][magix.data.load][objects].Count>0
                        magix.viewport.show-message
                          message=>user exists from before with same username
                      else
                        magix.data.save
                          object
                            type=>zigano.travel.user
                        set=>[@][magix.log.append][body][username].Value
                          value=>[@][..][..][magix.forms.get-value:0][value].Value
                        magix.log.append
                          header=>new user created
                          body=>user {0} created
                        zigano.travel.create-users-form
                        zigano.travel.edit-user


event=>magix.ide.create-properties
  code
    _buffer
    _data
    _onclick
      _prop-name
      _prop-value
      if=>![/][_prop-value].Value
        if=>[/][_prop-value].Count>0
          for-each=>[/][_prop-value]
            add=>[/][if][if][magix.code.node-2-code][json]
              value=>[.]
          magix.code.node-2-code
            json
          set=>[_prop-value].Value
            value=>[/][if][if][magix.code.node-2-code][code].Value
      set=>[/][magix.forms.create-form][controls][text-area][text].Value
        value=>[/][_prop-value].Value
      set=>[/][magix.forms.create-form][controls][label][text].Value
        value=>[/][_prop-name].Value
      magix.forms.create-form
        form-id=>edit-property
        container=>modal
        css=>modal-body
        header=>change property
        controls
          label=>expl
            text=>change property of your widget
          text-area=>val
            place-holder=>empty ...
            rows=>6
            css=>span5 input-block-level monospaced
      magix.forms.create-form
        form-id=>edit-property-footer
        container=>modalFtr
        controls
          button=>ok
            text=>ok
            css=>btn btn-primary
            onclick
              _prop-name2
              magix.forms.get-value
                form-id=>edit-property
                id=>val
              get-selected-widget
              set=>[/][change-widget][dna].Value
                value=>[/][get-selected-widget][value][dna].Value
              if=>![/][magix.forms.get-value][value].Value
                set=>[/][change-widget][remove][[/][_prop-name2].Value].Value
                  value=>
              else
                set=>[/][change-widget][change][[/][_prop-name2].Value].Value
                  value=>[/][magix.forms.get-value][value].Value
              change-widget
              magix.viewport.clear-controls
                container=>modal
              magix.ide.create-properties
    get-selected-widget
    if=>[/][get-selected-widget][value][widget]
      list-widget-types
      for-each=>[/][if][list-widget-types][types]
        if=>[.][type].Value==[/][get-selected-widget][value][widget][type].Value
          add=>[/][_data]
            value=>[.]
      for-each=>[/][_data][widget][properties]
        while=>[/][_onclick][_prop-value].Count>0
          set=>[/][_onclick][_prop-value][0]
        while=>[/][_buffer].Count>0
          set=>[/][_buffer][0]
        set=>[/][_buffer][link-button].Value
          value=>[.].Name
        set=>[/][_buffer][link-button][text].Value
          value=>[.].Name
        set=>[/][_buffer][link-button][css].Value
          value=>span2
        if=>[/][get-selected-widget][value][widget][properties][[.].Name].Value
          set=>[/][_buffer][link-button][css].Value
            value=>span2 fat
          set=>[/][_onclick][_prop-value].Value
            value=>[/][get-selected-widget][value][widget][properties][[.].Name].Value
        else
          for-each=>[/][get-selected-widget][value][widget][properties][[.].Name]
            set=>[/][_buffer][link-button][css].Value
              value=>span2 fat
            add=>[/][_onclick][_prop-value]
              value=>[.]
          set=>[/][_onclick][_prop-value].Value
        set=>[/][_onclick][_prop-name].Value
          value=>[.].Name
        set=>[/][_onclick][magix.forms.create-form:1][controls][button][onclick][_prop-name2].Value
          value=>[.].Name
        add=>[/][_buffer][link-button]
          value=>[/][_onclick]
        set=>[/][_buffer][link-button][_onclick].Name
          value=>onclick
        add=>[/][magix.forms.create-form][controls][panel][controls]
          value=>[/][_buffer][link-button]
    magix.forms.create-form
      form-id=>toolbox-properties
      container=>content2
      css=>span10
      events
        magix.forms.widget-selected
          magix.ide.create-properties
      controls
        label=>header
          text=>properties
          tag=>h4
        panel=>wrpProprties
          css=>span12
          controls
        label=>ruler
          css=>span11
          tag=>hr
