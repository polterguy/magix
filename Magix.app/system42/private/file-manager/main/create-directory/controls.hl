

inspect=>@"contains controls for create directory web part

contains the controls necessary to show the create directory web part"


panel=>create-dir-wrapper
  class=>span-6 last
  onesc
    magix.viewport.clear-controls
      container=>create-file-directory-container
      reset-class=>true
  default=>create-directory-save
  controls
    text-box=>new-directory-name
      class=>span-4
      placeholder=>directory name ...
      onfirstload
        magix.forms.effect
          type=>focus-and-select
          form-id=>create-file-directory-container
          id=>new-directory-name
    button=>create-directory-save
      class=>span-2 last
      value=>create
      onclick
        magix.forms.get-value
          form-id=>create-file-directory-container
          id=>new-directory-name
        if=>equals
          lhs=>magix.forms.get-value
          rhs=>
          code
            magix.viewport.show-message
              message=>you must give your directory a name
              color=>#ffbbbb
        else
          magix.viewstate.get
            id=>magix.file-manager.current-folder
          if=>not-equals
            lhs=>[@][magix.viewstate.get][value].value
            rhs=>
            code
              set=>[@][..][..][magix.file.create-directory][directory].value
                value=>{0}/{1}
                  =>[@][..][..][magix.viewstate.get][value].value
                  =>[magix.forms.get-value][value].value
          else
            set=>[@][..][magix.file.create-directory][directory].value
              value=>[magix.forms.get-value][value].value
          magix.file.create-directory
          magix.viewport.clear-controls
            container=>create-file-directory-container
            reset-class=>true
          set=>[@][magix.viewstate.set][value].value
            value=>[@][magix.file.create-directory][directory].value
          magix.viewstate.set
            id=>magix.file-manager.current-folder
          magix.file-manager.open-directory
          magix.file-manager.directory-changed

