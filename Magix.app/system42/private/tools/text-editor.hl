

inspect=>@"allows editing of text files

creates a text-area, where you can edit contents of files on the server, 
such as hyperlisp or magix markup language files

[file] is an optional parameter, telling it which file to initially load up

[container] is an optional parameter, informing which viewport container to 
load up web part in.  if no [container] is given, the web part will load up 
in the content1 container

[class] is optional, and defines the css class(es) of the we part itself

if [allow-filename-change] is false, then the user cannot change the filename 
of the file being edited

you can also supply extra buttons, by supplying extra controls through 
the [extra-buttons] parameters, that will be appended after the save 
button

if you supply [auto-scroll] and set its value to true, then it will 
automatically scroll to the container where it is created upon creation

if you supply [auto-focus] and set its value to true, then the editing 
surface will gain automatic focus after loading

if you supply an [onesc] piece of hyperlisp code, then this code will 
execute when the user clicks esc"


// loading file, if we are given a [file] input parameter
if=>exist
  lhs=>[$][file].Value
  code
    using=>magix.file
      load
        file=>[$][file].Value
    replace=>[@][using][load][value].Value
      what=>&
      with=>&amp;
    replace=>[@][using][load][value].Value
      what=><
      with=>&lt;
    replace=>[@][using][load][value].Value
      what=>>
      with=>&gt;
    set=>[**text-area=>editor-surface][value].Value
      value=>[@][using][load][value].Value
    set=>[**text-box=>txt-file-name][value].Value
      value=>[$][file].Value

    // enabling the preview button, but only if file is an mml or html file
    split=>[$][file].Value
      what=>.
    while=>more-than
      lhs=>[@][split][result].Count
      rhs=>1
      code
        set=>[@][..][..][split][result][0]
    if=>equals
      lhs=>[@][split][result][0].Value
      rhs=>mml
      or=>equals
        lhs=>[@][split][result][0].Value
        rhs=>html
      or=>equals
        lhs=>[@][split][result][0].Value
        rhs=>htm
      code
        set=>[**button=>preview][disabled].Value
          value=>false
    else
      set=>[**button=>preview][disabled].Value
        value=>true

// disabling the text-box that allows changing the filename, if we're not allowed to change filename of edited file
if=>equals
  lhs=>[$][allow-filename-change].Value
  rhs=>false
  code
    set=>[**text-box=>txt-file-name][disabled].Value
      value=>true

// putting in container, if we're given one, otherwise we're using default container
if=>exist
  lhs=>[$][container]
  code
    set=>[magix.forms.create-web-part][container].Value
      value=>[$][container].Value

// adding css classes, if we're given any, otherwise using default of last span-17
if=>exist
  lhs=>[$][class]
  code
    set=>[magix.forms.create-web-part][class].Value
      value=>[$][class].Value

// adding extra buttons, if we're given any
if=>exist
  lhs=>[$][extra-buttons]
  code
    set=>[**button=>save][class].Value
      value=>span-2
    add=>[**panel=>save-wrapper][controls]
      value=>[$][extra-buttons]
      children-only=>true

// adding automatical scroll, if we're supposed to
if=>equals
  lhs=>[$][auto-scroll].Value
  rhs=>true
  code
    set=>[**panel=>save-wrapper][onfirstload][magix.browser.scroll][id].Value
      value=>[magix.forms.create-web-part][container].Value

// automatically giving focus, if we're supposed to
if=>not-equals
  lhs=>[$][auto-focus].Value
  rhs=>true
  code
    set=>[**text-area=>editor-surface][onfirstload][magix.forms.effect].Name
      value=>_not

// adding [onesc] event handler, if we're given any
if=>exist
  lhs=>[$][onesc]
  code
    add=>[**panel=>main-wrapper][onesc]
      value=>[$][onesc]
      children-only=>true

// creating web part
magix.forms.create-web-part
  form-id=>editor
  container=>content1
  class=>span-17 last
  controls
    panel=>main-wrapper
      controls
        panel=>toolbar
          class=>fill-width column
          controls
            button=>preview
              class=>span-2 toolbox-preview
              value=>preview
              onclick
                magix.forms.get-value
                  form-id=>editor
                  id=>editor-surface
                using=>magix.forms
                  create-mml-web-part
                    container=>preview-container
                    form-id=>preview
                    mml=>[magix.forms.get-value][value].Value
                  create-web-part
                    container=>preview-toolbar
                    form-id=>preview-toolbar
                    controls
                      button=>close-preview
                        class=>span-2 last right
                        value=>close
                        title=>closes preview
                        onclick
                          magix.viewport.clear-controls
                            container=>preview-container
                          magix.viewport.clear-controls
                            container=>preview-toolbar
                magix.browser.scroll
                  id=>preview-container
        text-area=>editor-surface
          class=>fill-width
          rows=>25
          placeholder=>text content ...
          onfirstload
            magix.forms.effect
              type=>focus-and-select
              form-id=>editor
              id=>editor-surface
        panel=>save-wrapper
          default=>save
          class=>right column last
          controls
            label
              tag=>label
              class=>span-2
              value=>save as
            text-box=>txt-file-name
              class=>span-10
              placeholder=>filename ...
            button=>save
              class=>span-2 last
              value=>save
              onclick
                magix.forms.get-value
                  form-id=>editor
                  id=>txt-file-name
                magix.forms.get-value
                  form-id=>editor
                  id=>editor-surface
                using=>magix.file
                  save
                    file=>[magix.forms.get-value:0][value].Value
                    value=>[magix.forms.get-value:1][value].Value
                magix.viewport.show-message
                  message=>file was successfully saved
                magix.forms.effect
                  type=>focus-and-select
                  form-id=>editor
                  id=>editor-surface
                set=>[magix.editor.file-saved][file].Value
                  value=>[magix.forms.get-value:0][value].Value
                magix.editor.file-saved

        // preview container
        dynamic=>preview-container
          class=>fill-width column top-1
        dynamic=>preview-toolbar
          class=>fill-width column

