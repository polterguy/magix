

inspect=>@"creates the compose email web part

creates the compose email web part"


// cleaning out recipients and attachments
magix.viewstate.set
  id=>magix.email.recipients
magix.viewstate.set
  id=>magix.email.attachments

// adding recipients
_recipients
if=>exist
  lhs=>[$][to]
  code
    add=>[_recipients]
      value=>[$][to]
if=>exist
  lhs=>[$][cc]
  code
    add=>[_recipients]
      value=>[$][cc]
if=>exist
  lhs=>[$][bcc]
  code
    add=>[_recipients]
      value=>[$][bcc]

// checking to see if this is a reply, and if it is, we make sure the original email is loaded to be replied to, and not a fresh email
if=>exist
  lhs=>[$][reply-container]
  code
    set=>[magix.forms.create-web-part][container].value
      value=>[$][reply-container].value
    set=>[magix.forms.create-web-part][**button=>cancel][onclick][magix.viewport.clear-controls][container].value
      value=>[$][reply-container].value
if=>exist
  lhs=>[$][email-id]
  code
    set=>[magix.forms.create-web-part][**label=>header][value].value
      value=>reply
    using=>magix.data
      load
        id=>[$][email-id].value

    // only setting recipients if we are not forwarding email
    if=>not-equals
      lhs=>[$][forward].value
      rhs=>true
      code

        // setting to fields of email
        set=>[@][magix.viewstate.set][value][to][].value
          value=>[@][..][..][using][load][value][from].value
        if=>exist
          lhs=>[@][..][..][using][load][value][from][display-name]
          code
            set=>[@][..][..][magix.viewstate.set][value][to][0][display-name].value
              value=>[@][..][..][..][..][using][load][value][from][display-name].value

        // setting cc fields, if reply-all is true
        if=>equals
          lhs=>[$][reply-all].value
          rhs=>true
          code
            for-each=>[@][..][..][..][..][using][load][value][to]
              add=>[@][..][..][..][magix.viewstate.set][value][cc]
                value=>
                  value=>[.].value
              if=>exist
                lhs=>[.][display-name]
                code
                  add=>[@][..][..][..][..][..][magix.viewstate.set][value][cc][>last]
                    value=>display-name
                      value=>[.][display-name].value
            for-each=>[@][..][..][..][..][using][load][value][cc]
              add=>[@][..][..][..][magix.viewstate.set][value][cc]
                value=>
                  value=>[.].value
              if=>exist
                lhs=>[.][display-name]
                code
                  add=>[@][..][..][..][..][..][magix.viewstate.set][value][cc][>last]
                    value=>display-name
                      value=>[.][display-name].value

        // changing viewstate value to reflect to, cc and bcc fields
        magix.viewstate.set
          id=>magix.email.recipients

    // setting subject of email, making sure it starts with "re:"
    index-of=>[@][using][load][value][subject].value
      what=>re:
      case=>false
    if=>not-equals
      lhs=>[@][index-of][result][0].value
      rhs=>0
      code

        // no existing "re:" was found, adding our own
        set=>[@][..][..][using][load][value][subject].value
          value=>Re:{0}
            =>[@][..][..][using][load][value][subject].value
    set=>[magix.forms.create-web-part][**text-box=>compose-subject][value].value
      value=>[@][using][load][value][subject].value

    // setting body of email, trying the "plain" version first
    if=>exist
      lhs=>[@][using][load][value][body][plain].value
      code

        // we found a "plain" version of email, using that one instead of html version as reply body
        // trying to find signature of email, and removing it, but only if email is either reply or reply-all, and not forwarded
        split=>[@][..][..][using][load][value][body][plain].value
          what=>@"\n--\n"
        if=>equals
          lhs=>[@][split][result].count
          rhs=>2
          and=>not-equals
            lhs=>[$][forward].value
            rhs=>true
          code

            // we found a signature, now we'll remove it from the reply body
            set=>[@][..][..][..][..][using][load][value][body][plain].value
              value=>[@][..][..][split][result][0].value
            split=>[@][..][..][..][..][using][load][value][body][plain].value
              trim=>true
            set=>[@][..][..][..][..][using][load][value][body][plain].value
              value=>[@][split][result][0].value

        // adding up angle brackets to signify the quoted text
        replace=>[@][..][..][using][load][value][body][plain].value
          what=>\n
          with=>\n>

        // setting body of email to be replied-to email
        set=>[magix.forms.create-web-part][**text-area=>compose-body][value].value
          value=>>{0}
            =>[@][..][..][using][load][value][body][plain].value
    else-if=>exist
      lhs=>[@][using][load][value][body][html].value
      code

        // no plain version of email existed, using html version as reply body
        set=>[@][magix.web.strip-html][value].value
          value=>[@][..][..][using][load][value][body][html].value
        magix.web.strip-html
        replace=>[@][magix.web.strip-html][result].value
          what=>\n
          with=>\n>
        set=>[magix.forms.create-web-part][**text-area=>compose-body][value].value
          value=>>{0}
            =>[@][magix.web.strip-html][result].value

// checking to see if we should make compose-email container smaller than its default, which might be the case for replies
if=>equals
  lhs=>[$][smaller].value
  rhs=>true
  code

    // this is a smaller reply containerr, possibly a reply inside another email
    set=>[magix.forms.create-web-part][class].value
      value=>span-15 last boxed air-padding bottom-1 top-2
    set=>[magix.forms.create-web-part][**label=>header][class].value
      value=>span-13 bottom-1
    set=>[magix.forms.create-web-part][**text-box=>compose-subject][class].value
      value=>span-15 last bottom-1
    set=>[magix.forms.create-web-part][**text-area=>compose-body][class].value
      value=>span-15 last bottom-1

    // making sure other parts also understands this is a smaller reply container
    magix.viewstate.set
      id=>magix.email.smaller-composer
      value=>true
else

  // making sure other components knows this is not a smaller container, by setting a viewstate variable
  magix.viewstate.set
    id=>magix.email.smaller-composer

// if [to], [cc] or [bcc] parameters are given, we set the initial recipients
if=>not-equals
  lhs=>[_recipients].count
  rhs=>0
  code

    // we have initial recipients, which might occur when an email address is clicked inside another email message
    add=>[@][magix.viewstate.set][value]
      values=>[_recipients]
    magix.viewstate.set
      id=>magix.email.recipients

// including css files
magix.viewport.include-client-file
  type=>css
  file=>media/back-end/file-uploader.css

// setting signature
magix.data.load-username=>signature
  id=>magix.smtp.settings
set=>[magix.forms.create-web-part][**text-area=>compose-body][value].value
  value=>@"{0}


-- 
{1}"
    =>[magix.forms.create-web-part][**text-area=>compose-body][value].value
    =>[magix.data.load-username=>signature][value][email-signature].value

magix.forms.create-web-part
  container=>content3
  class=>span-18 last boxed air-padding bottom-1
  form-id=>composer
  events-file=>your-files/system-apps/magix-mail/components/composer/events.hl
  controls
    label=>header
      class=>span-16 bottom-1
      value=>compose email
      tag=>h3
    button=>add-recipient
      class=>span-2 last bottom-1
      value=>+
      title=>add recipient
      onfirstload
        magix.viewstate.get
          id=>magix.email.recipients
        if=>not-exist
          lhs=>[magix.viewstate.get][value]
          or=>less-than
            lhs=>[magix.viewstate.get][value].count
            rhs=>1
          code
            magix.email.add-recipient
      onclick
        magix.email.add-recipient
    dynamic=>dynamic-add-recipient
    dynamic=>dynamic-recipients
      onfirstload
        magix.email.create-recipient-list
    text-box=>compose-subject
      class=>span-18 last bottom-1
      placeholder=>subject ...
    text-area=>compose-body
      class=>span-18 last bottom-1
      rows=>15
      placeholder=>body ...
    dynamic=>compose-attachments
    panel
      class=>btn-group span-8 last right
      controls
        button=>send
          class=>span-2
          value=>send
          onclick
            magix.email.send-composed-email
        button=>sign
          class=>span-2 sign-email
          value=>sign
          title=>signs email with your ssl certificate
          onfirstload
            // checking to see if emails can be signed
            magix.data.load-username
              id=>magix.smtp.settings
            set=>[magix.cryptography.can-sign][email].value
              value=>[magix.data.load-username][value][email].value
            magix.cryptography.can-sign
            if=>not-equals
              lhs=>[magix.cryptography.can-sign][value].value
              rhs=>true
              code
                magix.forms.set-enabled
                  form-id=>composer
                  id=>sign
                  value=>false
            else
              // adding signature to email by default
              magix.forms.set-class
                form-id=>composer
                id=>sign
                value=>span-2 signed-email
          onclick
            magix.forms.get-class
              form-id=>composer
              id=>sign
            index-of=>[magix.forms.get-class][value].value
              what=>sign-email
            if=>exist
              lhs=>[index-of][result]
              code
                // adding signature to email
                magix.forms.set-class
                  form-id=>composer
                  id=>sign
                  value=>span-2 signed-email
            else
              // removing signature from email
              magix.forms.set-class
                form-id=>composer
                id=>sign
                value=>span-2 sign-email
              magix.viewport.show-message
                message=>this will send your email unsigned, which is usually a bad idea.&nbsp;&nbsp;click the sign button again to change your mind
                color=>#ffddaa
                time=>7000
            magix.forms.effect
              form-id=>composer
              id=>compose-subject
              type=>focus-and-select
        button=>encrypt
          class=>span-2 encrypt-email
          value=>encrypt
          title=>encrypts email using your ssl private key
          events

            // handling the creation of recipients, to check to see if we can encrypt emails when recipients are added
            magix.email.create-recipient-list
              magix.viewstate.get
                id=>magix.email.recipients
              _recipients
              for-each=>[magix.viewstate.get][value][to]
                add=>[/][_recipients]
                  value=>
                    value=>[.].value
              using=>magix.cryptography
                can-encrypt
                  emails=>[_recipients]
              if=>not-equals
                lhs=>[using][can-encrypt][value].value
                rhs=>true
                code
                  // disabling encrypt button, and making sure it is not in encrypt state
                  magix.forms.set-enabled
                    form-id=>composer
                    id=>encrypt
                    value=>false
                  magix.forms.set-class
                    form-id=>composer
                    id=>encrypt
                    value=>span-2 encrypt-email
              else
                // enabling encrypt button, and making sure it is set to encrypt state
                magix.forms.set-enabled
                  form-id=>composer
                  id=>encrypt
                  value=>true
                magix.forms.set-class
                  form-id=>composer
                  id=>encrypt
                  value=>span-2 encrypted-email

          onfirstload

            // making sure encrypt button is disabled by default, unless recipients are explicitly given
            magix.viewstate.get
              id=>magix.email.recipients
            if=>not-exist
              lhs=>[magix.viewstate.get][value]
              code

                // no recipients, disabling the encrypt button until we have recipients for our email
                magix.forms.set-enabled
                  form-id=>composer
                  id=>encrypt
                  value=>false
                magix.forms.set-class
                  form-id=>composer
                  id=>encrypt
                  value=>span-2 encrypt-email

          onclick

            magix.forms.get-class
              form-id=>composer
              id=>encrypt
            index-of=>[magix.forms.get-class][value].value
              what=>encrypt-email
            if=>exist
              lhs=>[index-of][result]
              code
                // adding encryption to email
                magix.forms.set-class
                  form-id=>composer
                  id=>encrypt
                  value=>span-2 encrypted-email
            else
              // removing encryption from email
              magix.forms.set-class
                form-id=>composer
                id=>encrypt
                value=>span-2 encrypt-email
              magix.viewport.show-message
                message=>this will send your email in plain text, which is usually a bad idea.&nbsp;&nbsp;click the encrypt button again to change your mind
                color=>#ffddaa
                time=>7000
            magix.forms.effect
              form-id=>composer
              id=>compose-subject
              type=>focus-and-select
        button=>cancel
          class=>span-2 last
          value=>cancel
          onclick
            magix.viewport.clear-controls
              container=>content3
              reset-class=>true
    lambda=>create-file-uploader
      oncreatecontrols
        _lb
          uploader=>attachments-uploader
            class=>mux-file-uploader
            onuploaded
              magix.viewstate.get
                id=>magix.email.attachments
              split=>[$][filename].value
                what=>/
              if=>exist
                lhs=>[magix.viewstate.get][value]
                code
                  if=>exist
                    lhs=>[magix.viewstate.get][value][[split][result][>last].value]
                    code
                      magix.viewport.show-message
                        message=>file already attached to email
                        color=>#ffaaaa
                      stop
                  add=>[magix.viewstate.set][value]
                    values=>[magix.viewstate.get][value]
              add=>[magix.viewstate.set][value]
                value=>[split][result][>last].value
                  value=>[$][filename].value
              magix.viewstate.set
                id=>magix.email.attachments
              magix.email.create-attachment-list
              magix.forms.effect
                form-id=>composer
                id=>compose-subject
                type=>focus-and-select

        // getting atttachment directory
        magix.data.load
          id=>magix.email.attachments-directory
        magix.session.get
          id=>magix.core.user
        set=>[_attachment-directory].value
          value=>{0}/{1}
            =>[magix.data.load][value][directory].value
            =>[magix.session.get][value][username].value
        _attachment-directory

        // making sure attachment directory exist
        using=>magix.file
          directory-exist
            directory=>[_attachment-directory].value
        if=>not-equals
          lhs=>[using][directory-exist][value].value
          rhs=>true
          code
            using=>magix.file
              create-directory
                directory=>[_attachment-directory].value

        // setting uploader's directory to user's attachment directory
        set=>[_lb][uploader][directory].value
          value=>[_attachment-directory].value
        add=>[$]
          values=>[_lb]

