
event:magix.execute

inspect=>@"loads up the widget toolbox for forms builder"

list-widget-types

_buffer

_onclick
  get-selected-widget
  magix.forms.get-value
    form-id=>toolbox-controls
    id=>before
  if=>[/][magix.forms.get-value][value].Value==True
    set=>[/][try][code][add-widget][where][position].Value
      value=>before
  else
    magix.forms.get-value
      form-id=>toolbox-controls
      id=>child
    if=>[/][else][magix.forms.get-value][value].Value==True
      set=>[/][try][code][add-widget][where][position].Value
        value=>child
    else
      set=>[/][try][code][add-widget][where][position].Value
        value=>after
  set=>[/][try][code][add-widget][where][dna].Value
    value=>[/][get-selected-widget][value][dna].Value
  try
    code
      add-widget
        auto-select=>True
        where
          dna
          position
        widget
          type
      magix.ide.update-widget-list
    catch
      magix.viewport.show-message
        header=>error
        message=>@"that widget probably don't support children.&nbsp;&nbsp;
choose either before or after as the position of the node"
        time=>1000

for-each=>[/][list-widget-types][types]
  while=>[/][_buffer].Count>0
    set=>[/][_buffer][0]
  set=>[/][_buffer][link-button].Value
    value=>[.][type].Value
  set=>[/][_buffer][link-button][text].Value
    value=>[.][type].Value
  set=>[/][_buffer][link-button][css].Value
    value=>span3
  set=>[/][_onclick][try][code][add-widget][widget][type].Value
    value=>[.][type].Value
  add=>[/][_buffer][link-button]
    value=>[/][_onclick]
  set=>[/][_buffer][link-button][_onclick].Name
    value=>onclick
  add=>[/][magix.forms.create-form][controls][panel:2][controls]
    value=>[/][_buffer][0]

magix.forms.create-form
  form-id=>toolbox-controls
  container=>menu
  controls
    label=>header
      tag=>h4
      text=>widgets
    panel=>wrpCpy
      css=>input-append input-prepend span12
      controls
        button=>copy
          text=>copy
          css=>btn btn-primary
          onclick
            copy-widget
        button=>cut
          text=>cut
          css=>btn btn-primary
          onclick
            cut-widget
        button=>paste
          text=>paste
          css=>btn btn-primary
          onclick
            magix.forms.get-value
              form-id=>toolbox-controls
              id=>before
            if=>[/][magix.forms.get-value][value].Value==True
              set=>[/][try][code][paste-widget][position].Value
                value=>before
            else
              magix.forms.get-value
                form-id=>toolbox-controls
                id=>child
              if=>[/][else][magix.forms.get-value][value].Value==True
                set=>[/][try][code][paste-widget][position].Value
                  value=>child
              else
                set=>[/][try][code][paste-widget][position].Value
                  value=>after
            try
              code
                paste-widget
                  auto-select=>True
                magix.ide.update-widget-list
              catch
                magix.viewport.show-message
                  header=>error
                  message=>@"that widget probably don't support children.&nbsp;&nbsp;
choose either before or after as the position of the node"
                  time=>1000
    label=>expl
      text=>insert ...
      css=>span12
    panel=>rdoWrp
      css=>input-append span12
      controls
        radio=>before
          group=>where
          css=>left
        label=>beforeLbl
          tag=>label
          for=>before
          text=>before
          css=>left right-20
        radio=>child
          group=>where
          checked=>true
          css=>left
        label=>childLbl
          tag=>label
          for=>child
          text=>child
          css=>left right-20
        radio=>after
          group=>where
          css=>left
        label=>afterLbl
          tag=>label
          for=>after
          text=>after
          css=>left right-20
    panel=>wrpTypes
      css=>span11
      controls
    label=>ruler
      tag=>hr
      css=>span11
