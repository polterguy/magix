

inspect=>@"contains add controls parts of form builder

used to create the form builder.  contains the controls necessary 
to create the add controls parts of the form builder"

// creates the panel that wraps all the controls in the system
// for the user to double click to add a control to the design surface
panel=>controls-wrapper
  class=>span-15 last boxed air-padding bottom-1 top-1
  controls

    // descriptive header
    label=>lbl-controls-header
      value=>click the control you wish to add
      class=>span-15 bottom-1 last
      tag=>h3

    // creates all the different add-control buttons
    lambda=>lambda-controls
      oncreatecontrols
        _data
          button
            onclick
              try
                code
                  // creates the magix.ide.add-control code to execute according to user selections, 
                  // and which button was clicked
                  add=>[@]
                    value=>[:magix.ide.add-control-to-surface[$]][magix.ide.add-control]
                catch
                  set=>[@][magix.viewport.show-message][message].value
                    value=>[@][exception].value
                  magix.viewport.show-message
                    color=>#ffbbbb
        list-events
          begins-with=>magix.forms.controls
        for-each=>[list-events][events]
          set=>[/][_data][button][value].value
            value=>[.].name
          replace=>[/][_data][button][value].value
            what=>magix.forms.controls.
          using=>magix.math
            modulo
              add
                =>[$].count
                =>1
              =>5
          if=>equals
            lhs=>[@][using][modulo].value
            rhs=>0
            code
              set=>[/][_data][button][class].value
                value=>span-3 last toolbox-{0}
                  =>[/][_data][button][value].value
          else
            set=>[/][_data][button][class].value
              value=>span-3 toolbox-{0}
                =>[/][_data][button][value].value
          add=>[$]
            value=>[/][_data][button]

    panel=>panel-position
      class=>span-15 last
      controls

        // spacer, horizontal ruler, for making some space between different segments
        label=>spacer
          tag=>hr
          class=>span-15 last clear bottom-1 top-1

        // desription of how to add a control, drop down list allowing
        // user to decide where to add, and the clear controls button
        label=>lbl-controls-description-begin
          class=>span-1 clear
          value=>add
          tag=>label
        select=>select-add-where
          class=>span-2
          items
            before=>before
            after=>after
            child=>as child of
          onselectedindexchanged
            set=>[magix.data.save-username][value][position].value
              value=>[$][value].value
            magix.data.save-username
              id=>magix.ide.where-position
          onfirstload
            magix.data.load-username
              id=>magix.ide.where-position
            if=>exist
              lhs=>[magix.data.load-username][value]
              code
                set=>[magix.forms.set-value][value].value
                  value=>[magix.data.load-username][value][position].value
            magix.forms.set-value
              form-id=>add-control
              id=>select-add-where
              value=>after
        label=>lbl-controls-description-end
          class=>span-6
          value=>selected control
          tag=>label
        button=>btn-clear-controls
          class=>span-3 last right
          value=>clear controls
          onclick
            magix.viewport.confirm
              message=>are you sure you wish to clear all controls from your form?
              code
                try
                  code
                    // in case surface has been disabled
                    magix.ide.enable-surface
                      value=>true
                    magix.ide.clear-controls
                  catch
                    set=>[@][magix.viewport.show-message][message].value
                      value=>[@][exception].value
                    magix.viewport.show-message
                      color=>#ffbbbb

