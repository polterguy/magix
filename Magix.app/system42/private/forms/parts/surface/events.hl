

inspect=>@"contains main surface active events for form builder

used to create the form builder.  contains the active events necessary 
to create the surface active events parts of the form builder"

// raised by the surface active module when something is changed on the design surface
// we're just storing the current form in our database, for later reference
magix.ide.surface-changed

  // saving current design area controls to database by user
  magix.ide.get-form
  if=>equals
    lhs=>[magix.ide.get-form].Count
    rhs=>0
    code
      magix.data.remove-username
        id=>magix.ide.current-surface-controls
  else
    add=>[@][magix.data.save-username][value]
      value=>[magix.ide.get-form]
      children-only=>true
    magix.data.save-username
      id=>magix.ide.current-surface-controls

// creating properties for selected control
magix.ide.control-selected
  magix.ide.request-properties

// creating properties for selected control
magix.ide.request-properties
  magix.ide.get-selected-control
  if=>exist
    lhs=>[magix.ide.get-selected-control][dna]
    code

      // creating properties web part
      magix.forms.create-web-part
        form-id=>control-properties
        container=>control-properties
        class=>span-20 last boxed top-1 air-padding
        controls
          label=>lbl-properties
            tag=>h3
            value=>properties

          // control containing actual properties
          lambda=>lambda-properties
            oncreatecontrols

              // finding selected control, and stuffing its properties out into the properties web part
              magix.ide.get-selected-control
              set=>[_evt-inspect].Name
                value=>magix.forms.controls.{0}
                  =>[magix.ide.get-selected-control][value][0].Name
              _evt-inspect
                inspect

              // looping through each control property
              for-each=>[**inspect=>?][..][**controls][0]
                index-of=>[.].Name
                  what=>on
                if=>not-exist
                  lhs=>[@][index-of][result]
                  and=>not-equals
                    lhs=>[.].Name
                    rhs=>items
                  code
                    // not event handler
                    set=>[@][_params]
                    set=>[@][_params][dna]
                      value=>[/][magix.ide.get-selected-control][dna]
                    if=>exist
                      lhs=>[/][magix.ide.get-selected-control][value][0][[.].Name]
                      code
                        set=>[@][..][..][_params][content].Value
                          value=>[/][magix.ide.get-selected-control][value][0][[.].Name].Value
                    add=>[@][_params][field-type]
                      value=>[.]
                    add=>[$]
                      value=>[:magix.ide.create-control-property-modifier-control[@][_params]][control]
                      children-only=>true

              // creating spacer between properties and events
              _hr
                label
                  tag=>hr
                  class=>span-20 last clear top-1 bottom-1
                label
                  tag=>h3
                  class=>span-20 last clear bottom-1
                  value=>events
              add=>[$]
                value=>[_hr][label]
              add=>[$]
                value=>[_hr][label:1]

              // looping through each control event and items collection
              for-each=>[**inspect=>?][..][**controls][0]
                index-of=>[.].Name
                  what=>on
                if=>exist
                  lhs=>[@][index-of][result]
                  or=>equals
                    lhs=>[.].Name
                    rhs=>items
                  code
                    // not event handler
                    set=>[@][_params]
                    set=>[@][_params][dna]
                      value=>[/][magix.ide.get-selected-control][dna]
                    if=>exist
                      lhs=>[/][magix.ide.get-selected-control][value][0][[.].Name]
                      code
                        add=>[@][..][..][_params][content]
                          value=>[/][magix.ide.get-selected-control][value][0][[.].Name]
                          children-only=>true
                    add=>[@][_params][field-type]
                      value=>[.]
                    add=>[$]
                      value=>[:magix.ide.create-control-event-modifier-control[@][_params]][control]
                      children-only=>true
      magix.browser.scroll
        id=>editor-toolbar
  else

    // clearing properties web part
    magix.viewport.clear-controls
      container=>control-properties

// creates one property modifier for properties web part
magix.ide.create-control-property-modifier-control
  _data
    panel
      class=>span-6
      controls
        label
          class=>span-3
          tag=>label
          value=>{field-type-name}
        text-box
          class=>span-3 last
          placeholder=>{field-type-value}
          value=>{value}
          ontextchanged
            _field-name=>{field-type-name}
            _dna=>{dna}
            set=>[magix.ide.change-control][dna].Value
              value=>[_dna].Value
            if=>not-equals
              lhs=>[$][value].Value
              rhs=>
              code
                set=>[magix.ide.change-control][change][[_field-name].Value].Value
                  value=>[$][value].Value
            else
              set=>[magix.ide.change-control][change][[_field-name].Value].Name
                value=>[_field-name].Value
            magix.ide.change-control
  iterate=>[_data]
    if=>equals
      lhs=>[.].Value
      rhs=>{dna}
      code
        set=>[.].Value
          value=>[$][dna].Value
    else-if=>equals
      lhs=>[.].Value
      rhs=>{value}
      code
        set=>[.].Value
          value=>[$][content].Value
    else-if=>equals
      lhs=>[.].Value
      rhs=>{field-type-value}
      code
        set=>[.].Value
          value=>[$][field-type][0].Value
    else-if=>equals
      lhs=>[.].Value
      rhs=>{field-type-name}
      code
        set=>[.].Value
          value=>[$][field-type][0].Name
  if=>equals
    lhs=>[$][field-type][0].Name
    rhs=>text
    code
      set=>[_data][panel][class].Value
        value=>12
      set=>[_data][panel][controls][text-box].Name
        value=>text-area
      set=>[_data][panel][controls][text-area][class].Value
        value=>span-10 last
      set=>[_data][panel][controls][text-area][rows].Value
        value=>5
  add=>[$][control]
    value=>[_data][panel]

// creates one property modifier for properties web part
magix.ide.create-control-event-modifier-control
  _data
    panel
      class=>span-20 last
      controls
        link-button
          class=>span-3
          tag=>label
          value=>{field-type-name}
          onclick
            magix.forms.get-style
              form-id=>control-properties
              id=>{field-type-name}
              key=>display
            if=>equals
              lhs=>[magix.forms.get-style][value].Value
              rhs=>none
              code
                magix.forms.effect
                  type=>roll-down
                  time=>250
                  form-id=>control-properties
                  id=>{field-type-name}
            else
              magix.forms.effect
                type=>roll-up
                time=>250
                form-id=>control-properties
                id=>{field-type-name}
        text-area=>{field-type-name}
          style=>display:none;
          class=>span-17 last
          rows=>10
          placeholder=>{field-type-value}
          value=>{value}
          ontextchanged
            _field-name=>{field-type-name}
            _dna=>{dna}
            set=>[magix.ide.change-control][dna].Value
              value=>[_dna].Value
            if=>not-equals
              lhs=>[$][value].Value
              rhs=>
              code
                set=>[@][code-2-node][code].Value
                  value=>[$][value].Value
                code-2-node
                  remove-root=>true
                add=>[magix.ide.change-control][change][[_field-name].Value]
                  value=>[@][code-2-node][node]
                  children-only=>true
            else
              set=>[magix.ide.change-control][change][[_field-name].Value].Name
                value=>[_field-name].Value
            magix.ide.change-control
  iterate=>[_data]
    if=>equals
      lhs=>[.].Value
      rhs=>{dna}
      code
        set=>[.].Value
          value=>[$][dna].Value
    else-if=>equals
      lhs=>[.].Value
      rhs=>{value}
      code
        if=>equals
          lhs=>[$][content].Count
          rhs=>0
          or=>not-exist
            lhs=>[$][content]
          code
            set=>[.].Value
        else
          set=>[/][_data][panel][controls][link-button][style].Value
            value=>font-weight:bold;
          node-2-code
            remove-root=>true
            node=>[$][content]
          set=>[.].Value
            value=>[@][node-2-code][code].Value
    else-if=>equals
      lhs=>[.].Value
      rhs=>{field-type-value}
      code
        set=>[.].Value
          value=>[$][field-type][0].Value
    else-if=>equals
      lhs=>[.].Value
      rhs=>{field-type-name}
      code
        set=>[.].Value
          value=>[$][field-type][0].Name
  add=>[$][control]
    value=>[_data][panel]

