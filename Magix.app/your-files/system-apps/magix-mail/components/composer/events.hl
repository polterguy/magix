

inspect=>@"active events for compose.hl file

contains active events for the compose email web part"


// adds up a recipient to an email being composed
magix.email.add-recipient

  // checking to see if we have a smaller surface to display our add-recipient web-part on
  magix.viewstate.get
    id=>magix.email.smaller-composer
  if=>equals
    lhs=>[magix.viewstate.get][value].value
    rhs=>true
    code
      set=>[magix.forms.create-web-part][class].value
        value=>span-14 last bottom-1
      set=>[magix.forms.create-web-part][**panel=>btn-group-add-recipient][class].value
        value=>btn-group span-14
      set=>[magix.forms.create-web-part][**text-box=>recipient][class].value
        value=>span-8

  magix.forms.create-web-part
    container=>dynamic-add-recipient
    form-id=>dynamic-add-recipient
    class=>span-17 last bottom-1
    controls
      panel=>btn-group-add-recipient
        class=>btn-group span-17
        default=>recipient-to
        controls

          lambda=>lambda-recipient-text-box
            oncreatecontrols
              // includes necessary javascript files for our autocompleter
              magix.viewport.include-client-file
                type=>javascript
                file=>media/bootstrap/js/jQuery.js
              magix.viewport.include-client-file
                type=>javascript
                file=>media/bootstrap/js/bootstrap.min.js
              magix.viewport.include-client-file
                type=>css
                file=>media/back-end/typeahead.css
              _json=>
              magix.data.load
                prototype
                  type=>magix.email.contact
              for-each=>[magix.data.load][objects]
                if=>not-equals
                  lhs=>[/][_json].value
                  rhs=>
                  code
                    set=>[/][_json].value
                      value=>{0},
                        =>[/][_json].value
                if=>exist
                  lhs=>[.][value][display-name].value
                  and=>not-equals
                    lhs=>[.][value][display-name].value
                    rhs=>
                  code
                    set=>[/][_json].value
                      value=>@"{0}""{1} - {2}"""
                        =>[/][_json].value
                        =>[.][value][display-name].value
                        =>[.][value][email].value
                else
                  set=>[/][_json].value
                    value=>@"{0}""{1}"""
                      =>[/][_json].value
                      =>[.][value][email].value
              _text
                text-box=>recipient
                  placeholder=>recipient ...
                  class=>span-11
                  autocomplete=>false
                  @data-provide=>typeahead
                  @data-items=>10
                  @data-source=>{data-source}
                  onfirstload
                    magix.forms.effect
                      form-id=>dynamic-add-recipient
                      id=>recipient
                      type=>focus-and-select
                  onesc
                    magix.viewport.clear-controls
                      reset-class=>true
                      container=>dynamic-add-recipient
                    magix.forms.effect
                      form-id=>composer
                      id=>compose-subject
                      type=>focus-and-select
              set=>[**@data-source=>{data-source}].value
                value=>\[{0}]
                  =>[_json].value
              add=>[$]
                values=>[_text]

          button=>recipient-to
            class=>span-2
            value=>to
            onclick
              magix.email.add-recipient-to-email
                field=>to
          button=>recipient-cc
            class=>span-2
            value=>cc
            onclick
              magix.email.add-recipient-to-email
                field=>cc
          button=>recipient-bcc
            class=>span-2 last
            value=>bcc
            onclick
              magix.email.add-recipient-to-email
                field=>bcc


// adds up a recipient to email
magix.email.add-recipient-to-email
  magix.viewstate.get
    id=>magix.email.recipients
  if=>exist
    lhs=>[magix.viewstate.get][value]
    code
      add=>[magix.viewstate.set][value]
        values=>[magix.viewstate.get][value]
  magix.forms.get-value
    form-id=>dynamic-add-recipient
    id=>recipient
  index-of=>[magix.forms.get-value][value].value
    what=>@" - "
  if=>exist
    lhs=>[index-of][result]
    code
      split=>[magix.forms.get-value][value].value
        where=>[index-of][result][>last].value
      using=>magix.math
        add=>[index-of][result][>last]
          =>[index-of][result][>last].value
          =>3
      split=>[magix.forms.get-value][value].value
        where=>[index-of][result][>last].value
      set=>[_recipient][].value
        value=>[@][split:1][result][1].value
      set=>[_recipient][][display-name].value
        value=>[@][split:0][result][0].value
  else
    set=>[_recipient][].value
      value=>[magix.forms.get-value][value].value
  _recipient
  for-each=>[magix.viewstate.get][value]
    for-each=>[.]
      if=>equals
        lhs=>[.].value
        rhs=>[/][_recipient][0].value
        code
          magix.viewport.show-message
            message=>recipient already in to, cc or bcc field of email
            color=>#ffaaaa
          set=>[/][_stop].value
            value=>true
          magix.forms.effect
            form-id=>dynamic-add-recipient
            id=>recipient
            type=>focus-and-select
  if=>equals
    lhs=>[_stop].value
    rhs=>true
    code
      stop
  _stop
  add=>[magix.viewstate.set][value][[$][field].value]
    values=>[_recipient]
  magix.viewstate.set
    id=>magix.email.recipients
  magix.viewport.clear-controls
    reset-class=>true
    container=>dynamic-add-recipient
  magix.email.create-recipient-list


// creates visual list of recipients
magix.email.create-recipient-list
  magix.viewstate.get
    id=>magix.email.recipients
  _controls
    label=>label-to
      class=>span-1
      value=>to:
      tag=>label
    panel=>recipients-to
      class=>span-17 last
    label=>label-cc
      class=>span-1
      value=>cc:
      tag=>label
    panel=>recipients-cc
      class=>span-17 last
    label=>label-bcc
      class=>span-1
      value=>bcc:
      tag=>label
    panel=>recipients-bcc
      class=>span-17 last
  if=>more-than
    lhs=>[magix.viewstate.get][value][to].count
    rhs=>0
    code
      for-each=>[magix.viewstate.get][value][to]
        _lb
          panel
            class=>column email-sent-to-item
            controls
              label
                style=>display:block;float:left;
              link-button
                value=>X
                style=>margin-left:10px;display:block;float:left;
                onclick
                  set=>[magix.emails.remove-recipient][email].value
                    value=>[$][info].value
                  magix.emails.remove-recipient
                    field=>to
        set=>[@][_lb][**link-button][info].value
          value=>[.].value
        if=>exist
          lhs=>[.][display-name]
          code
            set=>[@][..][..][_lb][**label][value].value
              value=>{0} &lt;{1}&gt;
                =>[.][display-name].value
                =>[.].value
        else
          set=>[@][..][_lb][**label][value].value
            value=>[.].value
        add=>[/][_controls][**panel=>recipients-to][controls]
          values=>[@][_lb]
  else
    set=>[_controls][**label=>label-to]
    set=>[_controls][**panel=>recipients-to]
  if=>more-than
    lhs=>[magix.viewstate.get][value][cc].count
    rhs=>0
    code
      for-each=>[magix.viewstate.get][value][cc]
        _lb
          panel
            class=>column email-sent-to-item
            controls
              label
                style=>display:block;float:left;
              link-button
                value=>X
                style=>margin-left:10px;display:block;float:left;
                onclick
                  set=>[magix.emails.remove-recipient][email].value
                    value=>[$][info].value
                  magix.emails.remove-recipient
                    field=>cc
        set=>[@][_lb][**link-button][info].value
          value=>[.].value
        if=>exist
          lhs=>[.][display-name]
          code
            set=>[@][..][..][_lb][**label][value].value
              value=>{0} &lt;{1}&gt;
                =>[.][display-name].value
                =>[.].value
        else
          set=>[@][..][_lb][**label][value].value
            value=>[.].value
        add=>[/][_controls][**panel=>recipients-cc][controls]
          values=>[@][_lb]
  else
    set=>[_controls][**label=>label-cc]
    set=>[_controls][**panel=>recipients-cc]
  if=>more-than
    lhs=>[magix.viewstate.get][value][bcc].count
    rhs=>0
    code
      for-each=>[magix.viewstate.get][value][bcc]
        _lb
          panel
            class=>column email-sent-to-item
            controls
              label
                style=>display:block;float:left;
              link-button
                value=>X
                style=>margin-left:10px;display:block;float:left;
                onclick
                  set=>[magix.emails.remove-recipient][email].value
                    value=>[$][info].value
                  magix.emails.remove-recipient
                    field=>bcc
        set=>[@][_lb][**link-button][info].value
          value=>[.].value
        if=>exist
          lhs=>[.][display-name]
          code
            set=>[@][..][..][_lb][**label][value].value
              value=>{0} &lt;{1}&gt;
                =>[.][display-name].value
                =>[.].value
        else
          set=>[@][..][_lb][**label][value].value
            value=>[.].value
        add=>[/][_controls][**panel=>recipients-bcc][controls]
          values=>[@][_lb]
  else
    set=>[_controls][**label=>label-bcc]
    set=>[_controls][**panel=>recipients-bcc]
  if=>more-than
    lhs=>[_controls].count
    rhs=>0
    code
      using=>magix.forms
        create-web-part
          container=>dynamic-recipients
          class=>span-18 last bottom-1
          controls=>[_controls]
  else
    magix.viewport.clear-controls
      container=>dynamic-recipients
      reset-class=>true
  if=>more-than
    lhs=>[_controls].count
    rhs=>0
    code
      magix.forms.effect
        form-id=>composer
        id=>compose-subject
        type=>focus-and-select
  

// removes a recipient from the to/cc/bcc list of recipients
magix.emails.remove-recipient
  magix.viewstate.get
    id=>magix.email.recipients
  set=>[magix.viewstate.get][value][[$][field].value][?=>[$][email].value]
  add=>[magix.viewstate.set][value]
    values=>[magix.viewstate.get][value]
  if=>equals
    lhs=>[magix.viewstate.set][value].count
    rhs=>0
    or=>equals
      lhs=>[magix.viewstate.set][value][to].count
      rhs=>0
      or=>not-exist
        lhs=>[magix.viewstate.set][value][to]
      and=>equals
        lhs=>[magix.viewstate.set][value][cc].count
        rhs=>0
        or=>not-exist
          lhs=>[magix.viewstate.set][value][cc]
      and=>equals
        lhs=>[magix.viewstate.set][value][bcc].count
        rhs=>0
        or=>not-exist
          lhs=>[magix.viewstate.set][value][bcc]
    code
      set=>[magix.viewstate.set][value]
  magix.viewstate.set
    id=>magix.email.recipients
  magix.email.create-recipient-list
  if=>not-exist
    lhs=>[magix.viewstate.set][value]
    code
      magix.email.add-recipient


// creates list of attachments in email
magix.email.create-attachment-list
  magix.viewstate.get
    id=>magix.email.attachments
  _controls
    label
      value=>attachments
      class=>span-10 last
      tag=>label
  if=>more-than
    lhs=>[magix.viewstate.get][value].count
    rhs=>0
    code
      // creating attachments web part
      for-each=>[magix.viewstate.get][value]
        _lb
          panel
            class=>clear span-10 last email-attachment
            controls
              label
                class=>span-9
              link-button
                class=>span-1 last text-right
                value=>X
                onclick
                  magix.viewstate.get
                    id=>magix.email.attachments
                  set=>[magix.viewstate.get][value][[$][info].value]
                  if=>more-than
                    lhs=>[magix.viewstate.get][value].count
                    rhs=>0
                    code
                      add=>[magix.viewstate.set][value]
                        values=>[magix.viewstate.get][value]
                  magix.viewstate.set
                    id=>magix.email.attachments
                  magix.email.create-attachment-list
                  magix.forms.effect
                    form-id=>composer
                    id=>compose-subject
                    type=>focus-and-select
        set=>[@][_lb][**label][value].value
          value=>[.].name
        set=>[@][_lb][**link-button][info].value
          value=>[.].name
        add=>[/][_controls]
          values=>[@][_lb]
      using=>magix.forms
        create-web-part
          container=>compose-attachments
          form-id=>compose-attachments
          class=>span-18 last
          controls=>[_controls]
  else
    // clearing attachments web part
    magix.viewport.clear-controls
      container=>compose-attachments
      reset-class=>true


// returns [value] as true if composer is already loaded
magix.email.is-composer-loaded
  set=>[$][value].value
    value=>true


// adds [to], [cc] or [bcc] recipient to existing email composer
magix.email.add-new-recipient
  magix.viewstate.get
    id=>magix.email.recipients
  iterate=>[magix.viewstate.get][value]
    if=>equals
      lhs=>[.].name
      rhs=>
      and=>exist
        lhs=>[$][to][=>[.].value]
        or=>exist
          lhs=>[$][cc][=>[.].value]
        or=>exist
          lhs=>[$][bcc][=>[.].value]
      code
        magix.viewport.show-message
          message=>recipient already added
          color=>#ffaaaa
        set=>[/][_stop].value
          value=>true
  _stop
  if=>equals
    lhs=>[_stop].value
    rhs=>true
    code
      stop
  if=>exist
    lhs=>[magix.viewstate.get][value]
    code
      add=>[magix.viewstate.set][value]
        values=>[magix.viewstate.get][value]
  if=>exist
    lhs=>[$][to]
    code
      add=>[magix.viewstate.set][value][to]
        values=>[$][to]
  if=>exist
    lhs=>[$][cc]
    code
      add=>[magix.viewstate.set][value][cc]
        values=>[$][cc]
  if=>exist
    lhs=>[$][bcc]
    code
      add=>[magix.viewstate.set][value][bcc]
        values=>[$][bcc]
  magix.viewstate.set
    id=>magix.email.recipients
  magix.email.create-recipient-list
  magix.viewport.clear-controls
    container=>dynamic-add-recipient
    reset-class=>true


// sends composed email
magix.email.send-composed-email
  magix.viewstate.get=>recipients
    id=>magix.email.recipients
  if=>not-exist
    lhs=>[magix.viewstate.get=>recipients][value][to]
    or=>equals
      lhs=>[magix.viewstate.get=>recipients][value][to].count
      rhs=>0
    code
      magix.viewport.show-message
        message=>your email must have at least one to recipient.&nbsp;&nbsp;click the plus button to add recipients to your email
        color=>#ffaaaa
      stop
  using=>magix.forms
    get-value=>subject
      form-id=>composer
      id=>compose-subject
    get-value=>body
      form-id=>composer
      id=>compose-body
  if=>equals
    lhs=>[using][get-value=>subject][value].value
    rhs=>
    code
      magix.viewport.show-message
        message=>your email has no subject
        color=>#ffaaaa
      magix.forms.effect
        form-id=>composer
        id=>compose-subject
        type=>focus-and-select
      stop
  if=>equals
    lhs=>[using][get-value=>body][value].value
    rhs=>
    code
      magix.viewport.show-message
        message=>your email has no body
        color=>#ffaaaa
      magix.forms.effect
        form-id=>composer
        id=>compose-body
        type=>focus-and-select
      stop
  magix.viewstate.get=>attachments
    id=>magix.email.attachments
  magix.data.load-username=>smtp-settings
    id=>magix.smtp.settings
  set=>[magix.smtp.send-message][from].value
    value=>[magix.data.load-username=>smtp-settings][value][email].value
  set=>[magix.smtp.send-message][from][display-name].value
    value=>[magix.data.load-username=>smtp-settings][value][display-name].value
  set=>[magix.data.load-username=>smtp-settings][value][email]
  add=>[magix.smtp.send-message]
    values=>[magix.data.load-username=>smtp-settings][value]

  // to recipients
  for-each=>[magix.viewstate.get=>recipients][value][to]
    add=>[/][magix.smtp.send-message][to]
      value=>
        value=>[.].value
    if=>exist
      lhs=>[.][display-name].value
      code
        add=>[/][magix.smtp.send-message][to][>last]
          value=>display-name
            value=>[.][display-name].value

  // cc recipients
  for-each=>[magix.viewstate.get=>recipients][value][cc]
    add=>[/][magix.smtp.send-message][cc]
      value=>
        value=>[.].value
    if=>exist
      lhs=>[.][display-name].value
      code
        add=>[/][magix.smtp.send-message][cc][>last]
          value=>display-name
            value=>[.][display-name].value

  // bcc recipients
  for-each=>[magix.viewstate.get=>recipients][value][bcc]
    add=>[/][magix.smtp.send-message][bcc]
      value=>
        value=>[.].value
    if=>exist
      lhs=>[.][display-name].value
      code
        add=>[/][magix.smtp.send-message][bcc][>last]
          value=>display-name
            value=>[.][display-name].value

  // subject and body
  set=>[magix.smtp.send-message][subject].value
    value=>[**get-value=>subject][value].value
  set=>[magix.smtp.send-message][body][plain].value
    value=>[**get-value=>body][value].value

  // attachments
  for-each=>[magix.viewstate.get=>attachments][value]
    add=>[/][magix.smtp.send-message][attachments]
      value=>
        value=>[.].value

  // retrieving username
  magix.session.get
    id=>magix.core.user

  // checking to see if we should sign email
  magix.execute
    _sign
    magix.forms.get-class=>sign
      form-id=>composer
      id=>sign
    index-of=>[magix.forms.get-class=>sign][value].value
      what=>signed-email
    if=>exist
      lhs=>[index-of][result]
      code
        // adding signature to email
        set=>[_signed].value
          value=>true

    // checking to see if we should encrypt email
    _encrypt
    magix.forms.get-class=>encrypt
      form-id=>composer
      id=>encrypt
    index-of=>[magix.forms.get-class=>encrypt][value].value
      what=>encrypted-email
    if=>exist
      lhs=>[index-of:1][result]
      code
        // adding signature to email
        set=>[_encrypt].value
          value=>true
  if=>equals
    lhs=>[magix.execute][_signed].value
    rhs=>true
    code
      set=>[magix.data.save][value][signed].value
        value=>true
      set=>[magix.smtp.send-message][signed].value
        value=>true
  if=>equals
    lhs=>[magix.execute][_encrypt].value
    rhs=>true
    code
      set=>[magix.data.save][value][encrypt].value
        value=>true
      set=>[magix.smtp.send-message][encrypt].value
        value=>true

  // saving sent email
  set=>[magix.data.save][value][username].value
    value=>[magix.session.get][value][username].value
  set=>[magix.data.save][value][from]
    value=>[magix.smtp.send-message][from]
  set=>[magix.data.save][value][date].value
    value=>[:magix.date.now][value].value
  add=>[magix.data.save][value][to]
    values=>[magix.smtp.send-message][to]
  if=>exist
    lhs=>[magix.smtp.send-message][cc]
    code
      add=>[magix.data.save][value][cc]
        values=>[magix.smtp.send-message][cc]
  if=>exist
    lhs=>[magix.smtp.send-message][bcc]
    code
      add=>[magix.data.save][value][bcc]
        values=>[magix.smtp.send-message][bcc]
  set=>[magix.data.save][value][subject].value
    value=>[**get-value=>subject][value].value
  set=>[magix.data.save][value][body][plain].value
    value=>[**get-value=>body][value].value
  magix.data.save
    value
      type=>magix.email.message
      read=>true
      folder=>sent

  // sending email
  magix.smtp.send-message

  // showing user feedback, and clearing viewport container
  magix.viewport.show-message
    message=>email successfully sent
    color=>#aaffaa
  magix.viewport.clear-controls
    container=>content3
    reset-class=>true

