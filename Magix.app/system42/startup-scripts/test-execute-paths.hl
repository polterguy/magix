

inspect=>@"creates a couple of execute-with-path unit tests, with
hyper lisp statements.&nbsp;&nbsp;partially to serve as 
hyper lisp unit tests, partially because they're needed as unit tests, and also because
they serve as great examples for yourself, in your own code.&nbsp;&nbsp;
if you wish to create your own unit tests, make sure they're in the 
magix.test. namespace"


event=>magix.test.execute-path-1
  inspect=>@"checks to see if [execute] with path behaves correctly"
  code
    _in
      item1=>hello 1
      item2=>hello 2
      item3=>hello 3
    _exe
      for-each=>[_in]
        set=>[/][_out][[.].Name].Value
          value=>[.].Value
    execute=>[_exe]
    _out
    if=>[_out][1].Value!=hello 2
      throw=>@"[execute] didn't behave
correctly in magix.test.verify-execute-path-1


event=>magix.test.execute-path-with-internal-exception-handler
  inspect=>@"checks to see if [execute], with [try] and [throw] inside of itself behaves correctly"
  code
    _exe
      try
        code
          throw=>howdy
        catch
    execute=>[_exe]
    if=>[_exe][try][catch][exception].Value!=howdy
      throw=>@"magix.test.execute-path-with-internal-exception failed, 
wrong text of exception"


event=>magix.test.execute-path-with-external-exception-handler
  inspect=>@"checks to see if [execute] with [throw] is correctly catched outside of [execute] statement"
  code
    _exe
      throw=>howdy
    try
      code
        execute=>[/][_exe]
      catch
    if=>[try][catch][exception].Value!=howdy
      throw=>@"magix.test.execute-path-with-external-exception failed, 
wrong text of exception"


event=>magix.test.execute.path-with-parameters
  inspect=>@"checks to see if parameter passing into 
[execute], into and out from, is bahving correctly"
  code
    _exe
      if=>[$][message].Value!=howdy world
        throw=>unit test check parameters with execute path failed
      set=>[$][ret].Value
        value=>5
    execute=>[_exe]
      message=>howdy world
    if=>[execute][value].Value!=5
      throw=>value not returned from execute path
    if=>[execute][message].Value!=howdy world
      throw=>value not returned input nodes from execute path

event=>magix.test.execute.with-without-period
  inspect=>@"verifies that [execute] versus [magix.execute] 
correctly chops up node tree"
  code
    _dt=>test
    _exe1
      if=>[/][_dt].Value!=test
        throw=>_exe1 failed
    _exe2
      _dt=>not-root
      if=>[/][_dt].Value!=not-root
        throw=>_exe2 failed
    execute=>[_exe1]
    magix.execute=>[_exe2]

event=>magix.test.execute.with-without-period-2
  inspect=>@"verifies that [execute] versus [magix.execute] 
correctly chops up node tree"
  code
    _exe1
      _dt=>5
      execute
        set=>[@][..][_dt].Value
          value=>6
      if=>[_dt].Value!=6
        throw=>set inside of execute didn't work
    execute=>[_exe1]
    _exe2
      _dt=>5
      execute
        set=>[@][..][_dt].Value
          value=>6
      if=>[_dt].Value!=6
        throw=>set inside of execute didn't work
    magix.execute=>[_exe2]
    if=>[_exe1][_dt].Value!=6
      throw=>node not correctly changed by [execute]
    if=>[_exe2][_dt].Value!=5
      throw=>node not correctly changed by [execute]
    

