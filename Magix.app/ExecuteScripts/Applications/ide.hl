
event:magix.execute

inspect=>@"loads up the toolbox for 
the ide"

magix.viewport.clear-controls
  container=>modal
magix.viewport.clear-controls
  container=>content
magix.viewport.clear-controls
  container=>header
magix.viewport.clear-controls
  container=>menu

_data
  x=>y

magix.viewport.load-module
  container=>footer
  css=>span12
  name=>Magix.ide.Builder

list-forms

for-each=>[list-forms][forms]
  set=>[/][_data][0].Name
    value=>[.][name].Value
  set=>[/][_data][0].Value
    value=>[.][name].Value
  if=>[.][name].Value!=_default
    add=>[/][magix.forms.create-form][controls][select][items]
      value=>[/][_data][0]

load-form
  name=>_default

list-widgets

_data2
  x=>y

for-each=>[/][list-widgets][widgets]
  set=>[/][_data2][0].Name
    value=>[.][dna].Value
  set=>[/][_data2][0].Value
    value=>[.][type].Value
  add=>[/][magix.forms.create-form][controls][panel:1][controls][select][items]
    value=>[/][_data2][0]

magix.forms.create-form
  form-id=>toolbox-load-file
  container=>header
  css=>span12
  events
    magix.ide.update-widget-list
      _items
      _buffer
        x=>y
      get-selected-widget
      list-widgets
      while=>[/][_items].Count>0
        set=>[/][_items][0]
      set=>[/][_items][_default].Value
        value=>widgets in form ...
      for-each=>[/][list-widgets][widgets]
        set=>[/][_buffer][0].Name
          value=>[.][dna].Value
        set=>[/][_buffer][0].Value
          value=>[.][type].Value
        add=>[/][_items]
          value=>[/][_buffer][0]
      set=>[/][magix.forms.set-values][items]
        value=>[/][_items]
      set=>[/][magix.forms.set-values][_items].Name
        value=>items
      magix.forms.set-values
        form-id=>toolbox-load-file
        id=>widgetsDdl
        items
      set=>[/][magix.forms.set-value][value].Value
        value=>[/][get-selected-widget][value][dna].Value
      magix.forms.set-value
        form-id=>toolbox-load-file
        id=>widgetsDdl
    magix.forms.widget-selected
      set=>[/][magix.forms.set-value][value].Value
        value=>[/][_p][dna].Value
      magix.forms.set-value
        form-id=>toolbox-load-file
        id=>widgetsDdl
  controls
    label=>header
      tag=>h1
      text=>forms
      css=>span11
    button=>help
      css=>btn btn-primary span1 btn-large
      text=>?
      onclick
        magix.help.start-help
    select=>form
      css=>span2
      onselectedindexchanged
        select-widget
        magix.forms.get-value
          form-id=>toolbox-load-file
          id=>form
        set=>[/][magix.forms.set-value][value].Value
          value=>[/][magix.forms.get-value][value].Value
        magix.forms.set-value
          form-id=>toolbox-load-file
          id=>name
        set=>[/][if][load-form][name].Value
          value=>[/][magix.forms.get-value][value].Value
        if=>[/][if][load-form][name].Value!=_default
          load-form
        _items
        _buffer
          x=>y
        list-widgets
        while=>[/][_items].Count>0
          set=>[/][_items][0]
        set=>[/][_items][_default].Value
          value=>widgets in form ...
        for-each=>[/][list-widgets][widgets]
          set=>[/][_buffer][0].Name
            value=>[.][dna].Value
          set=>[/][_buffer][0].Value
            value=>[.][type].Value
          add=>[/][_items]
            value=>[/][_buffer][0]
        set=>[/][magix.forms.set-values][items]
          value=>[/][_items]
        set=>[/][magix.forms.set-values][_items].Name
          value=>items
        magix.forms.set-values
          form-id=>toolbox-load-file
          id=>widgetsDdl
          items
        magix.forms.set-value
          form-id=>toolbox-load-file
          id=>widgetsDdl
          value=>_default
      items
        _default=>select form ...
    panel=>wrp
      css=>input-append offset1
      default=>save
      controls
        text-box=>name
          css=>span2
          place-holder=>name ...
          text=>_default
        button=>save
          text=>save
          css=>btn btn-primary
          onclick
            magix.forms.get-value
              form-id=>toolbox-load-file
              id=>name
            set=>[/][save-form][name].Value
              value=>[/][magix.forms.get-value][value].Value
            save-form
            _data
              x=>y
            list-forms
            for-each=>[/][list-forms][forms]
              if=>[.][name].Value!=_default
                set=>[/][_data][0].Name
                  value=>[.][name].Value
                set=>[/][_data][0].Value
                  value=>[.][name].Value
                add=>[/][magix.forms.set-values][items]
                  value=>[/][_data][0]
            magix.forms.set-values
              form-id=>toolbox-load-file
              id=>form
              items
                _default=>select form ...
            set=>[/][magix.forms.set-value][value].Value
              value=>[/][magix.forms.get-value][value].Value
            magix.forms.set-value
              form-id=>toolbox-load-file
              id=>form
              value
            magix.viewport.show-message
              message=>form was saved
              header=>done
              time=>500
        button=>delete
          text=>delete
          css=>btn btn-primary
          onclick
            magix.viewport.verify
              header=>verify deletion of form
              button-text=>delete
              message=>@"please verify that you wish to 
delete the form.&nbsp;&nbsp;this action is not possible to undo"
              code
                magix.forms.get-value
                  form-id=>toolbox-load-file
                  id=>name
                set=>[/][delete-form][name].Value
                  value=>[/][magix.forms.get-value][value].Value
                delete-form
                _data
                  x=>y
                list-forms
                for-each=>[/][list-forms][forms]
                  if=>[.][name].Value!=_default
                    set=>[/][_data][0].Name
                      value=>[.][name].Value
                    set=>[/][_data][0].Value
                      value=>[.][name].Value
                    add=>[/][magix.forms.set-values][items]
                      value=>[/][_data][0]
                magix.forms.set-values
                  form-id=>toolbox-load-file
                  id=>form
                  items
                    _default=>select form ...
                set=>[/][magix.forms.set-value][value].Value
                  value=>_default
                magix.forms.set-value
                  form-id=>toolbox-load-file
                  id=>form
                  value
                select-widget
                magix.forms.set-value
                  form-id=>toolbox-load-file
                  id=>widgetsDdl
                  value=>_default
                load-form
                  name=>_default
                magix.forms.set-value
                  form-id=>toolbox-load-file
                  id=>name
                  value=>_default
                magix.viewport.clear-controls
                  container=>modal
    panel=>wrp2
      css=>input-append offset1
      controls
        select=>widgetsDdl
          css=>span2
          onselectedindexchanged
            magix.forms.get-value
              form-id=>toolbox-load-file
              id=>widgetsDdl
            set=>[/][select-widget][dna].Value
              value=>[/][magix.forms.get-value][value].Value
            select-widget
          items
            _default=>widgets in form ...
        button=>deleteWidget
          text=>delete widget
          css=>btn btn-primary
          onclick
            get-selected-widget
            set=>[/][remove-widget][dna].Value
              value=>[/][get-selected-widget][value][dna].Value
            remove-widget
            select-widget
            magix.ide.update-widget-list
    label=>ruler
      tag=>hr
      css=>span11

magix.admin.run-file
  file=>ExecuteScripts/Applications/ide-properties.hl