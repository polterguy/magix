

inspect=>@"creates the server log grid form, from 
where you can view the system log from"


if=>exist
  lhs=>[$][start]
  code
    set=>[magix.data.load][start].Value
      value=>[$][start].Value
    set=>[magix.data.load][end].Value
      value=>[$][end].Value

if=>exist
  lhs=>[$][filter].Value
  code
    set=>[magix.data.load][prototype][body].Value
      value=>@"%{0}%"
        pr0=>[$][filter].Value
    set=>[magix.data.count][prototype][body].Value
      value=>@"%{0}%"
        pr0=>[$][filter].Value
    set=>[magix.forms.create-web-part][controls][panel:1][controls][panel][controls][text-box][text].Value
      value=>[$][filter].Value
    set=>[magix.forms.create-web-part][controls][panel:1][controls][link-button:0][onclick][execute-file][filter].Value
      value=>[$][filter].Value
    set=>[magix.forms.create-web-part][controls][panel:1][controls][link-button:1][onclick][execute-file][filter].Value
      value=>[$][filter].Value

magix.data.load
  start=(int)>0
  end=(int)>10
  prototype
    type=>magix.log.item

_dt
  panel
    controls
      label
        css=>span-13
        tag=>label
        text
      label
        css=>right
      label
        css=>span-17 last
        text

for-each=>[magix.data.load][objects]
  set=>[/][_dt][panel][controls][label:0][text].Value
    value=>[.][header].Value
  set=>[/][_dt][panel][controls][label:1][text].Value
    value=>[.][date].Value
  set=>[/][_dt][panel][controls][label:2][text].Value
    value=>[.][body].Value
  if=>equals
    lhs=>[.][error].Value
    rhs=>true
    code
      set=>[/][_dt][panel][css].Value
        value=>span-17 boxed-light-red air-padding bottom-1 relative
  else
    set=>[/][_dt][panel][css].Value
      value=>span-17 boxed-light-green air-padding bottom-1 relative
  if=>exist
    lhs=>[.][code]
    code
      set=>[@][node-2-code][node]
      while=>more-than
        lhs=>[.][code].Count
        rhs=>0
        code
          add=>[@][..][node-2-code][node]
            value=>[.][code][0]
          set=>[.][code][0]
      node-2-code
      set=>[/][_dt][panel][controls][label:2][text].Value
        value=>@"{0} <pre>{1}</pre>"
          pr0=>[/][_dt][panel][controls][label:2][text].Value
          pr1=>[@][node-2-code][code].Value
  add=>[/][magix.forms.create-web-part][controls][panel][controls]
    value=>[/][_dt][panel]

magix.data.count
  prototype
    type=>magix.log.item

if=>more-than
  lhs=>[magix.data.count][count].Value
  rhs=>[magix.data.load][end].Value
  code
    set=>[magix.forms.create-web-part][controls][panel:1][controls][label][text].Value
      value=>@"{0}/{1} of {2}"
        pr0=>[magix.data.load][start].Value+1
        pr1=>[magix.data.load][end].Value
        pr2=>[magix.data.count][count].Value
else
  set=>[magix.forms.create-web-part][controls][panel:1][controls][label][text].Value
    value=>@"{0}/{1} of {2}"
      pr0=>[magix.data.load][start].Value+1
      pr1=>[magix.data.count][count].Value
      pr2=>[magix.data.count][count].Value

if=>equals
  lhs=>[magix.data.load][start].Value
  rhs=>0
  code
    set=>[magix.forms.create-web-part][controls][panel:1][controls][link-button:0][enabled].Value
      value=(bool)>False
else
  set=>[magix.forms.create-web-part][controls][panel:1][controls][link-button:0][enabled].Value
    value=(bool)>True
  using=>magix.math
    subtract
      =>[magix.data.load][start].Value
      =>10
  using=>magix.math
    subtract
      =>[magix.data.load][end].Value
      =>10
  set=>[magix.forms.create-web-part][controls][panel:1][controls][link-button:0][onclick][execute-file][start].Value
    value=>[@][using:0][subtract].Value
  set=>[magix.forms.create-web-part][controls][panel:1][controls][link-button:0][onclick][execute-file][end].Value
    value=>[@][using:1][subtract].Value
if=>exist
  lhs=>[magix.data.load][end].Value>=[magix.data.count][count].Value
  code
    set=>[magix.forms.create-web-part][controls][panel:1][controls][link-button:1][enabled].Value
      value=(bool)>False
else
  set=>[magix.forms.create-web-part][controls][panel:1][controls][link-button:1][enabled].Value
    value=(bool)>True
  using=>magix.math
    add
      =>[magix.data.load][start].Value
      =>10
  using=>magix.math
    add
      =>[magix.data.load][end].Value
      =>10
  set=>[magix.forms.create-web-part][controls][panel:1][controls][link-button:1][onclick][execute-file][start].Value
    value=>[@][using:0][add].Value
  set=>[magix.forms.create-web-part][controls][panel:1][controls][link-button:1][onclick][execute-file][end].Value
    value=>[@][using:1][add].Value

magix.forms.create-web-part
  form-id=>server-log
  container=>content2
  css=>span-17 last
  controls
    label=>header
      css=>span-13 left-6 last
      text=>server log
      tag=>h2
    panel=>items
      css=>span-17 last
      controls
    panel=>pagerWrp
      css=>span-19
      controls
        link-button=>previous
          text=>&lt;&lt;
          css=>span-1 left-7
          onclick
            execute-file=>system42/admin/server-log-grid.hl
        label=>count
          css=>span-2
          text=>54
        link-button=>next
          text=>&gt;&gt;
          css=>span-1
          onclick
            execute-file=>system42/admin/server-log-grid.hl
        panel
          css=>span-2 right last
          default=>btnSearch
          controls
            text-box=>search
              place-holder=>filter ...
              css=>span-2
            button=>btnSearch
              css=>hide
              onclick
                magix.forms.get-value
                  form-id=>server-log
                  id=>search
                set=>[execute-file][filter].Value
                  value=>[magix.forms.get-value][value].Value
                execute-file=>system42/admin/server-log-grid.hl

if=>exist
  lhs=>[$][filter].Value
  code
    magix.forms.select-all
      form-id=>server-log
      id=>search
    magix.forms.set-focus
      form-id=>server-log
      id=>search

