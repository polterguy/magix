

inspect=>@"inspects active events in the system

creates a web-part to inspect active events in your system.  pass 
in [header] as the header to use, [namespace] to declare which 
active events to show and [exclude] as a list of active events to 
exclude, even though they're inside the correct namespace.  in addition 
you can pass in [overridden] to show only overridden active events, 
[open] to show open events and [remoted] to show remoted active events.  
you can also pass in [extra-buttons] which is a list of additional 
buttons that will be associated with the web-part and coupled together 
with the already existing 'inspect' button.  if you pass in [no-inspect], 
then there will be no inspect button, to view and inspect the code

by default this web-part is loaded into the content1 container, but 
this can be overridden by supplying a [container] parameter

if you pass in [description-class], this will serve as the css class 
of the description parts of your web-part.  if you pass in [list-class], 
this will serve as the css class of the list of active events parts of 
your web part, and [form-class] becomes the main web part's css class.  
the default css class of the description parts is 'span-12', for the 
list of active events wrapper 'span-5 last' and for the web-part it is 
'span-17 last'

by default, when you inspect active events, these will be loaded into 
the hyperlisp executor, which will be loaded into container content2.  
if you wish to override the container that the executor is loaded into, 
then pass in [inspect-container].  if you wish to override the css class 
for the hyperlisp executor, once loaded, then pass in [inspect-class]"


// verifies user is in admin role
magix.core.verify-role
  role=>admin

// changing css class(es) if requested
if=>exist
  lhs=>[$][description-class]
  code
    set=>[_controls][**panel=>pnl-description-wrp][class].value
      value=>[$][description-class].value
if=>exist
  lhs=>[$][list-class]
  code
    set=>[_controls][**panel=>pnl-events][class].value
      value=>[$][list-class].value
if=>exist
  lhs=>[$][form-class]
  code
    set=>[using][create-web-part][class].value
      value=>[$][form-class].value

// changing container, if requested
if=>exist
  lhs=>[$][container]
  code
    set=>[using][**container=>?].value
      value=>[$][container].value

// changing inspect container and css class, if requested
if=>exist
  lhs=>[$][inspect-container]
  code
    set=>[**panel=>btn-wrappers][controls][button][onclick][magix.viewport.load-module][container].value
      value=>[$][inspect-container].value
if=>exist
  lhs=>[$][inspect-class]
  code
    set=>[**panel=>btn-wrappers][controls][button][onclick][magix.viewport.load-module][class].value
      value=>[$][inspect-class].value

// removing inspect button, if we should
if=>equals
  lhs=>[$][no-inspect].value
  rhs=>true
  code
    set=>[_controls][**button=>inspect]

// listing events according to input parameter
if=>exist
  lhs=>[$][namespace]
  code
    set=>[list-events][begins-with].value
      value=>[$][namespace].value
if=>exist
  lhs=>[$][overridden]
  code
    set=>[list-events][overridden].value
      value=>[$][overridden].value
if=>exist
  lhs=>[$][open]
  code
    set=>[list-events][open].value
      value=>[$][open].value
if=>exist
  lhs=>[$][remoted]
  code
    set=>[list-events][remoted].value
      value=>[$][remoted].value
list-events

// removing excluded active events according to input parameter
for-each=>[$][exclude]
  set=>[/][list-events][events][[.].name]

// setting header according to input parameters
set=>[**label=>lbl-header][value].value
  value=>[$][header].value

// creates a link-button web control that it returns back to called
// taking [event] as active event and [namespace] as namespace to remove
_create-link-button
  _data
    link-button
      style=>width:100%;display:block;
      onclick
        try
          code
            _event
              inspect
          catch
            using=>magix.viewport
              show-message
                message=>active event didn't allow inspection, error message from inspect invocation was; '{0}'
                  =>[@][..][exception].value
                color=>#ffaaaa
                time=>5000
            set=>[/][**inspect].value
              value=>[@][exception].value
        magix.forms.set-value
          form-id=>active-events
          id=>lbl-header
        set=>[magix.forms.set-value:1][value].value
          value=>[try][code][0][inspect].value
        magix.forms.set-value
          form-id=>active-events
          id=>lbl-description
        set=>[try][code][0][inspect]
        add=>[node-2-code][node]
          value=>[try][code][0]
          children-only=>true
        node-2-code
        replace=>[node-2-code][code].value
          what=>&
          with=>&amp;
        replace=>[node-2-code][code].value
          what=><
          with=>&lt;
        replace=>[node-2-code][code].value
          what=>>
          with=>&gt;
        set=>[magix.forms.set-value:2][value].value
          value=>[node-2-code][code].value
        magix.forms.set-value
          form-id=>active-events
          id=>lbl-code

  // setting some onclick behavior, before changing name of active event
  set=>[_data][link-button][onclick][magix.forms.set-value:0][value].value
    value=>[$][event].value
  set=>[_data][link-button][onclick][**_event].name
    value=>[$][event].value

  // setting properties of link-button
  if=>exist
    lhs=>[$][namespace].value
    code
      replace=>[$][event].value
        what=>{0}.
          =>[$][namespace].value
  set=>[_data][link-button][value].value
    value=>[$][event].value

  // returning link-button
  add=>[$]
    value=>[_data][link-button]

// creating the clickable link-buttons in our 'grid'
for-each=>[list-events][events]

  // forwarding namespace
  if=>exist
    lhs=>[$][namespace]
    code
      set=>[@][..][..][lambda][namespace]
        value=>[$][namespace]

  // forwarding event name
  set=>[@][lambda][event].value
    value=>[.].name

  // creating link-button and adding to [_controls] node
  lambda=>[/][_create-link-button]
  add=>[/][_controls][panel=>pnl-events][controls]
    value=>[@][lambda][link-button]

// adding extra buttons, if any
for-each=>[$][extra-buttons]
  add=>[/][**panel=>btn-wrappers][controls]
    value=>[.]

// changing style of button group if extra buttons are supplied
if=>exist
  lhs=>[$][extra-buttons]
  code
    set=>[_controls][**panel=>btn-wrappers][class].value
      value=>{0} btn-group
        =>[_controls][**panel=>btn-wrappers][class].value

// our list of web controls to associate with our create-web-part invocation
_controls
  label=>lbl-header
    tag=>h2
  panel=>pnl-description-wrp
    class=>span-12
    controls
      panel=>btn-wrappers
        class=>fill-width column
        controls
          button=>inspect
            value=>inspect
            class=>span-2 last
            onclick
              magix.forms.get-value
                form-id=>active-events
                id=>lbl-code
              replace=>[magix.forms.get-value][value].value
                what=>&amp;
                with=>&
              replace=>[magix.forms.get-value][value].value
                what=>&lt;
                with=><
              replace=>[magix.forms.get-value][value].value
                what=>&gt;
                with=>>
              magix.viewport.load-module
                class=>span-17 last left-5
                container=>content2
                name=>Magix.ide.modules.ExecutorForm
              set=>[magix.executor.set-code][code].value
                value=>[magix.forms.get-value][value].value
              magix.executor.set-code
      label=>lbl-code
        tag=>pre
        style=>overflow-x:auto;overflow-y:auto;max-height:360px;
        class=>column fill-width bottom-1
        value=>example code
      label=>lbl-description
        style=>width:100%;display:block;
        value=>@"<h3>description of active event</h3><p>more description ...</p>"
  panel=>pnl-events
    class=>span-5 last
    style=>max-height:500px;overflow-y:auto;overflow-x:hidden;

// creating web-part to show active events requested
using=>magix.forms
  create-web-part
    form-id=>active-events
    container=>content1
    class=>span-17 last
    controls=>[_controls]

