

inspect=>@"contains the menu parts of email client

contains the menu parts events definition file of the 
magix mail application"


// compose new email link clicked
magix.email.compose-clicked
  execute-script
    file=>your-files/system-apps/magix-mail/components/composer/compose.hl


// inbox link was clicked, making sure we show all emails in inbox
magix.email.inbox-clicked
  execute-script
    file=>your-files/system-apps/magix-mail/components/inbox/show-emails.hl


// contacts link was clicked, making sure we show all contacts
magix.email.contacts-clicked
  execute-script
    file=>your-files/system-apps/magix-mail/components/contacts/show-contacts.hl


// sent items link was clicked, making sure we show all emails that have been sent
magix.email.sent-clicked
  execute-script
    file=>your-files/system-apps/magix-mail/components/inbox/show-emails.hl
    params
      mailbox=>sent


// trash link was clicked, making sure we show all emails in trashcan
magix.email.trash-clicked
  execute-script
    file=>your-files/system-apps/magix-mail/components/inbox/show-emails.hl
    params
      mailbox=>trash


// logout link was clicked, logging out user
magix.email.logout-clicked
  magix.session.set
    id=>magix.core.user

  // reloading page
  magix.viewport.execute-javascript
    script=>@"location.reload();"


// invoked when settings link is clicked.  show settings form, and make sure inbox is shown after settings is canceled or saved
magix.email.settings-clicked
  execute-script
    file=>your-files/system-apps/magix-mail/components/user-setup/setup-settings.hl
    params
      extra-buttons
        button=>cancel
          class=>span-2
          value=>cancel
          onclick
            execute-script
              file=>your-files/system-apps/magix-mail/components/menu/pop3/create-menu.hl
            execute-script
              file=>your-files/system-apps/magix-mail/components/inbox/show-emails.hl
      execute-afterwards
        execute-script
          file=>your-files/system-apps/magix-mail/components/menu/pop3/create-menu.hl
        execute-script
          file=>your-files/system-apps/magix-mail/components/inbox/show-emails.hl


// checks for new emails towards pop3 server, and shows error if an error have occurred
magix.email.check-for-new-emails

  // running script that checks for new emails
  execute-script
    file=>your-files/system-apps/magix-mail/components/workers/pop3/check-for-new-emails.hl

  switch=>[execute-script][params][status].value

    case=>fetching

      // email retriever is still fetching emails from pop3 server
      magix.email.update-unread-email-count

    case=>finished

      // some thread has just finished downloading new messages
      if=>exist
        lhs=>[execute-script][params][download-count]
        code
          set=>[@][..][..][magix.email.new-messages-downloaded][download-count].value
            value=>[execute-script][params][download-count].value
      magix.email.update-unread-email-count
      magix.email.new-messages-downloaded

    case=>error

      // last retrieval operation went bad, showing error message box back to user
      using=>magix.viewport
        show-message
          message=>@"something went wrong when trying to download messages from pop3 server, message from server was; '{0}'"
            =>[execute-script][params][error].value
          time=>10000
          color=>#ffaaaa

    default

      // a new thread was probably spawned to check for emails towards server.  do nothing ...


// invoked when new messages have arrived
magix.email.new-messages-downloaded
  using=>magix.viewport
    show-message
      message=>finished downloading {0} messages from your email server.&nbsp;&nbsp;click your inbox to view email
        =>[$][download-count].value
      color=>#aaffaa

  // making "ping" sound
  magix.forms.create-web-part
    container=>misc-dynamic
    controls
      label
        tag=>audio
        @src=>media/audio/ping.wav
        @autoplay


// updates number of new emails visual clues to user
magix.email.update-unread-email-count
  magix.session.get
    id=>magix.core.user
  set=>[magix.data.count][prototype][username].value
    value=>[magix.session.get][value][username].value
  magix.data.count
    prototype
      type=>magix.email.message
      folder=>inbox
      read=>false
  using=>magix.forms
    set-value
      form-id=>inbox
      id=>inbox-link
      value=>inbox ({0})
        =>[magix.data.count][count].value

