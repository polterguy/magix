

if=>[$][directory].Value
  set=>[magix.file.list-files][directory].Value
    value=>[$][directory].Value
  set=>[magix.file.list-directories][directory].Value
    value=>[$][directory].Value
  set=>[magix.forms.create-form][controls][label][text].Value
    value=>@"files from {0}"
      pr0=>[$][directory].Value
  add=>[/][magix.forms.create-form][controls]
    value=>[/][_back]
  set=>[/][magix.forms.create-form][controls][_back].Name
    value=>link-button
  set=>[/][magix.forms.create-form][events][magix.ide.file-deleted][magix.admin.run-file][directory].Value
    value=>[$][directory].Value
  set=>[/][magix.forms.create-form][events][magix.ide.file-saved][magix.admin.run-file][directory].Value
    value=>[$][directory].Value
  set=>[/][_btn][onclick][magix.admin.edit-file][file].Value
    value=>@"{0}/{1}"
      pr0=>[$][directory].Value
      pr1=>empty.txt
  set=>[/][magix.forms.create-form][controls][uploader][folder].Value
    value=>[$][directory].Value
  set=>[/][magix.forms.create-form][controls][uploader][uploaded][if][magix.admin.run-file][directory].Value
    value=>[$][directory].Value
else
  magix.viewport.set-viewstate
    id=>stack-of-pages
    value
      root=>

_back
  css=>span-5 directory back-directory
  text=>..
  onclick
    magix.viewport.get-viewstate
      id=>stack-of-pages
    set=>[@][magix.viewport.get-viewstate][value][[@][magix.viewport.get-viewstate][value].Count-1]
    add=>[@][magix.viewport.set-viewstate]
      value=>[@][magix.viewport.get-viewstate][value]
    magix.viewport.set-viewstate
      id=>stack-of-pages
    set=>[@][magix.admin.run-file][directory].Value
      value=>[@][magix.viewport.set-viewstate][value][[@][magix.viewport.set-viewstate][value].Count-1].Value
    magix.admin.run-file
      file=>zigano/file-browser/file-browser.hl

_link-button
  css=>span-5 file
  onclick
    magix.admin.edit-file
      container=>content2
      css=>span-24 min-height-16 top-2

_link-buttonDir
  css=>span-5 directory
  onclick
    magix.admin.run-file
      file=>zigano/file-browser/file-browser.hl
    magix.viewport.get-viewstate
      id=>stack-of-pages
    set=>[@][magix.viewport.get-viewstate][value][[@][magix.viewport.get-viewstate][value].Count].Value
      value=>[@][magix.admin.run-file][directory].Value
    add=>[@][magix.viewport.set-viewstate]
      value=>[@][magix.viewport.get-viewstate][value]
    magix.viewport.set-viewstate
      id=>stack-of-pages

magix.file.list-files

for-each=>[magix.file.list-files][files]
  set=>[/][_link-button][text].Value
    value=>[.].Value
  if=>[$][directory]
    set=>[@][replace][what].Value
      value=>@"{0}/"
        pr0=>@"[$][directory].Value"
    replace=>[/][_link-button][text].Value
  set=>[/][_link-button][onclick][magix.admin.edit-file][file].Value
    value=>[.].Value
  add=>[/][magix.forms.create-form][controls]
    value=>[/][_link-button]
  set=>[/][magix.forms.create-form][controls][_link-button].Name
    value=>link-button

magix.file.list-directories

for-each=>[magix.file.list-directories][directories]
  set=>[/][_link-buttonDir][text].Value
    value=>[.].Value
  set=>[/][_link-buttonDir][onclick][magix.admin.run-file][directory].Value
    value=>[.].Value
  if=>[$][directory]
    set=>[@][replace][what].Value
      value=>@"{0}/"
        pr0=>@"[$][directory].Value"
    replace=>[/][_link-buttonDir][text].Value
  add=>[/][magix.forms.create-form][controls]
    value=>[/][_link-buttonDir]
  set=>[/][magix.forms.create-form][controls][_link-buttonDir].Name
    value=>link-button

_btn
  css=>span-3 btn-large left-16 last top-1
  text=>new file
  onclick
    magix.admin.edit-file
      css=>span-24 last top-2
      container=>content2
      new-file=>true
      file=>empty.txt

add=>[magix.forms.create-form][controls]
  value=>[_btn]
set=>[magix.forms.create-form][controls][_btn].Name
  value=>button

magix.forms.create-form
  events

    magix.ide.file-saved
      magix.admin.run-file
        file=>zigano/file-browser/file-browser.hl

    magix.ide.file-deleted
      magix.admin.run-file
        file=>zigano/file-browser/file-browser.hl
      magix.viewport.clear-controls
        container=>content2
      magix.viewport.clear-controls
        container=>content3
      magix.viewport.clear-controls
        container=>content4
      magix.viewport.clear-controls
        container=>content5

  container=>content1
  form-id=>file-explorer
  css=>span-19 last min-height-30
  controls
    label=>desc
      css=>span-15 last left-4
      tag=>h2
      text=>files from /
    uploader=>uploader
      folder=>
      onuploaded
        magix.forms.get-value
          form-id=>file-explorer
          id=>uploader
        set=>[magix.viewport.show-message][message].Value
          value=>@"file {0} successfully uploaded"
            pr0=>[/][magix.forms.get-value][value].Value
        magix.viewport.show-message
          time=>1250
        magix.forms.has-more-data
          form-id=>file-explorer
          id=>uploader
        if=>![magix.forms.has-more-data][value].Value
          magix.admin.run-file
            file=>zigano/file-browser/file-browser.hl















