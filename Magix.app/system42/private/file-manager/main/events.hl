

inspect=>@"contains the active events for the main web part of the file manager

contains all active events for the main web part of the file manager"


// creates the web controls that shows all files and directories in one directory
// parameters are [root], [directory] and [filter]
// called whenever user changes directory, or initially when main web part is created
magix.file-manager.create-file-controls

  using=>magix.forms
    set-value
      form-id=>file-manager
      id=>file-header
      value=>files in /{0}
        =>[$][directory].value

  if=>equals
    lhs=>[$][directory].value
    rhs=>
    or=>equals
      lhs=>[$][directory].value
      rhs=>[$][root].value
    code
      magix.forms.set-enabled
        form-id=>file-manager
        id=>go-up
        value=>false
  else
    magix.forms.set-enabled
      form-id=>file-manager
      id=>go-up
      value=>true

  // listing files and directories according to input parameters
  using=>magix.file
    list-files
      directory=>[$][directory].value
      filter=>[$][filter].value
    list-directories
      directory=>[$][directory].value

  // iterating through all directories, creating web controls for displaying one directory
  for-each=>[using:1][list-directories][directories]
    _data
      link-button
        class=>span-4 last clear
        onclick
          // onclick event handler for clicking a folder
          set=>[magix.viewstate.set][value].value
            value=>[$][info].value
          magix.viewstate.set
            id=>magix.file-manager.current-folder
          magix.file-manager.open-directory

    split=>[.].name
      what=>/
    while=>not-equals
      lhs=>[@][split][result].count
      rhs=>1
      code
        set=>[@][..][..][split][result][0]
    set=>[@][_data][link-button][value].value
      value=>[@][split][result][0].value
    set=>[@][_data][link-button][info].value
      value=>[.].name
    add=>[/][_controls][panel:0][controls]
      value=>[@][_data][link-button]

  magix.viewstate.get
    id=>magix.file-manager.selected-file

  // iterating through all files, creating web controls for displaying one file
  for-each=>[using:1][list-files][files]
    _data
      link-button
        class=>span-5 last clear
        onclick
          magix.viewstate.get
            id=>magix.file-manager.selected-file
          if=>not-equals
            lhs=>[magix.viewstate.get][value].value
            rhs=>[$][info].value
            code
              set=>[magix.file-manager.select-file][file].value
                value=>[$][info].value
          else
            magix.viewport.clear-controls
              container=>preview
          magix.file-manager.select-file
    split=>[.].name
      what=>/
    while=>not-equals
      lhs=>[@][split][result].count
      rhs=>1
      code
        set=>[@][..][..][split][result][0]
    set=>[@][_data][link-button][info].value
      value=>[.].name
    set=>[@][_data][link-button][value].value
      value=>[@][split][result][0].value
    if=>equals
      lhs=>[.].name
      rhs=>[@][..][magix.viewstate.get][value].value
      code
        set=>[@][..][..][_data][link-button][style].value
          value=>text-decoration:underline;
    add=>[/][_controls][panel:1][controls]
      value=>[@][_data][link-button]

  // creates the web part to display all files and directories
  _controls
    panel=>pnl-folders-wrapper
      class=>span-4 last
      style=>max-height:252px;overflow-y:auto;overflow-x:hidden;
      controls
        label
          tag=>label
          class=>span-4 last
          value=>folders
    panel=>pnl-files-wrapper
      class=>span-5
      style=>max-height:252px;overflow-y:auto;overflow-x:hidden;
      controls
        label
          tag=>label
          class=>span-5 last
          value=>files
  using=>magix.forms
    create-web-part
      form-id=>files
      container=>files
      controls=>[_controls]

  // enabling or disabling 'move file' button
  if=>exist
    lhs=>[magix.viewstate.get][value]
    code
      _filename
      _folder
      set=>[@][_filename].value
        value=>[/][magix.viewstate.get][value].value
      set=>[@][_folder].value
        value=>[$][directory].value
      if=>not-equals
        lhs=>[@][_folder].value
        rhs=>
        code
          set=>[@][..][..][_folder].value
            value=>{0}/
              =>[@][..][..][_folder].value
      split=>[@][_filename].value
        what=>/
      while=>not-equals
        lhs=>[@][split][result].count
        rhs=>1
        code
          set=>[@][..][..][split][result][0]
      replace=>[@][_filename].value
        what=>[@][split][result][0].value
      if=>equals
        lhs=>[@][_filename].value
        rhs=>[@][_folder].value
        code
          magix.forms.set-enabled
            form-id=>preview
            id=>move-file
            value=>false
      else
        magix.forms.set-enabled
          form-id=>preview
          id=>move-file
          value=>true

// invoked when user selects a file
magix.file-manager.select-file

  // changing the viewstate value, signaling which file was selected, and enabling the 'select' button
  if=>exist
    lhs=>[$][file]
    code
      set=>[magix.viewstate.set][value].value
        value=>[$][file].value
  magix.viewstate.set
    id=>magix.file-manager.selected-file
  magix.viewstate.get
    id=>magix.file-manager.has-select-button
  if=>exist
    lhs=>[magix.viewstate.get][value]
    and=>exist
      lhs=>[$][file]
    code
      magix.forms.set-enabled
        form-id=>file-manager
        id=>select
        value=>true
  else-if=>exist
    lhs=>[magix.viewstate.get][value]
    code
      magix.forms.set-enabled
        form-id=>file-manager
        id=>select
        value=>false
  magix.file-manager.open-directory

// creates 'preview' web part
magix.file-manager.create-preview
  magix.viewstate.get
    id=>magix.file-manager.selected-file
  if=>exist
    lhs=>[magix.viewstate.get][value]
    code
      split=>[magix.viewstate.get][value].value
        what=>/
      while=>not-equals
        lhs=>[@][split][result].count
        rhs=>1
        code
          set=>[@][..][..][split][result][0]
      set=>[/][**label=>preview-label][value].value
        value=>[@][split][result][0].value
      set=>[/][**label=>preview-label][title].value
        value=>[magix.viewstate.get][value].value
      split=>[@][split][result][0].value
        what=>.
      while=>not-equals
        lhs=>[@][split:1][result].count
        rhs=>1
        code
          set=>[@][..][..][split:1][result][0]
      _can-edit=>false
      _is-image=>false
      _is-inspect=>false

      // checking filetype, and setting a descriptive label, and whether or not the file can be edited
      switch=>[@][split:1][result][0].value
        case=>hl
          set=>[@][..][..][_can-edit].value
            value=>true
          set=>[@][..][..][_is-inspect].value
            value=>true
        case=>mml
          set=>[/][**value=>unknown filetype].value
            value=>magix markup language file
          set=>[@][..][..][_can-edit].value
            value=>true
        case=>html
          set=>[/][**value=>unknown filetype].value
            value=>hyper text markup language file
          set=>[@][..][..][_can-edit].value
            value=>true
        case=>htm
          set=>[/][**value=>unknown filetype].value
            value=>hyper text markup language file
          set=>[@][..][..][_can-edit].value
            value=>true
        case=>config
          set=>[/][**value=>unknown filetype].value
            value=>configuration file
          set=>[@][..][..][_can-edit].value
            value=>true
        case=>css
          set=>[/][**value=>unknown filetype].value
            value=>stylesheet file
          set=>[@][..][..][_can-edit].value
            value=>true
        case=>png
          set=>[/][**value=>unknown filetype].value
            value=>image file
          set=>[@][..][..][_is-image].value
            value=>true
        case=>jpg
          set=>[/][**value=>unknown filetype].value
            value=>image file
          set=>[@][..][..][_is-image].value
            value=>true
        case=>jpeg
          set=>[/][**value=>unknown filetype].value
            value=>image file
          set=>[@][..][..][_is-image].value
            value=>true
        case=>gif
          set=>[/][**value=>unknown filetype].value
            value=>image file
          set=>[@][..][..][_is-image].value
            value=>true
        case=>cs
          set=>[/][**value=>unknown filetype].value
            value=>c# file
        case=>aspx
          set=>[/][**value=>unknown filetype].value
            value=>asp.net page file
        case=>ascx
          set=>[/][**value=>unknown filetype].value
            value=>asp.net user control file
        case=>asax
          set=>[/][**value=>unknown filetype].value
            value=>asp.net application file
        case=>ico
          set=>[/][**value=>unknown filetype].value
            value=>icon file
        case=>csproj
          set=>[/][**value=>unknown filetype].value
            value=>c# project file
        case=>user
          set=>[/][**value=>unknown filetype].value
            value=>c# project user settings file
        case=>txt
          set=>[/][**value=>unknown filetype].value
            value=>text file
          set=>[@][..][..][_can-edit].value
            value=>true
        case=>dll
          set=>[/][**value=>unknown filetype].value
            value=>dynamic library file
        case=>js
          set=>[/][**value=>unknown filetype].value
            value=>javascript file
          set=>[@][..][..][_can-edit].value
            value=>true

      // enabling or disabling 'edit' button
      if=>equals
        lhs=>[@][_can-edit].value
        rhs=>true
        code
          magix.viewstate.get
            id=>magix.file-manager.no-edit-button
          if=>not-equals
            lhs=>[@][magix.viewstate.get][value].value
            rhs=>true
            code
              set=>[/][**button=>edit-file][disabled].value
                value=>false

      // enabling or disabling view image preview
      if=>equals
        lhs=>[@][_is-image].value
        rhs=>true
        code
          set=>[/][**img=>image-preview][visible].value
            value=>true
          set=>[/][**img=>image-preview][src].value
            value=>[magix.viewstate.get][value].value

      // inspecting file, if we're supposed to
      if=>equals
        lhs=>[@][_is-inspect].value
        rhs=>true
        code
          set=>[@][magix.file.load][file].value
            value=>[magix.viewstate.get][value].value
          magix.file.load
          code-2-node
            code=>[@][magix.file.load][value].value
          if=>exist
            lhs=>[@][code-2-node][node][inspect]
            code
              set=>[**value=>unknown filetype].value
                value=>[:magix.file-manager.create-inspect[@][..][..][code-2-node]][result].value
          else
            set=>[**value=>unknown filetype].value
              value=>hyperlisp file

      // turning on or off editing of files, according to input
      magix.viewstate.get=>no-edit
        id=>magix.file-manager.no-edit
      if=>equals
        lhs=>[@][magix.viewstate.get=>no-edit][value].value
        rhs=>true
        code
          set=>[**panel=>edit-wrapper][visible].value
            value=>false

      // creating actual preview web part
      magix.forms.create-web-part
        container=>preview
        form-id=>preview
        controls
          label=>preview-label
            tag=>label
            class=>span-6 last
            value=>selected file
          label=>lbl-file-type
            class=>span-6 last
            style=>max-height:144px;overflow-y:auto;
            value=>unknown filetype
          img=>image-preview
            visible=>false
            style=>cursor:pointer;max-height:180px;max-width:230px;
            class=>clear column
            onclick
              using=>magix.viewport
                execute-javascript
                  script=>@"window.open('/{0}', '_blank');"
                    =>[$][value].value
          panel=>edit-wrapper
            class=>btn-group
            controls
              button=>edit-file
                class=>span-2 top-1 clear
                value=>edit
                disabled=>true
                onclick
                  magix.viewstate.get
                    id=>magix.file-manager.selected-file
                  set=>[magix.file-manager.edit-file][file].value
                    value=>[magix.viewstate.get][value].value
                  magix.file-manager.edit-file
              button=>delete-file
                class=>span-2 top-1
                value=>delete
                title=>deletes the currently selected file
                onclick

                  // asking user to confirm deletion, before executing deletion
                  magix.viewport.confirm
                    message=>are you sure you wish to delete this file?
                    code
                      // deleting selected file
                      magix.viewstate.get
                        id=>magix.file-manager.selected-file
                      using=>magix.file
                        delete
                          file=>[magix.viewstate.get:0][value].value

                      // clearing selected file
                      magix.viewstate.set
                        id=>magix.file-manager.selected-file

                      // clearing 'preview' container
                      magix.viewport.clear-controls
                        container=>preview

                      // reloading files
                      magix.file-manager.open-directory

              button=>move-file
                class=>span-2 top-1 last
                disabled=>true
                value=>move
                title=>moves the selected file to your current folder
                onclick
                  using=>magix.viewstate
                    get
                      id=>magix.file-manager.selected-file
                    get
                      id=>magix.file-manager.current-folder

                    // clearing selected file
                    set
                      id=>magix.file-manager.selected-file

                  // removing folder name from selected file, and adding filename to 'to-folder'
                  split=>[using][get:0][value].value
                    what=>/
                  while=>not-equals
                    lhs=>[split][result].count
                    rhs=>1
                    code
                      set=>[@][..][..][split][result][0]

                  // changing the current folder, appending the filename to it
                  set=>[using][get:1][value].value
                    value=>{0}/{1}
                      =>[using][get:1][value].value
                      =>[split][result][0].value

                  // actually moving file
                  using=>magix.file
                    move-file
                      from=>[using][get:0][value].value
                      to=>[using][get:1][value].value

                  // clearing 'preview' container
                  magix.viewport.clear-controls
                    container=>preview

                  // reloading files
                  magix.file-manager.open-directory
              button=>copy-file
                class=>span-3 top-1
                value=>create copy
                title=>creates a copy of the selected file, inside the currently selected directory
                onclick
                  magix.viewstate.get
                    id=>magix.file-manager.selected-file
                  magix.viewstate.get
                    id=>magix.file-manager.current-folder
                  _copy-filename
                  split=>[magix.viewstate.get][value].value
                    what=>/
                  while=>not-equals
                    lhs=>[split][result].count
                    rhs=>1
                    code
                      set=>[split][result][0]
                  set=>[_copy-filename].value
                    value=>[magix.viewstate.get:1][value].value
                  set=>[_copy-filename].value
                    value=>{0}/copy - {1}
                      =>[_copy-filename].value
                      =>[split][result][0].value
                  using=>magix.file
                    file-exist
                      file=>[_copy-filename].value
                  _file-exist
                  _counter=>2
                  set=>[_file-exist].value
                    value=>[using][file-exist][value].value
                  while=>equals
                    lhs=>[_file-exist].value
                    rhs=>true
                    code
                      set=>[_copy-filename].value
                        value=>{0}/copy-{1}-{2}
                          =>[magix.viewstate.get:1][value].value
                          =>[_counter].value
                          =>[split][result][0].value
                      using=>magix.file
                        file-exist
                          file=>[_copy-filename].value
                      set=>[_file-exist].value
                        value=>[@][using][file-exist][value].value
                      using=>magix.math
                        add
                          =>[_counter].value
                          =>1
                      set=>[_counter].value
                        value=>[@][using:1][add].value
                  using=>magix.file
                    copy-file
                      from=>[magix.viewstate.get:0][value].value
                      to=>[_copy-filename].value
                  set=>[magix.viewstate.set][value].value
                    value=>[_copy-filename].value
                  magix.viewstate.set
                    id=>magix.file-manager.selected-file

                  // reloading files
                  magix.file-manager.open-directory

              button=>rename-file
                class=>span-3 top-1 last
                value=>rename file
                title=>allows you to rename file
                onclick
                  magix.forms.create-web-part
                    form-id=>rename-file
                    container=>rename-file-container
                    controls
                      panel
                        class=>span-6 last top-1
                        default=>save-new-name
                        controls
                          text-box=>new-file-name
                            placeholder=>new name ...
                            class=>span-4
                            onesc
                              magix.viewport.clear-controls
                                container=>rename-file-container
                            onfirstload
                              magix.viewstate.get
                                id=>magix.file-manager.selected-file
                              split=>[magix.viewstate.get][value].value
                                what=>/
                              while=>not-equals
                                lhs=>[split][result].count
                                rhs=>1
                                code
                                  set=>[split][result][0]
                              set=>[magix.forms.set-value][value].value
                                value=>[split][result][0].value
                              magix.forms.set-value
                                form-id=>rename-file
                                id=>new-file-name
                              set=>[magix.forms.set-info][value].value
                                value=>[magix.viewstate.get][value].value
                              magix.forms.set-info
                                form-id=>rename-file
                                id=>save-new-name
                              magix.forms.effect
                                form-id=>rename-file
                                id=>new-file-name
                                type=>focus-and-select
                          button=>save-new-name
                            class=>span-2 last
                            value=>save
                            onclick
                              magix.forms.get-value
                                form-id=>rename-file
                                id=>new-file-name
                              split=>[$][info].value
                                what=>/
                              using=>magix.math
                                subtract
                                  =>[split][result].count
                                  =>1
                              set=>[split][result][[using][subtract].value]
                              _new-file-name
                              for-each=>[split][result]
                                set=>[/][_new-file-name].value
                                  value=>{0}{1}/
                                    =>[/][_new-file-name].value
                                    =>[.].value
                              set=>[_new-file-name].value
                                value=>{0}{1}
                                  =>[_new-file-name].value
                                  =>[magix.forms.get-value][value].value
                              if=>not-equals
                                lhs=>[_new-file-name].value
                                rhs=>[$][info].value
                                code
                                  using=>magix.file
                                    file-exist
                                      file=>[_new-file-name].value
                                  if=>equals
                                    lhs=>[@][using][file-exist][value].value
                                    rhs=>true
                                    code
                                      magix.viewport.show-message
                                        message=>that file already exist
                                        color=>#ffbbbb
                                      magix.forms.effect
                                        type=>focus-and-select
                                        form-id=>rename-file
                                        id=>new-file-name
                                  else
                                    using=>magix.file
                                      move-file
                                        from=>[$][info].value
                                        to=>[_new-file-name].value
                                    magix.viewport.clear-controls
                                      container=>rename-file-container
                                    set=>[@][magix.viewstate.set][value].value
                                      value=>[_new-file-name].value
                                    magix.viewstate.set
                                      id=>magix.file-manager.selected-file

                                    // reloading files
                                    magix.file-manager.open-directory
                              else
                                magix.viewport.clear-controls
                                  container=>rename-file-container

          dynamic=>rename-file-container
            class=>span-6 last

// formats inspect node with paragraphs and such for clarity
magix.file-manager.create-inspect
  _inspect
  set=>[_inspect].value
    value=>[$][node][inspect].value
  set=>[_inspect].value
    value=>@"<p>{0}</p>"
      =>[_inspect].value
  replace=>[_inspect].value
    what=>@"

"
    with=></p><p>
  set=>[$][result].value
    value=>[_inspect].value

// edits files that allows to be edited, such as mml, html and hyperlisp files
// [file] parameter is file to edit
// wil open text-editor if file is mml or hyperlisp, if it is html, it will open wysiwyg-editor
magix.file-manager.edit-file
  split=>[$][file].value
    what=>.
  while=>not-equals
    lhs=>[split][result].count
    rhs=>1
    code
      set=>[split][result][0]
  if=>equals
    lhs=>[split][result][0].value
    rhs=>html
    or=>equals
      lhs=>[split][result][0].value
      rhs=>htm
    code
      set=>[@][execute-script][params][file].value
        value=>[$][file].value
      magix.viewstate.get
        id=>magix.file-manager.editor-container
      set=>[@][execute-script][params][container].value
        value=>[@][magix.viewstate.get][value].value
      execute-script
        file=>system42/private/tools/wysiwyg-editor.hl
        params
          class=>span-22 last top-1
          extra-buttons
            button=>close
              class=>span-2 last
              value=>close
              title=>close editor
              onclick
                magix.viewstate.get
                  id=>magix.file-manager.editor-container
                set=>[magix.viewport.clear-controls][container].value
                  value=>[magix.viewstate.get][value].value
                magix.viewport.clear-controls
                  reset-class=>true
  else
    set=>[@][execute-script][params][file].value
      value=>[$][file].value
    magix.viewstate.get
      id=>magix.file-manager.editor-container
    set=>[@][execute-script][params][container].value
      value=>[@][magix.viewstate.get][value].value
    execute-script
      file=>system42/private/tools/text-editor.hl
      params
        class=>span-22 last top-1
        onesc
          magix.viewstate.get
            id=>magix.file-manager.editor-container
          set=>[magix.viewport.clear-controls][container].value
            value=>[magix.viewstate.get][value].value
          magix.viewport.clear-controls
            reset-class=>true
        extra-buttons
          button=>close
            class=>span-2 last
            value=>close
            title=>close editor
            onclick
              magix.viewstate.get
                id=>magix.file-manager.editor-container
              set=>[magix.viewport.clear-controls][container].value
                value=>[magix.viewstate.get][value].value
              magix.viewport.clear-controls
                reset-class=>true

// raised by text-editor when file is saved.  rebinding files, in case filename was changed
// will be given a [file] parameter.  we're checking to see if the [file] parameter is the same 
// as the currently selected file, if it is not, we rebind the web part
magix.editor.file-saved
  magix.viewstate.get
    id=>magix.file-manager.selected-file
  if=>not-equals
    lhs=>[$][file].value
    rhs=>[magix.viewstate.get][value].value
    code
      // we need to rebind the web part
      set=>[@][magix.viewstate.set][value].value
        value=>[$][file].value
      magix.viewstate.set
        id=>magix.file-manager.selected-file
      magix.file-manager.open-directory

// raised when current directory is changed
magix.file-manager.directory-changed
  magix.viewstate.get
    id=>magix.file-manager.current-folder
  magix.viewstate.get
    id=>magix.file-manager.has-upload
  if=>equals
    lhs=>[magix.viewstate.get:1][value].value
    rhs=>true
    code
      set=>[@][magix.forms.uploader-set-directory][value].value
        value=>[magix.viewstate.get][value].value
      magix.forms.uploader-set-directory
        form-id=>file-manager
        id=>uploader
  if=>not-equals
    lhs=>[magix.viewstate.get][value].value
    rhs=>
    and=>not-equals
      lhs=>[magix.viewstate.get][value].value
      rhs=>/
    code
      magix.forms.set-enabled
        form-id=>file-manager
        id=>delete-directory
        value=>true
  else
    magix.forms.set-enabled
      form-id=>file-manager
      id=>delete-directory
      value=>false

// iteratest through the current directory, and populates the web controls for showing all files and directories
magix.file-manager.open-directory
  magix.viewstate.get
    id=>magix.file-manager.filter
  if=>exist
    lhs=>[magix.viewstate.get:0][value]
    code
      set=>[magix.file-manager.create-file-controls][filter].value
        value=>[magix.viewstate.get:0][value].value
  magix.viewstate.get
    id=>magix.file-manager.root
  if=>exist
    lhs=>[magix.viewstate.get:1][value]
    code
      set=>[magix.file-manager.create-file-controls][root].value
        value=>[magix.viewstate.get:1][value].value
  magix.viewstate.get
    id=>magix.file-manager.current-folder
  if=>exist
    lhs=>[magix.viewstate.get:2][value]
    code
      set=>[magix.file-manager.create-file-controls][directory].value
        value=>[magix.viewstate.get:2][value].value
  magix.file-manager.create-preview
  magix.file-manager.create-file-controls
    filter=>
    root=>
    directory=>
  magix.file-manager.directory-changed

// create-directory was clicked
magix.file-manager.create-directory_onclick
  magix.forms.create-web-part
    container=>create-file-directory-container
    class=>span-6 left-9 last top-1 bottom-1
    form-id=>create-file-directory-container
    controls-file=>system42/private/file-manager/main/create-directory/controls.hl

// delete-directory was clicked
magix.file-manager.delete-directory_onclick
  magix.viewport.confirm
    message=>are you sure you wish to delete this directory?
    code
      magix.viewstate.get
        id=>magix.file-manager.current-folder

      // checking to see if selected file is inside of folder being deleted, and if so, un-selecting it
      magix.viewstate.get
        id=>magix.file-manager.selected-file
      if=>exist
        lhs=>[magix.viewstate.get:1][value]
        code
          index-of=>[magix.viewstate.get:1][value].value
            what=>[magix.viewstate.get:0][value].value
          if=>not-equals
            lhs=>[@][index-of][result].count
            rhs=>0
            code
              magix.viewstate.set
                id=>magix.file-manager.selected-file

      using=>magix.file
        delete-directory
          directory=>[magix.viewstate.get][value].value
      split=>[magix.viewstate.get][value].value
        what=>/
      using=>magix.math
        subtract
          =>[split][result].count
          =>1
      set=>[split][result][[using:1][subtract].value]
      _new-current=>
      for-each=>[split][result]
        if=>not-equals
          lhs=>[/][_new-current].value
          rhs=>
          code
            set=>[/][_new-current].value
              value=>{0}/
                =>[/][_new-current].value
        set=>[/][_new-current].value
          value=>{0}{1}
            =>[/][_new-current].value
            =>[.].value
      set=>[magix.viewstate.set][value].value
        value=>[_new-current].value
      magix.viewstate.set
        id=>magix.file-manager.current-folder
      magix.file-manager.open-directory

// go-up button was clicked
magix.file-manager.go-up_onclick
  magix.viewstate.get
    id=>magix.file-manager.current-folder
  split=>[magix.viewstate.get][value].value
    what=>/
  using=>magix.math
    subtract
      =>[split][result].count
      =>1
  set=>[split][result][[using][subtract].value]
  _folder=>
  for-each=>[split][result]
    if=>not-equals
      lhs=>[/][_folder].value
      rhs=>
      code
        set=>[/][_folder].value
          value=>{0}/
            =>[/][_folder].value
    set=>[/][_folder].value
      value=>{0}{1}
        =>[/][_folder].value
        =>[.].value
  set=>[magix.viewstate.set][value].value
    value=>[_folder].value
  magix.viewstate.set
    id=>magix.file-manager.current-folder
  magix.file-manager.open-directory

// creates a text file
magix.file-manager.create-file_onclick
  magix.forms.create-web-part
    container=>create-file-directory-container
    class=>span-6 left-9 last top-1 bottom-1
    form-id=>create-file-directory-container
    controls-file=>system42/private/file-manager/main/create-file/controls.hl

