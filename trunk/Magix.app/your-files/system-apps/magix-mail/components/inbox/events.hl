

inspect=>@"active events for show-emails.hl file

contains active events for the inbox list of emails web part"


// toggles email as visible or invisible according to whether or not it's already being viewed
magix.emails.toggle-email
  using=>magix.forms
    get-value
      form-id=>inbox-list
      id=>viewing-{0}
        =>[$][id].value
  if=>equals
    lhs=>[using][get-value][value].value
    rhs=>true
    code
      // already viewing, "unviewing" email
      using=>magix.forms
        set-value
          form-id=>inbox-list
          id=>viewing-{0}
            =>[$][id].value
          value=>false
      set=>[@][magix.viewport.clear-controls][container].value
        value=>dynamic-{0}
          =>[$][id].value
      magix.viewport.clear-controls
        reset-class
          new-class=>emails-sleeping-dynamic
  else
    // making email show
    using=>magix.forms
      set-value
        form-id=>inbox-list
        id=>viewing-{0}
          =>[$][id].value
        value=>true
      set-style
        form-id=>inbox-list
        id=>subject-{0}
          =>[$][id].value
        key=>font-weight
        value=>normal
    set=>[@][magix.emails.load-email][container].value
      value=>dynamic-{0}
        =>[$][id].value
    set=>[@][magix.emails.load-email][email-id].value
      value=>[$][email-id].value
    magix.emails.load-email


// loads email for viewing
magix.emails.load-email
  iterate=>[magix.execute]
    if=>equals
      lhs=>[.].value
      rhs=>{container}
      code
        set=>[.].value
          value=>[$][container].value
    else-if=>equals
      lhs=>[.].value
      rhs=>{reply-container}
      code
        set=>[.].value
          value=>reply-{0}
            id=>[$][container].value
  add=>[magix.execute]
    value=>[$]
  magix.execute
    using=>magix.data
      load
        id=>[$][email-id].value

    // making sure email is marked as read
    add=>[using:1][save][value]
      values=>[using:0][load][value]
    set=>[using:1][save][value][read].value
      value=>true
    using=>magix.data
      save
        id=>[$][email-id].value

    // extracting body, prioritizing html view
    _body
    if=>exist
      lhs=>[using][load][value][body][html]
      code
        set=>[_body].value
          value=>[using][load][value][body][html].value
    else
      set=>[_body].value
        value=>[using][load][value][body][plain].value

      // email is not html, making sure all carriage returns are replaced with <br/> tags
      replace=>[_body].value
        what=>\n
        with=><br/>
      replace=>[_body].value
        what=>\r

    set=>[_controls][**label=>body][value].value
      value=>[_body].value

    set=>[_controls][**label=>email-header][value].value
      value=>[using][load][value][subject].value
    if=>more-than
      lhs=>[using][load][value][attachments].count
      rhs=>0
      code
        for-each=>[using][load][value][attachments]
          _lb
            link-button
              class=>span-7 last
              onclick
                set=>[magix.file.download][file].value
                  value=>[$][info].value
                set=>[magix.file.download][as].value
                  value=>[$][value].value
                magix.file.download
          set=>[@][_lb][link-button][value].value
            value=>[.].value
          set=>[@][_lb][link-button][info].value
            value=>[.][local-file-name].value
          add=>[/][_controls][panel=>attachments][controls]
            values=>[@][_lb]
    else
      set=>[_controls][panel=>attachments]
    set=>[_controls][**button=>reply][info].value
      value=>[$][email-id].value
    set=>[_controls][**button=>delete][info].value
      value=>[$][email-id].value
    magix.viewstate.get
      id=>magix.emails.current-view
    if=>equals
      lhs=>[@][magix.viewstate.get][value][mailbox].value
      rhs=>trash
      code
        set=>[**button=>reply]
        set=>[**panel=>btn-group][class].value
          value=>span-2 last right clear
    else-if=>equals
      lhs=>[@][magix.viewstate.get][value][mailbox].value
      rhs=>sent
      code
        set=>[**button=>reply]
        set=>[**button=>delete]

    // adding up from field
    _lb
      link-button
        class=>email-sent-from-item
        onclick
          code-2-node
            code=>[$][info].value
          add=>[magix.email.compose-email][to]
            values=>[code-2-node][node]
          magix.email.compose-email
    if=>exist
      lhs=>[using][load][value][from][display-name]
      code
        set=>[@][..][..][_lb][link-button][value].value
          value=>{0} &lt;{1}&gt;
            =>[using][load][value][from][display-name].value
            =>[using][load][value][from].value
    else
      set=>[@][..][_lb][link-button][value].value
        value=>[using][load][value][from].value
    node-2-code
      node=>[using][load][value][from]
    set=>[@][_lb][link-button][info].value
      value=>[@][node-2-code][code].value
    add=>[@][_controls][**panel=>email-from][controls][panel][controls]
      values=>[@][_lb]

    // adding up list of to recipients
    for-each=>[using][load][value][to]
      _lb
        link-button
          class=>email-sent-to-item
          onclick
            code-2-node
              code=>[$][info].value
            add=>[magix.email.compose-email][to]
              values=>[code-2-node][node]
            magix.email.compose-email
      if=>exist
        lhs=>[.][display-name]
        code
          set=>[@][..][..][_lb][link-button][value].value
            value=>{0} &lt;{1}&gt;
              =>[.][display-name].value
              =>[.].value
      else
        set=>[@][..][_lb][link-button][value].value
          value=>[.].value
      node-2-code
        node=>[.]
      set=>[@][_lb][link-button][info].value
        value=>[@][node-2-code][code].value
      add=>[@][..][_controls][**panel=>email-to][controls][panel][controls]
        values=>[@][_lb]

    // adding up list of cc recipients
    if=>exist
      lhs=>[using][load][value][cc]
      code
        for-each=>[using][load][value][cc]
          _lb
            link-button
              class=>email-sent-to-item
              onclick
                code-2-node
                  code=>[$][info].value
                add=>[magix.email.compose-email][to]
                  values=>[code-2-node][node]
                magix.email.compose-email
          if=>exist
            lhs=>[.][display-name]
            code
              set=>[@][..][..][_lb][link-button][value].value
                value=>{0} &lt;{1}&gt;
                  =>[.][display-name].value
                  =>[.].value
          else
            set=>[@][..][_lb][link-button][value].value
              value=>[.].value
          node-2-code
            node=>[.]
          set=>[@][_lb][link-button][info].value
            value=>[@][node-2-code][code].value
          add=>[@][..][..][..][_controls][**panel=>email-cc][controls][panel][controls]
            values=>[@][_lb]
    else
      set=>[@][..][_controls][**panel=>email-cc]

    // adding up list of bcc recipients
    if=>exist
      lhs=>[using][load][value][bcc]
      code
        for-each=>[using][load][value][bcc]
          _lb
            link-button
              class=>email-sent-to-item
              onclick
                code-2-node
                  code=>[$][info].value
                add=>[magix.email.compose-email][to]
                  values=>[code-2-node][node]
                magix.email.compose-email
          if=>exist
            lhs=>[.][display-name]
            code
              set=>[@][..][..][_lb][link-button][value].value
                value=>{0} &lt;{1}&gt;
                  =>[.][display-name].value
                  =>[.].value
          else
            set=>[@][..][_lb][link-button][value].value
              value=>[.].value
          node-2-code
            node=>[.]
          set=>[@][_lb][link-button][info].value
            value=>[@][node-2-code][code].value
          add=>[@][..][..][..][_controls][**panel=>email-bcc][controls][panel][controls]
            values=>[@][_lb]
    else
      set=>[@][..][_controls][**panel=>email-bcc]

    // checking if email is signed, and if so, enabling "import signature button"
    if=>equals
      lhs=>[using][load][value][signed].value
      rhs=>true
      code
        // email was signed, enabling import signature button
        set=>[**_controls][**panel=>btn-group][class].value
          value=>btn-group span-6 last right clear
        set=>[**_controls][**button=>import-signature][info].value
          value=>[$][email-id].value
    else
      // email was not signed, removing import signature button from button row
      set=>[**_controls][**button=>import-signature]

    _controls
      panel=>email-content
        class=>span-16 email-content-body
        controls
          label=>email-header
            class=>span-15 last bottom-1
            tag=>h3
          link-button=>close-email
            class=>span-1 email-content-close
            value=>X
            onclick
              // "unviewing" email
              replace=>[$][container].value
                what=>dynamic-
              using=>magix.forms
                set-value
                  form-id=>inbox-list
                  id=>viewing-{0}
                    =>[$][container].value
                  value=>false
              set=>[@][magix.viewport.clear-controls][container].value
                value=>dynamic-{0}
                  =>[$][container].value
              magix.viewport.clear-controls
                reset-class
                  new-class=>emails-sleeping-dynamic
          panel=>email-from
            class=>span-17 last email-sent-from
            controls
              label
                class=>span-1 email-address-label
                tag=>label
                value=>from:
              panel
                class=>span-16 last
                controls
          panel=>email-to
            class=>span-17 last email-sent-to
            controls
              label
                class=>span-1 email-address-label
                tag=>label
                value=>to:
              panel
                class=>span-16 last
                controls
          panel=>email-cc
            class=>span-17 last email-sent-to
            controls
              label
                class=>span-1 email-address-label
                tag=>label
                value=>cc:
              panel
                class=>span-16 last
                controls
          panel=>email-bcc
            class=>span-17 last email-sent-to
            controls
              label
                class=>span-1 email-address-label
                tag=>label
                value=>bcc:
              panel
                class=>span-16 last
                controls
          label=>body
            class=>clear last email-content-body-body top-1 column bottom-1
            tag=>div
      panel=>attachments
        class=>span-7 clear email-attachments
        controls
          label
            class=>span-7 last
            tag=>label
            value=>attachments
      panel=>btn-group
        class=>btn-group span-4 last right clear
        controls
          button=>reply
            class=>span-2
            value=>reply
            onclick
              magix.email.current-reply-container
              if=>exist
                lhs=>[magix.email.current-reply-container][container]
                code
                  using=>magix.browser
                    scroll
                      id=>[magix.email.current-reply-container][container].value
                  magix.viewport.show-message
                    message=>you must first finish your existing reply
                    color=>#ffaaaa
                  stop
              set=>[execute-script][params][email-id].value
                value=>[$][info].value
              set=>[execute-script][params][reply-container].value
                value=>reply-{0}
                  =>[$][container].value
              execute-script
                file=>your-files/system-apps/magix-mail/components/reply/reply-web-part.hl
          button=>import-signature
            class=>span-2
            value=>import
            title=>imports certificate from sender into certificate database
            onclick
              using=>magix.data
                load
                  id=>[$][info].value
              for-each=>[using][load][value][attachments]
                if=>equals
                  lhs=>[.].value
                  rhs=>smime.p7s
                  code
                    set=>[@][magix.cryptography.import-certificate][certificate].value
                      value=>[.][local-file-name].value
                    magix.cryptography.import-certificate
                    magix.viewport.show-message
                      message=>certificate was successfully imported into certificate database
                      color=>#aaffaa
                      time=>5000
                    stop
          button=>delete
            class=>span-2 last
            value=>delete
            onfirstload
              magix.viewstate.get
                id=>magix.emails.current-view
              if=>equals
                lhs=>[magix.viewstate.get][value][mailbox].value
                rhs=>trash
                code
                  magix.forms.set-value
                    form-id=>{container}
                    id=>delete
                    value=>un-delete
            onclick
              magix.viewstate.get
                id=>magix.emails.current-view
              if=>equals
                lhs=>[magix.viewstate.get][value][mailbox].value
                rhs=>trash
                code

                  // we're in trashcan, undeleting email
                  using=>magix.data
                    load
                      id=>[$][info].value
                  set=>[@][using][load][value][folder].value
                    value=>inbox
                  using=>magix.data
                    save
                      id=>[$][info].value
                      value=>[@][..][using][load][value]
                  magix.viewport.show-message
                    message=>message was moved to inbox
                    time=>1000
                  execute-script
                    file=>your-files/system-apps/magix-mail/components/inbox/show-emails.hl
                    params
                      frozen=>true
              else

                // we're in inbox, deleting message
                using=>magix.data
                  load
                    id=>[$][info].value
                set=>[@][using][load][value][folder].value
                  value=>trash
                using=>magix.data
                  save
                    id=>[$][info].value
                    value=>[@][..][using][load][value]
                magix.viewport.show-message
                  message=>message was moved to trash
                  time=>1000
                execute-script
                  file=>your-files/system-apps/magix-mail/components/inbox/show-emails.hl
                  params
                    frozen=>true
      dynamic=>{reply-container}
    using=>magix.forms
      create-web-part
        container=>{container}
        form-id=>{container}
        class=>span-18 last email-item-body bottom-1 top-1
        controls=>[_controls]
    magix.email.update-unread-email-count


// opens up compose email web part with [to], [cc] and [bcc] recipients as given in parameters, or adds recipient to existing composer
magix.email.compose-email
  if=>equals
    lhs=>[:magix.email.is-composer-loaded][value].value
    rhs=>true
    code
      add=>[@][magix.email.add-new-recipient]
        values=>[$]
      magix.email.add-new-recipient
  else
    // loading composer with 
    add=>[@][execute-script][params]
      values=>[$]
    execute-script
      file=>your-files/system-apps/magix-mail/components/composer/compose.hl

