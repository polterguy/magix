

inspect=>@"creates the edit-page web form, from 
where you can edit, delete and create new web pages, 
using a word-like wysiwyg editor, which you can inject 
web forms into"


magix.data.load
  prototype
    type=>magix.pages.page

_dt
  name=>value

for-each=>[magix.data.load][objects]
  set=>[/][_dt][0].Name
    value=>[.].Name
  set=>[/][_dt][0].Value
    value=>[.][name].Value
  add=>[/][magix.forms.create-web-part][controls][select][items]
    value=>[/][_dt][0]

magix.data.load
  prototype
    type=>magix.forms.form

for-each=>[magix.data.load:1][objects]
  set=>[/][_dt][0].Name
    value=>[.].Name
  set=>[/][_dt][0].Value
    value=>[.][name].Value
  add=>[/][magix.forms.create-web-part][controls][select:1][items]
    value=>[/][_dt][0]

magix.forms.create-web-part
  form-id=>edit-pages
  container=>content1
  css=>span-19 last
  controls
    label=>header
      tag=>h2
      css=>span-13 last left-6
      text=>edit pages
    select=>page
      css=>span-4
      onselectedindexchanged
        magix.forms.get-value
          form-id=>edit-pages
          id=>page
        set=>[magix.data.load][id].Value
          value=>[magix.forms.get-value][value].Value
        magix.data.load
        if=>more-then
          lhs=>[magix.data.load][objects].Count
          rhs=>0
          code
            set=>[magix.forms.set-value:0][value].Value
              value=>[magix.data.load][objects][0][name].Value
            set=>[magix.forms.set-value:1][value].Value
              value=>[magix.data.load][objects][0][value].Value
            magix.forms.set-enabled
              form-id=>edit-pages
              id=>delete
              value=>true
        else
          magix.forms.set-enabled
            form-id=>edit-pages
            id=>delete
            value=>false
        magix.forms.set-value
          form-id=>edit-pages
          id=>name
          value=>
        magix.forms.set-value
          form-id=>wysiwyg-editor
          id=>wysiwyg
          value=>
      items
        default=>choose web page ...
    panel=>saveWrp
      css=>span-10
      default=>save
      controls
        text-box=>name
          place-holder=>name ...
          css=>span-4
        button=>save
          css=>span-3
          text=>save
          onclick
            magix.forms.get-value
              form-id=>edit-pages
              id=>name
            magix.forms.get-value
              form-id=>wysiwyg-editor
              id=>wysiwyg
            magix.forms.get-value
              form-id=>edit-pages
              id=>page
            if=>not-exist
              lhs=>[magix.forms.get-value:0][value].Value
              code
                magix.viewport.show-message
                  message=>no name given to page
            else
              set=>[@][magix.data.save][value][name].Value
                value=>[magix.forms.get-value][value].Value
              set=>[@][magix.data.remove][prototype][name].Value
                value=>[magix.forms.get-value][value].Value
              set=>[@][magix.data.save][value][name].Value
                value=>[magix.forms.get-value][value].Value
              set=>[@][magix.data.save][value][value].Value
                value=>[magix.forms.get-value:1][value].Value
              magix.data.remove
                prototype
                  type=>magix.pages.page
              magix.data.save
                value
                  type=>magix.pages.page
              magix.viewport.show-message
                message=>page was successfully saved
              magix.data.load
                prototype
                  type=>magix.pages.page
              _id
              for-each=>[@][magix.data.load][objects]
                if=>equals
                  lhs=>[.][name].Value
                  rhs=>[/][magix.forms.get-value][value].Value
                  code
                    set=>[@][..][..][..][_id].Value
                      value=>[.].Name
                _dt
                  name=>value
                set=>[@][_dt][0].Name
                  value=>[.].Name
                set=>[@][_dt][0].Value
                  value=>[.][name].Value
                add=>[@][..][_lst]
                  value=>[@][_dt][0]
              _lst
                default=>choose web page ...
              for-each=>[@][_lst]
                add=>[@][..][magix.forms.set-values][values]
                  value=>[.]
              magix.forms.set-values
                form-id=>edit-pages
                id=>page
              set=>[@][magix.forms.set-value][value].Value
                value=>[@][_id].Value
              magix.forms.set-value
                form-id=>edit-pages
                id=>page
              set=>[@][magix.log.append][body][pr0].Value
                value=>[/][magix.forms.get-value][value].Value
              magix.log.append
                header=>page created
                body=>@"page {0} was created or modified"
              magix.forms.set-enabled
                form-id=>edit-pages
                id=>delete
                value=>true
        button=>delete
          css=>span-3 last
          text=>delete
          enabled=>false
          onclick
            magix.viewport.confirm
              message=>please confirm deletion of page
              code
                magix.forms.get-value
                  form-id=>edit-pages
                  id=>name
                set=>[magix.data.load][prototype][name].Value
                  value=>[magix.forms.get-value][value].Value
                magix.data.load
                  prototype
                    type=>magix.pages.page
                if=>not-exist
                  lhs=>[magix.data.load][objects]
                  code
                    magix.viewport.show-message
                      message=>no such page
                else
                  set=>[@][magix.data.remove][id].Value
                    value=>[/][magix.data.load][objects][0].Name
                  magix.data.remove
                  magix.viewport.show-message
                    message=>page removed
                  magix.forms.set-value
                    form-id=>wysiwyg-editor
                    id=>wysiwyg
                    value=>
                  magix.forms.set-value
                    form-id=>edit-pages
                    id=>name
                    value=>
                  magix.data.load
                    prototype
                      type=>magix.pages.page
                  for-each=>[@][magix.data.load][objects]
                    _dt
                      name=>value
                    set=>[@][_dt][0].Name
                      value=>[.].Name
                    set=>[@][_dt][0].Value
                      value=>[.][name].Value
                    add=>[@][..][_lst]
                      value=>[@][_dt][0]
                  _lst
                    default=>choose web page ...
                  for-each=>[@][_lst]
                    add=>[@][..][magix.forms.set-values][values]
                      value=>[.]
                  magix.forms.set-values
                    form-id=>edit-pages
                    id=>page
                  magix.forms.set-value
                    form-id=>edit-pages
                    id=>page
                    value=>default
                  set=>[@][magix.log.append][body][pr0].Value
                    value=>[magix.forms.get-value][value].Value
                  magix.log.append
                    header=>page deleted
                    body=>@"page {0} was deleted"
                  magix.forms.set-enabled
                    form-id=>edit-pages
                    id=>delete
                    value=>false
    select=>form
      css=>span-5 last
      onselectedindexchanged
        magix.forms.get-value
          form-id=>edit-pages
          id=>form
        if=>not-equals
          lhs=>[magix.forms.get-value][value].Value
          rhs=>default
          code
            set=>[@][magix.data.load][id].Value
              value=>[magix.forms.get-value][value].Value
            magix.data.load
            set=>[@][set:2][value][pr0].Value
              value=>[@][magix.data.load][objects][0][name].Value
            set=>[@][magix.viewport.execute-javascript][script].Value
              value=>@"window.actualEditor.composer.commands.exec('insertHTML', '{{{{form=>{0}}}}}');"
            magix.viewport.execute-javascript
            magix.forms.set-value
              form-id=>edit-pages
              id=>form
              value=>default
      items
        default=>insert form ...


magix.forms.create-web-part
  form-id=>wysiwyg-editor
  container=>content2
  css=>span-24
  parent-css=>span-24
  controls
    wysiwyg=>wysiwyg
      css=>wysiwyg span-24
      place-holder=>your text ...

