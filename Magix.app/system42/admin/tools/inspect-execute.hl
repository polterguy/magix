

inspect=>@"will show a screen of all the 
magix.execute active events in the system, aka; hyper lisp keywords - and allow
the user to inspect them by clicking them"


list-events
  begins-with=>magix.execute.
  all=>false
set=>[list-events].Name
  value=>Data
for-each=>[Data][events]
  _tpl
    link-button
      class=>span-3 hover-shadow
      onclick
        void.foo
          inspect
        set=>[2][value].Value
          value=>[0][inspect].Value
        magix.forms.set-value
          form-id=>form1
          id=>desc
          value
        set=>[4][value].Value
          value=>[0].Name
        magix.forms.set-value
          form-id=>form1
          id=>header
          value
        set=>[0][inspect]
        set=>[node-2-code][node].Value
          value=>[0]
        node-2-code
          remove-root=>true
        replace=>[node-2-code][code].Value
          what=>&
          with=>&amp;
        replace=>[node-2-code][code].Value
          what=><
          with=>&lt;
        replace=>[node-2-code][code].Value
          what=>>
          with=>&gt;
        set=>[magix.forms.set-value:2][value].Value
          value=>[node-2-code][code].Value
        magix.forms.set-value
          form-id=>form1
          id=>code
          value
        set=>[0][inspect].Name
          value=>inspect
        magix.viewport.clear-controls
          container=>content2
  set=>[.ip][..][_tpl][link-button][onclick][0].Name
    value=>[.].Name
  set=>[.ip][..][_tpl][link-button][value].Value
    value=>[.].Name
  replace=>[.ip][..][_tpl][link-button][value].Value
    what=>magix.execute.
  replace=>[.ip][..][_tpl][link-button][value].Value
    what=>magix.execute
    with=>execute
  add=>[/][cf][controls][panel][controls]
    value=>[.ip][..][_tpl][link-button]
set=>[cf][controls][panel2].Name
  value=>panel
set=>[cf].Name
  value=>magix.forms.create-web-part
magix.viewport.clear-controls
  container=>content2
if=>exist
  lhs=>[$][container]
  code
    set=>[magix.forms.create-web-part][container].Value
      value=>[$][container].Value
cf
  form-id=>form1
  class=>span-17 last
  container=>content1
  controls
    panel2
      class=>span-13
      controls
        label=>header
          value=>keyword name
          class=>span-13
          tag=>h3
        label=>code
          class=>span-13
          value=>code example
          tag=>pre
        button=>run
          value=>inspect
          class=>span-2 right last
          onclick
            magix.forms.get-value
              form-id=>form1
              id=>code
            replace=>[magix.forms.get-value][value].Value
              what=>&amp;
              with=>&
            replace=>[magix.forms.get-value][value].Value
              what=>&lt;
              with=><
            replace=>[magix.forms.get-value][value].Value
              what=>&gt;
              with=>>
            set=>[@][magix.admin.load-executor-code][code].Value
              value=>[@][magix.forms.get-value][value].Value
            magix.admin.load-executor-code
              code
              class=>span-17 last left-5 bottom-3 top-1
        label=>lblDesc
          class=>span-13 top-2
          tag=>label
          value=>description
        label=>desc
          class=>span-13
          value=>@"description of your magix.execute keyword.&nbsp;&nbsp;
click any of the keyword active events to the right to inspect them"
    panel=>wrp
      class=>span-4 last
      controls
