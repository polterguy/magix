

inspect=>@"contains the controls for the create dashboard shortcut wizard

contains the controls for the web part that allows the user to create a dashboard 
shortcut active event"


panel
  class=>span-15 last air-padding boxed top-1
  default=>save
  controls
    label
      class=>span-13
      tag=>h2
      value=>create dashboard shortcut active event wizard
    link-button
      class=>span-2 last text-right
      value=>back
      onclick
        execute-script
          file=>system42/private/app-wizard/app-wizard.hl
    label
      class=>span-15 last bottom-1
      value=>from here you can create a dashboard shortcut that will raise an active event of your choice when clicked
    label
      tag=>label
      class=>span-2 bottom-1
      value=>link text
    text-box=>link-text
      class=>span-7 bottom-1
      placeholder=>link text ...
      onfirstload
        magix.forms.effect
          form-id=>wizard
          id=>link-text
          type=>focus-and-select
    label
      tag=>label
      class=>span-2 text-right bottom-1
      value=>category
    text-box=>link-category
      class=>span-4 last bottom-1
      placeholder=>category ...
    label
      tag=>label
      class=>span-2 clear
      value=>active event
    lambda=>lambda-active-event
      oncreatecontrols
        // includes necessary javascript files for our autocompleter
        magix.viewport.include-client-file
          type=>javascript
          file=>media/bootstrap/js/jQuery.js
        magix.viewport.include-client-file
          type=>javascript
          file=>media/bootstrap/js/bootstrap.min.js
        magix.viewport.include-client-file
          type=>css
          file=>media/back-end/typeahead.css
        _json=>
        list-events
        for-each=>[list-events][events]
          if=>not-equals
            lhs=>[/][_json].value
            rhs=>
            code
              set=>[/][_json].value
                value=>{0},
                  =>[/][_json].value
          set=>[/][_json].value
            value=>@"{0}""{1}"""
              =>[/][_json].value
              =>[.].name
        _text
          text-box=>active-event
            placeholder=>active event ...
            class=>span-7
            autocomplete=>false
            @data-provide=>typeahead
            @data-items=>5
            @data-source=>{data-source}
        set=>[**@data-source=>{data-source}].value
          value=>\[{0}]
            =>[_json].value
        add=>[$]
          value=>[_text][text-box]
    button=>save
      class=>span-2 last right top-1 clear
      value=>save
      onclick
        magix.forms.get-children-values
          form-id=>wizard
          id=>content1
        if=>equals
          lhs=>[magix.forms.get-children-values][values][active-event].value
          rhs=>
          or=>equals
            lhs=>[magix.forms.get-children-values][values][link-text].value
            rhs=>
          or=>equals
            lhs=>[magix.forms.get-children-values][values][link-category].value
            rhs=>
          code
            magix.viewport.show-message
              message=>you must fill in active event name, link text and link category
              color=>#ffaaaa
              time=>5000
            magix.forms.effect
              form-id=>wizard
              id=>link-text
              type=>focus-and-select
            stop
        _event
          event
            name=>{name}
            inspect=>@"automatically wizard generated shortcut

returns a dashboard shortcut link, which has been automatically generated
by the create dashboard shortcut wizard.  the dashboard link invokes the 
[{0}] active event"
              inspect=>{active-event}
            code
              _data
                {date}
                  text=>{link-text}
                  category=>{category}
                  code
                    {active-event}
              add=>[$]
                value=>[_data][0]
        magix.date.now
        replace=>[magix.date.now][value].value
          what=>@" "
          with=>-
        replace=>[magix.date.now][value].value
          what=>.
        replace=>[magix.date.now][value].value
          what=>@":"
        _event-name
        set=>[_event-name].value
          value=>magix.admin.desktop-shortcuts.wizard-created-{0}
            =>[magix.date.now][value].value
        iterate=>[_event]
          if=>equals
            lhs=>[.].value
            rhs=>{name}
            code
              set=>[.].value
                value=>[/][_event-name].value
          else-if=>equals
            lhs=>[.].name
            rhs=>{active-event}
            code
              set=>[.].name
                value=>[/][magix.forms.get-children-values][values][active-event].value
          else-if=>equals
            lhs=>[.].value
            rhs=>{active-event}
            code
              set=>[.].value
                value=>[/][magix.forms.get-children-values][values][active-event].value
          else-if=>equals
            lhs=>[.].name
            rhs=>{date}
            code
              set=>[.].name
                value=>name-{0}
                  =>[/][magix.date.now][value].value
          else-if=>equals
            lhs=>[.].value
            rhs=>{link-text}
            code
              set=>[.].value
                value=>[/][magix.forms.get-children-values][values][link-text].value
          else-if=>equals
            lhs=>[.].value
            rhs=>{category}
            code
              set=>[.].value
                value=>[/][magix.forms.get-children-values][values][link-category].value
        lambda=>[_event]
        magix.viewport.show-message
          message=>dashboard shortcut was created
        using=>magix.forms
          set-info
            form-id=>wizard
            id=>edit-event
            value=>[_event][**name=>?].value
          set-visible
            form-id=>wizard
            id=>edit-event
            value=>true
    link-button=>edit-event
      class=>span-3 right clear text-right last
      value=>edit active event
      visible=>false
      onclick
        set=>[magix.event.edit-event][event].value
          value=>[$][info].value
        magix.event.edit-event

