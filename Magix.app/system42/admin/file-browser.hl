
inspect=>@"creates the file browser web form, 
from where you can edit existing, delete and 
upload new files from"


if=>[$][directory].Value
  set=>[magix.file.list-files].Value
    value=>[$][directory].Value
  set=>[magix.file.list-directories].Value
    value=>[$][directory].Value
  set=>[magix.forms.create-form][controls][label][text].Value
    value=>@"files from {0}"
      pr0=>[$][directory].Value
  add=>[/][magix.forms.create-form][controls]
    value=>[/][_back]
  set=>[/][magix.forms.create-form][controls][_back].Name
    value=>link-button
  set=>[/][magix.forms.create-form][events][magix.ide.file-deleted][execute-file][directory].Value
    value=>[$][directory].Value
  set=>[/][magix.forms.create-form][events][magix.ide.file-saved][execute-file][directory].Value
    value=>[$][directory].Value
  set=>[/][_btn][onclick][magix.admin.edit-file][file].Value
    value=>@"{0}/{1}"
      pr0=>[$][directory].Value
      pr1=>empty.txt
  set=>[/][_newDir][onclick][magix.file.create-directory].Value
    value=>@"{0}/{1}"
      pr0=>[$][directory].Value
      pr1=>new-directory
  set=>[/][_newDir][onclick][execute-file][directory].Value
    value=>[$][directory].Value
  set=>[/][_delDir][onclick][magix.viewport.confirm][code][magix.file.delete-directory].Value
    value=>[$][directory].Value
  set=>[/][_ren][controls][text-box][text].Value
    value=>[$][directory].Value
  set=>[/][_ren][controls][text-box][ontextchanged][magix.file.move-directory].Value
    value=>[$][directory].Value
  set=>[/][magix.forms.create-form][controls][uploader][folder].Value
    value=>[$][directory].Value
  set=>[/][magix.forms.create-form][controls][uploader][onuploaded][if][execute-file][directory].Value
    value=>[$][directory].Value
  set=>[/][magix.forms.create-form][controls][uploader][uploaded][if][execute-file][directory].Value
    value=>[$][directory].Value
else
  magix.viewport.set-viewstate=>stack-of-pages
    value
      root=>
  set=>[/][_delDir][enabled].Value
    value=>false
  set=>[/][_ren][controls][text-box][enabled].Value
    value=>false

_back
  css=>span-5 directory back-directory
  text=>..
  onclick
    magix.viewport.get-viewstate
      id=>stack-of-pages
    set=>[@][magix.viewport.get-viewstate][value][[@][magix.viewport.get-viewstate][value].Count-1]
    add=>[@][magix.viewport.set-viewstate]
      value=>[@][magix.viewport.get-viewstate][value]
    magix.viewport.set-viewstate=>stack-of-pages
    set=>[@][execute-file][directory].Value
      value=>[@][magix.viewport.set-viewstate][value][[@][magix.viewport.set-viewstate][value].Count-1].Value
    execute-file=>system42/admin/file-browser.hl

_link-button
  css=>span-5 file
  onclick
    magix.admin.edit-file
      container=>content2
      css=>span-24 min-height-16 top-2

_link-buttonDir
  css=>span-5 directory
  onclick
    execute-file=>system42/admin/file-browser.hl
    magix.viewport.get-viewstate
      id=>stack-of-pages
    set=>[@][magix.viewport.get-viewstate][value][[@][magix.viewport.get-viewstate][value].Count].Value
      value=>[@][execute-file][directory].Value
    add=>[@][magix.viewport.set-viewstate]
      value=>[@][magix.viewport.get-viewstate][value]
    magix.viewport.set-viewstate=>stack-of-pages

magix.file.list-files

for-each=>[magix.file.list-files][files]
  set=>[/][_link-button][text].Value
    value=>[.].Name
  if=>[$][directory]
    set=>[@][replace][what].Value
      value=>@"{0}/"
        pr0=>@"[$][directory].Value"
    replace=>[/][_link-button][text].Value
  set=>[/][_link-button][onclick][magix.admin.edit-file][file].Value
    value=>[.].Name
  add=>[/][magix.forms.create-form][controls]
    value=>[/][_link-button]
  set=>[/][magix.forms.create-form][controls][_link-button].Name
    value=>link-button

magix.file.list-directories

for-each=>[magix.file.list-directories][directories]
  set=>[/][_link-buttonDir][text].Value
    value=>[.].Name
  set=>[/][_link-buttonDir][onclick][execute-file][directory].Value
    value=>[.].Name
  if=>[$][directory]
    set=>[@][replace][what].Value
      value=>@"{0}/"
        pr0=>@"[$][directory].Value"
    replace=>[/][_link-buttonDir][text].Value
  add=>[/][magix.forms.create-form][controls]
    value=>[/][_link-buttonDir]
  set=>[/][magix.forms.create-form][controls][_link-buttonDir].Name
    value=>link-button

_btn=>newfile
  css=>span-3 btn-large left-10 top-1 clear
  text=>new file
  onclick
    magix.admin.edit-file
      css=>span-24 last top-2
      container=>content2
      file=>empty.txt

_newDir=>newdir
  css=>span-3 btn-large top-1
  text=>new dir
  onclick
    magix.file.create-directory=>new-directory
    execute-file=>system42/admin/file-browser.hl

_delDir=>deldir
  css=>span-3 btn-large last top-1
  text=>del dir
  onclick
    magix.viewport.confirm
      message=>do you really want to delete the open directory?
      code
        magix.file.delete-directory
        execute-file=>system42/admin/file-browser.hl

_ren=>renWrp
  css=>span-9 last top-1 right btn-group
  placeholder=>name of directory ...
  default=>saveDirName
  controls
    text-box=>dirName
      css=>span-7
      ontextchanged
        magix.forms.get-value
          form-id=>file-explorer
          id=>dirName
        set=>[magix.file.move-directory][to].Value
          value=>[magix.forms.get-value][value].Value
        magix.file.move-directory
        set=>[execute-file][directory].Value
          value=>[magix.file.move-directory][to].Value
        execute-file=>system42/admin/file-browser.hl
    button=>saveDirName
      css=>span-2 last
      text=>save

add=>[magix.forms.create-form][controls]
  value=>[_btn]
set=>[magix.forms.create-form][controls][_btn].Name
  value=>button

add=>[magix.forms.create-form][controls]
  value=>[_newDir]
set=>[magix.forms.create-form][controls][_newDir].Name
  value=>button

add=>[magix.forms.create-form][controls]
  value=>[_delDir]
set=>[magix.forms.create-form][controls][_delDir].Name
  value=>button

add=>[magix.forms.create-form][controls]
  value=>[_ren]
set=>[magix.forms.create-form][controls][_ren].Name
  value=>panel

magix.forms.create-form
  events

    magix.ide.file-saved
      execute-file=>system42/admin/file-browser.hl

    magix.ide.file-deleted
      execute-file=>system42/admin/file-browser.hl
      magix.viewport.clear-controls
        container=>content2
      magix.viewport.clear-controls
        container=>content3
      magix.viewport.clear-controls
        container=>content4
      magix.viewport.clear-controls
        container=>content5

  container=>content1
  form-id=>file-explorer
  css=>span-19 last min-height-30
  controls
    label=>desc
      css=>span-15 last left-4
      tag=>h2
      text=>files from /
    uploader=>uploader
      folder=>
      onuploaded
        magix.forms.get-value
          form-id=>file-explorer
          id=>uploader
        set=>[magix.viewport.show-message][message].Value
          value=>@"file {0} successfully uploaded"
            pr0=>[/][magix.forms.get-value][value].Value
        magix.viewport.show-message
          time=>1250
        magix.forms.has-more-data
          form-id=>file-explorer
          id=>uploader
        if=>![magix.forms.has-more-data][value].Value
          execute-file=>system42/admin/file-browser.hl

le-browser.hl

