

inspect=>@"tests for the [execute-script] keyword

creates unit tests to test that the [execute-script] 
keyword works the way it should"


event
  name=>magix.test.execute-script-parameters
  persist=>false
  inspect=>@"verifies [execute-script] works with parameters

tries to pass parameters into the [execute-script] keyword, 
making sure parameters are passed both into the execute-script, 
and out from"
  code
    execute-script
      params
        input=>this
      script=>@"
set=>[$][output].value
  value=>{0} {1}
    =>[$][input].value
    =>worked"
    if=>not-equals
      lhs=>[execute-script][params][output].value
      rhs=>this worked
      code
        throw=>[execute-script] misbehaved


event
  name=>magix.test.execute-script-remove-parameters
  persist=>false
  inspect=>@"verifies [execute-script] can remove parameters

tries to pass parameters into [execute-script] which it 
removes from inside of the script, making sure the parameters 
are actually removed also from the outside"
  code
    execute-script
      params
        input=>this
      script=>@"
set=>[$][input]"
    if=>exist
      lhs=>[execute-script][params][input]
      code
        throw=>[execute-script] misbehaved


event
  name=>magix.test.execute-script-file
  persist=>false
  inspect=>@"verifies [execute-script] works with a script file

executes a script from a file, passing in parameters, making 
sure the parameters and the script is correctly executed"
  code
    _file=>your-files/tmp/test-script.txt
    using=>magix.file
      save
        file=>[_file].value
        value=>@"
set=>[$][output].value
  value=>{0} world
    =>[$][input].value"
    execute-script
      params
        input=>howdy
      file=>[_file].value
    using=>magix.file
      delete
        file=>[_file].value
    if=>not-equals
      lhs=>[execute-script][params][output].value
      rhs=>howdy world
      code
        throw=>[execute-script] misbehaved


event
  name=>magix.test.execute-script-file-exception
  persist=>false
  inspect=>@"verifies [execute-script] works with exceptions

executes a script from a file, where the script in the file 
is throwing an exception, making sure the exception is correctly 
catched from the outside of the file"
  code
    _file=>your-files/tmp/test-script.txt
    _exception
    using=>magix.file
      save
        file=>[_file].value
        value=>@"
throw=>success"
    try
      code
        execute-script
          params
            input=>howdy
          file=>[_file].value
      catch
        set=>[_exception].value
          value=>[@][exception].value
    using=>magix.file
      delete
        file=>[_file].value
    if=>not-equals
      lhs=>[_exception].value
      rhs=>success
      code
        throw=>[execute-script] misbehaved

