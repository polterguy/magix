panel=>password-wrapper
  class=>span-6 min-height-5 boxed-light-green air-padding
  default=>save
  controls
    label=>label-0
      value=>old password
      class=>span-6
      tag=>label
    text-box=>old-password
      class=>span-6
      placeholder=>old password ...
      type=>password
    label=>label-1
      value=>new password
      class=>span-6 top-1
      tag=>label
    text-box=>new-password
      class=>span-6
      placeholder=>new password ...
      type=>password
    label=>label-2
      value=>repeat new password
      class=>span-6 top-1
      tag=>label
    text-box=>password-repeat
      class=>span-6
      placeholder=>repeat new password ...
      type=>password
    button=>save
      value=>save
      class=>span-2 top-1 right last
      onclick
        magix.forms.get-children-values
          id=>password-wrapper
        with=>[magix.forms.get-children-values][values]
          if=>not-equals
            lhs=>[new-password].value
            rhs=>[password-repeat].value
            code
              magix.viewport.show-message
                message=>passwords don't match
                color=>#ffaaaa
              magix.forms.effect
                id=>new-password
                type=>focus-and-select
          else
            using=>magix.encryption
              hash-string
                value=>[old-password].value
            magix.session.get
              id=>magix.core.user
            set=>[@][magix.data.load][prototype][pwd].value
              value=>[@][using][hash-string][hash].value
            set=>[@][magix.data.load][prototype][username].value
              value=>[@][magix.session.get][value][username].value
            magix.data.load
              prototype
                type=>magix.core.user
            if=>not-exist
              lhs=>[@][magix.data.load][objects]
              code
                magix.viewport.show-message
                  message=>old password didn't match
                  color=>#ffaaaa
                magix.forms.effect
                  id=>old-password
                  type=>focus-and-select
            else
              using=>magix.encryption
                hash-string
                  value=>[new-password].value
              set=>[@][magix.data.save][id].value
                value=>[@][..][magix.data.load][objects][0][id].value
              add=>[@][magix.data.save]
                value=>[@][..][magix.data.load][objects][0][value]
              set=>[@][magix.data.save][value][pwd].value
                value=>[@][using][hash-string][hash].value
              magix.data.save
              debug
              magix.viewport.show-message
                message=>password was changed
                color=>#aaffaa
