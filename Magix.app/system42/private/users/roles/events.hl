

inspect=>@"contains events for roles web part

contains the active events necessary to show the edit roles tree"


// creates edit roles tree
magix.roles.create-role-tree-controls

  // loading all roles
  magix.data.load
    prototype
      type=>magix.core.role

  // iterating through all "root roles"
  for-each=>[magix.data.load][objects]

    // creating web controls representing role, and returning to caller
    add=>[$][controls]
      values=>[:magix.role.create-tree-node-control[.][value]][controls]

// recursively creates one role link-button
magix.role.create-tree-node-control

  // buffer holding the panel and link-button that becomes the visual representation of the role
  _btn
    panel
      class=>column clear
      controls
        link-button
          class=>span-5
          onclick
            set=>[magix.role.edit-role][role].value
              value=>[$][value].value
            magix.role.edit-role
  set=>[_btn][panel][controls][link-button][value].value
    value=>[$][name].value

  // checking to see if role has child roles, and if it does, iterates through all children roles, and creates their visual representation
  if=>exist
    lhs=>[$][child]
    code

      // role has children, iterating through all child nodes, finding children roles
      for-each=>[$]
        if=>equals
          lhs=>[.].name
          rhs=>child
          code

            // this is a child role, emptying buffer to be safe
            set=>[@][_pnl][0]
            _pnl

            // recursively calling self with current iterating child role
            add=>[@][_pnl]
              values=>[:magix.role.create-tree-node-control[.]][controls]

            // making sure this role is indented on screen
            set=>[@][_pnl][panel][class].value
              value=>column clear
            set=>[@][_pnl][panel][style].value
              value=>padding-left:10px;background-repeat:no-repeat;background-image:url(media/images/icons/tree-indent.png);

            // adding child role to returned value
            add=>[/][_btn][panel][controls]
              value=>[@][_pnl][0]

  // returning controls collection back to caller
  add=>[$][controls]
    value=>[_btn][panel]

// allows user to edit on specific role
magix.role.edit-role
  set=>[**label=>role-name][value].value
    value=>edit role {0}
      =>[$][role].value
  set=>[**button=>save][info].value
    value=>[$][role].value
  set=>[**button=>delete][info].value
    value=>[$][role].value
  if=>equals
    lhs=>[$][role].value
    rhs=>admin
    code
      set=>[**button=>delete][visible].value
        value=>false

  // creates the web part that allows adding a new role to another role as a child
  magix.forms.create-web-part
    container=>edit-role
    form-id=>edit-role
    class=>span-10 last boxed-light-green air-padding top-1
    events-file=>system42/private/users/roles/edit-role/events.hl
    controls
      label=>role-name
        class=>span-10 last
        tag=>h3
        value=>edit role

      panel=>new-role-wrapper
        class=>span-10 last top-1
        default=>save
        onesc
          magix.viewport.clear-controls
            container=>edit-role
            reset-class=>true
        controls
          label
            tag=>label
            class=>span-3
            value=>add child role
          text-box=>txt-role-name
            class=>span-5
            placeholder=>new role name ...
            onfirstload
              magix.forms.effect
                type=>focus-and-select
                form-id=>edit-role
                id=>txt-role-name
                time=>50
          button=>save
            class=>span-2 last
            value=>save
            onclick
              set=>[**parent].value
                value=>[$][info].value
              magix.roles.add-role
                parent

      button=>delete
        class=>span-2 top-1
        value=>delete
        onclick
          set=>[**role].value
            value=>[$][info].value
          using=>magix.viewport
            confirm
              message=>are you sure you wish to delete the '{0}' role? this action cannot be undone
                =>[$][info].value
              code
                magix.roles.delete-role
                  role
                magix.roles.show-roles-tree

