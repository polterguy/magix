

inspect=>@"file run upon every initial page load request, 
such as refreshing, or first load from user/machine, etc.&nbsp;&nbsp;
will create the common header, with the login box and logout logic.&nbsp;&nbsp;
then depending upon if it is a user logged in, belinging to 
an admin role, the desktop will be loaded, or the if the user is 
not an admin, then the run-page.hl script will be executed"

magix.execute
  magix.data.load
    id=>magix.admin.settings.default-front-end-css-file
  set=>[magix.viewport.include-client-file][file].Value
    value=>[magix.data.load][objects][0][file].Value
  magix.viewport.include-client-file
    type=>css

magix.forms.create-mml-web-part
  mml-file=>system42/public/index-header.mml
  container=>header
  form-id=>header
  class=>span-24


magix.web.get=>page
magix.web.get=>dashboard

if=>exist
  lhs=>[magix.web.get][value]
  code
    execute-script
      file=>system42/pages/run-page.hl
else
  magix.web.get-session=>magix.core.user
  if=>exist
    lhs=>[@][magix.web.get-session][value][roles][admin]
    code
      if=>exist
        lhs=>[magix.web.get:1][value]
        code
          magix.forms.create-mml-web-part
            mml-file=>system42/admin/desktop/menu.mml
            container=>menu
            form-id=>admin-menu
            class=>span-5
          execute-script
            file=>system42/admin/desktop/show-desktop.hl
          magix.web.get-session=>magix.admin.setting.show-help
          if=>not-exist
            lhs=>[@][magix.web.get-session][0]
            code
              magix.data.load
                prototype
                  type=>magix.admin.setting.show-help
              set=>[@][magix.web.set-session][value].Value
                value=>[@][magix.data.load][objects][0][value].Value
              magix.web.set-session=>magix.admin.setting.show-help
          magix.web.get-session=>magix.admin.setting.show-help
          if=>equals
            lhs=>[@][magix.web.get-session:1][value].Value
            rhs=>true
            code
              magix.help.start-help
      else
        execute-script
          file=>system42/pages/run-page.hl
  else
    if=>exist
      lhs=>[magix.web.get:1][value]
      code
        magix.web.redirect=>~
    else
      execute-script
        file=>system42/pages/run-page.hl

