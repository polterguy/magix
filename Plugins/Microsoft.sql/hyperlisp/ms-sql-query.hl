

inspect=>@"opens up the microsoft sql 
server query tool"


magix.viewport.clear-controls
  container=>content2
magix.forms.create-web-part
  container=>content1
  class=>span-17 last
  form-id=>sql

  events

    magix.ms.sql.show-columns
      magix.forms.get-value
        id=>chkColumns
      set=>[$][value].Value
        value=>[magix.forms.get-value][value].Value

    magix.ms.sql.show-tables
      magix.forms.get-value
        id=>chkTables
      set=>[$][value].Value
        value=>[magix.forms.get-value][value].Value

    magix.ms.sql.visible-records-count
      magix.forms.get-value
        id=>visible-records-count
      if=>equals
        lhs=>[magix.forms.get-value][value].Value
        rhs=>
        code
          set=>[$][value].Value
            value=>10
      else
        set=>[$][value].Value
          value=>[magix.forms.get-value][value].Value

    magix.ms.sql.populate-selector
      magix.data.load
        prototype
          type=>magix.ms.sql.connection
      for-each=>[magix.data.load][objects]
        _con
        set=>[@][0].Name
          value=>[.][value].Name
        set=>[@][0].Value
          value=>[.][value][connection].Value
        add=>[/][magix.forms.set-values][values]
          value=>[@][0]
      magix.forms.set-values
        form-id=>sql
        id=>ddlConnection
        values
          _default=>choose connection ...

    magix.ms.sql.test-connection
      set=>[try][code][magix.ms.sql.select][connection].Value
        value=>[$][connection].Value
      try
        code
          magix.ms.sql.select
            sql=>@"use master"
          set=>[$][success].Value
            value=>true
        catch
          set=>[@][magix.viewport.show-message][message].Value
            value=>[@][exception].Value
          set=>[$][success].Value
            value=>false
          magix.viewport.show-message
            color=>#ffaaaa
            time=>5000

    magix.ms.sql.open-selected-connection
      magix.forms.set-enabled
        id=>visible-records-count
        value=>true
      magix.forms.set-enabled
        id=>txtNewConnection
        value=>false
      magix.forms.get-value
        form-id=>sql
        id=>ddlConnection
      set=>[magix.data.load][id].Value
        value=>[magix.forms.get-value][value].Value
      magix.data.load
      set=>[magix.ms.sql.test-connection][connection].Value
        value=>[magix.data.load][value][connection].Value
      magix.viewport.clear-controls
        container=>table-list
      magix.viewport.clear-controls
        container=>column-list
      magix.viewport.clear-controls
        container=>select-query
      magix.viewport.clear-controls
        container=>content2
      magix.ms.sql.test-connection
      if=>equals
        lhs=>[magix.ms.sql.test-connection][success].Value
        rhs=>false
        code
          magix.viewport.show-message
            message=>that is not a valid connection
            color=>#ffaaaa
            time=>5000
          stop
      set=>[magix.forms.set-value][value].Value
        value=>[magix.data.load][value][connection].Value
      magix.forms.set-value
        id=>lblHeader
      set=>[magix.forms.set-value:1][value].Value
        value=>[magix.data.load][value][connection].Value
      magix.forms.set-value
        form-id=>sql
        id=>txtNewConnection
      set=>[magix.ms.sql.select][connection].Value
        value=>[magix.data.load][value][connection].Value
      magix.ms.sql.select
        sql=>select distinct * from information_schema.tables where table_type='base table'
      for-each=>[magix.ms.sql.select][result]
        _lb
          onclick
            set=>[magix.ms.sql.create-table-definition-and-sql][info].Value
              value=>[$][info].Value
        set=>[@][_lb][value].Value
          value=>[.][TABLE_NAME].Value
        set=>[@][_lb][info].Value
          value=>[.][TABLE_NAME].Value
        set=>[@][_lb][class].Value
          value=>span-5
        add=>[/][magix.forms.create-web-part][controls]
          value=>[@][_lb]
        set=>[/][magix.forms.create-web-part][controls][_lb].Name
          value=>link-button

      if=>equals
        lhs=>[:magix.ms.sql.show-tables][value].Value
        rhs=>false
        code
          set=>[magix.forms.create-web-part].Name
            value=>_not
          set=>[magix.forms.effect].Name
            value=>_not
          magix.ms.sql.create-table-definition-and-sql
      magix.forms.create-web-part
        class=>span-17 last
        container=>table-list
        form-id=>table-list
        controls
          label=>tblHeader
            tag=>h3
            class=>top-1
            value=>tables
      magix.forms.effect
        type=>fade-in
        form-id=>table-list
        id=>table-list
        time=>500
        joined
          e0
            type=>roll-down

    magix.ms.sql.close-selected-connection
      magix.forms.set-enabled
        id=>visible-records-count
        value=>true
      magix.forms.set-value
        id=>lblHeader
        value=>ms sql query tool
      magix.forms.set-enabled
        id=>txtNewConnection
        value=>true
      magix.forms.set-value
        form-id=>sql
        id=>txtNewConnection
        value=>
      magix.viewport.clear-controls
        container=>table-list
      magix.viewport.clear-controls
        container=>column-list
      magix.viewport.clear-controls
        container=>select-query
      magix.viewport.clear-controls
        container=>content2

    magix.ms.sql.create-table-definition-and-sql
      magix.forms.set-enabled
        id=>visible-records-count
        value=>true
      magix.viewport.clear-controls
        container=>content2
      magix.forms.get-value
        form-id=>sql
        id=>txtNewConnection
      set=>[magix.ms.sql.select][connection].Value
        value=>[magix.forms.get-value][value].Value
      set=>[magix.ms.sql.select][sql].Value
        value=>@"select * from information_schema.columns where table_name = N'{0}'"
          v0=>[$][info].Value
      magix.ms.sql.select
            
      set=>[magix.ms.sql.select:1][connection].Value
        value=>[magix.forms.get-value][value].Value
      set=>[magix.ms.sql.select:1][sql].Value
        value=>@"select * from 
    information_schema.table_constraints tab, 
    information_schema.constraint_column_usage col 
where 
    col.constraint_name = tab.constraint_name
    and col.table_name = tab.table_name
    and constraint_type = 'primary key'
    and col.table_name = N'{0}'"
          v0=>[$][info].Value
      magix.ms.sql.select
      _pks
      for-each=>[magix.ms.sql.select][result]
        _lb
        set=>[@][_lb][value].Value
          value=>[.][COLUMN_NAME].Value
        set=>[@][_lb][class].Value
          value=>span-5 clear table-column
        _pk
        set=>[@][_pk].Value
          value=>[.][COLUMN_NAME].Value
        for-each=>[@][..][magix.ms.sql.select:1][result]
          if=>equals
            lhs=>[.][COLUMN_NAME].Value
            rhs=>[@][..][_pk].Value
            code
              set=>[@][..][..][..][_pk].Value
                value=>true
              add=>[@][..][..][..][..][_pks]
                value=>[.][COLUMN_NAME].Value
                  value
              stop
        if=>equals
          lhs=>[@][_pk].Value
          rhs=>true
          code
            set=>[@][..][..][_lb][class].Value
              value=>{0} primary-key-column
                v0=>[@][..][..][_lb][class].Value
            set=>[@][..][..][_lb][title].Value
              value=>primary key
        else
          set=>[@][..][_lb][title]
        add=>[/][magix.forms.create-web-part:0][controls]
          value=>[@][_lb]
        set=>[/][magix.forms.create-web-part:0][controls][_lb].Name
          value=>label

        set=>[@][_lb][value].Value
          value=>[.][DATA_TYPE].Value
        set=>[@][_lb][class].Value
          value=>span-2
        add=>[/][magix.forms.create-web-part:0][controls]
          value=>[@][_lb]
        set=>[/][magix.forms.create-web-part:0][controls][_lb].Name
          value=>label

        set=>[@][_lb][value].Value
          value=>@"&nbsp;{0}"
            v0=>[.][CHARACTER_MAXIMUM_LENGTH].Value
        set=>[@][_lb][class].Value
          value=>span-3
        add=>[/][magix.forms.create-web-part:0][controls]
          value=>[@][_lb]
        set=>[/][magix.forms.create-web-part:0][controls][_lb].Name
          value=>label
        if=>equals
          lhs=>[.][IS_NULLABLE].Value
          rhs=>NO
          code
            set=>[@][..][..][_lb][value].Value
              value=>not nullable
            set=>[@][..][..][_lb][class].Value
              value=>span-2
            add=>[/][magix.forms.create-web-part:0][controls]
              value=>[@][..][..][_lb]
            set=>[/][magix.forms.create-web-part:0][controls][_lb].Name
              value=>label
        else
          set=>[@][..][_lb][value].Value
            value=>nullable
          set=>[@][..][_lb][class].Value
            value=>span-2
          add=>[/][magix.forms.create-web-part:0][controls]
            value=>[@][..][_lb]
          set=>[/][magix.forms.create-web-part:0][controls][_lb].Name
            value=>label
      add=>[@][magix.viewport.set-viewstate][value]
        value=>[@][_pks]
        children-only=>true
      magix.viewport.set-viewstate
        id=>magix.ms.sql.current-primary-keys
      set=>[magix.forms.create-web-part:0][controls][label:1][value].Value
        value=>[$][info].Value
      set=>[@][magix.viewport.set-viewstate:1][value].Value
        value=>[$][info].Value
      magix.viewport.set-viewstate
        id=>magix.ms.sql.current-table
      if=>equals
        lhs=>[:magix.ms.sql.show-columns][value].Value
        rhs=>false
        code
          set=>[/][magix.forms.create-web-part].Name
            value=>_not
          magix.viewport.clear-controls
            container=>column-list
      magix.forms.create-web-part
        container=>column-list
        forms-id=>column-list
        class=>span-17 last
        controls
          label
            tag=>hr
          label=>tableHeader
            tag=>h3
      set=>[@][**text-area=>sqlQuery][value].Value
        value=>@"select * from [{0}]"
          v0=>[$][info].Value
      magix.forms.create-web-part
        container=>select-query
        form-id=>select-query
        class=>span-17 last
        controls
          text-area=>sqlQuery
            class=>span-17 top-1 last
            rows=>10
            placeholder=>type your sql query here ...
          button=>btnRunQuery
            class=>span-2 right last
            value=>run
            onclick
              magix.ms.sql.run-query
      if=>equals
        lhs=>[:magix.ms.sql.show-columns][value].Value
        rhs=>false
        code
          magix.forms.effect
            type=>timeout
            id=>content1
            time=>500
            chained
              e0
                type=>focus-and-select
                id=>sqlQuery
      magix.forms.effect
        type=>fade-in
        form-id=>select-query
        id=>select-query
        time=>500
        joined
          e0
            type=>roll-down

    magix.ms.sql.run-query
      _sql
      if=>exist
        lhs=>[$][keep-position]
        code
          magix.viewport.get-viewstate
            id=>magix.ms.sql.current-position
          if=>exist
            lhs=>[@][magix.viewport.get-viewstate][value]
            code
              add=>[_position]
                value=>[@][..][..][magix.viewport.get-viewstate][value]
                children-only=>true
      if=>exist
        lhs=>[$][force-select]
        code
          magix.viewport.get-viewstate
            id=>magix.ms.sql.current-select-sql
          if=>exist
            lhs=>[@][magix.viewport.get-viewstate][value]
            code
              set=>[_sql].Value
                value=>[@][..][..][magix.viewport.get-viewstate][value].Value
          else
            magix.forms.get-value
              form-id=>select-query
              id=>sqlQuery
            set=>[_sql].Value
              value=>[@][magix.forms.get-value][value].Value
      else
        magix.forms.get-value
          form-id=>select-query
          id=>sqlQuery
        set=>[_sql].Value
          value=>[@][magix.forms.get-value][value].Value
      magix.forms.get-value
        form-id=>sql
        id=>txtNewConnection
      index-of=>[_sql].Value
        what=>select
      if=>less-than
        lhs=>[index-of][result][0].Value
        rhs=>10
        and=>exist
          lhs=>[index-of][result]
        code
          magix.forms.set-enabled
            id=>visible-records-count
            value=>false
          set=>[@][try][code][magix.ms.sql.select][sql].Value
            value=>[_sql].Value
          set=>[@][try][code][magix.ms.sql.select][connection].Value
            value=>[magix.forms.get-value][value].Value
          set=>[@][magix.viewport.set-viewstate][value].Value
            value=>[_sql].Value
          magix.viewport.set-viewstate
            id=>magix.ms.sql.current-select-sql
          try
            code
              set=>[@][magix.ms.sql.select][end].Value
                value=>[:magix.ms.sql.visible-records-count][value].Value
              set=>[@][magix.viewport.set-viewstate][value][end].Value
                value=>[:magix.ms.sql.visible-records-count][value].Value
              if=>exist
                lhs=>[_position]
                code
                  set=>[@][..][..][magix.ms.sql.select][start].Value
                    value=>[_position][start].Value
                  set=>[@][..][..][magix.ms.sql.select][end].Value
                    value=>[_position][end].Value
                  set=>[@][..][..][magix.viewport.set-viewstate][value][start].Value
                    value=>[_position][start].Value
                  set=>[@][..][..][magix.viewport.set-viewstate][value][end].Value
                    value=>[_position][end].Value
              else-if=>exist
                lhs=>[$][start]
                code
                  set=>[@][..][..][magix.ms.sql.select][start].Value
                    value=>[$][start].Value
                  set=>[@][..][..][magix.ms.sql.select][end].Value
                    value=>[$][end].Value
                  set=>[@][..][..][magix.viewport.set-viewstate][value][start].Value
                    value=>[$][start].Value
                  set=>[@][..][..][magix.viewport.set-viewstate][value][end].Value
                    value=>[$][end].Value
              magix.ms.sql.select
                start=>0
              if=>more-than
                lhs=>[@][magix.ms.sql.select][end].Value
                rhs=>[@][magix.ms.sql.select][record-count].Value
                code
                  set=>[@][..][..][**v1=>?].Value
                    value=>[@][..][..][magix.ms.sql.select][record-count].Value
              set=>[/][**panel=>tblPaging][controls][label=>lblCount][value].Value
                value=>@"{0}/{1} of {2}"
                  v0=>[@][magix.ms.sql.select][start].Value
                  v1=>[@][magix.ms.sql.select][end].Value
                  v2=>[@][magix.ms.sql.select][record-count].Value

              set=>[@][magix.viewport.set-viewstate][value][count].Value
                value=>[@][magix.ms.sql.select][record-count].Value
              magix.viewport.set-viewstate
                id=>magix.ms.sql.current-position
                value
                  start=>0
            catch
              set=>[@][magix.viewport.show-message][message].Value
                value=>[@][exception].Value
              magix.viewport.show-message
                time=>10000
                color=>#ffaaaa
              stop
          for-each=>[@][try][code][magix.ms.sql.select][result][0]
            _th
              tag=>th
            set=>[@][_th][value].Value
              value=>[.].Name
            add=>[@][..][**panel=>tbl][controls][panel][controls]
              value=>[@][_th]
            set=>[@][..][**panel=>tbl][controls][panel][controls][_th].Name
              value=>label
          for-each=>[@][try][code][magix.ms.sql.select][result]
            set=>[@][_pnl][controls]
            set=>[@][_pnl][onclick][_columns]
            add=>[@][_pnl][onclick][_columns]
              value=>[.]
              children-only=>true
            _pnl
              tag=>tr
              style=>cursor:pointer;
              title=>click to create update sql statement
              onclick
                _sql=>@"update [[___table]] set"
                magix.viewport.get-viewstate
                  id=>magix.ms.sql.current-table
                magix.viewport.get-viewstate
                  id=>magix.ms.sql.current-primary-keys
                if=>equals
                  lhs=>[magix.viewport.get-viewstate:1][value].Count
                  rhs=>0
                  code
                    magix.viewport.show-message
                      time=>15000
                      message=>@"cannot automatically create update statement for table, 
either because table has no primary keys, or because table definitions is not turned on.
&nbsp;&nbsp;turn on table definitions by clicking the 'tables' checkbox, and make sure 
you select the table from the list of tables, before you run your select from it"
                    stop
                replace=>[_sql].Value
                  what=>\[___table]
                  with=>[magix.viewport.get-viewstate][value].Value
                _first=>true
                for-each=>[_columns]
                  if=>not-exist
                    lhs=>[/][magix.viewport.get-viewstate:1][value][[.].Name]
                    code
                      if=>equals
                        lhs=>[/][_first].Value
                        rhs=>true
                        code
                          set=>[/][_first].Value
                            value=>false
                      else
                        set=>[/][_sql].Value
                          value=>@"{0},"
                            =>[/][_sql].Value
                      replace=>[.].Value
                        what=>'
                        with=>''
                      set=>[/][_sql].Value
                        value=>@"{0}
  {1}='{2}'"
                          =>[/][_sql].Value
                          =>[.].Name
                          =>[.].Value
                set=>[/][_sql].Value
                  value=>@"{0}
where"
                    =>[/][_sql].Value
                set=>[_first].Value
                  value=>true
                for-each=>[_columns]
                  if=>exist
                    lhs=>[/][magix.viewport.get-viewstate:1][value][[.].Name]
                    code
                      if=>exist
                        lhs=>[.].Value
                        code
                          if=>equals
                            lhs=>[/][_first].Value
                            rhs=>true
                            code
                              set=>[/][_first].Value
                                value=>false
                          else
                            set=>[/][_sql].Value
                              value=>{0} and
                                =>[/][_sql].Value
                          set=>[/][_sql].Value
                            value=>@"{0}
  {1}='{2}'"
                              =>[/][_sql].Value
                              =>[.].Name
                              =>[.].Value
                set=>[magix.forms.set-value][value].Value
                  value=>[_sql].Value
                magix.forms.set-value
                  form-id=>select-query
                  id=>sqlQuery
            for-each=>[.]
              _lbl
                tag=>td
              set=>[@][_lbl][value].Value
                value=>[.].Value
              add=>[@][..][_pnl][controls]
                value=>[@][_lbl]
              set=>[@][..][_pnl][controls][_lbl].Name
                value=>label
            add=>[@][..][magix.forms.create-web-part][controls][panel=>tbl][controls]
              value=>[@][_pnl]
            set=>[@][..][magix.forms.create-web-part][controls][panel=>tbl][controls][_pnl].Name
              value=>panel

          magix.forms.create-web-part
            container=>content2
            form-id=>sql-result
            class=>span-22 last top-1
            controls
              panel=>tblPaging
                class=>span-8 left-14 last bottom-1
                controls
                  link-button=>previous
                    class=>span-1
                    value=>&lt;&lt;
                    onclick
                      magix.viewport.get-viewstate
                        id=>magix.ms.sql.current-position
                      if=>equals
                        lhs=>[magix.viewport.get-viewstate][value][start].Value
                        rhs=>0
                        code
                          magix.viewport.show-message
                            message=>already at beginning
                          stop
                      using=>magix.math
                        subtract
                          =>[/][magix.viewport.get-viewstate][value][start].Value
                          =>[:magix.ms.sql.visible-records-count][value].Value
                        subtract
                          =>[/][magix.viewport.get-viewstate][value][end].Value
                          =>[:magix.ms.sql.visible-records-count][value].Value
                      set=>[magix.ms.sql.run-query][start].Value
                        value=>[using][subtract:0].Value
                      set=>[magix.ms.sql.run-query][end].Value
                        value=>[using][subtract:1].Value
                      magix.ms.sql.run-query
                        force-select=>true
                  label=>lblCount
                    class=>span-4 text-center
                    value=>count
                  link-button=>next
                    class=>span-1
                    value=>&gt;&gt;
                    onclick
                      magix.viewport.get-viewstate
                        id=>magix.ms.sql.current-position
                      if=>more-than-equals
                        lhs=>[magix.viewport.get-viewstate][value][end].Value
                        rhs=>[magix.viewport.get-viewstate][value][count].Value
                        code
                          magix.viewport.show-message
                            message=>already at end
                          stop
                      using=>magix.math
                        add
                          =>[/][magix.viewport.get-viewstate][value][start].Value
                          =>[:magix.ms.sql.visible-records-count][value].Value
                        add
                          =>[/][magix.viewport.get-viewstate][value][end].Value
                          =>[:magix.ms.sql.visible-records-count][value].Value
                      set=>[magix.ms.sql.run-query][start].Value
                        value=>[using][add:0].Value
                      set=>[magix.ms.sql.run-query][end].Value
                        value=>[using][add:1].Value
                      magix.ms.sql.run-query
                        force-select=>true
                  button=>close-select
                    value=>done
                    class=>span-2 last
                    onclick
                      magix.viewport.clear-controls
                        container=>content2
                      magix.forms.set-enabled
                        id=>visible-records-count
                        value=>true
              panel=>tbl
                tag=>table
                class=>sql-result-table
                controls
                  panel
                    tag=>tr
      else
        set=>[@][try][code][magix.ms.sql.execute][sql].Value
          value=>[/][_sql].Value
        set=>[@][try][code][magix.ms.sql.execute][connection].Value
          value=>[/][magix.forms.get-value][value].Value
        try
          code
            magix.ms.sql.execute
            set=>[@][magix.viewport.show-message][message].Value
              value=>@"{0} records affected"
                v0=>[@][magix.ms.sql.execute][result].Value
            magix.ms.sql.run-query
              force-select=>true
              keep-position=>true
            magix.viewport.show-message
              time=>5000
          catch
            set=>[@][magix.viewport.show-message][message].Value
              value=>[@][exception].Value
            magix.viewport.show-message
              time=>10000
              color=>#ffaaaa

  controls
    label=>lblHeader
      tag=>h3
      value=>ms sql query tool
    panel=>connectionWrp
      class=>span-17 last
      default=>btnNew
      controls
        label=>conLbl
          tag=>label
          class=>span-2
          value=>connection
        select=>ddlConnection
          class=>span-5
          onfirstload
            magix.ms.sql.populate-selector
          onselectedindexchanged
            if=>not-equals
              lhs=>[$][value].Value
              rhs=>_default
              code
                magix.forms.set-enabled
                  form-id=>sql
                  id=>delete
                  value=>true
                magix.ms.sql.open-selected-connection
            else
              magix.forms.set-enabled
                form-id=>sql
                id=>delete
                value=>false
              magix.ms.sql.close-selected-connection
        button=>delete
          class=>span-2
          value=>delete
          disabled=>true
          onclick
            magix.forms.get-value
              form-id=>sql
              id=>ddlConnection
            set=>[magix.data.remove][id].Value
              value=>[magix.forms.get-value][value].Value
            magix.data.remove
            magix.ms.sql.populate-selector
            magix.forms.set-enabled
              form-id=>sql
              id=>delete
              value=>false
            magix.ms.sql.close-selected-connection
        label=>lblNew
          tag=>label
          class=>span-1 right-text
          value=>new
        text-box=>txtNewConnection
          placeholder=>connection string ...
          class=>span-5
        button=>btnNew
          value=>create
          class=>span-2 last
          onclick
            magix.forms.get-value
              form-id=>sql
              id=>txtNewConnection
            set=>[magix.ms.sql.test-connection][connection].Value
              value=>[magix.forms.get-value][value].Value
            magix.ms.sql.test-connection
            if=>equals
              lhs=>[magix.ms.sql.test-connection][success].Value
              rhs=>false
              code
                magix.viewport.show-message
                  message=>that is not a good connection
                stop
            set=>[magix.data.save][value][connection].Value
              value=>[magix.forms.get-value][value].Value
            magix.data.save
              value
                type=>magix.ms.sql.connection
            magix.ms.sql.populate-selector
            set=>[magix.forms.set-value][value].Value
              value=>[magix.data.save][id].Value
            magix.forms.set-value
              form-id=>sql
              id=>ddlConnection
            magix.forms.set-enabled
              form-id=>sql
              id=>delete
              value=>true
            magix.ms.sql.open-selected-connection
        label=>lblVisibleRecords
          value=>records
          tag=>label
          class=>span-2 top-1
        text-box=>visible-records-count
          placeholder=>10 ...
          type=>number
          class=>span-1 top-1
        label=>lblCheckTables
          for=>chkTables
          tag=>label
          value=>tables
          class=>span-2 top-1 right-text
        check-box=>chkTables
          checked=>true
          class=>span-1 top-1
          oncheckedchanged
            if=>equals
              lhs=>[$][value].Value
              rhs=>false
              code
                magix.forms.set-enabled
                  id=>chkColumns
                  value=>false
                magix.forms.set-value
                  id=>chkColumns
                  value=>false
            else
              magix.forms.set-enabled
                id=>chkColumns
                value=>true
            magix.forms.get-enabled
              id=>txtNewConnection
            if=>equals
              lhs=>[magix.forms.get-enabled][value].Value
              rhs=>false
              code
                magix.ms.sql.open-selected-connection
        label=>lblCheckColumns
          for=>chkColumns
          tag=>label
          value=>columns
          class=>span-2 top-1 right-text
        check-box=>chkColumns
          checked=>true
          class=>span-1 top-1
          oncheckedchanged
            magix.viewport.get-viewstate
              id=>magix.ms.sql.current-table
            if=>exist
              lhs=>[magix.viewport.get-viewstate][value].Value
              code
                set=>[magix.ms.sql.create-table-definition-and-sql][info].Value
                  value=>[magix.viewport.get-viewstate][value].Value
    dynamic=>table-list
    dynamic=>column-list
    dynamic=>select-query
