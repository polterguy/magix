
<div class="span-22 last relative">
{{
system42.travel.change-login-box
  magix.web.get-session
    name=>magix.core.user
  if=>not-exist
    lhs=>[magix.web.get-session][value]
    code
      magix.forms.set-visible
        form-id=>header
        id=>loginWrp
        value=>true
      magix.forms.set-visible
        form-id=>header
        id=>logoutWrp
        value=>false
  else
    magix.forms.set-visible
      form-id=>header
      id=>loginWrp
      value=>false
    magix.forms.set-visible
      form-id=>header
      id=>logoutWrp
      value=>true

system42.travel.try-login
  set=>[magix.data.load][prototype][username].Value
    value=>[$][username].Value
  set=>[magix.data.load][prototype][pwd].Value
    value=>[$][pwd].Value
  magix.data.load
    prototype
      type=>magix.core.user
  if=>more-than
    lhs=>[magix.data.load][objects].Count
    rhs=>0
    code
      add=>[@][magix.web.set-session][value]
        value=>[magix.data.load][objects][0]
        children-only=>true
      magix.web.set-session
        name=>magix.core.user
      if=>exist
        lhs=>[magix.data.load][objects][0][roles][admin]
        code
          magix.browser.redirect=>~?dashboard=
      else
        magix.viewport.execute-javascript
          script=>@"location.reload();"
  else
    magix.forms.set-value
      form-id=>header
      id=>error
      value=>(*) error
    magix.forms.set-focus
      form-id=>header
      id=>username
    magix.forms.select-all
      form-id=>header
      id=>username

lambda=>header
  oncreatecontrols
    _data
      label=>headerCtrl
        class=>span-14 top-2 bottom-1
        value=>magix illuminate - phosphorus
        tag=>h1
    magix.configuration.get-setting
      name=>magix.core.portal-name
    set=>[_data][label][value].Value
      value=>[magix.configuration.get-setting][value].Value
    add=>[$]
      value=>[_data][label]

panel=>loginWrp
  onfirstload
    system42.travel.change-login-box
  class=>absolute top right span-6 top-1 z-1 last
  controls
    panel=>loginPnl
      class=>span-4 left-2
      default=>loginBtn
      controls
        label=>descr
          tag=>label
          value=>please login
        text-box=>username
          placeholder=>username ...
          class=>span-4
        text-box=>pwd
          placeholder=>password ...
          type=>password
          class=>span-4
        label=>error
          class=>error span-2
        button=>loginBtn
          value=>login
          class=>right span-2 btn-large last
          onclick
            magix.forms.get-value
              form-id=>header
              id=>username
            magix.forms.get-value
              form-id=>header
              id=>pwd
            set=>[system42.travel.try-login][username].Value
              value=>[magix.forms.get-value][value].Value
            set=>[system42.travel.try-login][pwd].Value
              value=>[magix.forms.get-value:1][value].Value
            system42.travel.try-login

panel=>logoutWrp
  class=>absolute top right span-4 top-1 z-1 last
  controls
    button=>logOut
      class=>span-4 btn-large
      value=>logout
      onclick
        magix.web.set-session
          name=>magix.core.user
        magix.browser.redirect=>~
    lambda=>createLoadDashboard
      oncreatecontrols
        magix.web.get-session
          name=>magix.core.user
        if=>exist
          lhs=>[magix.web.get-session][value][roles][admin]
          code
            _lnk=>load_dashboard
              class=>span-4 right-text
            magix.web.get-parameter
              name=>dashboard
            if=>exist
              lhs=>[@][magix.web.get-parameter][value]
              code
                set=>[if][code][_lnk][href].Value
                  value=>~
                set=>[if][code][_lnk][value].Value
                  value=>website
            else
              set=>[if][code][_lnk][href].Value
                value=>~?dashboard=
              set=>[if][code][_lnk][value].Value
                value=>dashboard
            add=>[$]
              value=>[@][_lnk]
            set=>[$][_lnk].Name
              value=>hyperlink
}}
</div>

