

inspect=>@"contains the events for the database manager

contains the events for the database manager web part"


// raised when the search button is clicked
magix.database-manager.search_onclick
  magix.forms.effect
    form-id=>database-manager-wrp
    id=>pnl-search-wrp
    type=>fade-in
    time=>250
    chained
      e0
        form-id=>database-manager-wrp
        id=>prototype-search
        type=>focus-and-select


// insert button was clicked
magix.database-manager.insert_onclick
  magix.forms.effect
    form-id=>database-manager-wrp
    id=>pnl-insert-wrp
    type=>fade-in
    time=>250
    chained
      e0
        form-id=>database-manager-wrp
        id=>insert-txt
        type=>focus-and-select


// prototype-search is initially loaded
magix.database-manager.prototype-search_onfirstload
  magix.data.load-username
    id=>magix.database-manager.current-view
  if=>exist
    lhs=>[magix.data.load-username][value]
    code
      using=>magix.forms
        set-value
          form-id=>database-manager-wrp
          id=>prototype-search
          value=>[magix.data.load-username][value][current-view].value


// prototype-search had focus while the esc key was pressed
magix.database-manager.prototype-search_onesc
  magix.data.load-username
    id=>magix.database-manager.current-view
  if=>exist
    lhs=>[magix.data.load-username][value]
    code
      using=>magix.forms
        set-value
          form-id=>database-manager-wrp
          id=>prototype-search
          value=>[magix.data.load-username][value][current-view].value
  magix.forms.effect
    form-id=>database-manager-wrp
    id=>pnl-search-wrp
    type=>fade-out
    time=>250


// apply-search was clicked
magix.database-manager.apply-search_onclick
  magix.database-manager.create-data-grid
  magix.forms.effect
    form-id=>database-manager-wrp
    id=>pnl-search-wrp
    type=>fade-out
    time=>250
  magix.forms.get-value
    form-id=>database-manager-wrp
    id=>prototype-search
  set=>[magix.data.save-username][value][current-view].value
    value=>[magix.forms.get-value][value].value
  magix.data.save-username
    id=>magix.database-manager.current-view


// esc key was pressed when insert-txt had focus
magix.database-manager.insert-txt_onesc
  magix.forms.effect
    form-id=>database-manager-wrp
    id=>pnl-insert-wrp
    type=>fade-out
    time=>250


// creates tha actual data-grid of items from database
magix.database-manager.create-data-grid

  // checking input parameters
  if=>exist
    lhs=>[$][start]
    code
      set=>[**start=>{start}].value
        value=>[$][start].value
      set=>[**end=>{end}].value
        value=>[$][end].value
  else
    set=>[**start=>{start}].value
      value=>0
    set=>[**end=>{end}].value
      value=>25

  // creating actual datagrid
  magix.forms.create-web-part
    container=>data-grid
    form-id=>database-manager
    class=>span-17 last
    controls
      lambda=>items
        oncreatecontrols
          magix.database-manager.create-data-grid-items
            start=>{start}
            end=>{end}
          add=>[$]
            values=>[0][controls]


// returns the controls for the datagrid being the items from the database
magix.database-manager.create-data-grid-items
  _controls
    panel
      class=>span-17 last
      controls
        label
          tag=>label
          class=>span-12
          value=>id
        label
          tag=>label
          class=>span-4
          value=>created
        label
          tag=>label
          class=>span-1 last
          value=>rev.
  magix.forms.get-value
    form-id=>database-manager-wrp
    id=>prototype-search
  using=>magix.forms
    set-title
      form-id=>database-manager-wrp
      id=>search
      value=>[magix.forms.get-value][value].value
  if=>not-equals
    lhs=>[magix.forms.get-value][value].value
    rhs=>
    code
      set=>[using=>magix.data][load][prototype]
      code-2-node
        code=>[magix.forms.get-value][value].value
      if=>not-exist
        lhs=>[@][code-2-node][node][prototype]
        and=>not-exist
          lhs=>[@][code-2-node][node][or]
        code
          add=>[@][..][..][code-2-node][node]
            value=>prototype
      if=>exist
        lhs=>[@][code-2-node][node][start]
        code
          set=>[using=>magix.data][load][start]
          set=>[using=>magix.data][load][end]
          set=>[$][start].value
            value=>[@][..][..][code-2-node][node][start].value
          set=>[$][end].value
            value=>[@][..][..][code-2-node][node][end].value
      add=>[using=>magix.data][load]
        values=>[@][code-2-node][node]
  add=>[magix.viewstate.set=>current-view][value]
    values=>[using=>magix.data][load]
  set=>[magix.viewstate.set=>current-view][value][only-id]
  set=>[magix.viewstate.set=>current-view][value][start].value
    value=>[$][start].value
  set=>[magix.viewstate.set=>current-view][value][end].value
    value=>[$][end].value
  magix.viewstate.set=>current-view
    id=>magix.database-manager.current-view
  add=>[using=>magix.data][count]
    values=>[using=>magix.data][load]
  set=>[using=>magix.data][count][start]
  set=>[using=>magix.data][count][end]
  set=>[using=>magix.data][count][only-id]
  set=>[using=>magix.data][count][sort]
  using=>magix.data
    load
      start=>[$][start].value
      end=>[$][end].value
      only-id=>true
      prototype
    count
  if=>more-than
    lhs=>[$][end].value
    rhs=>[using=>magix.data][count][count].value
    code
      set=>[$][end].value
        value=>[using=>magix.data][count][count].value
  using=>magix.math
    add=>[$][start]
      =>[$][start].value
      =>1
  using=>magix.forms
    set-value
      form-id=>database-manager-wrp
      id=>items-header
      value=>database manager - {0}/{1} of {2}
        =>[$][start].value
        =>[$][end].value
        =>[using=>magix.data][count][count].value
  for-each=>[using=>magix.data][load][objects]
    _pn
      panel=>{wrp-pnl-}
        class=>database-item
        onclick
          set=>[magix.database-manager.select-item][id].value
            value=>[$][info].value
          replace=>[$][id].value
            what=>wrp-pnl-
            with=>wrp-dyn-
          set=>[magix.database-manager.select-item][container].value
            value=>[$][id].value
          magix.database-manager.select-item
        controls
          label=>id
            class=>span-12
          label=>created
            class=>span-4
          label=>revision-count
            class=>span-1 last text-right
      hidden=>{is-viewing-}
      dynamic=>{wrp-dyn-}
        class=>db-sleeping-dynamic
    set=>[@][_pn][panel][info].value
      value=>[id].value
    set=>[@][_pn][**label=>id][value].value
      value=>[id].value
    set=>[@][_pn][**label=>created][value].value
      value=>[created].value
    set=>[@][_pn][**label=>revision-count][value].value
      value=>[revision-count].value
    add=>[/][_controls][panel][controls]
      values=>[@][_pn]
  add=>[$][controls]
    values=>[_controls]


// pages to previous items
magix.database-manager.previous-items
  magix.viewstate.get=>current-view
    id=>magix.database-manager.current-view
  if=>more-than
    lhs=>[magix.viewstate.get=>current-view][value][start].value
    rhs=>0
    code
      _delta
      using=>magix.math
        subtract=>[@][..][_delta]
          =>[magix.viewstate.get=>current-view][value][end].value
          =>[magix.viewstate.get=>current-view][value][start].value
        subtract=>[magix.viewstate.get=>current-view][value][start]
          =>[magix.viewstate.get=>current-view][value][start].value
          =>[@][..][_delta].value
        subtract=>[magix.viewstate.get=>current-view][value][end]
          =>[magix.viewstate.get=>current-view][value][end].value
          =>[@][..][_delta].value
      set=>[@][magix.database-manager.create-data-grid][start].value
        value=>[magix.viewstate.get=>current-view][value][start].value
      set=>[@][magix.database-manager.create-data-grid][end].value
        value=>[magix.viewstate.get=>current-view][value][end].value
      magix.forms.get-value
        form-id=>database-manager-wrp
        id=>prototype-search
      if=>not-equals
        lhs=>[@][magix.forms.get-value][value].value
        rhs=>
        code
          code-2-node
            code=>[@][..][..][magix.forms.get-value][value].value
          set=>[@][code-2-node][node][start]
          set=>[@][code-2-node][node][end]
          node-2-code
            node=>[@][code-2-node][node]
            remove-root=>true
          using=>magix.forms
            set-value
              form-id=>database-manager-wrp
              id=>prototype-search
              value=>[@][..][node-2-code][code].value
      magix.database-manager.create-data-grid
  else
    magix.viewport.show-message
      message=>already at beginning of dataset
      time=>2000
      color=>#ffaaaa


// pages to previous items
magix.database-manager.next-items
  magix.viewstate.get=>current-view
    id=>magix.database-manager.current-view
  add=>[@][magix.data.count]
    values=>[magix.viewstate.get=>current-view][value]
  set=>[magix.data.count][start]
  set=>[magix.data.count][end]
  set=>[magix.data.count][only-id]
  set=>[magix.data.count][sort]
  magix.data.count
  if=>less-than
    lhs=>[magix.viewstate.get=>current-view][value][end].value
    rhs=>[magix.data.count][count].value
    code
      _delta
      using=>magix.math
        subtract=>[@][..][_delta]
          =>[magix.viewstate.get=>current-view][value][end].value
          =>[magix.viewstate.get=>current-view][value][start].value
        add=>[magix.viewstate.get=>current-view][value][start]
          =>[magix.viewstate.get=>current-view][value][start].value
          =>[@][..][_delta].value
        add=>[magix.viewstate.get=>current-view][value][end]
          =>[magix.viewstate.get=>current-view][value][end].value
          =>[@][..][_delta].value
      set=>[@][magix.database-manager.create-data-grid][start].value
        value=>[magix.viewstate.get=>current-view][value][start].value
      set=>[@][magix.database-manager.create-data-grid][end].value
        value=>[magix.viewstate.get=>current-view][value][end].value
      magix.forms.get-value
        form-id=>database-manager-wrp
        id=>prototype-search
      if=>not-equals
        lhs=>[@][magix.forms.get-value][value].value
        rhs=>
        code
          code-2-node
            code=>[@][..][..][magix.forms.get-value][value].value
          set=>[@][code-2-node][node][start]
          set=>[@][code-2-node][node][end]
          node-2-code
            node=>[@][code-2-node][node]
            remove-root=>true
          using=>magix.forms
            set-value
              form-id=>database-manager-wrp
              id=>prototype-search
              value=>[@][..][node-2-code][code].value
      magix.database-manager.create-data-grid
  else
    magix.viewport.show-message
      message=>already at end of dataset
      time=>2000
      color=>#ffaaaa


// creates the "view single item" web part
magix.database-manager.select-item
  _viewing
  set=>[_viewing].value
    value=>[$][container].value
  replace=>[_viewing].value
    what=>wrp-dyn-
    with=>is-viewing-
  using=>magix.forms
    get-value
      form-id=>database-manager
      id=>[_viewing].value
  if=>equals
    lhs=>[using=>magix.forms][get-value][value].value
    rhs=>true
    code
      using=>magix.forms
        set-value
          form-id=>database-manager
          id=>[_viewing].value
          value=>
      using=>magix.viewport
        clear-controls
          container=>[$][container].value
          reset-class=>true
            new-class=>db-sleeping-dynamic
  else
    using=>magix.forms
      set-value
        form-id=>database-manager
        id=>[_viewing].value
        value=>true
    magix.viewstate.get=>current-view
      id=>magix.database-manager.current-view
    if=>exist
      lhs=>[@][magix.viewstate.get=>current-view][value][extract]
      code
        add=>[@][..][..][using=>magix.data][load]
          value=>[@][..][..][magix.viewstate.get=>current-view][value][extract]
    using=>magix.data
      load
        id=>[$][id].value
    node-2-code
      node=>[@][using=>magix.data][load][value]
      remove-root=>true
    replace=>[@][node-2-code][code].value
      what=>&
      with=>&amp;
    replace=>[@][node-2-code][code].value
      what=>>
      with=>&gt;
    replace=>[@][node-2-code][code].value
      what=><
      with=>&lt;
    set=>[@][**label=>code][value].value
      value=>[@][node-2-code][code].value
    set=>[@][**button=>edit][info].value
      value=>[$][id].value
    set=>[@][**button=>delete][info].value
      value=>[$][id].value
    set=>[@][**button=>save][info].value
      value=>[$][id].value
    using=>magix.forms
      create-web-part
        container=>[$][container].value
        form-id=>[$][container].value
        class=>span-17 last
        controls
          panel=>view-wrp
            class=>span-15 air-padding last view-code bottom-1
            tag=>pre
            controls
              label=>code
                class=>span-15 last
              panel
                class=>span-4 last right clear btn-group
                controls
                  button=>edit
                    class=>span-2
                    value=>edit
                    onclick
                      using=>magix.data
                        load
                          id=>[$][info].value
                      node-2-code
                        node=>[using=>magix.data][load][value]
                        remove-root=>true
                      using=>magix.forms
                        set-value
                          form-id=>[$][form-id].value
                          id=>edit-object
                          value=>[node-2-code][code].value
                        set-visible
                          form-id=>[$][form-id].value
                          id=>edit-object
                          value=>true
                        set-visible
                          form-id=>[$][form-id].value
                          id=>save
                          value=>true
                        effect
                          form-id=>[$][form-id].value
                          id=>edit-object
                          type=>focus-and-select
                  button=>delete
                    class=>span-2
                    value=>delete
                    onclick
                      add=>[magix.viewport.confirm][code]
                        value=>[$]
                      magix.viewport.confirm
                        message=>are you sure you wish to delete this item from the database?
                        code
                          using=>magix.data
                            remove
                              id=>[$][info].value
                          magix.viewstate.get=>current-view
                            id=>magix.database-manager.current-view
                          if=>exist
                            lhs=>[magix.viewstate.get=>current-view][value]
                            code
                              set=>[magix.database-manager.create-data-grid][start].value
                                value=>[magix.viewstate.get=>current-view][value][start].value
                              set=>[magix.database-manager.create-data-grid][end].value
                                value=>[magix.viewstate.get=>current-view][value][end].value
                          magix.database-manager.create-data-grid
              text-area=>edit-object
                class=>span-15 last top-1
                rows=>10
                visible=>false
                placeholder=>object data ...
                onesc
                  using=>magix.forms
                    set-visible
                      form-id=>[$][form-id].value
                      id=>edit-object
                    set-visible
                      form-id=>[$][form-id].value
                      id=>save
              button=>save
                class=>span-2 top-1 last right
                value=>save
                visible=>false
                onclick
                  using=>magix.forms
                    get-value
                      form-id=>[$][form-id].value
                      id=>edit-object
                  code-2-node
                    code=>[using=>magix.forms][get-value][value].value
                  using=>magix.data
                    save
                      id=>[$][info].value
                      value=>[code-2-node][node]
                  magix.viewport.show-message
                    message=>object was saved
                  using=>magix.viewport
                    clear-controls
                      container=>[$][form-id].value
                  replace=>[$][container].value
                    what=>wrp-dyn-
                    with=>is-viewing-
                  using=>magix.forms
                    set-value
                      form-id=>database-manager-wrp
                      id=>[$][container].value


// inserts a new item into database
magix.database-manager.apply-insert_onclick
  magix.forms.get-value
    form-id=>database-manager-wrp
    id=>insert-txt
  if=>equals
    lhs=>[magix.forms.get-value][value].value
    rhs=>
    code
      magix.viewport.show-message
        message=>you'll need to give me some data for me to be able to create an object for you
        color=>#ffaaaa
      stop
  code-2-node
    code=>[magix.forms.get-value][value].value
  if=>exist
    lhs=>[code-2-node][node][id]
    code
      using=>magix.data
        load
          id=>[code-2-node][node][id].value
      if=>exist
        lhs=>[@][using=>magix.data][load][value]
        code
          magix.viewport.show-message
            message=>@"object already exists, choose to edit it to update it, 
or remove id parameter to your creation statement to create a new object"
            color=>#ffaaaa
            time=>5000
          stop
  add=>[@][magix.data.save]
    values=>[@][code-2-node][node]
  magix.data.save
  add=>[@][magix.data.save]
    value=>[@][magix.data.save][value]
  set=>[@][magix.data.save][value]
  node-2-code
    node=>[@][magix.data.save]
    remove-root=>true
  using=>magix.forms
    set-value
      form-id=>database-manager-wrp
      id=>insert-txt
      value=>[@][..][node-2-code][code].value
  magix.database-manager.create-data-grid
  magix.viewport.show-message
    message=>object successfully inserted
    color=>#aaffaa
  magix.forms.effect
    form-id=>database-manager-wrp
    id=>pnl-insert-wrp
    type=>fade-out
    time=>250


// removes all records from database matching the current criteria
magix.database-manager.remove-all
  magix.viewstate.get=>current-view
    id=>magix.database-manager.current-view
  set=>[magix.viewstate.get=>current-view][value][start]
  set=>[magix.viewstate.get=>current-view][value][end]
  set=>[magix.viewstate.get=>current-view][value][sort]
  if=>equals
    lhs=>[magix.viewstate.get=>current-view][value].count
    rhs=>0
    or=>equals
      lhs=>[magix.viewstate.get=>current-view][value][prototype].count
      rhs=>0
    code
      magix.viewport.show-message
        message=>@"i am sorry, i cannot simply allow you to delete the entire database just like that.&nbsp;&nbsp;create a criteria 
to delete a sub-portion, or use the hyperlisp executor if you really know what you're doing"
        color=>#ffaaaa
        time=>7000
      stop
  magix.viewport.confirm
    message=>are you sure you wish to remove all records matching the current criteria?&nbsp;&nbsp;this action cannot be undone!
    code
      magix.viewstate.get=>current-view
        id=>magix.database-manager.current-view
      add=>[magix.data.remove]
        values=>[magix.viewstate.get=>current-view][value]
      set=>[magix.data.remove][start]
      set=>[magix.data.remove][end]
      set=>[magix.data.remove][sort]
      magix.data.remove
      magix.database-manager.create-data-grid


// exports the current view to a hyperlisp file
magix.database-manager.export
  magix.forms.effect
    form-id=>database-manager-wrp
    id=>pnl-export-wrp
    type=>fade-in
    time=>250
    chained
      e0
        form-id=>database-manager-wrp
        id=>export-type
        type=>focus-and-select


// esc key was pressed when pnl-export-wrp had focus
magix.database-manager.pnl-export-wrp_onesc
  magix.forms.effect
    form-id=>database-manager-wrp
    id=>pnl-export-wrp
    type=>fade-out
    time=>250


// do-export button was clicked
magix.database-manager.do-export_onclick
  magix.forms.get-value
    form-id=>database-manager-wrp
    id=>export-type
  set=>[_evt].name
    value=>magix.database-manager.do-export-{0}
      =>[magix.forms.get-value][value].value
  _evt
  magix.forms.effect
    form-id=>database-manager-wrp
    id=>pnl-export-wrp
    type=>fade-out
    time=>250


// exports items to hyperlisp file
magix.database-manager.do-export-hyperlisp
  magix.viewstate.get=>current-view
    id=>magix.database-manager.current-view
  set=>[magix.viewstate.get=>current-view][value][start]
  set=>[magix.viewstate.get=>current-view][value][end]
  add=>[magix.data.load]
    values=>[magix.viewstate.get=>current-view][value]
  magix.data.load
    meta-data=>false
  node-2-code
    node=>[magix.data.load][objects]
    remove-root=>true
  magix.date.now
  replace=>[magix.date.now][value].value
    what=>.
  replace=>[magix.date.now][value].value
    what=>:
  replace=>[magix.date.now][value].value
    what=>@" "
  using=>magix.file
    save
      file=>your-files/tmp/{0}-export-hyperlisp.hl
        =>[magix.date.now][value].value
      value=>[node-2-code][code].value
  set=>[magix.file.download][file].value
    value=>your-files/tmp/{0}-export-hyperlisp.hl
      =>[magix.date.now][value].value
  magix.file.download


// exports items to csv file
magix.database-manager.do-export-csv
  magix.viewstate.get=>current-view
    id=>magix.database-manager.current-view
  if=>not-exist
    lhs=>[magix.viewstate.get=>current-view][value][extract]
    code
      magix.viewport.show-message
        message=>you cannot export to csv unless you create an [extract] parameter
      stop
  set=>[magix.viewstate.get=>current-view][value][start]
  set=>[magix.viewstate.get=>current-view][value][end]
  add=>[magix.data.load]
    values=>[magix.viewstate.get=>current-view][value]
  magix.data.load
    meta-data=>false
  _content=>
  for-each=>[magix.viewstate.get=>current-view][value][extract]
    if=>equals
      lhs=>[/][_content].value
      rhs=>
      code
        set=>[/][_content].value
          value=>@"""{0}"""
            =>[.].name
    else
      set=>[/][_content].value
        value=>@"{0}, ""{1}"""
          =>[/][_content].value
          =>[.].name
  for-each=>[magix.data.load][objects]
    _obj=>
    for-each=>[.]
      replace=>[.].value
        what=>@""""
        with=>@""""""
      if=>equals
        lhs=>[@][..][_obj].value
        rhs=>
        code
          set=>[@][..][..][..][_obj].value
            value=>@"""{0}"""
              =>[.].value
      else
        set=>[@][..][..][_obj].value
          value=>@"{0},""{1}"""
            =>[@][..][..][_obj].value
            =>[.].value
    set=>[/][_content].value
      value=>@"{0}
{1}"
        =>[/][_content].value
        =>[@][_obj].value
  magix.date.now
  replace=>[magix.date.now][value].value
    what=>.
  replace=>[magix.date.now][value].value
    what=>:
  replace=>[magix.date.now][value].value
    what=>@" "
  using=>magix.file
    save
      file=>your-files/tmp/{0}-export-csv.csv
        =>[magix.date.now][value].value
      value=>[_content].value
  set=>[magix.file.download][file].value
    value=>your-files/tmp/{0}-export-csv.csv
      =>[magix.date.now][value].value
  magix.file.download


// exports items to zipped hyperlisp file
magix.database-manager.do-export-hyperlisp-zip
  magix.viewstate.get=>current-view
    id=>magix.database-manager.current-view
  set=>[magix.viewstate.get=>current-view][value][start]
  set=>[magix.viewstate.get=>current-view][value][end]
  add=>[magix.data.load]
    values=>[magix.viewstate.get=>current-view][value]
  magix.data.load
    meta-data=>false
  node-2-code
    node=>[magix.data.load][objects]
    remove-root=>true
  magix.date.now
  replace=>[magix.date.now][value].value
    what=>.
  replace=>[magix.date.now][value].value
    what=>:
  replace=>[magix.date.now][value].value
    what=>@" "
  using=>magix.file
    save
      file=>your-files/tmp/{0}-export-hyperlisp.hl
        =>[magix.date.now][value].value
      value=>[node-2-code][code].value
  add=>[magix.package.pack][files]
    value=>
      value=>your-files/tmp/{0}-export-hyperlisp.hl
        =>[magix.date.now][value].value
  set=>[magix.package.pack][zip].value
    value=>your-files/tmp/{0}-export-hyperlisp.zip
      =>[magix.date.now][value].value
  magix.package.pack
  set=>[magix.file.download][file].value
    value=>your-files/tmp/{0}-export-hyperlisp.zip
      =>[magix.date.now][value].value
  magix.file.download


// import button was clicked
magix.database-manager.import_onclick
  magix.viewport.show-message
    message=>@"drag and drop the file(s) you wish to import into the surface of this form, and 
they will be automatically imported.&nbsp;&nbsp;supported file formats are hyperlisp (.hl) and zipped 
hyperlisp (.zip)"
    time=>7000
  magix.forms.effect
    form-id=>database-manager-wrp
    id=>pnl-import-wrp
    type=>fade-in
    time=>250
    chained
      e0
        form-id=>database-manager-wrp
        id=>chk-drop-ids
        type=>focus-and-select


// esc key was pressed when pnl-import-wrp had focus
magix.database-manager.pnl-import-wrp_onesc
  magix.forms.effect
    form-id=>database-manager-wrp
    id=>pnl-import-wrp
    type=>fade-out
    time=>250


//  file was dragged and dropped into form for import, expects [file] as path to file to import
magix.database-manager.import-uploader_onuploaded
  set=>[magix.database-manager.import-file][file].value
    value=>[$][file].value
  magix.database-manager.import-file


// imports the given [file]
magix.database-manager.import-file
  split=>[$][file].value
    what=>.
  switch=>[split][result][>last].value
    case=>hl
      set=>[@][magix.database-manager.import-hyperlisp-file][file].value
        value=>[$][file].value
      magix.database-manager.import-hyperlisp-file
    case=>zip
      set=>[@][magix.database-manager.import-zip-file][file].value
        value=>[$][file].value
      magix.database-manager.import-zip-file
    default
      using=>magix.viewport
        show-message
          message=>i am sorry, i don't know how to import the given '{0}' file
            =>[$][file].value
          color=>#ffaaaa


// imports a hyperlisp file
magix.database-manager.import-hyperlisp-file
  using=>magix.file
    load
      file=>[$][file].value
  code-2-node
    code=>[using=>magix.file][load][value].value
  add=>[magix.data.save][objects]
    values=>[code-2-node][node]
  magix.forms.get-value
    form-id=>database-manager-wrp
    id=>chk-drop-ids
  if=>equals
    lhs=>[magix.forms.get-value][value].value
    rhs=>true
    code
      // dropping ids of objects
      for-each=>[magix.data.save][objects]
        set=>[.][id]
  magix.data.save
  magix.database-manager.create-data-grid


// imports a zip file
magix.database-manager.import-zip-file
  magix.date.now
  replace=>[magix.date.now][value].value
    what=>.
  replace=>[magix.date.now][value].value
    what=>:
  replace=>[magix.date.now][value].value
    what=>@" "
  using=>magix.file
    create-directory
      directory=>your-files/tmp/unzipped_{0}
        =>[magix.date.now][value].value
  using=>magix.package
    unpack
      zip=>[$][file].value
      directory=>your-files/tmp/unzipped_{0}
        =>[magix.date.now][value].value
  set=>[magix.database-manager.import-directory][directory].value
    value=>your-files/tmp/unzipped_{0}
      =>[magix.date.now][value].value
  magix.database-manager.import-directory


// imports all files in given [directory]
magix.database-manager.import-directory
  using=>magix.file
    list-files
      directory=>[$][directory].value
  for-each=>[using=>magix.file][list-files][files]
    set=>[@][magix.database-manager.import-file][file].value
      value=>[.].name
    magix.database-manager.import-file

