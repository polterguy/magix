

inspect=>@"creates the show emails web part

creates the show emails web part in content3"


_start=>0
_end=>25
_count=>25
_search=>

if=>exist
  lhs=>[$][start]
  code
    set=>[_start].value
      value=>[$][start].value
    set=>[_end].value
      value=>[$][end].value
    using=>magix.math
      subtract=>[_count]
        =>[_end].value
        =>[_start].value

if=>exist
  lhs=>[$][search]
  code
    set=>[_search].value
      value=>[$][search].value

iterate=>[magix.forms.create-web-part]
  if=>equals
    lhs=>[.].value
    rhs=>{emails-start}
    code
      set=>[.].value
        value=>[/][_start].value
  else-if=>equals
    lhs=>[.].value
    rhs=>{emails-end}
    code
      set=>[.].value
        value=>[/][_end].value
  else-if=>equals
    lhs=>[.].value
    rhs=>{emails-count}
    code
      set=>[.].value
        value=>[/][_count].value
  else-if=>equals
    lhs=>[.].value
    rhs=>{emails-search}
    code
      set=>[.].value
        value=>[/][_search].value

if=>equals
  lhs=>[_start].value
  rhs=>0
  code
    set=>[**button=>previous-emails][disabled].value
      value=>true

magix.forms.create-web-part
  container=>content3
  class=>span-18 last boxed air-padding bottom-5
  form-id=>inbox-list
  events-file=>system42/private/email/inbox/parts/show-emails/events.hl
  controls
    label=>inbox-header
      class=>span-9 bottom-1
      tag=>h3
      onfirstload
        magix.viewstate.get
          id=>magix.email.view-count
        using=>magix.forms
          set-value
            form-id=>inbox-list
            id=>inbox-header
            value=>inbox ({0})
              =>[magix.viewstate.get][value].value
    panel
      class=>span-9 last btn-group email-search-bar
      controls
        button=>previous-emails
          class=>span-2
          value=><
          onclick
            _start=>{emails-start}
            _end=>{emails-end}
            _count=>{emails-count}
            using=>magix.math
              subtract=>[_start]
                =>[_start].value
                =>[_count].value
              subtract=>[_end]
                =>[_end].value
                =>[_count].value
            set=>[execute-script][params][start].value
              value=>[_start].value
            set=>[execute-script][params][end].value
              value=>[_end].value
            execute-script
              file=>system42/private/email/inbox/parts/show-emails.hl
              params
                search=>{emails-search}
        text-box=>search-emails
          class=>span-5
          placeholder=>search ...
          value=>{emails-search}
          onenterpressed
            set=>[execute-script][params][search].value
              value=>[$][value].value
            execute-script
              file=>system42/private/email/inbox/parts/show-emails.hl
        button=>next-emails
          class=>span-2 last
          value=>>
          onclick
            _start=>{emails-start}
            _end=>{emails-end}
            _count=>{emails-count}
            using=>magix.math
              add=>[_start]
                =>[_start].value
                =>[_count].value
              add=>[_end]
                =>[_end].value
                =>[_count].value
            set=>[execute-script][params][start].value
              value=>[_start].value
            set=>[execute-script][params][end].value
              value=>[_end].value
            execute-script
              file=>system42/private/email/inbox/parts/show-emails.hl
              params
                search=>{emails-search}
    lambda=>list-of-emails
      oncreatecontrols
        magix.data.load=>load-emails
          prototype
            type=>magix.email.message
            deleted=>false

        // removing emails not meeting search criteria, if any criteria is given
        _search=>{emails-search}
        if=>not-equals
          lhs=>[@][_search].value
          rhs=>
          code
            for-each=>[magix.data.load=>load-emails][objects]
              if=>not-exist
                lhs=>[.][value][body]
                code
                  set=>[.][value][body].value
                    value=>
              if=>not-exist
                lhs=>[.][value][from][display-name]
                code
                  set=>[.][value][from][display-name].value
                    value=>
              index-of=>[.][value][subject].value
                what=>[/][_search].value
              if=>not-exist
                lhs=>[@][index-of][result]
                code
                  index-of=>[.][value][body].value
                    what=>[/][_search].value
                  if=>not-exist
                    lhs=>[@][index-of][result]
                    code
                      index-of=>[.][value][from].value
                        what=>[/][_search].value
                      if=>not-exist
                        lhs=>[@][index-of][result]
                        code
                          index-of=>[.][value][from][display-name].value
                            what=>[/][_search].value
                          if=>not-exist
                            lhs=>[@][index-of][result]
                            code
                              // removing item since it didn't match search criteria on any of the searchable fields
                              set=>[.]

        // sorting emails according to date
        sort=>[magix.data.load=>load-emails][objects]
          if=>less-than
            lhs=>[@][_first][0][value][date].value
            rhs=>[@][_second][0][value][date].value
            code
              add=>[@][..][..][_result]
                value=>[@][..][..][_second]
          if=>more-than
            lhs=>[@][_first][0][value][date].value
            rhs=>[@][_second][0][value][date].value
            code
              add=>[@][..][..][_result]
                value=>[@][..][..][_first]

        // settings view count for later references
        set=>[@][magix.viewstate.set][value].value
          value=>[magix.data.load=>load-emails][objects].count
        magix.viewstate.set
          id=>magix.email.view-count

        // removing all emails but the 25 emails we're supposed to see from the list of emails
        _objects
        while=>less-than
          lhs=>[_objects].count
          rhs=>{emails-count}
          and=>more-than
            lhs=>[magix.data.load=>load-emails][objects].count
            rhs=>{emails-start}
          code
            _start=>{emails-start}
            add=>[_objects]
              value=>[magix.data.load=>load-emails][objects][[@][_start].value]
            set=>[magix.data.load=>load-emails][objects][[@][_start].value]
        set=>[magix.data.load=>load-emails][objects]
        add=>[magix.data.load=>load-emails][objects]
          values=>[_objects]
        _pnl
          panel
            class=>span-18 last
            controls
        for-each=>[magix.data.load][objects]
          _pnl
            label=>{subject-}
              class=>span-9 email-item-subject
              value=>&nbsp;
              onclick
                split=>[$][id].value
                  what=>subject-
                index-of=>[$][info].value
                  what=>viewing-
                if=>equals
                  lhs=>[index-of][result][0].value
                  rhs=>0
                  code
                    // already viewing, clearing viewing viewport to "unview" email
                    replace=>[$][info].value
                      what=>viewing-
                    using=>magix.forms
                      set-info
                        form-id=>inbox-list
                        id=>[$][id].value
                        value=>[$][info].value
                    set=>[@][magix.viewport.clear-controls][container].value
                      value=>dynamic-{0}
                        =>[split][result][0].value
                    magix.viewport.clear-controls
                      reset-class
                        new-class=>emails-sleeping-dynamic
                    stop
                set=>[magix.emails.load-email][container].value
                  value=>dynamic-{0}
                    =>[split][result][0].value
                set=>[magix.emails.load-email][email-id].value
                  value=>[$][info].value
                magix.emails.load-email
                using=>magix.forms
                  set-style
                    form-id=>inbox-list
                    id=>[$][id].value
                    key=>font-weight
                  set-info
                    form-id=>inbox-list
                    id=>[$][id].value
                    value=>viewing-{0}
                      =>[$][info].value
            label=>{from-}
              class=>span-6 email-item-from
            label=>{date-}
              class=>span-3 last email-item-date
            dynamic=>{dynamic-}
              class=>emails-sleeping-dynamic
          set=>[@][_pnl][label=>{subject-}][info].value
            value=>[.][id].value
          if=>not-equals
            lhs=>[.][value][read].value
            rhs=>true
            code
              set=>[@][..][..][_pnl][label=>{subject-}][style].value
                value=>font-weight:bold;
          if=>not-equals
            lhs=>[.][value][subject].value
            rhs=>
            code
              split=>[.][value][subject].value
                where=>40
              set=>[@][..][..][_pnl][label=>{subject-}][value].value
                value=>[@][split][result][0].value
              if=>more-than
                lhs=>[@][split][result].count
                rhs=>1
                code
                  set=>[@][..][..][..][..][_pnl][label=>{subject-}][title].value
                    value=>[.][value][subject].value
                  set=>[@][..][..][..][..][_pnl][label=>{subject-}][value].value
                    value=>{0} ...
                      =>[@][..][..][..][..][_pnl][label=>{subject-}][value].value
          if=>equals
            lhs=>[.][value][from][display-name].value
            rhs=>
            code
              set=>[@][..][..][_pnl][label=>{from-}][value].value
                value=>[.][value][from].value
          else
            set=>[@][..][_pnl][label=>{from-}][value].value
              value=>[.][value][from][display-name].value
            set=>[@][..][_pnl][label=>{from-}][title].value
              value=>[.][value][from].value
          using=>magix.date
            format
              date=>[.][value][date].value
              format=>g
              current-culture=>true
          set=>[@][_pnl][**label=>{date-}][value].value
            value=>[@][using][format][value].value
          add=>[@][..][_pnl][panel][controls]
            values=>[@][_pnl]
        add=>[$]
          values=>[@][_pnl]





