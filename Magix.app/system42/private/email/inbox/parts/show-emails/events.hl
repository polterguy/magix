

inspect=>@"active events for show-emails.hl file

contains active events for the inbox list of emails web part"


magix.emails.load-email
  iterate=>[magix.execute]
    if=>equals
      lhs=>[.].value
      rhs=>{container}
      code
        set=>[.].value
          value=>[$][container].value
    else-if=>equals
      lhs=>[.].value
      rhs=>{reply-container}
      code
        set=>[.].value
          value=>reply-{0}
            id=>[$][container].value
  add=>[magix.execute]
    value=>[$]
  magix.execute
    using=>magix.data
      load
        id=>[$][email-id].value
    if=>not-exist
      lhs=>[using][load][value][body]
      code
        // downloading email, then recursively calling self, before stopping execution of event
        add=>[@][magix.emails.download-email]
          values=>[$]
        add=>[@][magix.emails.load-email]
          values=>[$]
        magix.emails.download-email
        magix.emails.load-email
        stop

    // saving updated version of email, including downloaded body
    add=>[using:1][save][value]
      values=>[using:0][load][value]
    set=>[using:1][save][value][read].value
      value=>true
    using=>magix.data
      save
        id=>[$][email-id].value

    // creating view email body web part
    set=>[_controls][**label=>body][value].value
      value=>[using][load][value][body].value
    set=>[_controls][**label=>email-header][value].value
      value=>[using][load][value][subject].value
    if=>equals
      lhs=>[using][load][value][is-html].value
      rhs=>false
      code
        // email is not html, making sure all carriage returns are replaced with <br/> tags
        replace=>[_controls][**label=>body][value].value
          what=>\n
          with=><br/>
        replace=>[_controls][**label=>body][value].value
          what=>\r
    if=>exist
      lhs=>[using][load][value][attachments]
      code
        for-each=>[using][load][value][attachments]
          _lb
            link-button
              class=>span-7 last
              onclick
                set=>[magix.file.download][file].value
                  value=>[$][info].value
                set=>[magix.file.download][as].value
                  value=>[$][value].value
                magix.file.download
          set=>[@][_lb][link-button][value].value
            value=>[.][original-name].value
          set=>[@][_lb][link-button][info].value
            value=>[.].value
          add=>[/][_controls][panel=>attachments][controls]
            values=>[@][_lb]
    else
      set=>[_controls][panel=>attachments]
    set=>[_controls][**button=>reply][info].value
      value=>[$][email-id].value
    _controls
      panel=>email-content
        class=>span-17 last email-content-body
        controls
          label=>email-header
            class=>span-17 last bottom-1
            tag=>h3
          label=>body
            class=>clear last email-content-body-body
            tag=>div
      panel=>attachments
        class=>span-7 clear email-attachments
        controls
          label
            class=>span-7 last
            tag=>label
            value=>attachments
      panel
        class=>btn-group span-8 last right clear
        controls
          button=>reply
            class=>span-2
            value=>reply
            onclick
              magix.email.current-reply-container
              if=>exist
                lhs=>[magix.email.current-reply-container][container]
                code
                  using=>magix.browser
                    scroll
                      id=>[magix.email.current-reply-container][container].value
                  magix.viewport.show-message
                    message=>you must first finish your existing reply
                    color=>#ffaaaa
                  stop
              set=>[execute-script][params][email-id].value
                value=>[$][info].value
              set=>[execute-script][params][reply-container].value
                value=>reply-{0}
                  =>[$][container].value
              execute-script
                file=>system42/private/email/inbox/parts/reply/reply-web-part.hl
          button
            class=>span-2
            value=>print
          button
            class=>span-2
            value=>delete
          button
            class=>span-2 last
            value=>close
            onclick
              _container=>{container}
              _label
              replace=>[_container].value
                what=>dynamic-
              set=>[_label].value
                value=>subject-{0}
                  =>[_container].value
              using=>magix.forms
                get-info
                  form-id=>inbox-list
                  id=>[_label].value
              replace=>[using][get-info][value].value
                what=>viewing-
              using=>magix.forms
                set-info
                  form-id=>inbox-list
                  id=>[_label].value
                  value=>[using:0][get-info][value].value
              magix.viewport.clear-controls
                container=>{container}
                reset-class
                  new-class=>emails-sleeping-dynamic
      dynamic=>{reply-container}
    using=>magix.forms
      create-web-part
        container=>{container}
        class=>span-15 last email-item-body bottom-3 top-1
        controls=>[_controls]

magix.emails.download-email
  // checking what type of account user has
  magix.data.load-username
    id=>magix.email.account-type
  switch=>[magix.data.load-username][value][account-type].value
    case=>pop3
      add=>[@][magix.emails.download-email-pop3]
        values=>[$]
      magix.emails.download-email-pop3
    case=>imap
      add=>[@][magix.emails.download-email-imap]
        values=>[$]
      magix.emails.download-email-imap

magix.emails.download-email-imap
  magix.viewport.show-message
    message=>implement this sucker ...

magix.emails.download-email-pop3
  // downloading body of email
  magix.data.load-username=>pop3-settings
    id=>magix.pop3.settings
  add=>[magix.pop3.get-message]
    values=>[magix.data.load-username=>pop3-settings][value]
  split=>[$][email-id].value
    what=>magix.email.pop3-message-
  set=>[magix.pop3.get-message][index].value
    value=>[split][result][0].value
  magix.pop3.get-message
    headers-only=>false
  using=>magix.data
    load
      id=>[$][email-id].value



