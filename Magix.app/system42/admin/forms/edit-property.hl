

inspect=>@"creates the edit-property web form, used
to edit a hyper lisp property in the edit-forms web form"

get-selected-widget

set=>[magix.forms.create-web-part][controls][text-area][ontextchanged][change-widget][dna].Value
  value=>[get-selected-widget][value][dna].Value

if=>more-than
  lhs=>[get-selected-widget][value][widget][properties][[$][property].Value].Count
  rhs=>0
  code
    for-each=>[get-selected-widget][value][widget][properties][[$][property].Value]
      add=>[@][..][node-2-code][node]
        value=>[.]
    node-2-code
    set=>[/][magix.forms.create-web-part][controls][text-area][value].Value
      value=>[@][node-2-code][code].Value

set=>[magix.forms.create-web-part][controls][text-area][ontextchanged][_prop].Value
  value=>[$][property].Value

set=>[magix.forms.create-web-part][controls][label][value].Value
  value=>@"edit property {0}"
    pr0=>[$][property].Value

magix.forms.create-web-part
  form-id=>edit-property
  container=>content5
  class=>span-20 air-padding boxed relative
  controls
    link-button=>close
      class=>absolute top right span-1
      value=>close
      onclick
        magix.viewport.clear-controls
          container=>content5
    label
      value=>edit property
      tag=>label
      class=>span-20 last
    text-area=>prop
      class=>span-20 min-height-13 last
      placeholder=>code ...
      ontextchanged
        magix.forms.get-value
          form-id=>edit-property
          id=>prop
        if=>exist
          lhs=>[magix.forms.get-value][value].Value
          code
            set=>[/][change-widget][change][[/][_prop].Value].Value
              value=>[/][magix.forms.get-value][value].Value
            set=>[@][magix.forms.set-class][id].Value
              value=>[/][_prop].Value
            magix.forms.set-class
              form-id=>edit-properties
              value=>span-4 top-1 bold
        else
          set=>[/][change-widget][remove][[/][_prop].Value].Value
            value=>
          set=>[@][magix.forms.set-class][id].Value
            value=>[/][_prop].Value
          magix.forms.set-class
            form-id=>edit-properties
            value=>span-4 top-1
        change-widget
          change

magix.forms.set-focus
  form-id=>edit-property
  id=>prop


