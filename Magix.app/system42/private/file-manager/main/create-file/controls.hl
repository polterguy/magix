

inspect=>@"contains controls for create file web part

contains the controls necessary to show the create file web part"


panel=>create-file-wrapper
  class=>span-6 last
  onesc
    magix.viewport.clear-controls
      container=>create-file-directory-container
      reset-class=>true
  default=>create-file-save
  controls
    text-box=>new-file-name
      class=>span-4
      placeholder=>file name ...
      onfirstload
        magix.forms.effect
          type=>focus-and-select
          form-id=>create-file-directory-container
          id=>new-file-name
    button=>create-file-save
      class=>span-2 last
      value=>create
      onclick
        magix.forms.get-value
          form-id=>create-file-directory-container
          id=>new-file-name
        if=>equals
          lhs=>magix.forms.get-value
          rhs=>
          code
            magix.viewport.show-message
              message=>you must give your new file a name
              color=>#ffbbbb
        else
          magix.viewstate.get
            id=>magix.file-manager.current-folder
          if=>not-equals
            lhs=>[@][magix.viewstate.get][value].value
            rhs=>
            code
              set=>[@][..][..][magix.file.save][file].value
                value=>{0}/{1}
                  =>[@][..][..][magix.viewstate.get][value].value
                  =>[magix.forms.get-value][value].value
          else
            set=>[@][..][magix.file.save][file].value
              value=>[magix.forms.get-value][value].value
          magix.file.save
            value=>
          magix.viewport.clear-controls
            container=>create-file-directory-container
            reset-class=>true
          set=>[@][magix.file-manager.select-file][file].value
            value=>[@][magix.file.save][file].value
          magix.file-manager.select-file
            id=>magix.file-manager.selected-file
          magix.file-manager.open-directory
          magix.file-manager.directory-changed

