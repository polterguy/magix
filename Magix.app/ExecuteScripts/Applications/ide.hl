
event:magix.execute

inspect=>@"loads up the toolbox for 
the ide"

_data
  x=>y

magix.viewport.load-module
  container=>content
  css=>span12
  name=>Magix.ide.Builder

list-forms

for-each=>[list-forms][forms]
  set=>[/][_data][0].Name
    value=>[.][name].Value
  set=>[/][_data][0].Value
    value=>[.][name].Value
  if=>[.][name].Value!=_default
    add=>[/][magix.forms.create-form][controls][panel][controls][select][items]
      value=>[/][_data][0]

load-form
  name=>_default

list-widgets

_data2
  x=>y

for-each=>[/][list-widgets][widgets]
  set=>[/][_data2][0].Name
    value=>[.][dna].Value
  set=>[/][_data2][0].Value
    value=>[.][type].Value
  add=>[/][magix.forms.create-form][controls][panel][controls][select:1][items]
    value=>[/][_data2][0]

magix.forms.create-form
  form-id=>toolbox-load-file
  container=>header
  css=>span12
  events
    magix.forms.widget-selected
      set=>[/][magix.forms.set-value][value].Value
        value=>[/][_p][dna].Value
      magix.forms.set-value
        form-id=>toolbox-load-file
        id=>widgets
  controls
    label=>header
      tag=>h1
      text=>ide
      css=>span12
    panel=>wrp
      css=>input-append
      controls
        select=>form
          css=>span3
          onselectedindexchanged
            select-widget
            magix.forms.get-value
              form-id=>toolbox-load-file
              id=>form
            set=>[/][magix.forms.set-value][value].Value
              value=>[/][magix.forms.get-value][value].Value
            magix.forms.set-value
              form-id=>toolbox-load-file
              id=>name
            set=>[/][if][load-form][name].Value
              value=>[/][magix.forms.get-value][value].Value
            if=>[/][if][load-form][name].Value!=_default
              load-form
            _items
            _buffer
              x=>y
            list-widgets
            while=>[/][_items].Count>0
              set=>[/][_items][0]
            set=>[/][_items][_default].Value
              value=>widgets in form ...
            for-each=>[/][list-widgets][widgets]
              set=>[/][_buffer][0].Name
                value=>[.][dna].Value
              set=>[/][_buffer][0].Value
                value=>[.][type].Value
              add=>[/][_items]
                value=>[/][_buffer][0]
            set=>[/][magix.forms.set-values][items]
              value=>[/][_items]
            set=>[/][magix.forms.set-values][_items].Name
              value=>items
            magix.forms.set-values
              form-id=>toolbox-load-file
              id=>widgets
              items
            magix.forms.set-value
              form-id=>toolbox-load-file
              id=>widgets
              value=>_default
          items
            _default=>select form ...
        text-box=>name
          css=>span3
          place-holder=>name ...
          text=>_default
        button=>save
          text=>save
          css=>span2 btn btn-primary
          onclick
            magix.forms.get-value
              form-id=>toolbox-load-file
              id=>name
            set=>[/][save-form][name].Value
              value=>[/][magix.forms.get-value][value].Value
            save-form
            _data
              x=>y
            list-forms
            for-each=>[/][list-forms][forms]
              if=>[.][name].Value!=_default
                set=>[/][_data][0].Name
                  value=>[.][name].Value
                set=>[/][_data][0].Value
                  value=>[.][name].Value
                add=>[/][magix.forms.set-values][items]
                  value=>[/][_data][0]
            magix.forms.set-values
              form-id=>toolbox-load-file
              id=>form
              items
                _default=>select form ...
            set=>[/][magix.forms.set-value][value].Value
              value=>[/][magix.forms.get-value][value].Value
            magix.forms.set-value
              form-id=>toolbox-load-file
              id=>form
              value
        button=>delete
          text=>delete
          css=>span2 btn btn-primary
          onclick
            magix.viewport.verify
              header=>verify deletion of form
              button-text=>delete
              message=>@"please verify that you wish to 
delete the form.&nbsp;&nbsp;this action is not possible to undo"
              code
                magix.forms.get-value
                  form-id=>toolbox-load-file
                  id=>name
                set=>[/][delete-form][name].Value
                  value=>[/][magix.forms.get-value][value].Value
                delete-form
                _data
                  x=>y
                list-forms
                for-each=>[/][list-forms][forms]
                  if=>[.][name].Value!=_default
                    set=>[/][_data][0].Name
                      value=>[.][name].Value
                    set=>[/][_data][0].Value
                      value=>[.][name].Value
                    add=>[/][magix.forms.set-values][items]
                      value=>[/][_data][0]
                magix.forms.set-values
                  form-id=>toolbox-load-file
                  id=>form
                  items
                    _default=>select form ...
                set=>[/][magix.forms.set-value][value].Value
                  value=>_default
                magix.forms.set-value
                  form-id=>toolbox-load-file
                  id=>form
                  value
                select-widget
                magix.forms.set-value
                  form-id=>toolbox-load-file
                  id=>widgets
                  value=>_default
                load-form
                  name=>_default
                magix.forms.set-value
                  form-id=>toolbox-load-file
                  id=>name
                  value=>_default
                magix.viewport.clear-controls
                  container=>modal
        select=>widgets
          onselectedindexchanged
            magix.forms.get-value
              form-id=>toolbox-load-file
              id=>widgets
            set=>[/][select-widget][dna].Value
              value=>[/][magix.forms.get-value][value].Value
            select-widget
          items
            _default=>widgets in form ...
    label=>ruler
      tag=>hr
      css=>span11

magix.admin.run-file
  file=>ExecuteScripts/Applications/ide-properties.hl