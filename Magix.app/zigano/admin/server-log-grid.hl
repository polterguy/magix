

if=>[$][start]
  set=>[magix.data.load][start].Value
    value=>[$][start].Value
  set=>[magix.data.load][end].Value
    value=>[$][end].Value

if=>[$][filter].Value
  set=>[magix.data.load][prototype][body].Value
    value=>@"%{0}%"
      pr0=>[$][filter].Value
  set=>[magix.data.count][prototype][body].Value
    value=>@"%{0}%"
      pr0=>[$][filter].Value
  set=>[magix.forms.create-form][controls][panel:1][controls][panel][controls][text-box][text].Value
    value=>[$][filter].Value
  set=>[magix.forms.create-form][controls][panel:1][controls][link-button:0][onclick][magix.admin.run-file][filter].Value
    value=>[$][filter].Value
  set=>[magix.forms.create-form][controls][panel:1][controls][link-button:1][onclick][magix.admin.run-file][filter].Value
    value=>[$][filter].Value

magix.data.load
  start=(int)>0
  end=(int)>10
  prototype
    type=>magix.log.item

_dt
  panel
    css=>span-17 boxed air-padding bottom-1 relative
    controls
      label
        css=>span-13
        tag=>label
        text
      label
        css=>right
      label
        css=>span-17 last
        text

for-each=>[magix.data.load][objects]
  set=>[/][_dt][panel][controls][label:0][text].Value
    value=>[.][header].Value
  set=>[/][_dt][panel][controls][label:1][text].Value
    value=>[.][date].Value
  set=>[/][_dt][panel][controls][label:2][text].Value
    value=>[.][body].Value
  add=>[/][magix.forms.create-form][controls][panel][controls]
    value=>[/][_dt][panel]

magix.data.count
  prototype
    type=>magix.log.item

if=>[magix.data.count][count].Value>[magix.data.load][end].Value
  set=>[magix.forms.create-form][controls][panel:1][controls][label][text].Value
    value=>@"{0}/{1} of {2}"
      pr0=>[magix.data.load][start].Value+1
      pr1=>[magix.data.load][end].Value
      pr2=>[magix.data.count][count].Value
else
  set=>[magix.forms.create-form][controls][panel:1][controls][label][text].Value
    value=>@"{0}/{1} of {2}"
      pr0=>[magix.data.load][start].Value+1
      pr1=>[magix.data.count][count].Value
      pr2=>[magix.data.count][count].Value

if=>[magix.data.load][start].Value==0
  set=>[magix.forms.create-form][controls][panel:1][controls][link-button:0][enabled].Value
    value=>false
else
  set=>[magix.forms.create-form][controls][panel:1][controls][link-button:0][enabled].Value
    value=>true
  set=>[magix.forms.create-form][controls][panel:1][controls][link-button:0][onclick][magix.admin.run-file][start].Value
    value=>[magix.data.load][start].Value-10
  set=>[magix.forms.create-form][controls][panel:1][controls][link-button:0][onclick][magix.admin.run-file][end].Value
    value=>[magix.data.load][end].Value-10
if=>[magix.data.load][end].Value>=[magix.data.count][count].Value
  set=>[magix.forms.create-form][controls][panel:1][controls][link-button:1][enabled].Value
    value=>false
else
  set=>[magix.forms.create-form][controls][panel:1][controls][link-button:1][enabled].Value
    value=>true
  set=>[magix.forms.create-form][controls][panel:1][controls][link-button:1][onclick][magix.admin.run-file][start].Value
    value=>[magix.data.load][start].Value+10
  set=>[magix.forms.create-form][controls][panel:1][controls][link-button:1][onclick][magix.admin.run-file][end].Value
    value=>[magix.data.load][end].Value+10

magix.forms.create-form
  form-id=>server-log
  container=>content2
  css=>span-19 last
  controls
    panel=>items
      css=>span-19 last
      controls
    panel=>pagerWrp
      css=>span-19
      controls
        link-button=>previous
          text=>&lt;&lt;
          css=>span-1 left-7
          onclick
            magix.admin.run-file
              file=>zigano/admin/server-log-grid.hl
        label=>count
          css=>span-2
          text=>54
        link-button=>next
          text=>&gt;&gt;
          css=>span-1
          onclick
            magix.admin.run-file
              file=>zigano/admin/server-log-grid.hl
        panel
          css=>span-2 right
          default=>btnSearch
          controls
            text-box=>search
              place-holder=>filter ...
              css=>span-2
            button=>btnSearch
              css=>hide
              onclick
                magix.forms.get-value
                  form-id=>server-log
                  id=>search
                set=>[magix.admin.run-file][filter].Value
                  value=>[magix.forms.get-value][value].Value
                magix.admin.run-file
                  file=>zigano/admin/server-log-grid.hl

if=>[$][filter].Value
  magix.forms.select-all
    form-id=>server-log
    id=>search
  magix.forms.set-focus
    form-id=>server-log
    id=>search










