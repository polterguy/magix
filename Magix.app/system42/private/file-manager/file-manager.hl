
inspect=>@"creates the file manager

the file manager allows you to manage the files in your magix installation, 
such as moving them, renaming then, editing them to some extend, and so on.  
in addition, it also features a user interface for allowing the user to 
select files, for cases where you need a gui for allowing the user to browse 
and select a specific file

parameters are [container] for choosing the viewport container to put it in.  
[class] for changing its default css class.  [directory] for setting its 
initial directory.  [root] for setting the root directory of where it is 
allowed to browse.  [onselect] is hyperlisp code supposed to execute when 
user has selected a file.  if [onselect] is defined, then two additional 
buttons will appear in the file manager, 'select' and 'cancel'.  'select' 
will run the hyperlisp code in [onselect], while [cancel] will execute the 
[oncancel] hyperlisp code.  if you supply [onselect] you must also supply 
[oncancel].  if you supply [onselect], then your onselect hyperlisp code 
will be executed with a [$] parameter being [file], which was the filename 
of the file the user selected"


// resetting selected file from previous calls
magix.viewport.set-viewstate
  id=>magix.file-manager.selected-file

// changing css class and container, if we're supposed to
if=>exist
  lhs=>[$][container]
  code
    set=>[magix.forms.create-web-part][container].Value
      value=>[$][container].Value
if=>exist
  lhs=>[$][class]
  code
    set=>[magix.forms.create-web-part][class].Value
      value=>[$][class].Value

// changing its directory and root directory
if=>exist
  lhs=>[$][directory]
  code
    set=>[magix.forms.create-web-part][controls][dynamic][onfirstload][magix.file-manager.create-file-controls][directory].Value
      value=>[$][directory].Value
if=>exist
  lhs=>[$][root]
  code
    set=>[magix.forms.create-web-part][controls][dynamic][onfirstload][magix.file-manager.create-file-controls][root].Value
      value=>[$][root].Value

if=>exist
  lhs=>[$][onselect]
  code

    // if user supplies [onselect], then he must supply also [oncancel]
    if=>not-exist
      lhs=>[$][oncancel]
      code
        magix.viewport.show-message
          message=>you must supply [oncancel] if you supply [onselect]
          color=>#ffbbbb
          time=>5000
        stop

    // contains actual controls for the 'select' and 'cancel' buttons
    _select
      panel=>ok-cancel-wrapper
        class=>span-17 last
        controls
          button=>cancel
            class=>span-2 right last
            value=>cancel
          button=>select
            class=>span-2 right
            disabled=>true
            value=>select
            onclick
              _lambda
              magix.viewport.get-viewstate
                id=>magix.file-manager.selected-file
              set=>[lambda][file].Value
                value=>[magix.viewport.get-viewstate][value].Value
              lambda=>[_lambda]

    // adding [onselect] and [oncancel] hyperlisp code
    add=>[@][**button=>select][onclick][_lambda]
      value=>[$][onselect]
      children-only=>true
    add=>[@][**button=>cancel][onclick]
      value=>[$][oncancel]
      children-only=>true
    add=>[magix.forms.create-web-part][controls]
      value=>[@][_select][panel]
    magix.viewport.set-viewstate
      id=>magix.file-manager.has-select-button
      value=>true

// creating the main web part, wrapping everything else
magix.forms.create-web-part
  form-id=>file-manager
  class=>span-15 last boxed air-padding top-1
  events-file=>system42/private/file-manager/main/events.hl
  controls
    button=>go-up
      class=>span-2
      value=>&lt;&lt;
      onclick
        magix.viewport.get-viewstate
          id=>magix.file-manager.current-folder
        split=>[magix.viewport.get-viewstate][value].Value
          what=>/
        using=>magix.math
          subtract
            =>[split][result].Count
            =>1
        set=>[split][result][[using][subtract].Value]
        _folder=>
        for-each=>[split][result]
          if=>not-equals
            lhs=>[/][_folder].Value
            rhs=>
            code
              set=>[/][_folder].Value
                value=>{0}/
                  =>[/][_folder].Value
          set=>[/][_folder].Value
            value=>{0}{1}
              =>[/][_folder].Value
              =>[.].Value
        set=>[@][magix.file-manager.create-file-controls][directory].Value
          value=>[_folder].Value
        using=>magix.viewport
          get-viewstate
            id=>magix.file-manager.filter
          get-viewstate
            id=>magix.file-manager.root
        set=>[@][magix.file-manager.create-file-controls][filter].Value
          value=>[using:1][get-viewstate:0][value].Value
        set=>[@][magix.file-manager.create-file-controls][root].Value
          value=>[using:1][get-viewstate:1][value].Value
        magix.file-manager.create-file-controls
    label=>file-header
      class=>span-13 bottom-1 last
      tag=>label
    dynamic=>files
      class=>span-9
      onfirstload
        magix.file-manager.create-file-controls
          directory=>
          filter=>
          root=>
    dynamic=>preview
      class=>span-6 last

