
inspect=>@"creates the file manager

the file manager allows you to manage the files in your magix installation, 
such as moving them, renaming then, editing them to some extend, and so on.  
in addition, it also features a user interface for allowing the user to 
select files, for cases where you need a gui for allowing the user to browse 
and select a specific file

parameters are [container] for choosing the viewport container to put it in.  
[class] for changing its default css class.  [directory] for setting its 
initial directory.  [root] for setting the root directory of where it is 
allowed to browse.  [onselect] is hyperlisp code supposed to execute when 
user has selected a file.  if [onselect] is defined, then two additional 
buttons will appear in the file manager, 'select' and 'cancel'.  'select' 
will run the hyperlisp code in [onselect], while [cancel] will execute the 
[oncancel] hyperlisp code.  if you supply [onselect] you must also supply 
[oncancel].  if you supply [onselect], then your onselect hyperlisp code 
will be executed with a [$] parameter being [file], which was the filename 
of the file the user selected"


// resetting selected file, folder and filter from previous calls
magix.viewport.set-viewstate
  id=>magix.file-manager.selected-file
magix.viewport.set-viewstate
  id=>magix.file-manager.filter
magix.viewport.set-viewstate
  id=>magix.file-manager.current-folder
  value=>

// changing css class, container and filter, if we're supposed to
if=>exist
  lhs=>[$][container]
  code
    set=>[magix.forms.create-web-part][container].Value
      value=>[$][container].Value
if=>exist
  lhs=>[$][class]
  code
    set=>[magix.forms.create-web-part][class].Value
      value=>[$][class].Value
if=>exist
  lhs=>[$][filter]
  code
    set=>[@][magix.viewport.set-viewstate][value].Value
      value=>[$][filter].Value
    magix.viewport.set-viewstate
      id=>magix.file-manager.filter

// changing its directory and root directory
if=>exist
  lhs=>[$][directory]
  code
    set=>[@][magix.viewport.set-viewstate][value].Value
      value=>[$][directory].Value
    magix.viewport.set-viewstate
      id=>magix.file-manager.current-folder
    if=>not-equals
      lhs=>[$][directory].Value
      rhs=>
      and=>not-equals
        lhs=>[$][directory].Value
        rhs=>/
      code
        set=>[**button=>delete-directory][disabled].Value
          value=>false
if=>exist
  lhs=>[$][root]
  code
    set=>[@][magix.viewport.set-viewstate][value].Value
      value=>[$][root].Value
    magix.viewport.set-viewstate
      id=>magix.file-manager.root

// checking to see if caller has denied creation of directories
if=>equals
  lhs=>[$][deny-create-directory].Value
  rhs=>true
  code
    set=>[**panel=>directory-wrapper][visible].Value
      value=>false

// if user provides [onselect] we allow selection of files
if=>exist
  lhs=>[$][onselect]
  code

    // if user supplies [onselect], then he must supply also [oncancel]
    if=>not-exist
      lhs=>[$][oncancel]
      code
        magix.viewport.show-message
          message=>you must supply [oncancel] if you supply [onselect]
          color=>#ffbbbb
          time=>5000
        stop

    // contains actual controls for the 'select' and 'cancel' buttons
    _select
      panel=>ok-cancel-wrapper
        class=>span-15 last
        controls
          button=>cancel
            class=>span-2 right last
            value=>cancel
          button=>select
            class=>span-2 right
            disabled=>true
            value=>select
            onclick
              _lambda
              magix.viewport.get-viewstate
                id=>magix.file-manager.selected-file
              set=>[lambda][file].Value
                value=>[magix.viewport.get-viewstate][value].Value
              lambda=>[_lambda]

    // adding [onselect] and [oncancel] hyperlisp code
    add=>[@][**button=>select][onclick][_lambda]
      value=>[$][onselect]
      children-only=>true
    add=>[@][**button=>cancel][onclick]
      value=>[$][oncancel]
      children-only=>true
    add=>[magix.forms.create-web-part][controls]
      value=>[@][_select][panel]
    magix.viewport.set-viewstate
      id=>magix.file-manager.has-select-button
      value=>true
else
  magix.viewport.set-viewstate
    id=>magix.file-manager.has-select-button

// creating the main web part, wrapping everything else
magix.forms.create-web-part
  form-id=>file-manager
  class=>span-15 last boxed air-padding top-1
  events-file=>system42/private/file-manager/main/events.hl
  controls
    button=>go-up
      class=>span-2
      value=>&lt;&lt;
      onclick
        magix.file-manager.go-up_onclick
    label=>file-header
      class=>span-13 bottom-1 last
      tag=>label
    dynamic=>files
      class=>span-9
      onfirstload
        magix.file-manager.open-directory
    dynamic=>preview
      class=>span-6 last
    panel=>directory-wrapper
      class=>span-15 last top-1
      controls
        button=>create-directory
          class=>span-3 left-9
          value=>create directory
          onclick
            magix.file-manager.create-directory_onclick
        button=>delete-directory
          class=>span-3 last
          disabled=>true
          value=>delete directory
          onclick
            magix.file-manager.delete-directory_onclick
        dynamic=>create-directory-container
          class=>span-6 left-9 last top-1

