

inspect=>@"contains the controls for the execute hyperlisp event wizard

contains the controls for the web part that allows the user to create an active event 
that executes a hyperlisp file"


panel
  class=>span-15 last air-padding boxed top-1 wizard-content desktop-widget
  controls
    label
      tag=>h2
      class=>span-13
      value=>create execute hyperlisp file event wizard
    link-button
      class=>span-2 last text-right
      value=>back
      onclick
        execute-script
          file=>system42/private/app-wizard/app-wizard.hl
    label
      class=>span-15 last bottom-1
      value=>@"from here you can create an active event that will execute a hyperlisp file when raised"
    label
      class=>clear span-3
      tag=>label
      value=>active event name
    text-box=>active-event-name
      class=>span-12 last
      placeholder=>active event name ...
      onfirstload
        magix.forms.effect
          type=>focus-and-select
          form-id=>wizard
          id=>active-event-name
    label
      class=>clear span-3 top-1
      tag=>label
      value=>hyperlisp file
    panel
      class=>btn-group span-12 last top-1
      controls
        lambda=>lambda-active-event
          oncreatecontrols

            // includes necessary javascript files for our autocompleter
            magix.viewport.include-client-file
              type=>javascript
              file=>media/bootstrap/js/jQuery.js
            magix.viewport.include-client-file
              type=>javascript
              file=>media/bootstrap/js/bootstrap.min.js
            magix.viewport.include-client-file
              type=>css
              file=>media/back-end/typeahead.css
            magix.wizard.retrieve-hyperlisp-files
            _json=>
            for-each=>[magix.wizard.retrieve-hyperlisp-files][files]
              if=>not-equals
                lhs=>[/][_json].value
                rhs=>
                code
                  set=>[/][_json].value
                    value=>{0},
                      =>[/][_json].value
              set=>[/][_json].value
                value=>@"{0}""{1}"""
                  =>[/][_json].value
                  =>[.].value
            _text
              text-box=>filename
                placeholder=>hyperlisp file ...
                title=>this is the active event you wish to associate with the value of the get parameter
                class=>span-10 last bottom-1
                autocomplete=>false
                @data-provide=>typeahead
                @data-items=>5
                @data-source=>{data-source}
            set=>[**@data-source=>{data-source}].value
              value=>\[{0}]
                =>[_json].value
            add=>[$]
              value=>[_text][text-box]
        button=>browse
          class=>span-2 last
          value=>browse
          onclick
            execute-script
              file=>system42/private/file-manager/file-manager.hl
              params
                filter=>*.hl;
                container=>content2
                onselect
                  magix.viewport.clear-controls
                    container=>content2
                    reset-class=>true
                  using=>magix.forms
                    set-value
                      id=>filename
                      value=>[$][file].value
                oncancel
                  magix.viewport.clear-controls
                    container=>content2
                    reset-class=>true
    link-button=>show-advanced
      class=>span-2 top-1
      value=>advanced
      onclick
        magix.wizard.show-advanced-settings
    panel=>advanced
      class=>span-15 last top-1
      style=>display:none;
      controls
        label
          class=>span-3
          tag=>label
          value=>parameters
        text-area=>parameters
          class=>span-12 last bottom-1
          rows=>8
          placeholder=>parameters ...
        dynamic=>css-files
          class=>span-15 last last
          onfirstload
            magix.wizard.create-css-files-container
    button=>create
      class=>span-2 last top-1 right
      value=>create
      onclick
        magix.wizard.create-active-event

    link-button=>edit-event
      class=>span-3 right clear text-right last
      value=>edit active event
      visible=>false
      onclick
        set=>[magix.event.edit-event][event].value
          value=>[$][info].value
        magix.event.edit-event

