

inspect=>@"opens up the wizard web part

opens up the wizard web part, such that user can follow a wizard to 
create stuff following wizards, instead of having to code directly 
himself

to create a wizard, create a hyperlisp file within the 'wizards' directory, 
beneath the directory where this file is, and make sure it has an [inspect] 
node, which becomes its description, a [_name] node, which is the name of the 
wizard.  then make sure it allows the user to follow a wizard interface to 
create whatever functionality your wizard allows the user to create"


// verifies user is in admin role
magix.core.verify-role
  role=>admin

magix.file.list-files
  directory=>system42/private/app-wizard/wizards
  filter=>*.hl

_dt
  panel
    class=>boxed span-5 min-height-7 air-padding bottom-1 desktop-widget
    style=>cursor:pointer;
    controls
      label
        tag=>label
        value=>name
      label
        tag=>p
        value=>description

for-each=>[magix.file.list-files][files]
  set=>[@][code-2-node][file].value
    value=>[.].name
  code-2-node
  set=>[/][_dt][panel][controls][label:0][value].value
    value=>[@][code-2-node][node][_name].value
  set=>[/][_dt][panel][controls][label:1][value].value
    value=>[@][code-2-node][node][inspect].value
  set=>[/][_dt][panel][onclick][execute-script][file].value
    value=>[.].name
  add=>[/][magix.forms.create-web-part][controls]
    value=>[/][_dt][panel]

magix.forms.create-web-part
  form-id=>app-wiz
  class=>span-17 last top-1

