

inspect=>@"creates verify role active event

creates an active event that will verify the currently logged in user belongs 
to a specific role, and if not, throws an exception.  expects [role] as input 
parameter, being the role to check the user belongs to.  also expects that a 
user is logged in"


event
  name=>magix.core.verify-role
  persist=>false
  inspect=>@"verifies user belongs to given [role]

will throw an exception if currently logged in user does not belong
the to given [role]"
  code
    magix.session.get
      id=>magix.core.user
    if=>not-exist
      lhs=>[magix.session.get][value]
      code
        throw=>no logged in user
    _found=>false
    magix.data.load
      prototype
        type=>magix.core.role
    _user-roles
    iterate=>[magix.data.load][objects]
      if=>equals
        lhs=>[.].name
        rhs=>name
        and=>exist
          lhs=>[/][magix.session.get][value][roles][[.].value]
        code
          add=>[/][_user-roles]
            value=>[.][..]
    iterate=>[_user-roles]
      if=>equals
        lhs=>[.].name
        rhs=>name
        and=>equals
          lhs=>[.].value
          rhs=>[$][role].value
        code
          set=>[/][_found].value
            value=>true
          stop
    if=>equals
      lhs=>[_found].value
      rhs=>false
      code
        throw=>user not in role


event
  name=>magix.core.check-role
  persist=>false
  inspect=>@"checks if user belongs to given [role]

will return false as [result] unless currently logged in user 
belongs to [role]"
  code
    magix.session.get
      id=>magix.core.user
    if=>not-exist
      lhs=>[magix.session.get][value]
      code
        set=>[$][result].value
          value=>false
        stop
    _found=>false
    magix.data.load
      prototype
        type=>magix.core.role
    _user-roles
    iterate=>[magix.data.load][objects]
      if=>equals
        lhs=>[.].name
        rhs=>name
        and=>exist
          lhs=>[/][magix.session.get][value][roles][[.].value]
        code
          add=>[/][_user-roles]
            value=>[.][..]
    iterate=>[_user-roles]
      if=>equals
        lhs=>[.].name
        rhs=>name
        and=>equals
          lhs=>[.].value
          rhs=>[$][role].value
        code
          set=>[/][_found].value
            value=>true
          stop
    if=>equals
      lhs=>[_found].value
      rhs=>false
      code
        set=>[$][result].value
          value=>false
    else
      set=>[$][result].value
        value=>true

