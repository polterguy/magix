

inspect=>@"allows you to create an application where 
you can collect data in lists using a datagrid and crud
graphical interface"

_name=>crud app

set=>[magix.forms.create-web-part][controls][label][text].Value
  value=>@"{0}{1}"
    v0=>[magix.forms.create-web-part][controls][label][text].Value
    v1=>[$][name].Value

set=>[magix.forms.create-web-part][events][magix.crud-app.rebind-data-grid][magix.data.load][id].Value
  value=>@"{0}{1}"
    v0=>[magix.forms.create-web-part][events][magix.crud-app.rebind-data-grid][magix.data.load][id].Value
    v1=>[$][name].Value

set=>[magix.forms.create-web-part][events][magix.crud-app.delete-field][magix.data.load][id].Value
  value=>@"{0}{1}"
    v0=>[magix.forms.create-web-part][events][magix.crud-app.delete-field][magix.data.load][id].Value
    v1=>[$][name].Value

set=>[magix.forms.create-web-part][controls][panel][controls][button][onclick][magix.data.load][id].Value
  value=>@"{0}{1}"
    v0=>[magix.forms.create-web-part][controls][panel][controls][button][onclick][magix.data.load][id].Value
    v1=>[$][name].Value

set=>[magix.forms.create-web-part][controls][panel][controls][button][onclick][magix.data.save][id].Value
  value=>@"{0}{1}"
    v0=>[magix.forms.create-web-part][controls][panel][controls][button][onclick][magix.data.save][id].Value
    v1=>[$][name].Value

set=>[magix.forms.create-web-part][controls][panel][controls][button][onclick][if][code][magix.data.load][id].Value
  value=>@"{0}{1}"
    v0=>[magix.forms.create-web-part][controls][panel][controls][button][onclick][if][code][magix.data.load][id].Value
    v1=>[$][name].Value

set=>[magix.forms.create-web-part][controls][panel][controls][button][onclick][if][code][magix.data.save][id].Value
  value=>@"{0}{1}"
    v0=>[magix.forms.create-web-part][controls][panel][controls][button][onclick][if][code][magix.data.save][id].Value
    v1=>[$][name].Value

set=>[magix.forms.create-web-part][controls][hidden][value].Value
  value=>[$][name].Value

set=>[magix.forms.create-web-part:1][controls][button:1][onclick][magix.viewport.confirm][code][magix.data.remove][id].Value
  value=>@"magix.crud-app.template.{0}"
    v0=>[$][name].Value

set=>[magix.forms.create-web-part][events][magix.crud-app.generate-application][execute-file][name].Value
  value=>[$][name].Value

set=>[magix.forms.create-web-part][events][magix.crud-app.generate-application][magix.data.load][id].Value
  value=>@"magix.crud-app.template.{0}"
    v0=>[$][name].Value

set=>[magix.forms.create-web-part][events][magix.crud-app.generate-application][set][value][v0].Value
  value=>[$][name].Value

set=>[magix.forms.create-web-part][events][magix.crud-app.preview-application][set][value][v0].Value
  value=>[$][name].Value

set=>[magix.forms.create-web-part][events][magix.crud-app.preview-application][magix.forms.create-web-part][controls][label:1][text].Value
  value=>@"inspect=>@""to open application, use code below""
execute-file=>system42\admin\apps\{0}.hl
  css=>span-22
  container=>content3
  start=>0
  end=>10"
    v0=>[$][name].Value

magix.forms.create-web-part
  container=>content2
  form-id=>init-app
  css=>span-17 last

  events

    magix.crud-app.generate-application
      magix.data.load
      set=>[execute-file][datasource].Value
        value=>magix.generic-apps.datasource.crud-app.{0}
      add=>[execute-file]
        value=>[magix.data.load][objects][0][fields]
      for-each=>[$]
        add=>[/][execute-file][_after-code]
          value=>[.]
      execute-file=>system42/admin/app-wizard/wizards/crud-app/crud-app-generator.hl

    magix.crud-app.preview-application
      set=>[execute-file].Value
        value=>system42\admin\apps\{0}.hl
      execute-file
        container=>content5
        css=>span-22 last top-3
      magix.forms.create-web-part
        container=>content6
        form-id=>close-preview
        css=>span-22 last top-3
        controls
          label=>spacer
            tag=>hr
          label=>runDescription
            css=>span-10
            tag=>pre
          button=>btnClosePreview
            css=>span-3 right last
            text=>close preview
            onclick
              magix.viewport.clear-controls
                container=>content5
              magix.viewport.clear-controls
                container=>content6

    magix.crud-app.delete-field
      magix.data.load
        id=>magix.crud-app.template.
      for-each=>[magix.data.load][objects][0][fields]
        if=>equals
          lhs=>[.][field-name].Value
          rhs=>[$][name].Value
          code
            set=>[.]
      set=>[magix.data.save][id].Value
        value=>[magix.data.load][id].Value
      add=>[magix.data.save]
        value=>[magix.data.load][objects][0]
      set=>[magix.data.save][1].Name
        value=>value
      magix.data.save
      magix.crud-app.rebind-data-grid
      magix.viewport.show-message
        message=>field deleted

    magix.crud-app.rebind-data-grid
      _label
        css=>span-5
      magix.data.load
        id=>magix.crud-app.template.
      for-each=>[magix.data.load][objects][0][fields]

        set=>[/][_label][css].Value
          value=>span-5 clear
        set=>[/][_label][text].Value
          value=>[.][field-name].Value
        add=>[/][magix.forms.create-web-part][controls][panel][controls]
          value=>[/][_label]
        set=>[/][magix.forms.create-web-part][controls][panel][controls][_label].Name
          value=>label

        set=>[/][_label][css].Value
          value=>span-5
        set=>[/][_label][text].Value
          value=>[.][field-type].Value
        add=>[/][magix.forms.create-web-part][controls][panel][controls]
          value=>[/][_label]
        set=>[/][magix.forms.create-web-part][controls][panel][controls][_label].Name
          value=>label

        set=>[/][_label][css].Value
          value=>span-3 right-text
        set=>[/][_label][text].Value
          value=>X
        set=>[/][_label][onclick][magix.crud-app.delete-field][name].Value
          value=>[.][field-name].Value
        add=>[/][magix.forms.create-web-part][controls][panel][controls]
          value=>[/][_label]
        set=>[/][magix.forms.create-web-part][controls][panel][controls][_label].Name
          value=>link-button

      magix.forms.create-web-part
        container=>content3
        form-id=>init-app-fields
        css=>span-17 top-1 last
        controls
          label=>lblHeaderName
            tag=>label
            text=>name
            css=>span-5
          label=>lblHeaderType
            tag=>label
            text=>type
            css=>span-5
          label=>lblDelete
            tag=>label
            css=>span-3 right-text
            text=>delete
          panel=>pnlWrpGrid
            css=>span-17
            controls
      magix.forms.set-focus
        form-id=>init-app
        id=>name

  controls
    hidden=>nameOfApp
    label=>lblHeader
      text=>fields of - 
      css=>span-10
      tag=>h2
    panel=>newWrp
      default=>addBtn
      controls
        text-box=>name
          css=>clear span-5
          place-holder=>name of field ...
        select=>fieldType
          css=>span-5
          items
            _default=>field type ...
            text-box=>text-box
            text-area=>text-area
        button=>addBtn
          css=>span-3
          text=>+
          onclick
            magix.data.load
              id=>magix.crud-app.template.
            if=>not-exist
              lhs=>[magix.data.load][objects]
              code
                magix.data.save
                  id=>magix.crud-app.template.
                  value
                    type=>magix.crud-app.template
                magix.data.load
                  id=>magix.crud-app.template.
                add=>[magix.data.load][objects]
                  value=>[@][magix.data.load][objects][0]
            _data
              field-type
              field-name
            magix.forms.get-value
              form-id=>init-app
              id=>fieldType
            if=>equals
              lhs=>[@][magix.forms.get-value:0][value].Value
              rhs=>_default
              code
                magix.viewport.show-message
                  message=>please choose type of field to add
                magix.forms.set-focus
                  form-id=>init-app
                  id=>fieldType
                stop
            magix.forms.get-value
              form-id=>init-app
              id=>name
            if=>equals
              lhs=>[@][magix.forms.get-value:1][value].Value
              rhs=>
              code
                magix.viewport.show-message
                  message=>please choose a name of field to add
                magix.forms.set-focus
                  form-id=>init-app
                  id=>name
                stop
            for-each=>[magix.data.load][objects][0][fields]
              if=>equals
                lhs=>[.][field-name].Value
                rhs=>[/][magix.forms.get-value:1][value].Value
                code
                  magix.viewport.show-message
                    message=>use a unique name for your field, name already exist
                  magix.forms.set-focus
                    form-id=>init-app
                    id=>name
                  stop
            set=>[@][_data][field-type].Value
              value=>[@][magix.forms.get-value:0][value].Value
            set=>[@][_data][field-name].Value
              value=>[@][magix.forms.get-value:1][value].Value
            add=>[magix.data.load][objects][0][fields]
              value=>[@][_data]
            set=>[magix.data.load][objects][0][fields][_data].Name
              value=>field
            for-each=>[magix.data.load][objects][0]
              add=>[/][magix.data.save][value]
                value=>[.]
            magix.data.save
              id=>magix.crud-app.template.
            magix.forms.set-value
              form-id=>init-app
              id=>fieldType
              value=>_default
            magix.forms.set-value
              form-id=>init-app
              id=>name
              value=>
            magix.crud-app.rebind-data-grid
            magix.viewport.show-message
              message=>fields were updated

magix.forms.create-web-part
  container=>content4
  form-id=>init-delete-app
  css=>span-17 last
  controls
    button=>back
      css=>span-3 left-1 top-1
      text=>&lt;&lt; back
      onclick
        execute-file=>system42/admin/app-wizard/new-app.hl
          file=>system42\admin\app-wizard\wizards\crud-app.hl
        magix.viewport.clear-controls
          container=>content3
        magix.viewport.clear-controls
          container=>content4
        magix.viewport.clear-controls
          container=>content5
        magix.viewport.clear-controls
          container=>content6
    button=>deleteApp
      css=>span-3 top-1
      text=>delete
      onclick
        magix.viewport.confirm
          message=>are you sure you wish to delete this application
          code
            magix.data.remove
            execute-file=>system42/admin/app-wizard/new-app.hl
              file=>system42\admin\app-wizard\wizards\crud-app.hl
            magix.viewport.clear-controls
              container=>content3
            magix.viewport.clear-controls
              container=>content4
            magix.viewport.clear-controls
              container=>content5
            magix.viewport.clear-controls
              container=>content6
            magix.viewport.show-message
              message=>crud application was deleted
    button=>generateApp
      css=>span-3 top-1
      text=>generate
      onclick
        magix.crud-app.generate-application
    button=>preview
      css=>span-3 top-1
      text=>preview
      tip=>will generate application and then preview it
      onclick
        magix.crud-app.generate-application
          magix.crud-app.preview-application

magix.crud-app.rebind-data-grid
