event:magix.execute

inspect=>@"loads an rss file from ajaxian.com, and displays it"

magix.file.load
  path=>http://feeds.feedburner.com/ajaxian
set=>[/][magix.xml.xml-2-node][xml].Value
  value=>[/][magix.file.load][file].Value
magix.xml.xml-2-node

set=>[/][cf][controls][Label][Text].Value
  value=>[magix.xml.xml-2-node][dom][rss][channel][title].Value

_tpl
  Label
    Tag=>h3
    CssClass=>span10 offset1
    Text
  Label
    Tag=>div
    CssClass=>span10 offset1
    Text

for-each=>[/][magix.xml.xml-2-node][dom][rss][channel]
  if=>[.].Name==item
    set=>[/][_tpl][0][Text].Value
      value=>[.][title].Value
    set=>[/][_tpl][1][Text].Value
      value=>[.][description][#cdata-section].Value
    add=>[/][cf][controls][Panel][controls]
      value=>[/][_tpl][0]
    add=>[/][cf][controls][Panel][controls]
      value=>[/][_tpl][1]

set=>[/][cf].Name
  value=>magix.forms.create-form

cf
  form-id=>rss
  container=>header
  css=>span12
  controls
    Label=>hdr
      Tag=>h1
    Panel=>wrp
      controls
