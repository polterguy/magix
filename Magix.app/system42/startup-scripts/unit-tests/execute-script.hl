

inspect=>@"creates unit tests to 
test the [magix.execute.execute-script] keyword"


event
  name=>magix.test.execute-script-pass-parameters
  persist=>false
  inspect=>@"asserts that [execute-script] works the 
way it should work"
  code
    execute-script
      script=>@"
set=>[$][output].Value
  value=>{0} {1}
    =>[$][input].Value
    =>worked"
      params
        input=>this
    if=>not-equals
      lhs=>[execute-script][params][output].Value
      rhs=>this worked
      code
        throw=>[execute-script] did not correctly pass parameters


event
  name=>magix.test.execute-script-remove-parameters
  persist=>false
  inspect=>@"asserts that [execute-script] removes
output parameters when expected"
  code
    execute-script
      script=>@"
set=>[$][input]"
      params
        input=>this
    if=>exist
      lhs=>[execute-script][params][input]
      code
        throw=>[execute-script] did not correctly remove parameters


event
  name=>magix.test.execute-script-from-file-with-parameters
  persist=>false
  inspect=>@"asserts that [execute-script] loads a 
script correctly, by passing in parameters to script 
on disc"
  code
    _file=>tmp/test-script.txt
    using=>magix.file
      save
        file=>[_file].Value
        value=>@"
set=>[$][output].Value
  value=>howdy"
    execute-script
      file=>[_file].Value
    using=>magix.file
      delete
        file=>[_file].Value
    if=>not-equals
      lhs=>[execute-script][params][output].Value
      rhs=>howdy
      code
        throw=>[execute-script] did not correctly remove parameters

