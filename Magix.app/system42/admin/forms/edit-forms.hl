

inspect=>@"creates the edit-forms web form, from which you can 
create, modify existing and delete forms from"


magix.data.load
  prototype
    type=>magix.forms.form

_dt
  name=>value

for-each=>[magix.data.load][objects]
  set=>[/][_dt][0].Name
    value=>[.].Name
  set=>[/][_dt][0].Value
    value=>[.][name].Value
  add=>[/][magix.forms.create-web-part][controls][panel][controls][select][items]
    value=>[/][_dt][0]

magix.viewport.load-module
  container=>content2
  class=>span-24 showgrid top-1 bottom-1 min-height-5
  name=>Magix.ide.Surface

magix.forms.list-widget-types

_buffer

_onclick
  get-selected-widget
  set=>[/][try][code][add-widget][where][dna].Value
    value=>[/][get-selected-widget][value][dna].Value
  try
    code
      add-widget
        auto-select=>True
        where
          dna
          position=>child
        widget
          type
      magix.ide.update-widget-list
    catch
      magix.viewport.show-message
        header=>error
        message=>@"that widget probably don't support children.&nbsp;&nbsp;
choose either before or after as the position of the node"
        time=>1000

for-each=>[magix.forms.list-widget-types][types]
  if=>not-exist
    lhs=>[.][_no-embed]
    code
      while=>more-than
        lhs=>[/][_buffer].Count
        rhs=>0
        code
          set=>[/][_buffer][0]
      set=>[/][_buffer][button].Value
        value=>[.][type].Value
      set=>[/][_buffer][button][value].Value
        value=>[.][type].Value
      replace=>[/][_buffer][button][value].Value
        what=>magix.forms.controls.
      set=>[/][_buffer][button][class].Value
        value=>span-2 bottom-1 toolbox-{0}
          v0=>[/][_buffer][button][value].Value
      set=>[/][_onclick][try][code][add-widget][widget][type].Value
        value=>[.][type].Value
      add=>[/][_buffer][button]
        value=>[/][_onclick]
      set=>[/][_buffer][button][_onclick].Name
        value=>onclick
      add=>[/][magix.forms.create-web-part][controls][panel][controls][panel:1][controls]
        value=>[/][_buffer][0]

magix.forms.create-web-part
  form-id=>edit-forms
  container=>content1
  class=>span-19 last
  events

    magix.ide.active-control-changed
      _label
        class=>span-3 top-1 right-text
      _text-box
        class=>span-4 top-1
        ontextchanged
          magix.forms.get-value
            form-id=>edit-properties
          set=>[change-widget][change][[magix.forms.get-value][id].Value].Value
            value=>[magix.forms.get-value][value].Value
          get-selected-widget
          set=>[change-widget][dna].Value
            value=>[get-selected-widget][value][dna].Value
          change-widget
            dna
            change
      _link-button
        class=>span-4 top-1
        onclick
          execute-script
            file=>system42/admin/forms/edit-property.hl
      _check
        class=>span-4 top-1
        oncheckedchanged
          magix.forms.get-value
            form-id=>edit-properties
          set=>[change-widget][change][[magix.forms.get-value][id].Value].Value
            value=>[magix.forms.get-value][value].Value
          get-selected-widget
          set=>[change-widget][dna].Value
            value=>[get-selected-widget][value][dna].Value
          change-widget
            dna
            change
      magix.viewport.clear-controls
        container=>content3
      magix.viewport.clear-controls
        container=>content4
      magix.viewport.clear-controls
        container=>content5
      get-selected-widget
      if=>exist
        lhs=>[get-selected-widget][value][widget]
        code
          magix.forms.set-enabled
            form-id=>edit-forms
            id=>copy
            value=>true
          magix.forms.set-enabled
            form-id=>edit-forms
            id=>cut
            value=>true
          magix.forms.list-widget-types
          for-each=>[@][magix.forms.list-widget-types][types]
            if=>equals
              lhs=>[.][type].Value
              rhs=>[/][get-selected-widget][value][widget][type].Value
              code
                add=>[@][..][..][..][_data]
                  value=>[.]

          for-each=>[@][_data][widget][properties]
            _type
            if=>equals
              lhs=>[.].Name
              rhs=>controls
              code
                set=>[@][..][..][_type].Value
                  value=>nothing
            else-if=>more-than
              lhs=>[.].Count
              rhs=>0
              code
                set=>[@][..][..][_type].Value
                  value=>link-button
            else-if=>equals
              lhs=>hyper lisp code
              rhs=>[.].Value
              code
                set=>[@][..][..][_type].Value
                  value=>link-button
            else
              if=>equals
                lhs=>[.].Value
                rhs=>true
                code
                  set=>[@][..][..][..][_type].Value
                    value=>check
              else-if=>equals
                lhs=>false
                rhs=>[.].Value
                code
                  set=>[@][..][..][..][_type].Value
                    value=>check
              else
                set=>[@][..][..][_type].Value
                  value=>text-box
            if=>not-equals
              lhs=>[@][_type].Value
              rhs=>nothing
              code
                set=>[/][_label][value].Value
                  value=>[.].Name
                add=>[@][..][..][..][magix.forms.create-web-part][controls][panel][controls]
                  value=>[/][_label]
                set=>[@][..][..][..][magix.forms.create-web-part][controls][panel][controls][_label].Name
                  value=>label
            if=>equals
              lhs=>[@][_type].Value
              rhs=>link-button
              code
                if=>more-than
                  lhs=>[/][get-selected-widget][value][widget][properties][[.].Name].Count
                  rhs=>0
                  code
                    set=>[/][_link-button][class].Value
                      value=>span-4 top-1 bold
                else
                  set=>[/][_link-button][class].Value
                    value=>span-4 top-1
                set=>[/][_link-button].Value
                  value=>[.].Name
                set=>[/][_link-button][value].Value
                  value=>[.].Name
                set=>[/][_link-button][onclick][execute-script][params][property].Value
                  value=>[.].Name
                add=>[@][..][..][..][magix.forms.create-web-part][controls][panel][controls]
                  value=>[/][_link-button]
                set=>[@][..][..][..][magix.forms.create-web-part][controls][panel][controls][_link-button].Name
                  value=>link-button
            else-if=>equals
              lhs=>text-box
              rhs=>[@][_type].Value
              code
                set=>[/][_text-box].Value
                  value=>[.].Name
                set=>[/][_text-box][ontextchanged][magix.forms.get-value][id].Value
                  value=>[.].Name
                set=>[/][_text-box][place-holder].Value
                  value=>[@][..][..][..][_data][0][properties][[/][_text-box].Value].Value
                set=>[/][_text-box][value].Value
                  value=>
                if=>exist
                  lhs=>[/][get-selected-widget][value][widget][properties][[/][_text-box].Value].Value
                  code
                    set=>[/][_text-box][value].Value
                      value=>[/][get-selected-widget][value][widget][properties][[/][_text-box].Value].Value
                add=>[@][..][..][..][magix.forms.create-web-part][controls][panel][controls]
                  value=>[/][_text-box]
                set=>[@][..][..][..][magix.forms.create-web-part][controls][panel][controls][_text-box].Name
                  value=>text-box
            else-if=>equals
              lhs=>check
              rhs=>[@][_type].Value
              code
                set=>[/][_check].Value
                  value=>[.].Name
                set=>[/][_check][oncheckedchanged][magix.forms.get-value][id].Value
                  value=>[.].Name
                if=>equals
                  lhs=>false
                  rhs=>[/][get-selected-widget][value][widget][properties][[/][_check].Value].Value
                  code
                    set=>[/][_check][checked].Value
                      value=>false
                else-if=>equals
                  lhs=>true
                  rhs=>[@][..][..][..][_data][0][properties][[/][_check].Value].Value
                  code
                    set=>[/][_check][checked].Value
                      value=>true
                else
                  set=>[/][_check][checked].Value
                    value=>true
                add=>[@][..][..][..][magix.forms.create-web-part][controls][panel][controls]
                  value=>[/][_check]
                set=>[@][..][..][..][magix.forms.create-web-part][controls][panel][controls][_check].Name
                  value=>check

          _data
          magix.forms.create-web-part
            form-id=>edit-properties
            container=>content3
            class=>span-24 bottom-1
            controls
              panel=>propWrp
                class=>span-22 boxed air-padding
                controls
                  label=>desc2
                    class=>span-22 last
                    value=>properties
                    tag=>label
          magix.forms.set-enabled
            form-id=>edit-forms
            id=>deleteWidg
            value=>true
      else
        magix.forms.set-enabled
          form-id=>edit-forms
          id=>deleteWidg
          value=>false
        magix.forms.set-enabled
          form-id=>edit-forms
          id=>copy
          value=>false
        magix.forms.set-enabled
          form-id=>edit-forms
          id=>cut
          value=>false
        magix.viewport.clear-controls
          container=>content4

    magix.ide.surface-changed
      list-widgets
      _dt
      for-each=>[list-widgets][widgets]
        set=>[/][_dt][0].Name
          value=>[.][dna].Value
        set=>[/][_dt][0].Value
          value=>@"{0}-->{1}"
            pr0=>[.][type].Value
            pr1=>[.][properties][id].Value
        add=>[/][_items]
          value=>[@][/][_dt][0]
      _items
      for-each=>[_items]
        add=>[/][magix.forms.set-values][values]
          value=>[.]
      magix.forms.set-values
        form-id=>edit-forms
        id=>widget
        values
          default=>select widget ...
      magix.forms.set-value
        form-id=>edit-forms
        id=>widget
        value=>default
      if=>more-than
        lhs=>[list-widgets][widgets].Count
        rhs=>0
        code
          magix.forms.set-enabled
            form-id=>edit-forms
            id=>preview
            value=>true
      else
        magix.forms.set-enabled
          form-id=>edit-forms
          id=>preview
          value=>false

    magix.ide.widget-selected
      set=>[magix.forms.set-value][value].Value
        value=>[$][dna].Value
      magix.forms.set-value
        form-id=>edit-forms
        id=>widget
      magix.ide.active-control-changed

  controls
  
    label=>header
      value=>edit forms
      tag=>h2
      class=>span-13 last left-6

    panel=>menuWrp
      controls
        select=>form
          class=>span-3
          onselectedindexchanged
            magix.forms.get-value
              form-id=>edit-forms
              id=>form
            set=>[magix.data.load][id].Value
              value=>[magix.forms.get-value][value].Value
            magix.data.load
            set=>[load-form][name].Value
              value=>[magix.data.load][objects][0][name].Value
            load-form
            set=>[magix.forms.set-value][value].Value
              value=>[magix.data.load][objects][0][name].Value
            magix.forms.set-value
              form-id=>edit-forms
              id=>name
            magix.ide.active-control-changed
            if=>not-equals
              lhs=>default
              rhs=>[magix.forms.get-value][value].Value
              code
                magix.forms.set-enabled
                  form-id=>edit-forms
                  id=>delete
                  value=>true
            else
              magix.forms.set-enabled
                form-id=>edit-forms
                id=>delete
                value=>false
          items
            default=>choose form ...

        panel=>saveWrp
          class=>span-12
          default=>save
          controls

            text-box=>name
              place-holder=>name ...
              class=>span-3
              ontextchanged
                magix.forms.get-value
                  form-id=>edit-forms
                  id=>name
                if=>exist
                  lhs=>[magix.forms.get-value][value].Value
                  code
                    magix.forms.set-enabled
                      form-id=>edit-forms
                      id=>delete
                      value=>false
                else
                  magix.forms.set-enabled
                    form-id=>edit-forms
                    id=>delete
                    value=>false

            button=>save
              class=>span-2
              value=>save
              onclick
                magix.forms.get-value
                  form-id=>edit-forms
                  id=>name
                if=>equals
                  lhs=>[magix.forms.get-value][value].Value
                  rhs=>
                  code
                    magix.viewport.show-message
                      message=>you need to supply a name for your form
                    stop
                set=>[save-form][name].Value
                  value=>[magix.forms.get-value][value].Value
                save-form
                magix.viewport.show-message
                  message=>form saved
                magix.data.load
                  prototype
                    type=>magix.forms.form
                for-each=>[@][magix.data.load][objects]
                  _dt
                    name=>value
                  if=>equals
                    lhs=>[/][magix.forms.get-value][value].Value
                    rhs=>[.][name].Value
                    code
                      set=>[@][..][..][..][_lst].Value
                        value=>[.].Name
                  set=>[@][_dt][0].Name
                    value=>[.].Name
                  set=>[@][_dt][0].Value
                    value=>[.][name].Value
                  add=>[@][..][_lst]
                    value=>[@][_dt][0]
                _lst
                  default=>choose form ...
                for-each=>[@][_lst]
                  add=>[@][..][magix.forms.set-values][values]
                    value=>[.]
                magix.forms.set-values
                  form-id=>edit-forms
                  id=>form
                set=>[@][magix.forms.set-value][value].Value
                  value=>[@][_lst].Value
                magix.forms.set-value
                  form-id=>edit-forms
                  id=>form
                set=>[@][magix.log.append][body][pr0].Value
                  value=>[/][magix.forms.get-value][value].Value
                magix.log.append
                  header=>form saved
                  body=>@"form {0} was save"
                magix.forms.set-enabled
                  form-id=>edit-forms
                  id=>delete
                  value=>true

            button=>delete
              class=>span-2
              disabled=>true
              value=>delete
              onclick
                magix.viewport.confirm
                  message=>please confirm deletion of form
                  code
                    magix.forms.get-value
                      form-id=>edit-forms
                      id=>name
                    set=>[magix.data.load][prototype][name].Value
                      value=>[magix.forms.get-value][value].Value
                    magix.data.load
                      prototype
                        type=>magix.forms.form
                    if=>not-exist
                      lhs=>[magix.data.load][objects]
                      code
                        magix.viewport.show-message
                          message=>no such form
                    else
                      set=>[@][magix.data.remove][id].Value
                        value=>[/][magix.data.load][objects][0].Name
                      magix.data.remove
                      magix.forms.set-value
                        form-id=>edit-forms
                        id=>name
                        value=>
                      load-form
                        name
                      magix.data.load
                        prototype
                          type=>magix.forms.form
                      for-each=>[@][magix.data.load][objects]
                        _dt
                          name=>value
                        set=>[@][_dt][0].Name
                          value=>[.].Name
                        set=>[@][_dt][0].Value
                          value=>[.][name].Value
                        add=>[@][..][_lst]
                          value=>[@][_dt][0]
                      _lst
                        default=>choose form ...
                      for-each=>[@][_lst]
                        add=>[@][..][magix.forms.set-values][values]
                          value=>[.]
                      magix.forms.set-values
                        form-id=>edit-forms
                        id=>form
                      magix.forms.set-value
                        form-id=>edit-forms
                        id=>form
                        value=>default
                      set=>[@][magix.log.append][body][pr0].Value
                        value=>[magix.forms.get-value][value].Value
                      magix.forms.set-values
                        form-id=>edit-forms
                        id=>widget
                        values
                          default=>select widget ...
                      magix.ide.active-control-changed
                      magix.forms.set-enabled
                        form-id=>edit-forms
                        id=>delete
                        value=>false
                      magix.log.append
                        header=>form deleted
                        body=>@"form {0} was deleted"
                      magix.viewport.show-message
                        message=>form deleted
                      magix.viewport.clear-controls
                        container=>content6
                      magix.viewport.clear-controls
                        container=>content7

            select=>widget
              class=>span-5 last
              onselectedindexchanged
                magix.forms.get-value
                  form-id=>edit-forms
                  id=>widget
                if=>not-equals
                  lhs=>[magix.forms.get-value][value].Value
                  rhs=>default
                  code
                    set=>[/][select-widget][dna].Value
                      value=>[magix.forms.get-value][value].Value
                select-widget
              items
                default=>select widget ...

        button=>deleteWidg
          class=>span-4 last
          value=>delete widget
          disabled=>true
          onclick
            magix.forms.get-value
              form-id=>edit-forms
              id=>widget
            set=>[if][code][remove-widget][dna].Value
              value=>[magix.forms.get-value][value].Value
            if=>not-equals
              lhs=>[magix.forms.get-value][value].Value
              rhs=>default
              code
                remove-widget
                select-widget
            else
              magix.viewport.show-message
                message=>choose a widget first

        button=>copy
          class=>span-3 top-1
          value=>copy
          disabled=>true
          onclick
            copy-widget
            magix.forms.set-enabled
              form-id=>edit-forms
              id=>paste
              value=>true

        button=>cut
          class=>span-2 top-1
          value=>cut
          disabled=>true
          onclick
            cut-widget
            magix.forms.set-enabled
              form-id=>edit-forms
              id=>paste
              value=>true
            magix.viewport.clear-controls
              container=>content3
            magix.viewport.clear-controls
              container=>content4
            magix.viewport.clear-controls
              container=>content5

        button=>paste
          class=>span-3 top-1
          value=>paste
          disabled=>true
          onclick
            paste-widget
              auto-select=>true

        button=>preview
          class=>span-3 top-1
          value=>preview
          tip=>saves form, and then shows it in preview mode
          disabled=>true
          onclick
            magix.forms.get-value
              form-id=>edit-forms
              id=>name
            set=>[save-form][name].Value
              value=>[magix.forms.get-value][value].Value
            save-form
            magix.forms.effect
              type=>fade-out
              time=>500
              form-id=>edit-forms
              id=>ctrlWrp
              chained
                e0
                  type=>fade-out
                  time=>500
                  form-id=>edit-forms
                  id=>menuWrp
            get-form
            magix.viewport.clear-controls
              container=>content2
            magix.viewport.clear-controls
              container=>content3
            magix.viewport.clear-controls
              container=>content4
            magix.viewport.clear-controls
              container=>content5
            magix.viewport.clear-controls
              container=>content6
            magix.viewport.clear-controls
              container=>content7
            magix.forms.create-mml-web-part
              container=>content2
              class=>span-24
              form-id=>myPage
              mml=>@"
<h2 class=""span-24 top-1"">preview of form</h2>
<p>below is your form in executable style, meaning
all events and everything is hooked up and ready to be tested<br/>
{{
link-button=>closepreview
  value=>close preview
  onclick
    magix.forms.effect
      type=>fade-in
      time=>500
      form-id=>edit-forms
      id=>menuWrp
      chained
        e0
          type=>fade-in
          time=>500
          form-id=>edit-forms
          id=>ctrlWrp
    magix.viewport.clear-controls
      container=>content2
    magix.viewport.clear-controls
      container=>content7
    magix.viewport.load-module
      container=>content2
      class=>span-24 showgrid top-1 bottom-1 min-height-5
      name=>Magix.ide.Surface
    magix.forms.get-value
      form-id=>edit-forms
      id=>form
    set=>[magix.data.load][id].Value
      value=>[magix.forms.get-value][value].Value
    magix.data.load
    set=>[load-form][name].Value
      value=>[magix.data.load][objects][0][name].Value
    load-form
    magix.ide.active-control-changed
}}
</p>"
            magix.forms.get-value
              form-id=>edit-forms
              id=>name
            add=>[magix.forms.create-web-part]
              value=>[get-form][controls]
            magix.forms.get-value
              id=>name
              form-id=>edit-forms
            set=>[@][magix.forms.create-web-part][form-id].Value
              value=>[@][magix.forms.get-value][value].Value
            magix.forms.create-web-part
              container=>content7
              class=>span-24 bottom-2
              form-id

        panel=>ctrlWrp
          class=>span-17 last top-1 boxed air-padding
          controls

            label=>desc
              value=>web controls
              tip=>click to add up a web control to your design surface
              tag=>label
              class=>span-17 bottom-1 last











