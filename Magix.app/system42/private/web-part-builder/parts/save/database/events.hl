


inspect=>@"creates the events for saving form into database

contains the events for helping user saving his form into the database"


// tries to save to database, asking user to confirm any overwritings
magix.ide.save_onclick
  magix.forms.get-value
    form-id=>save-content
    id=>database-id
  if=>equals
    lhs=>[magix.forms.get-value][value].value
    rhs=>
    code
      magix.viewport.show-message
        message=>you must supply an id to save your form into the database
  else
    set=>[@][magix.data.load][id].value
      value=>[magix.forms.get-value][value].value
    magix.data.load
    if=>exist
      lhs=>[@][magix.data.load][value]
      code
        magix.viewport.confirm
          message=>that object already exist in database, do you wish to overwrite it?
          code
            magix.ide.save-to-database
    else
      magix.ide.save-to-database

// actually saves form to database
magix.ide.save-to-database
  magix.forms.get-value
    form-id=>save-content
    id=>database-id
  set=>[magix.data.save][id].value
    value=>[magix.forms.get-value][value].value
  magix.ide.get-form
  add=>[magix.data.save][value][surface]
    value=>[magix.ide.get-form]
    children-only=>true
  magix.data.save
    value
      type=>magix.forms.web-part
  magix.viewport.clear-controls
    container=>editor-persist-wrapper
    reset-class=>true
  set=>[magix.viewstate.set][value].value
    value=>[magix.forms.get-value][value].value
  magix.viewstate.set
    id=>magix.ide.last-database-id
  using=>magix.viewport
    show-message
      message=>form was successfully save to database as '{0}'
        =>[magix.forms.get-value][value].value
  set=>[magix.log.append-username][body].value
    value=>web part was saved to database with id of '{0}'
      =>[magix.forms.get-value][value].value
  magix.log.append-username
    header=>web part was saved

// sets the last used db id on the text-box asking user for id to save web part as
magix.ide.database-id_onfirstload
  magix.viewstate.get
    id=>magix.ide.last-database-id
  if=>exist
    lhs=>[magix.viewstate.get][value]
    code
      set=>[@][magix.forms.set-value][value].value
        value=>[magix.viewstate.get][value].value
      magix.forms.set-value
        form-id=>save-content
        id=>database-id
  magix.forms.effect
    type=>timeout
    time=>25
    chained
      e0
        type=>focus-and-select
        form-id=>save-content
        id=>database-id

