

inspect=>@"allows wysiwyg editing of html files

creates a wysiwyg editor where the user can edit html files"


// verifies user is in admin role
magix.core.verify-role
  role=>admin

// including wysiwyg css
magix.viewport.include-client-file
  type=>css
  file=>media/back-end/wysihtml.css

// loading file, if we are given a [file] input parameter
if=>exist
  lhs=>[$][file].value
  code
    using=>magix.file
      load
        file=>[$][file].value
    replace=>[@][using][load][value].value
      what=>&
      with=>&amp;
    replace=>[@][using][load][value].value
      what=><
      with=>&lt;
    replace=>[@][using][load][value].value
      what=>>
      with=>&gt;
    set=>[**wysiwyg=>editor-surface][value].value
      value=>[@][using][load][value].value
    set=>[**text-box=>txt-file-name][value].value
      value=>[$][file].value

// putting in container, if we're given one, otherwise we're using default container
if=>exist
  lhs=>[$][container]
  code
    set=>[magix.forms.create-web-part][container].value
      value=>[$][container].value

// adding css classes, if we're given any, otherwise using default of last span-17
if=>exist
  lhs=>[$][class]
  code
    set=>[magix.forms.create-web-part][class].value
      value=>[$][class].value

// adding extra buttons, if we're given any
if=>exist
  lhs=>[$][extra-buttons]
  code
    set=>[**button=>save][class].value
      value=>span-2
    add=>[**panel=>save-wrapper][controls]
      values=>[$][extra-buttons]

// adding automatical scroll, if we're supposed to
if=>equals
  lhs=>[$][auto-scroll].value
  rhs=>true
  code
    set=>[**panel=>save-wrapper][onfirstload][magix.browser.scroll][id].value
      value=>[magix.forms.create-web-part][container].value

// automatically giving focus, if we're supposed to
if=>not-equals
  lhs=>[$][auto-focus].value
  rhs=>true
  code
    set=>[**text-area=>editor-surface][onfirstload][magix.forms.effect].name
      value=>_not

// adding [onesc] event handler, if we're given any
if=>exist
  lhs=>[$][onesc]
  code
    add=>[**panel=>main-wrapper][onesc]
      values=>[$][onesc]

// creating web part
magix.forms.create-web-part
  form-id=>editor
  container=>content1
  class=>span-17 last
  events
    magix.wysiwyg-editor.save_onclick
      magix.forms.get-value
        form-id=>editor
        id=>txt-file-name
      magix.forms.get-value
        form-id=>editor
        id=>editor-surface
      if=>equals
        lhs=>[magix.forms.get-value:1][value].value
        rhs=>
        code
          magix.viewport.show-message
            message=>nothing to save
            color=>#ffaaaa
          stop
      if=>equals
        lhs=>[magix.forms.get-value:0][value].value
        rhs=>
        code
          // no filename given, opening up browse to figure out a filename to save file as
          execute-script
            file=>system42/private/file-manager/file-manager.hl
            params
              container=>content2
              class=>span-20 last boxed air-padding top-1 left-5
              onselect
                set=>[magix.forms.set-value][value].value
                  value=>[$][file].value
                magix.forms.set-value
                  form-id=>editor
                  id=>txt-file-name
                magix.viewport.clear-controls
                  container=>content2
                  reset-class=>true
                magix.wysiwyg-editor.save-file
              oncancel
                magix.viewport.clear-controls
                  container=>content2
                  reset-class=>true
      else
        magix.wysiwyg-editor.save-file

    // actually saves file
    magix.wysiwyg-editor.save-file
      magix.forms.get-value
        form-id=>editor
        id=>txt-file-name
      magix.forms.get-value
        form-id=>editor
        id=>editor-surface
      if=>equals
        lhs=>[magix.forms.get-value:1][value].value
        rhs=>
        code
          magix.viewport.show-message
            message=>nothing to save
            color=>#ffaaaa
          stop
      using=>magix.file
        save
          file=>[magix.forms.get-value:0][value].value
          value=>[magix.forms.get-value:1][value].value
      magix.viewport.show-message
        message=>file was successfully saved
      magix.forms.effect
        type=>focus-and-select
        form-id=>editor
        id=>editor-surface
  controls
    panel=>main-wrapper
      class=>fill-width
      style=>overflow:hidden;
      controls
        wysiwyg=>editor-surface
          class=>fill-width
          placeholder=>text content ...
        panel=>save-wrapper
          default=>save
          class=>right column last btn-group
          controls
            text-box=>txt-file-name
              class=>span-10
              placeholder=>filename ...
            button=>save
              class=>span-2 last
              value=>save
              onclick
                magix.wysiwyg-editor.save_onclick
        label
          tag=>label
          class=>span-2 last text-right
          value=>filename

