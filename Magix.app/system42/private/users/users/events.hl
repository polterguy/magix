

inspect=>@"contains events for users web part

contains the active events necessary to show the edit users grid"


// event to load users
magix.users.load
  if=>exist
    lhs=>[$][start]
    code
      set=>[magix.data.load][start]
        value=>[$][start]
  if=>exist
    lhs=>[$][end]
    code
      set=>[magix.data.load][end]
        value=>[$][end]
  if=>exist
    lhs=>[$][query]
    code
      set=>[magix.data.load][prototype][username].value
        value=>%{0}%
          =>[$][query].value
      set=>[magix.data.count][prototype][username].value
        value=>%{0}%
          =>[$][query].value
  magix.data.load
    prototype
      type=>magix.core.user
  magix.data.count
    prototype
      type=>magix.core.user
  set=>[$][count]
    value=>[magix.data.count][count]
  add=>[$][users]
    value=>[magix.data.load][objects]
    children-only=>true

// creates the controls for the datagrid showing the users
magix.users.create-grid-controls
  _ctrl
  _ctrl1
    panel
      class=>span-15 last
      controls
        label
          class=>span-5
          value=>{username}
        label
          class=>span-6
          value=>{name}
        label
          class=>span-2
          value=>{role}
        link-button
          class=>span-1
          value=>edit
          info=>{id}
          onclick
            set=>[magix.users.edit-user][id].value
              value=>[$][info].value
            magix.users.edit-user
        link-button
          class=>span-1 last
          value=>delete
          info=>{id}
          onclick
            set=>[magix.viewport.confirm][code][magix.users.delete-user][id].value
              value=>[$][info].value
            magix.viewport.confirm
              message=>are you sure you wish to delete the user?
              code
                magix.users.delete-user
  magix.viewstate.get
    id=>magix.users.current-paging
  set=>[magix.users.load][start]
    value=>[magix.viewstate.get][value][start]
  set=>[magix.users.load][end]
    value=>[magix.viewstate.get][value][end]
  if=>exist
    lhs=>[magix.viewstate.get][value][query].value
    code
      set=>[magix.users.load][query]
        value=>[magix.viewstate.get][value][query]
  magix.users.load
  for-each=>[magix.users.load][users]
    _buffer
    set=>[@][_buffer][0]
    set=>[@][_buffer][0]
    add=>[@][_buffer]
      value=>[.]
      children-only=>true
    set=>[/][_ctrl][0]
      value=>[/][_ctrl1][0]
    iterate=>[/][_ctrl]
      switch=>[.].value
        case=>{id}
          set=>[.].value
            value=>[/][**_buffer][id].value
        case=>{name}
          set=>[.].value
            value=>[/][**_buffer][value][name].value
        case=>{username}
          set=>[.].value
            value=>[/][**_buffer][value][username].value
        case=>{role}
          if=>equals
            lhs=>[/][**_buffer][value][roles].count
            rhs=>0
            code
              set=>[.].value
                value=>&nbsp;
          else
            set=>[.].value
              value=>[/][**_buffer][value][roles][0].name
    add=>[$][controls]
      value=>[/][_ctrl][panel]
  if=>more-than
    lhs=>[magix.users.load][count].value
    rhs=>[magix.users.load][users].count
    code
      // need to create paging ...
      _paging
        panel
          class=>span-15 last top-1
          controls
            link-button=>previous
              class=>span-1 left-11
              value=>&lt;&lt;
              onclick
                magix.viewstate.get
                  id=>magix.users.current-paging
                using=>magix.math
                  subtract
                    =>[magix.viewstate.get][value][start].value
                    =>10
                  subtract
                    =>[magix.viewstate.get][value][end].value
                    =>10
                set=>[magix.viewstate.set][value][start].value
                  value=>[using][subtract:0].value
                set=>[magix.viewstate.set][value][end].value
                  value=>[using][subtract:1].value
                set=>[magix.viewstate.set][value][query].value
                  value=>[magix.viewstate.get][query].value
                magix.viewstate.set
                  id=>magix.users.current-paging
                magix.users.show-users-grid
            label=>lbl-count
              class=>span-2
            link-button=>next
              class=>span-1 last
              value=>&gt;&gt;
              onclick
                magix.viewstate.get
                  id=>magix.users.current-paging
                using=>magix.math
                  add
                    =>[magix.viewstate.get][value][start].value
                    =>10
                  add
                    =>[magix.viewstate.get][value][end].value
                    =>10
                set=>[magix.viewstate.set][value][start].value
                  value=>[using][add:0].value
                set=>[magix.viewstate.set][value][end].value
                  value=>[using][add:1].value
                set=>[magix.viewstate.set][value][query].value
                  value=>[magix.viewstate.get][query].value
                magix.viewstate.set
                  id=>magix.users.current-paging
                magix.users.show-users-grid
      magix.viewstate.get
        id=>magix.users.current-paging
      _max
      if=>more-than-equals
        lhs=>[@][magix.viewstate.get][value][end].value
        rhs=>[magix.users.load][count].value
        code
          set=>[@][..][..][_max].value
            value=>[magix.users.load][count].value
          set=>[/][**link-button=>next][disabled].value
            value=>true
      else
        set=>[@][..][_max].value
          value=>[@][..][magix.viewstate.get][value][end].value
      if=>equals
        lhs=>[@][magix.viewstate.get][value][start].value
        rhs=>0
        code
          set=>[/][**link-button=>previous][disabled].value
            value=>true
      set=>[@][_paging][**label=>lbl-count][value].value
        value=>{0}-{1} of {2}
          =>[@][magix.viewstate.get][value][start].value
          =>[@][_max].value
          =>[magix.users.load][count].value
      add=>[$][controls]
        value=>[@][_paging][panel]

// edits user, expects [id]
magix.users.edit-user
  magix.forms.set-value
    form-id=>users
    id=>edit-user-header
    value=>edit user
  set=>[magix.data.load][id]
    value=>[$][id]
  magix.data.load
  using=>magix.forms
    set-value
      form-id=>users
      id=>edit-pwd
      value=>[magix.data.load][value][pwd].value
    set-value
      form-id=>users
      id=>edit-pwd-repeat
      value=>[magix.data.load][value][pwd].value
    set-value
      form-id=>users
      id=>edit-username
      value=>[magix.data.load][value][username].value
    set-enabled
      form-id=>users
      id=>edit-username
      value=>false
    set-value
      form-id=>users
      id=>edit-name
      value=>[magix.data.load][value][name].value
    set-value
      form-id=>users
      id=>edit-role
      value=>[magix.data.load][value][roles][0].name
  magix.forms.set-class
    form-id=>users
    id=>edit-user-wrapper
    value=>span-15 last boxed-light-green air-padding top-1 clear
  set=>[magix.forms.set-info][value].value
    value=>[$][id].value
  magix.forms.set-info
    form-id=>users
    id=>save
  magix.forms.effect
    type=>focus-and-select
    form-id=>users
    id=>edit-name

// deletes user, expects [id]
magix.users.delete-user
  set=>[magix.data.load][id]
    value=>[$][id]
  magix.data.load
  magix.session.get
    id=>magix.core.user
  if=>equals
    lhs=>[magix.session.get][value][username].value
    rhs=>[magix.data.load][value][username].value
    code
      magix.viewport.show-message
        message=>for security reasons, you cannot delete the currently logged in user.&nbsp;&nbsp;if you wish to delete this user, then log in with another user, and then delete it
        time=>5000
      stop
  set=>[magix.data.remove][id]
    value=>[$][id]
  magix.data.remove
  magix.users.show-users-grid
  set=>[magix.log.append-username][body].value
    value=>user '{0}' was deleted
      =>[magix.data.load][value][username].value
  magix.log.append-username
    header=>user deleted

// saves a new or existing user
magix.users.save-user
  using=>magix.forms
    get-value
      form-id=>users
      id=>edit-username
    get-value
      form-id=>users
      id=>edit-name
    get-value
      form-id=>users
      id=>edit-pwd
    get-value
      form-id=>users
      id=>edit-pwd-repeat
    get-value
      form-id=>users
      id=>edit-role
  if=>exist
    lhs=>[$][id]
    code
      set=>[magix.data.save][id]
        value=>[$][id]
      set=>[@][magix.data.load][id]
        value=>[$][id]
      magix.data.load
      set=>[magix.data.save][value]
        value=>[@][magix.data.load][value]
  else
    set=>[@][magix.data.count][prototype][username].value
      value=>[using][get-value:0][value].value
    magix.data.count
      prototype
        type=>magix.core.user
    if=>not-equals
      lhs=>[@][magix.data.count][count].value
      rhs=>0
      code
        magix.viewport.show-message
          message=>username already exists
          time=>1000
          color=>red
        magix.forms.effect
          type=>focus-and-select
          form-id=>users
          id=>edit-username
        stop
  if=>equals
    lhs=>[using][get-value:0][value].value
    rhs=>
    code
      magix.viewport.show-message
        message=>user needs a username
        time=>1000
        color=>red
      magix.forms.effect
        type=>focus-and-select
        form-id=>users
        id=>edit-username
      stop
  if=>equals
    lhs=>[using][get-value:1][value].value
    rhs=>
    code
      magix.viewport.show-message
        message=>user needs a name
        time=>1000
        color=>red
      magix.forms.effect
        type=>focus-and-select
        form-id=>users
        id=>edit-name
      stop
  if=>equals
    lhs=>[using][get-value:2][value].value
    rhs=>
    code
      magix.viewport.show-message
        message=>user needs a password
        time=>1000
        color=>red
      magix.forms.effect
        type=>focus-and-select
        form-id=>users
        id=>edit-pwd
      stop
  if=>not-equals
    lhs=>[using][get-value:2][value].value
    rhs=>[using][get-value:3][value].value
    code
      magix.viewport.show-message
        message=>passwords don't match
        time=>1000
        color=>red
      magix.forms.effect
        type=>focus-and-select
        form-id=>users
        id=>edit-pwd
      stop
  set=>[magix.data.save][value][username].value
    value=>[using][get-value:0][value].value
  set=>[magix.data.save][value][name].value
    value=>[using][get-value:1][value].value
  set=>[magix.data.save][value][pwd].value
    value=>[using][get-value:2][value].value
  set=>[magix.data.save][value][roles]
  set=>[magix.data.save][value][roles][[using][get-value:4][value].value].value
    value=>true
  magix.data.save
    value
      type=>magix.core.user
  magix.users.show-users-grid
  set=>[magix.log.append-username][body].value
    value=>user '{0}' was created or edited
      =>[magix.data.save][value][username].value
  magix.log.append-username
    header=>user created or edited

// loads roles into drop down list
magix.users.load-roles
  magix.data.load
    prototype
      type=>magix.core.role
  _roles
  iterate=>[magix.data.load][objects]
    if=>equals
      lhs=>[.].name
      rhs=>name
      code
        add=>[/][_roles]
          value=>[.].value
            value=>[.].value
  add=>[magix.forms.set-values][values]
    value=>[_roles]
    children-only=>true
  magix.forms.set-values
    form-id=>users
    id=>edit-role

