

<div class="span-24 relative">
{{
system42.travel.change-login-box
  magix.web.get-session=>magix.core.user
  if=>![magix.web.get-session][value]
    magix.forms.show-control
      form-id=>header
      id=>loginWrp
      visible=>true
    magix.forms.show-control
      form-id=>header
      id=>logoutWrp
      visible=>false
  else
    magix.forms.show-control
      form-id=>header
      id=>loginWrp
      visible=>false
    magix.forms.show-control
      form-id=>header
      id=>logoutWrp
      visible=>true

system42.travel.try-login
  set=>[magix.data.load][prototype][username].Value
    value=>[$][username].Value
  set=>[magix.data.load][prototype][pwd].Value
    value=>[$][pwd].Value
  magix.data.load
    prototype
      type=>magix.core.user
  if=>[magix.data.load][objects].Count>0
    set=>[@][magix.web.set-session][value]
      value=>[magix.data.load][objects][0]
    set=>[@][magix.web.set-session][0].Name
      value=>value
    magix.web.set-session=>magix.core.user
      value
    if=>[magix.data.load][objects][0][roles][admin]
      magix.web.redirect=>~?dashboard=
    else
      magix.viewport.execute-javascript
        script=>@"location.reload();"
  else
    magix.forms.set-value
      form-id=>header
      id=>error
      value=>(*) error
    magix.forms.set-focus
      form-id=>header
      id=>username
    magix.forms.select-all
      form-id=>header
      id=>username

lambda=>header
  oncreatecontrols
    _lbl=>headerCtrl
      css=>span-14 top-2 bottom-3
      text=>magix illuminate - release lucifer
      tag=>h1
    magix.web.get-config-setting=>Magix.Core.Portal-Name
    set=>[_lbl][text].Value
      value=>[magix.web.get-config-setting][value].Value
    add=>[$]
      value=>[_lbl]
    set=>[$][_lbl].Name
      value=>label

panel=>loginWrp
  onfirstload
    system42.travel.change-login-box
  css=>absolute top right span-6 top-1 z-1 last
  controls
    panel=>loginPnl
      css=>span-4 left-2
      default=>loginBtn
      controls
        label=>descr
          tag=>label
          text=>please login
        text-box=>username
          place-holder=>username ...
          css=>span-4
        text-box=>pwd
          place-holder=>password ...
          mode=>password
          css=>span-4
        label=>error
          css=>error span-2
        button=>loginBtn
          text=>login
          css=>right span-2 btn-large last
          onclick
            magix.forms.get-value
              form-id=>header
              id=>username
            magix.forms.get-value
              form-id=>header
              id=>pwd
            set=>[system42.travel.try-login][username].Value
              value=>[magix.forms.get-value][value].Value
            set=>[system42.travel.try-login][pwd].Value
              value=>[magix.forms.get-value:1][value].Value
            system42.travel.try-login


panel=>logoutWrp
  css=>absolute top right span-4 top-1 z-1 last
  controls
    button=>logOut
      css=>span-4 btn-large
      text=>logout
      onclick
        magix.web.set-session=>magix.core.user
        magix.web.redirect=>~
    lambda=>createLoadDashboard
      oncreatecontrols
        magix.web.get-session=>magix.core.user
        if=>[magix.web.get-session][value][roles][admin]
          _lnk=>load_dashboard
            css=>span-4 right-text
          magix.web.get=>dashboard
          if=>[@][magix.web.get][value]
            set=>[if][_lnk][url].Value
              value=>~
            set=>[if][_lnk][text].Value
              value=>website
          else
            set=>[if][_lnk][url].Value
              value=>~?dashboard=
            set=>[if][_lnk][text].Value
              value=>dashboard
          add=>[$]
            value=>[@][_lnk]
          set=>[$][_lnk].Name
            value=>hyperlink
}}
</div>

