

inspect=>@"creates unit tests for the [with] keyword

creates unit tests to test the [with] keyword of magix"


event
  name=>magix.test.with
  persist=>false
  inspect=>@"verifies [with] works

creates several [with] statements, one inside the other, verifying 
they all behave as expected"
  code
    _data
      _item1
        yo=>test
    with=>[_data]
      set=>[_item1].value
        value=>test
      with=>[_item1]
        set=>[.][yo].value
          value=>hello
      set=>[.].value
        value=>success
    if=>not-equals
      lhs=>[_data][_item1].value
      rhs=>test
      or=>not-equals
        lhs=>[_data][_item1][yo].value
        rhs=>hello
      or=>not-equals
        lhs=>[_data].value
        rhs=>success
      code
        throw=>[with] misbehaved


event
  name=>magix.test.with-exceptions
  persist=>false
  inspect=>@"verifies [with] works with exceptions

creates a [with] statement, throws an exceptions from inside of it, 
for then to verify it behaves correctly"
  code
    _data
      _item1
        yo=>test
    with=>[_data]
      try
        code
          with=>[_item1]
            try
              code
                throw=>catched beneath here
              catch
                set=>[.].value
                  value=>item1
            throw=>eek
        catch
          set=>[_item1][yo].value
            value=>tested
      set=>[.].value
        value=>howdy
    if=>not-equals
      lhs=>[_data].value
      rhs=>howdy
      or=>not-equals
        lhs=>[_data][_item1].value
        rhs=>item1
      or=>not-equals
        lhs=>[_data][_item1][yo].value
        rhs=>tested
      code
        throw=>[with] misbehaved

