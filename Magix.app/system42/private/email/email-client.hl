

inspect=>@"starts magix mail

starts the magix mail client, first verifying that all 
settings have been correctly setup"

// including css file
magix.viewport.include-client-file
  type=>css
  file=>media/back-end/email-client.css

// creating header web part, and pinning it, such that it doesn't become cleared when all containers are cleared
magix.forms.create-web-part
  container=>content1
  class=>span-24 top-2 bottom-2
  controls-file=>system42/private/email/parts/header.hl
magix.viewport.pin-container
  container=>content1

// loading email settings to see if settings are setup
magix.data.load-username=>smtp
  id=>magix.smtp.settings
magix.data.load-username=>imap
  id=>magix.imap.settings
magix.data.load-username=>pop3
  id=>magix.pop3.settings

_controls
  label
    tag=>h1
    class=>fill-width top-1 bottom-1
    value=>magix mail

// making sure email settings exists, and if not, fire up settings web part
if=>not-exist
  lhs=>[magix.data.load-username=>smtp][value]
  or=>not-exist
    lhs=>[magix.data.load-username=>imap][value]
    and=>not-exist
      lhs=>[magix.data.load-username=>pop3][value]
  code
    magix.viewport.show-message
      message=>please setup email settings first
      color=>#ffaaaa
      time=>5000
    magix.viewport.clear-controls
      all=>true

    // start settings web part, making sure inbox is automatically loaded afterwards
    execute-script
      file=>system42/private/email/user-setup/setup-settings.hl
      params
        execute-afterwards
          execute-script
            file=>system42/private/email/inbox/inbox.hl
else
  execute-script
    file=>system42/private/email/inbox/inbox.hl

