

inspect=>@"will show the desktop according to which 
events in the system are within the magix.admin.desktop-shortcuts. 
namespace.&nbsp;&nbsp;see the file events-create-desktop-shortcuts.hl
in the startup folder to see an example of how you can extend
the desktop yourself"


// verifies user is in admin role
magix.core.verify-role
  role=>admin

list-events
  begins-with=>magix.admin.desktop-shortcuts.

_btn
  class=>span-5 last desktop-widget-item
  onclick

for-each=>[list-events][events]
  set=>[/][_evts][_evt].name
    value=>[.].name

_evts
execute=>[_evts]

for-each=>[_evts]
  for-each=>[.]
    set=>[/][_categories][[.][category].value].value
      value=>

_categories

_curCat

for-each=>[_categories]
  set=>[/][_pnl][controls][label][value].value
    value=>[.].name
  set=>[/][_pnl][controls][panel][controls]
  set=>[/][_curCat].value
    value=>[.].name
  set=>[/][_pnl].value
    value=>@"{0}Wrp"
      pr0=>[/][_curCat].value
  set=>[/][_pnl][controls][panel].value
    value=>@"{0}WrpOuter"
      pr0=>[/][_curCat].value
  for-each=>[/][_evts]
    for-each=>[.]
      if=>equals
        lhs=>[.][category].value
        rhs=>[/][_curCat].value
        code
          set=>[/][_btn][value].value
            value=>[.][text].value
          set=>[/][_btn].value
            value=>[.].name
          add=>[/][_btn]
            value=>[.][code]
          set=>[/][_btn][onclick]
          set=>[/][_btn][code].name
            value=>onclick
          add=>[/][_pnl][controls][panel][controls]
            value=>[/][_btn]
          set=>[/][_pnl][controls][panel][controls][_btn].name
            value=>link-button
  add=>[/][magix.forms.create-web-part][controls]
    value=>[/][_pnl]
  set=>[/][magix.forms.create-web-part][controls][_pnl].name
    value=>panel

_pnl
  class=>span-5 desktop-widget top-1 left-1
  controls
    label=>header
      class=>desktop-widget-header span-4
      tag=>label
    panel
      controls

magix.forms.create-web-part
  form-id=>desktop-surface
  container=>content1
  class=>span-17 last desktop-surface


