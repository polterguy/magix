Method:magix.execute

inspect=>@"Will show a screen of all the 
execute keywords in the system, and allow
the user to inspect them by clicking
them."

magix.admin.get-active-events
  begins-with=>magix.execute
set=>[magix.admin.get-active-events].Name
  value=>Data
for-each=>[Data][ActiveEvents]
  Tpl
    LinkButton
      CssClass=>span2 btn btn-small
      Text
      OnClick
        void.foo
          inspect
        set=>[2][value].Value
          value=>[0][inspect].Value
        magix.forms.set-value
          form-id=>form1
          id=>desc
          value
        set=>[4][value].Value
          value=>[0].Name
        magix.forms.set-value
          form-id=>form1
          id=>header
          value
        set=>[0][inspect]
        set=>[magix.code.node-2-code][JSON].Value
          value=>[0]
        magix.code.node-2-code
          JSON
        set=>[9][value].Value
          value=>[magix.code.node-2-code][code].Value
        magix.forms.set-value
          form-id=>form1
          id=>code
          value
        set=>[0][inspect].Name
          value=>inspect
  set=>[.ip][..][Tpl][LinkButton][OnClick][0].Name
    value=>[.].Value
  set=>[.ip][..][Tpl][LinkButton][Text].Value
    value=>[.].Value
  add=>[/][cf][controls][Panel][controls]
    value=>[.ip][..][Tpl][LinkButton]
set=>[cf][controls][Panel2].Name
  value=>Panel
set=>[cf].Name
  value=>magix.forms.create-form
cf
  form-id=>form1
  container=>content
  controls
    Panel2
      CssClass=>span5
      controls
        Label=>header
          Text=>Name of Active Event
          CssClass=>span5
          Tag=>strong
        Label=>desc
          CssClass=>span5
          Text=>@"Description of your Active Event. 
Click any of the ""magix.execute"" Active Events below to inspect them ..."
        Label=>code
          CssClass=>span5
          Text=>Code Example ...
          Tag=>pre
    Panel=>wrp
      CssClass=>span2 offset1
      controls
