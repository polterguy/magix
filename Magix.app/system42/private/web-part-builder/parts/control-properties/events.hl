

inspect=>@"contains events for properties

contains events for web part designer properties section"


magix.ide.create-properties-controls
  // finding selected control, and stuffing its properties out into the properties web part
  magix.ide.get-selected-control
  set=>[_evt-inspect].Name
    value=>magix.forms.controls.{0}
      =>[magix.ide.get-selected-control][value][0].Name
  _evt-inspect
    inspect

  // creating edit id property
  _data
    panel
      class=>span-9
      controls
        label
          class=>span-3 text-right
          tag=>label
          value=>id
        text-box
          class=>span-6 last
          placeholder=>id
          value=>{value}
          ontextchanged
            _dna=>{dna}
            set=>[magix.ide.change-control][dna].Value
              value=>[_dna].Value
            if=>not-equals
              lhs=>[$][value].Value
              rhs=>
              code
                set=>[magix.ide.change-control][change][id].Value
                  value=>[$][value].Value
            else
              magix.viewport.show-message
                message=>you cannot set the id property of a control to empty
            magix.ide.change-control
  set=>[_data][**value=>{value}].Value
    value=>[magix.ide.get-selected-control][value][0].Value
  set=>[_data][**_dna=>{dna}].Value
    value=>[magix.ide.get-selected-control][dna].Value
  add=>[$]
    value=>[_data]
    children-only=>true

  // looping through each control property
  for-each=>[**inspect=>?][..][**controls][0]
    index-of=>[.].Name
      what=>on
    if=>not-exist
      lhs=>[@][index-of][result]
      and=>not-equals
        lhs=>[.].Name
        rhs=>items
      code
        // not event handler
        set=>[@][_params]
        set=>[@][_params][dna]
          value=>[/][magix.ide.get-selected-control][dna]
        if=>exist
          lhs=>[/][magix.ide.get-selected-control][value][0][[.].Name]
          code
            set=>[@][..][..][_params][content].Value
              value=>[/][magix.ide.get-selected-control][value][0][[.].Name].Value
        add=>[@][_params][field-type]
          value=>[.]
        add=>[$]
          value=>[:magix.ide.create-control-property-modifier-control[@][_params]][control]
          children-only=>true

  // creating spacer between properties and events
  _hr
    label
      tag=>hr
      class=>span-20 last clear top-1 bottom-1
    label
      tag=>h3
      class=>span-20 last clear bottom-1
      value=>events
  add=>[$]
    value=>[_hr][label]
  add=>[$]
    value=>[_hr][label:1]

  // looping through each control event and items collection
  for-each=>[**inspect=>?][..][**controls][0]
    index-of=>[.].Name
      what=>on
    if=>exist
      lhs=>[@][index-of][result]
      or=>equals
        lhs=>[.].Name
        rhs=>items
      code
        // not event handler
        set=>[@][_params]
        set=>[@][_params][dna]
          value=>[/][magix.ide.get-selected-control][dna]
        if=>exist
          lhs=>[/][magix.ide.get-selected-control][value][0][[.].Name]
          code
            add=>[@][..][..][_params][content]
              value=>[/][magix.ide.get-selected-control][value][0][[.].Name]
              children-only=>true
        add=>[@][_params][field-type]
          value=>[.]
        add=>[$]
          value=>[:magix.ide.create-control-event-modifier-control[@][_params]][control]
          children-only=>true

// creates one property modifier for properties web part
magix.ide.create-control-property-modifier-control
  _data
    panel
      class=>span-9
      controls
        label
          class=>span-3 text-right
          tag=>label
          value=>{field-type-name}
        text-box=>{field-type-name}
          class=>span-6 last
          placeholder=>{field-type-value}
          value=>{value}
          ontextchanged
            _field-name=>{field-type-name}
            _dna=>{dna}
            set=>[magix.ide.change-control][dna].Value
              value=>[_dna].Value
            if=>not-equals
              lhs=>[$][value].Value
              rhs=>
              code
                set=>[magix.ide.change-control][change][[_field-name].Value].Value
                  value=>[$][value].Value
            else
              set=>[magix.ide.change-control][change][[_field-name].Value].Name
                value=>[_field-name].Value
            magix.ide.change-control

  // handling text [literal controls] specially
  if=>equals
    lhs=>[$][field-type][0].Name
    rhs=>text
    code
      set=>[_data][panel][class].Value
        value=>span-20 last clear top-1
      set=>[_data][panel][controls][text-box].Name
        value=>text-area
      set=>[_data][panel][controls][text-area][class].Value
        value=>span-15 last
      set=>[_data][panel][controls][text-area][rows].Value
        value=>10

  // handling src [image controls] specially
  if=>equals
    lhs=>[$][field-type][0].Name
    rhs=>src
    code
      _data
        button
          class=>span-2 last
          value=>browse
          onclick
            magix.browser.scroll
              id=>content3
            execute-script
              file=>system42/private/file-manager/file-manager.hl
              params
                filter=>*.png;*.jpg;*.jpeg
                directory=>media
                container=>content3
                editor-container=>
                class=>span-15 last boxed air-padding bottom-1 top-1 left-5
                no-edit=>true
                onselect
                  set=>[magix.forms.set-value][value].Value
                    value=>[$][file].Value
                  magix.forms.set-value
                    form-id=>control-properties-wrapper
                    id=>src
                  magix.viewport.clear-controls
                    container=>content3
                    reset-class=>true
                  _field-name=>{field-type-name}
                  _dna=>{dna}
                  set=>[magix.ide.change-control][dna].Value
                    value=>[_dna].Value
                  if=>not-equals
                    lhs=>[$][file].Value
                    rhs=>
                    code
                      set=>[magix.ide.change-control][change][[_field-name].Value].Value
                        value=>[$][file].Value
                  else
                    set=>[magix.ide.change-control][change][[_field-name].Value].Name
                      value=>[_field-name].Value
                  magix.ide.change-control
                oncancel
                  magix.viewport.clear-controls
                    container=>content3
                    reset-class=>true
      set=>[_data][panel][controls][text-box][class].Value
        value=>span-4
      add=>[_data][panel][controls]
        value=>[@][_data][button]

  iterate=>[_data]
    if=>equals
      lhs=>[.].Value
      rhs=>{dna}
      code
        set=>[.].Value
          value=>[$][dna].Value
    else-if=>equals
      lhs=>[.].Value
      rhs=>{value}
      code
        set=>[.].Value
          value=>[$][content].Value
    else-if=>equals
      lhs=>[.].Value
      rhs=>{field-type-value}
      code
        set=>[.].Value
          value=>[$][field-type][0].Value
    else-if=>equals
      lhs=>[.].Value
      rhs=>{field-type-name}
      code
        set=>[.].Value
          value=>[$][field-type][0].Name

  // returning control for modifying property back to caller
  add=>[$][control]
    value=>[_data][panel]

// creates one property modifier for properties web part
magix.ide.create-control-event-modifier-control
  _data
    panel
      class=>span-20 last
      controls
        link-button
          class=>span-3
          tag=>label
          value=>{field-type-name}
          onclick
            magix.forms.get-style
              form-id=>control-properties
              id=>{field-type-name}
              key=>display
            if=>equals
              lhs=>[magix.forms.get-style][value].Value
              rhs=>none
              code
                magix.viewstate.set
                  id=>magix.ide.visible-event-code-{0}-{1}
                    =>{dna}
                    =>{field-type-name}
                  value=>true
                magix.forms.effect
                  type=>roll-down
                  time=>250
                  form-id=>control-properties
                  id=>{field-type-name}
                  chained
                    e0
                      type=>focus-and-select
                      form-id=>control-properties
                      id=>{field-type-name}
            else
              magix.viewstate.set
                id=>magix.ide.visible-event-code-{0}-{1}
                  =>{dna}
                  =>{field-type-name}
              magix.forms.effect
                type=>roll-up
                time=>250
                form-id=>control-properties
                id=>{field-type-name}
        text-area=>{field-type-name}
          style=>display:none;
          class=>span-17 last
          rows=>10
          placeholder=>{field-type-value}
          value=>{value}
          onfirstload
            magix.viewstate.get
              id=>magix.ide.visible-event-code-{0}-{1}
                =>{dna}
                =>{field-type-name}
            if=>exist
              lhs=>[magix.viewstate.get][value]
              code
                magix.forms.set-style
                  form-id=>control-properties
                  id=>{field-type-name}
                  key=>display
                  value=>block
          ontextchanged
            _field-name=>{field-type-name}
            _dna=>{dna}
            set=>[magix.ide.change-control][dna].Value
              value=>[_dna].Value
            if=>not-equals
              lhs=>[$][value].Value
              rhs=>
              code
                set=>[@][code-2-node][code].Value
                  value=>[$][value].Value
                code-2-node
                  remove-root=>true
                add=>[magix.ide.change-control][change][[_field-name].Value]
                  value=>[@][code-2-node][node]
                  children-only=>true
            else
              set=>[magix.ide.change-control][change][[_field-name].Value].Name
                value=>[_field-name].Value
            magix.ide.change-control
  iterate=>[_data]
    if=>equals
      lhs=>[.].Value
      rhs=>{dna}
      code
        set=>[.].Value
          value=>[$][dna].Value
    else-if=>equals
      lhs=>[.].Value
      rhs=>{value}
      code
        if=>equals
          lhs=>[$][content].Count
          rhs=>0
          or=>not-exist
            lhs=>[$][content]
          code
            set=>[.].Value
        else
          set=>[/][_data][panel][controls][link-button][style].Value
            value=>font-weight:bold;
          node-2-code
            remove-root=>true
            node=>[$][content]
          set=>[.].Value
            value=>[@][node-2-code][code].Value
    else-if=>equals
      lhs=>[.].Value
      rhs=>{field-type-value}
      code
        set=>[.].Value
          value=>[$][field-type][0].Value
    else-if=>equals
      lhs=>[.].Value
      rhs=>{field-type-name}
      code
        set=>[.].Value
          value=>[$][field-type][0].Name
  add=>[$][control]
    value=>[_data][panel]

// form updated, updating property form
magix.ide.set-form
  magix.ide.request-properties

  