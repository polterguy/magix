

inspect=>@"tests for the [add] keyword

creates unit tests for testing the [add] 
hyperlisp keyword"


event
  name=>magix.test.add-expressions
  persist=>false
  inspect=>@"verifies [add] works with expressions

tries to add a node, pointed to by an expression, 
to another node list.  verifying that the operation 
added all nodes as expected"
  code
    _buffer
    _data1
      tmp=>testing
    _buffer
    add=>[_buffer]
      value=>[_data1]
    if=>not-equals
      lhs=>[_buffer][_data1][tmp].Value
      rhs=>testing
      code
        throw=>[add] misbehaved


event
  name=>magix.test.add-no-expressions
  persist=>false
  inspect=>@"verifies [add] works with a node tree

tries to add a list of nodes, being children of 
the [add] node, and verifies that the add operation 
added all nodes as expected"
  code
    _buffer
    add=>[_buffer]
      _data1
        tmp=>testing
    if=>not-equals
      lhs=>[_buffer][_data1][tmp].Value
      rhs=>testing
      code
        throw=>[add] misbehaved


event
  name=>magix.test.add-children-only
  persist=>false
  inspect=>@"verifies [add] works with [children-only]

tries to add a node to another, pointed to by an 
expression, where the add node has [children-only] 
being set to true, to make sure only the children 
of the value node are being added"
  code
    _data1
      tmp=>testing
    _buffer
    add=>[_buffer]
      value=>[_data1]
      children-only=>true
    if=>not-equals
      lhs=>[_buffer][tmp].Value
      rhs=>testing
      code
        throw=>[add] misbehaved


event
  name=>magix.test.add-single-node
  persist=>false
  inspect=>@"verifies [add] works with single nodes

tries to add a single node to another node, by using 
the [add] keyword, making sure the name and value of 
the newly added node is accurate"
  code
    _buffer
    add=>[_buffer]
      value=>tmp
        value=>testing
    if=>not-equals
      lhs=>[_buffer][tmp].Value
      rhs=>testing
      code
        throw=>[add] misbehaved


event
  name=>magix.test.add-single-null-node
  persist=>false
  inspect=>@"verifies [add] works with single nodes

tries to add a single node, where the value is not 
given, making sure the newly added node has the 
correct name, and a null value"
  code
    _buffer
    add=>[_buffer]
      value=>tmp
    if=>not-exist
      lhs=>[_buffer][tmp]
      or=>exist
        lhs=>[_buffer][tmp].Value
      code
        throw=>[add] misbehaved

