
inspect=>@"creates the file manager web part

creates a web part that allows you to browse the file system 
in your magix installation"


magix.viewport.include-client-file
  type=>css
  file=>media/back-end/file-manager.css

magix.viewport.include-client-file
  type=>css
  file=>media/back-end/file-uploader.css

if=>equals
  lhs=>[$][show-info].value
  rhs=>true
  code
    // creates the descriptive web part at the top
    magix.forms.create-web-part
      class=>span-15 last air-padding desktop-widget top-1
      container=>content1
      controls
        label
          class=>span-15 last
          tag=>h3
          value=>file manager
        label
          class=>span-15 last top-1
          value=>@"from here you can browse the file system in your local magix installation.&nbsp;&nbsp;you can move, rename, copy and 
delete files from here.&nbsp;&nbsp;if you wish to upload files to your system, then first browse to the directory you wish to upload 
the files to, then drag and drop the files you wish to upload to your browser, and they will be automatically uploaded to your system"

// clearing selected object, and other settings
magix.viewstate.set
  id=>magix.file-manager.selected-object

// changing settings according to parameters
if=>exist
  lhs=>[$][container]
  code
    set=>[magix.forms.create-web-part][container].value
      value=>[$][container].value
if=>exist
  lhs=>[$][class]
  code
    set=>[magix.forms.create-web-part][class].value
      value=>[$][class].value
if=>exist
  lhs=>[$][filter]
  code
    set=>[@][magix.viewstate.set][value].value
      value=>[$][filter].value
    magix.viewstate.set
      id=>magix.file-manager.file-filter
else
  magix.viewstate.set
    id=>magix.file-manager.file-filter
if=>exist
  lhs=>[$][onselect]
  code
    add=>[@][magix.viewstate.set][value]
      values=>[$][onselect]
    magix.viewstate.set
      id=>magix.file-manager.onselect
else
  magix.viewstate.set
    id=>magix.file-manager.onselect
if=>exist
  lhs=>[$][oncancel]
  code
    add=>[@][magix.viewstate.set][value]
      values=>[$][oncancel]
    magix.viewstate.set
      id=>magix.file-manager.oncancel
else
  magix.viewstate.set
    id=>magix.file-manager.oncancel

if=>exist
  lhs=>[$][directory]
  code
    set=>[@][magix.viewstate.set][value].value
      value=>[$][directory].value
    magix.viewstate.set
      id=>magix.file-manager.current-directory


// creates the actual file manager
magix.forms.create-web-part
  class=>span-20 last clear top-1 air-padding desktop-widget
  form-id=>file-manager
  container=>content2
  events-file=>system42/private/file-manager/parts/events.hl
  controls-file=>system42/private/file-manager/parts/controls.hl

