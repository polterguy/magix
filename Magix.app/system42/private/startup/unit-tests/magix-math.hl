

inspect=>@"creates unit tests for the magix.file namespace

creates unit tests to test that [magix.math.add], [magix.math.subtract], 
[magix.math.multiply], [magix.math.divide] and [magix.math.modulo] 
work the way they should"


event
  name=>magix.test.math-complex
  persist=>false
  inspect=>@"verifies the magix.math namespace works

creates a complex math expression, with several 
sub-expressions, verifying the result becomes 
what was expected"
  code
    using=>magix.math
      add
        =>5
        subtract
          =>10
          =>5
          =>3
        multiply
          =>3
          =>3
          modulo
            =>15
            =>6
    if=>not-equals
      lhs=>[using=>magix.math][add].value
      rhs=>34
      code
        throw=>magix.math misbehaved


event
  name=>magix.test.math-expression-result
  persist=>false
  inspect=>@"verifies the magix.math namespace works

creates a math expression, with the result node being an 
expression, verifying the result will end up in the 
expression's node"
  code
    _result
    using=>magix.math
      add=>[_result]
        =>5
        =>1
    if=>not-equals
      lhs=>[_result].value
      rhs=>6
      code
        throw=>magix.math misbehaved


event
  name=>magix.test.math-sub-events
  persist=>false
  inspect=>@"verifies the magix.math namespace works

creates a math expression, with sub active events, verifying 
the sub active events are invoked and used as a part of the 
math expression"
  code
    event
      name=>magix.test-event.foo
      persist=>false
      code
        set=>[$].value
          value=>2
    using=>magix.math
      add
        magix.test-event.foo
        =>1
    event
      name=>magix.test-event.foo
    if=>not-equals
      lhs=>[using][add].value
      rhs=>3
      code
        throw=>magix.math misbehaved

