

inspect=>@"active events for show-contacts.hl file

contains active events for the contacts list of emails web part"


magix.emails.toggle-contact
  using=>magix.forms
    get-value
      form-id=>contacts-list
      id=>viewing-contact-{0}
        =>[$][id].value
  if=>equals
    lhs=>[using][get-value][value].value
    rhs=>true
    code
      // already viewing, "unviewing" contact
      using=>magix.forms
        set-value
          form-id=>contacts-list
          id=>viewing-contact-{0}
            =>[$][id].value
          value=>false
      set=>[@][magix.viewport.clear-controls][container].value
        value=>dynamic-contact-{0}
          =>[$][id].value
      magix.viewport.clear-controls
        reset-class
          new-class=>contact-sleeping-dynamic
  else
    // making contact show
    using=>magix.forms
      set-value
        form-id=>contacts-list
        id=>viewing-contact-{0}
          =>[$][id].value
        value=>true
    set=>[@][magix.emails.load-contact][container].value
      value=>dynamic-contact-{0}
        =>[$][id].value
    set=>[@][magix.emails.load-contact][contact-id].value
      value=>[$][contact-id].value
    magix.emails.load-contact


magix.emails.load-contact
  using=>magix.data
    load
      id=>[$][contact-id].value
  iterate=>[magix.execute]
    if=>equals
      lhs=>[.].value
      rhs=>{container}
      code
        set=>[.].value
          value=>[$][container].value
    else-if=>equals
      lhs=>[.].value
      rhs=>{id}
      code
        set=>[.].value
          value=>[$][contact-id].value
    else-if=>equals
      lhs=>[.].value
      rhs=>{name}
      code
        if=>exist
          lhs=>[/][using][load][value][display-name].value
          and=>not-equals
            lhs=>[/][using][load][value][display-name].value
            rhs=>
          code
            set=>[.].value
              value=>[/][using][load][value][display-name].value
        else
          set=>[.].value
            value=>{unknown}
    else-if=>equals
      lhs=>[.].value
      rhs=>{email}
      code
        set=>[.].value
          value=>[/][using][load][value][email].value
    else-if=>equals
      lhs=>[.].value
      rhs=>{date}
      code
        using=>magix.date
          format-offset
            date=>[/][using][load][created].value
        set=>[.].value
          value=>[@][using][format-offset][value].value
    else-if=>equals
      lhs=>[.].value
      rhs=>{date-absolute}
      code
        set=>[.].value
          value=>[/][using][load][created].value
    else-if=>equals
      lhs=>[.].value
      rhs=>{description}
      code
        if=>exist
          lhs=>[/][using][load][value][description].value
          code
            set=>[.].value
              value=>[/][using][load][value][description].value
        else
          set=>[.].value
            value=>
  if=>exist
    lhs=>[/][using][load][value][description].value
    code
      set=>[magix.execute][**label=>description][value].value
        value=>[/][using][load][value][description].value
  magix.execute
    using=>magix.forms
      create-web-part
        container=>{container}
        form-id=>{container}
        class=>span-18 last contact-item-body bottom-1 top-1
        controls
          label=>name
            class=>span-14
            tag=>h3
            value=>{name}
          label=>date
            class=>span-3 last contact-date
            value=>{date}
            title=>{date-absolute}
          label=>email
            class=>span-7 contact-email top-1
            value=>{email}
          label=>description
            class=>span-17 top-1 last contact-description
            value=>&nbsp;
          button=>view-conversation
            class=>span-2 top-1 last right clear
            value=>emails
            onclick
              using=>magix.data
                load
                  id=>{id}
              set=>[execute-script][params][criteria][prototype][from].value
                value=>[using][load][value][email].value
              set=>[execute-script][params][criteria][or][to][].value
                value=>[using][load][value][email].value
              set=>[execute-script][params][criteria][or:1][cc][].value
                value=>[using][load][value][email].value
              set=>[execute-script][params][criteria][or:2][bcc][].value
                value=>[using][load][value][email].value
              set=>[execute-script][params][header-append].value
                value=>[using][load][value][email].value
              execute-script
                file=>your-files/system-apps/magix-mail/components/inbox/show-emails.hl
                params
                  container=>view-emails
                  prototype
                    type=>magix.email.message
                    username=>{user}
              magix.browser.scroll
                id=>view-emails
          link-button=>edit-contact
            class=>span-1 top-1 last contact-edit right clear
            value=>edit
            onclick
              using=>magix.forms
                set-visible
                  form-id=>{container}
                  id=>panel-edit
                  value=>true
                set-visible
                  form-id=>{container}
                  id=>edit-contact
                  value=>false
                set-value
                  form-id=>{container}
                  id=>edit-name
                  value=>{name}
                set-value
                  form-id=>{container}
                  id=>edit-description
                  value=>{description}
                effect
                  form-id=>{container}
                  id=>edit-name
                  type=>focus-and-select
          panel=>panel-edit
            visible=>false
            class=>span-17 last top-1 bottom-1
            onesc
              using=>magix.forms
                set-visible
                  form-id=>{container}
                  id=>panel-edit
                  value=>false
                set-visible
                  form-id=>{container}
                  id=>edit-contact
                  value=>true
            controls
              label
                class=>span-17
                value=>name
                tag=>label
              text-box=>edit-name
                class=>span-17 bottom-1
                placeholder=>name ...
              label
                class=>span-17
                value=>description
                tag=>label
              text-area=>edit-description
                class=>span-17 clear
                rows=>5
                placeholder=>description ...
              button=>update-contact
                class=>span-2 top-1 last right
                value=>update
                onclick
                  _id=>{id}
                  magix.forms.get-children-values
                    form-id=>{container}
                    id=>panel-edit
                  using=>magix.data
                    load
                      id=>[_id].value
                  set=>[using][load][value][display-name].value
                    value=>[magix.forms.get-children-values][values][edit-name].value
                  set=>[using][load][value][description].value
                    value=>[magix.forms.get-children-values][values][edit-description].value
                  using=>magix.data
                    save
                      id=>[_id].value
                      value=>[using][load][value]
                  magix.viewport.show-message
                    message=>contact was updated
                  set=>[@][magix.emails.load-contact][container].value
                    value=>{container}
                  set=>[@][magix.emails.load-contact][contact-id].value
                    value=>{id}
                  magix.emails.load-contact

