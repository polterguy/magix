

inspect=>@"creates unit tests for testing execution 
of active events created with [event]"


event=>magix._test._run-event-with-parameters

  inspect=>@"internally used by [magix.test.run-event-with-parameters]
to see if passing parameters in and out from dynamically created hyper lisp events
behaves as expected"

  code
    if=>[$][mumbo].Value!=howdy
      throw=>input parameter not accessible in [event]
    set=>[$][jumbo].Value
      value=>5


event=>magix.test.execute-event-with-parameters

  inspect=>@"verifies that executing a dynamically created [event] 
with parameters, both input and output is behaving
correctly"

  code
    magix._test._run-event-with-parameters
      mumbo=>howdy
    if=>[magix._test._run-event-with-parameters][mumbo].Value!=howdy
      throw=>input value not returned
    if=>[magix._test._run-event-with-parameters][jumbo].Value!=5
      throw=>output value not returned currectly


event=>magix.test.session-set-retrieve-delete

  inspect=>@"creates the unit test
necessary to test if session retrieve and set works correctly"

  code
    magix.web.set-session=>magix.test.test-session
      value=>howdy
        child1=>howdy2
    magix.web.get-session=>magix.test.test-session
    if=>![magix.web.get-session][value]
      throw=>couldn't find session object
    if=>[magix.web.get-session][value].Value!=howdy
      throw=>couldn't find correct value for session object
    if=>[magix.web.get-session][value][child1].Value!=howdy2
      throw=>couldn't find correct value for session child node object
    magix.web.set-session=>magix.test.test-session
    magix.web.get-session=>magix.test.test-session
    if=>[magix.web.get-session:1][value]
      throw=>couldn't delete session object
