

inspect=>@"contains the active events for the load web part event wizard

contains the active events for the web part that allows the user to create an active event 
that loads up a web part"


// populates the select css file drop down with all css files beneath the media folder
magix.wizard.populate-select-css-file

  // defaulting directory to media directory, unless a directory is explicitly given as input
  _directory=>media
  if=>exist
    lhs=>[$][directory]
    code
      set=>[_directory].value
        value=>[$][directory].value

  // fetching all files and directories from current directory
  using=>magix.file
    list-files
      directory=>[_directory].value
      filter=>*.css
    list-directories
      directory=>[_directory].value

  // looping through all files
  for-each=>[using][list-files][files]
    add=>[$][files]
      value=>[.].name
        value=>[.].name

  // looping through all directories, and recursively calling self
  for-each=>[using][list-directories][directories]
    set=>[@][magix.wizard.populate-select-css-file][directory].value
      value=>[.].name
    magix.wizard.populate-select-css-file
    if=>exist
      lhs=>[@][magix.wizard.populate-select-css-file][files]
      code
        add=>[$][files]
          value=>[@][..][..][magix.wizard.populate-select-css-file][files]
          children-only=>true

// creates the web part that holds the css files
magix.wizard.create-css-files-container

  magix.forms.create-web-part
    container=>css-files
    form-id=>css-files
    class=>span-7 last air-padding boxed-light-green
    controls
      label
        tag=>label
        class=>span-2
        value=>css files
      select=>add-css-file
        class=>span-5 last bottom-1
        onfirstload
          magix.wizard.populate-select-css-file
          add=>[magix.forms.set-values][values]
            value=>[magix.wizard.populate-select-css-file][files]
            children-only=>true
          magix.forms.set-values
            form-id=>css-files
            id=>add-css-file
            values
              _default=>insert css file ...
        onselectedindexchanged
          magix.forms.get-value
            form-id=>css-files
            id=>add-css-file
          magix.viewstate.get
            id=>magix.wizard.css-files

          // checking to see if file is already included
          _included
          for-each=>[magix.viewstate.get][value]
            if=>equals
              lhs=>[.].name
              rhs=>[/][magix.forms.get-value][value].value
              code
                magix.viewport.show-message
                  message=>that file is already included
                set=>[/][_included].value
                  value=>true
                magix.forms.set-value
                  form-id=>css-files
                  id=>add-css-file
                  value=>_default
          if=>not-equals
            lhs=>[_included].value
            rhs=>true
            code
              add=>[@][magix.viewstate.set][value]
                value=>[magix.viewstate.get][value]
                children-only=>true
              add=>[@][magix.viewstate.set][value]
                value=>[magix.forms.get-value][value].value
              magix.viewstate.set
                id=>magix.wizard.css-files
              magix.wizard.create-css-files-container
      lambda=>lambda-css-files
        oncreatecontrols
          _control
            panel
              class=>span-7 last hover-shade
              controls
                label
                  class=>span-5
                link-button
                  class=>span-2 last text-right
                  value=>remove
                  onclick
                    magix.viewstate.get
                      id=>magix.wizard.css-files
                    set=>[magix.viewstate.get][value][[$][info].value]
                    set=>[magix.viewstate.set][value]
                      value=>[magix.viewstate.get][value]
                    magix.viewstate.set
                      id=>magix.wizard.css-files
                    magix.wizard.create-css-files-container
          magix.viewstate.get
            id=>magix.wizard.css-files
          for-each=>[magix.viewstate.get][value]
            set=>[/][_control][panel][controls][label][value].value
              value=>[.].name
            set=>[/][_control][panel][controls][link-button][info].value
              value=>[.].name
            add=>[$]
              value=>[/][_control][panel]

// creates the 'load from where' web part, that depending upon the value of the 'load from where' select drop down list 
// asks the user for an id in the database, or to browse for a file on disc
magix.wizard.create-load-from-where-web-part
  magix.forms.get-value
    form-id=>wizard
    id=>sel-load-from-where

  // if user has selected to load from disc, we create the load from disc web part, otherwise we create the load from database web part
  switch=>[magix.forms.get-value][value].value

    // user has chosen to load web part from database
    case=>database
      magix.forms.create-web-part
        container=>dyn-load-from-where
        form-id=>from-where
        controls
          select=>database-id
            class=>span-9 last
            onfirstload

              // populating the select database id drop down list with all the ids from the database 
              // for web parts
              _items
                _default=>choose a database id ...
              magix.data.load
                prototype
                  type=>magix.forms.web-part
              for-each=>[magix.data.load][objects]
                add=>[/][_items]
                  value=>[.][id].value
                    value=>[.][id].value
              add=>[magix.forms.set-values][values]
                value=>[_items]
                children-only=>true
              magix.forms.set-values
                form-id=>from-where
                id=>database-id

    // user has chosen to load web part from file system
    case=>filesystem
      magix.forms.create-web-part
        container=>dyn-load-from-where
        form-id=>from-where
        controls
          text-box=>filename
            class=>span-7
            placeholder=>file name ...
          button=>browse
            class=>span-2 last
            value=>browse
            onclick
              execute-script
                file=>system42/private/file-manager/file-manager.hl
                params
                  filter=>*.hl
                  container=>file-browser
                  deny-create-directory=>true
                  onselect
                    magix.viewport.clear-controls
                      container=>file-browser
                      reset-class=>true
                    using=>magix.forms
                      set-value
                        form-id=>from-where
                        id=>filename
                        value=>[$][file].value
                  oncancel
                    magix.viewport.clear-controls
                      container=>file-browser
                      reset-class=>true

// shows davanced settings for load web part active event wizard
magix.wizard.show-advanced-settings
  magix.forms.get-value
    form-id=>wizard
    id=>show-advanced
  if=>equals
    lhs=>[magix.forms.get-value][value].value
    rhs=>advanced
    code
      magix.forms.set-visible
        form-id=>wizard
        id=>show-advanced
        value=>false
      magix.forms.effect
        form-id=>wizard
        id=>advanced
        type=>fade-in
        time=>250
        chained
          e0
            type=>focus-and-select
            form-id=>wizard
            id=>container

// actual creation of active event
magix.wizard.create-active-event
  // buffer holding event creation code
  _data
    event
      name=>{active-event-name}
      code
        magix.forms.create-web-part
          controls-id=>{database-id}
  _data2
    event
      name=>{active-event-name}
      code
        magix.forms.create-web-part
          controls-file=>{filename}

  // retrieving active event name
  magix.forms.get-value
    form-id=>wizard
    id=>active-event-name
  split=>[magix.forms.get-value][value].value
    what=>.
  if=>equals
    lhs=>[split][result].count
    rhs=>1
    or=>equals
      lhs=>[split][result][0].value
      rhs=>magix
    or=>equals
      lhs=>[magix.forms.get-value][value].value
      rhs=>
    code
      magix.viewport.show-message
        message=>you must supply a name for your active event.&nbsp;&nbsp;the name must contain a '.', and cannot start with 'magix.'
        time=>5000
        color=>#ffaaaa
      magix.forms.effect
        type=>focus-and-select
        form-id=>wizard
        id=>active-event-name
      stop

  // stores a reference to either [_data] or [_data2] depending upon if user has chosen database or filesystem as source
  _current

  // checking to see if active event has good name
  if=>equals
    lhs=>[magix.forms.get-value][value].value
    rhs=>
    code
      magix.viewport.show-message
        message=>you must give your active event a name
  else
    magix.forms.get-value
      form-id=>wizard
      id=>sel-load-from-where
    switch=>[@][magix.forms.get-value][value].value

      // user wants to use a form from database
      case=>database
        set=>[/][_current].value
          value=>_data
        magix.forms.get-value
          form-id=>wizard
          id=>database-id
        if=>equals
          lhs=>[@][magix.forms.get-value][value].value
          rhs=>_default
          code
            magix.viewport.show-message
              message=>you must choose a form from your database
        else
          set=>[/][_good].value
            value=>true
          iterate=>[/][_data]
            if=>equals
              lhs=>[.].value
              rhs=>{database-id}
              code
                set=>[.].value
                  value=>[@][..][..][..][..][magix.forms.get-value][value].value
      case=>filesystem
        set=>[/][_current].value
          value=>_data2
        magix.forms.get-value
          form-id=>from-where
          id=>filename
        if=>equals
          lhs=>[@][magix.forms.get-value][value].value
          rhs=>
          code
            magix.viewport.show-message
              message=>you must choose a web part hyperlisp file from your filesystem
        else
          set=>[/][_good].value
            value=>true
          iterate=>[/][_data2]
            if=>equals
              lhs=>[.].value
              rhs=>{filename}
              code
                set=>[.].value
                  value=>[@][..][..][..][..][magix.forms.get-value][value].value

  // checking to see if everything is ok
  if=>equals
    lhs=>[_good].value
    rhs=>true
    code

      // setting container for our web part, if there is any defined
      magix.forms.get-value
        form-id=>wizard
        id=>container
      if=>not-equals
        lhs=>[@][magix.forms.get-value:0][value].value
        rhs=>
        code
          set=>[[/][_current].value][event][code][magix.forms.create-web-part][container].value
            value=>[@][..][..][magix.forms.get-value:0][value].value

      // setting css classes for our web part, if there is any defined
      magix.forms.get-value
        form-id=>wizard
        id=>css-class
      if=>not-equals
        lhs=>[@][magix.forms.get-value:1][value].value
        rhs=>
        code
          set=>[[/][_current].value][event][code][magix.forms.create-web-part][class].value
            value=>[@][..][..][magix.forms.get-value:1][value].value

      // setting active event name
      iterate=>[/][[/][_current].value]
        if=>equals
          lhs=>[.].value
          rhs=>{active-event-name}
          code
            set=>[.].value
              value=>[/][magix.forms.get-value][value].value

      // setting css files for web part
      magix.viewstate.get
        id=>magix.wizard.css-files
      for-each=>[@][magix.viewstate.get][value]
        _css
          magix.viewport.include-client-file
            type=>css
        set=>[@][_css][magix.viewport.include-client-file][file].value
          value=>[.].name
        add=>[/][[/][_current].value][event][code]
          value=>[@][_css][magix.viewport.include-client-file]

      magix.forms.get-value
        form-id=>wizard
        id=>form-id
      if=>not-equals
        lhs=>[@][magix.forms.get-value:2][value].value
        rhs=>
        code
          set=>[[/][_current].value][event][code][magix.forms.create-web-part][form-id].value
            value=>[@][..][..][magix.forms.get-value:2][value].value

      // creating active event
      execute=>[[/][_current].value]
      magix.viewport.show-message
        message=>active event was created
      magix.forms.set-visible
        form-id=>wizard
        id=>edit-event
        value=>true
      using=>magix.forms
        set-info
          form-id=>wizard
          id=>edit-event
          value=>[magix.forms.get-value][value].value

