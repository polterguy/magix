

inspect=>@"contains add controls active events for form builder

used to create the form builder.  contains the active events necessary 
to create the add controls parts of the form builder"

// creates the [magix.ide.add-control] code for adding controls to design surface
magix.ide.add-control-to-surface
  magix.forms.get-value
    form-id=>add-control
    id=>select-add-where
  set=>[$][magix.ide.add-control][where][position].Value
    value=>[magix.forms.get-value][value].Value
  set=>[$][magix.ide.add-control][control][0].Name
    value=>[$][value].Value
  set=>[$][magix.ide.add-control][auto-select].Value
    value=>true
  // decorating returned control with default properties
  with=>[$][magix.ide.add-control][control][0]
    switch=>[$][0].Value
      case=>button
        set=>[value].Value
          value=>your button
        set=>[class].Value
          value=>span-3
      case=>hyperlink
        set=>[value].Value
          value=>your link
        set=>[href].Value
          value=>http://magixilluminate.com
      case=>img
        set=>[alt].Value
          value=>magix illuminate logo
        set=>[src].Value
          value=>media/images/icons/start-button.png
        set=>[class].Value
          value=>span-4 last
      case=>label
        set=>[value].Value
          value=>your label
        set=>[class].Value
          value=>span-3
      case=>link-button
        set=>[value].Value
          value=>your button
        set=>[class].Value
          value=>span-3
      case=>literal
        set=>[text].Value
          value=>your html
  // returning add control code
  set=>[$][0]

// raises the active event that changes the visibility of the "clear all" buttons according to
// if the form has any controls at all
magix.ide.surface-changed
  magix.ide.set-clear-all-control-enabled

// changes the enabled property of the "clear all" button according to if the form has controls or not
magix.ide.set-clear-all-control-enabled
  magix.ide.get-form
  if=>equals
    lhs=>[magix.ide.get-form].Count
    rhs=>0
    code
      magix.forms.set-enabled
        form-id=>add-control
        id=>btn-clear-controls
        value=>false
  else
    magix.forms.set-enabled
      form-id=>add-control
      id=>btn-clear-controls
      value=>true

